Прогр sapun { 


/*###########################################################*/
/*#########                     #############################*/
/*#########  ОПИСАНИЕ КОНСТАНТ  #############################*/
/*#########                     #############################*/
/*###########################################################*/
/* ?????????????? Таймауты взяты с потолка */
ТАКТ     50;    /* такт равен 500 миллисекундам */
КОНСТ Т0_1                       0;  /* Константа обеспечивающая паузу в 0.5 сек */
КОНСТ ОДНА_СЕКУНДА               2;  /* Константа обеспечивающая паузу в 1 сек */
КОНСТ ДВЕ_СЕКУНДЫ             2*ОДНА_СЕКУНДА;
КОНСТ ТРИ_СЕКУНДЫ             3*ОДНА_СЕКУНДА;
КОНСТ ПЯТЬ_СЕКУНД             5*ОДНА_СЕКУНДА;
КОНСТ ДЕСЯТЬ_СЕКУНД          10*ОДНА_СЕКУНДА;
КОНСТ ДВАДЦАТЬ_СЕКУНД        20*ОДНА_СЕКУНДА;
КОНСТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_ЗАТ_ВАК			10*ОДНА_СЕКУНДА;
КОНСТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_САПУНА           	ДЕСЯТЬ_СЕКУНД;
КОНСТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_УСТР_ПЛАВ_ПУСКА   3*ОДНА_СЕКУНДА;
КОНСТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_ВАК_НАСОСА 		3*ОДНА_СЕКУНДА;
КОНСТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_ВЕНТИЛЯТОРА		ПЯТЬ_СЕКУНД;
КОНСТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_РЕГ_Т				3*ОДНА_СЕКУНДА;
КОНСТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_РЕГ_УР			3*ОДНА_СЕКУНДА;
КОНСТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_НАСОС_ОХЛ			3*ОДНА_СЕКУНДА;
КОНСТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ  ДЕСЯТЬ_СЕКУНД;
КОНСТ ОДНА_МИНУТА          60*ОДНА_СЕКУНДА;
КОНСТ ДВЕ_МИНУТЫ          2*ОДНА_МИНУТА;
КОНСТ ПЯТЬ_МИНУТ          5*60*ОДНА_СЕКУНДА;
/* КОНСТ ТАЙМАУТ_ВАКУУМИРОВАНИЯ_ТРУБЫ  30*ОДНА_МИНУТА;*/
КОНСТ ТРИДЦАТЬ_МИНУТ          30*ОДНА_МИНУТА;
КОНСТ ТАЙМАУТ_ЗАДЕРЖКА_НА_ОТКАЧКУ ПЯТЬ_СЕКУНД;

КОНСТ ВКЛ                1;
КОНСТ ВЫК               0;
КОНСТ ОТКР              1;
КОНСТ ЗАКР          	0;
КОНСТ ЕСТЬ               1;
КОНСТ НЕТ                0;
КОНСТ НОРМА              1;
КОНСТ ВНЕ_НОРМЫ          0;



КОНСТ ТАЙМАУТ_НА_ДИАГНОСТИКУ_РАЗРЫВА_СВЯЗИ 5*ОДНА_СЕКУНДА;

КОНСТ Т_ПредельнаяВбакеОхлаждения          99.0; /*?????   градусов*/

КОНСТ ЗД_P_ВбакеКомпрессора						10.0; /*?????  атмосфер */

КОНСТ Т_ГИСТЕРЕЗИС_ВОДЫ_В_БАКЕ  1.0;
КОНСТ Т_ГИСТЕРЕЗИС_ВОДЫ_В_РУБАХЕ  1.0;


/*===========================================================*/
/*=== КОДЫ СООБЩЕНИЙ КОНТРОЛЯ                               =*/
/*===========================================================*/
ПЕРЕЧИСЛЕНИЕ {
    УРОВЕНЬ_ВОДЫ_В_БАКЕ_ОХЛАЖДЕНИЯ_НИЗКИЙ,
    УРОВЕНЬ_ВОДЫ_В_БАКЕ_ОХЛАЖДЕНИЯ_ВЫСОКИЙ,
	УРОВЕНЬ_ВОДЫ_В_БАКЕ_ОХЛАЖДЕНИЯ_В_НОРМЕ,
	ТЕМПЕРАТУРА_ВОДЫ_В_БАКЕ_ОХЛАЖДЕНИЯ_НИЗКАЯ,
	ТЕМПЕРАТУРА_ВОДЫ_В_БАКЕ_ОХЛАЖДЕНИЯ_ВЫСОКАЯ,
	ТЕМПЕРАТУРА_ВОДЫ_В_БАКЕ_ОХЛАЖДЕНИЯ_В_НОРМЕ,

    ТЕМПЕРАТУРА_ВОДЫ_В_РУБАШКЕ_ОХЛАЖДЕНИЯ_НИЗКАЯ,
	ТЕМПЕРАТУРА_ВОДЫ_В_РУБАШКЕ_ОХЛАЖДЕНИЯ_ВЫСОКАЯ,
	ТЕМПЕРАТУРА_ВОДЫ_В_РУБАШКЕ_ОХЛАЖДЕНИЯ_В_НОРМЕ,

	ДАВЛЕНИЕ_В_ВАКУУМНОЙ_ТРУБЕ_В_НОРМЕ,
	ДАВЛЕНИЕ_В_ВАКУУМНОЙ_ТРУБЕ_ВНЕ_НОРМЫ,
	
	СКОРОСТЬ_ОТКАЧКИ_В_НОРМЕ,
	СКОРОСТЬ_ОТКАЧКИ_НИЖЕ_НОРМЫ,
	
	ДАВЛЕНИЕ_В_БАКЕ_КОМПРЕССОРА_В_НОРМЕ,
    ДАВЛЕНИЕ_В_БАКЕ_КОМПРЕССОРА_НИЖЕ_НОРМЫ


	
};
/*===========================================================*/
/*========= КОМАНДЫ ОПЕРАТОРА ===============================*/
/*===========================================================*/


ПЕРЕЧИСЛЕНИЕ {
     ОТКР_ЗАТВОР_ВАК= ДАВЛЕНИЕ_В_БАКЕ_КОМПРЕССОРА_НИЖЕ_НОРМЫ + 1,
     ЗАКР_ЗАТВОР_ВАК    	,
     ОТКР_САПУН			    ,
     ЗАКР_САПУН			    ,
     ВКЛ_УСТР_ПЛАВ_ПУСКА    ,
     ВЫК_УСТР_ПЛАВ_ПУСКА    ,
	 ВКЛ_ПИТ_ВАК_НАСОСА     ,
	 ВЫК_ПИТ_ВАК_НАСОСА     ,
	 ВКЛ_ПИТ_ВЕНТИЛЯТОР    ,
	 ВЫК_ПИТ_ВЕНТИЛЯТОР    ,
     ВКЛ_ПИТ_РЕГ_Т    ,
	 ВЫК_ПИТ_РЕГ_Т    ,
     ВКЛ_ПИТ_РЕГ_УР     ,
	 ВЫК_ПИТ_РЕГ_УР     , 
	 ВКЛ_ПИТ_НАС_ОХЛ ,
	 ВЫК_ПИТ_НАС_ОХЛ    , 
	 ВКЛ_ВАКУУМИРОВАНИЕ_ТРУБЫ,
	 ВЫК_ВАКУУМИРОВАНИЕ_ТРУБЫ,
	 ВКЛ_НАСОС_ВАКУУМНЫЙ ,
	 ВЫК_НАСОС_ВАКУУМНЫЙ, 
     КОМАНДЫ_ОТ_ГУИ = ВЫК_НАСОС_ВАКУУМНЫЙ 
	
};


/*===========================================================*/
/*========= КОМАНДЫ ИЗМЕНЕНИЯ ФАКТИЧЕСКИХ ПАРАМЕТРОВ ========*/
/*===========================================================*/


ПЕРЕЧИСЛЕНИЕ {
	ИЗМН_ФП_P_ВбакеКомпрессора = КОМАНДЫ_ОТ_ГУИ + 1,
	ИЗМН_ФП_P_Впатрубке,
	ИЗМН_ФП_T_Врубашке,	
	ИЗМН_ФП_T_Вбаке,	
    ИЗМН_ФП_P_ВтрубеСидер,
    КОМАНДЫ_ИЗМ_ФП = ИЗМН_ФП_P_ВтрубеСидер
};
/*===========================================================*/
/*========= КОДЫ КОМАНД ПОСТУПАЮЩИЕ К ГУИ ===================*/
/*===========================================================*/
/*===========================================================*/
/*============= КОДЫ СООБЩЕНИЙ ВАКУУМНОЙ СИСТЕМЫ ============*/
/*===========================================================*/
/*===========================================================*/
ПЕРЕЧИСЛЕНИЕ {                    
      ПК_ГУИ_ОТКАЗ_ОТКРЫТИЯ_ЗАТВОРА_ВАКУУМНОГО = КОМАНДЫ_ИЗМ_ФП + 1,
      ПК_ГУИ_ОТКАЗ_ЗАКРЫТИЯ_ЗАТВОРА_ВАКУУМНОГО,
      ПК_ГУИ_ОТКАЗ_ОТКРЫТИЯ_САПУНА,
      ПК_ГУИ_ОТКАЗ_ЗАКРЫТИЯ_САПУНА,
	  ПК_ГУИ_ОТКАЗ_ВКЛЮЧЕНИЯ_ВЕНТИЛЯТОРА,
	  ПК_ГУИ_ОТКАЗ_ВЫКЛЮЧЕНИЯ_ВЕНТИЛЯТОРА,
	  ПК_ГУИ_ОТКАЗ_ВКЛЮЧЕНИЯ_УСТР_ПЛАВ_ПУСКА,
	  ПК_ГУИ_ОТКАЗ_ВЫКЛЮЧЕНИЯ_УСТР_ПЛАВ_ПУСКА,
	  ПК_ГУИ_ОТКАЗ_ВКЛЮЧЕНИЯ_РЕГУЛЯТОРА_ТЕМПЕРАТУРЫ,
	  ПК_ГУИ_ОТКАЗ_ВЫКЛЮЧЕНИЯ_РЕГУЛЯТОРА_ТЕМПЕРАТУРЫ,
	  ПК_ГУИ_ОТКАЗ_ВКЛЮЧЕНИЯ_РЕГУЛЯТОРА_УРОВНЯ,
	  ПК_ГУИ_ОТКАЗ_ВЫКЛЮЧЕНИЯ_РЕГУЛЯТОРА_УРОВНЯ,
	  ПК_ГУИ_ОТКАЗ_ВКЛЮЧЕНИЯ_НАСОСА_ОХЛАЖДЕНИЯ,
	  ПК_ГУИ_ОТКАЗ_ВЫКЛЮЧЕНИЯ_НАСОСА_ОХЛАЖДЕНИЯ,
	  ПК_ГУИ_ИДЕТ_ВАКУУМИРОВАНИЕ_ОТКАЗ_ВЫПОЛНЕНИЯ_КОМАНДЫ,
	  ПК_ГУИ_НЕТ_УСЛОВИЙ_ДЛЯ_ВЫПОЛНЕНИЯ_КОМАНДЫ,
	  ПК_ГУИ_УРОВЕНЬ_ВОДЫ_В_БАКЕ_ОХЛАЖДЕНИЯ_ВНЕ_НОРМЫ_ОТКАЗ_ВЫПОЛНЕНИЯ_КОМАНДЫ, 				
	  ПК_ГУИ_ОТРИЦАТЕЛЬНАЯ_ТЕМПЕРАТУРА_ВОДЫ_ОТКАЗ_ВЫПОЛНЕНИЯ_КОМАНДЫ,
	  ПК_ГУИ_ДАВЛЕНИЕ_В_ПАТРУБКЕ_НЕ_УМЕНЬШАЕТСЯ,	  
	  ПК_ГУИ_ОТКАЗ_ВКЛЮЧЕНИЯ_ПИТАНИЯ_ВАКУУМНОГО_НАСОСА,
	  ПК_ГУИ_ОТКАЗ_ВЫКЛЮЧЕНИЯ_ПИТАНИЯ_ВАКУУМНОГО_НАСОСА
      
      
};
/*===========================================================*/
/*========= КОДЫ СООБЩЕНИЙ ИЗМЕНЕНИЙ ФП         =============*/
/*===========================================================*/
ПЕРЕЧИСЛЕНИЕ {                    
      ПК_ГУИ_ФП_P_ВбакеКомпрессора = ПК_ГУИ_ОТКАЗ_ВЫКЛЮЧЕНИЯ_ПИТАНИЯ_ВАКУУМНОГО_НАСОСА + 1,
      ПК_ГУИ_ФП_P_Впатрубке,
      ПК_ГУИ_ФП_T_Врубашке,
      ПК_ГУИ_ФП_T_Вбаке,
      ПК_ГУИ_ФП_P_ВтрубеСидер
      
};




/*===========================================================*/
/*========= КОДЫ СООБЩЕНИЙ  О НЕВЕРНОЙ КОМАНДЕ ==============*/
/*===========================================================*/


ПЕРЕЧИСЛЕНИЕ {
     ПК_ГУИ_ОШИБКА_КОДА_КОМАНДЫ_ОТ_ГУИ = ПК_ГУИ_ФП_P_ВтрубеСидер + 1  
}; 

/*###########################################################*/
/*###########################################################*/
/*########                                          #########*/
/*########  ОПИСАНИЕ ФУНКЦИЙ                        #########*/
/*########                                          #########*/
/*########                                          #########*/
/*###########################################################*/

/*:::::::::Функции для работы  с ГУИ :::::::::::::::::::::   */

/*чтение кода и параметра события от ПИВ из кольцевого буфера в память */
ФУНКЦИЯ ЦЕЛ GetNextMsgGUI(ПУСТО);

ФУНКЦИЯ ЦЕЛ  GetMsgGUICode        (ПУСТО);  /*чтение из памяти кода события */
ФУНКЦИЯ ПЛАВ GetFloatParamPIV     (ПУСТО);  /*чтение из памяти параметра события */
/* ФУНКЦИЯ КЦЕЛ GetShortParamPIV     (ПУСТО); */
ФУНКЦИЯ ЦЕЛ  GetIntParamPIV       (ПУСТО);  /*чтение с экрана гуи - не исползуется */
ФУНКЦИЯ ДЦЕЛ GetLongParamPIV      (ПУСТО);  /*чтение с экрана гуи - не исползуется */

ФУНКЦИЯ ЦЕЛ  SendMsgGUICode       (ЦЕЛ);
ФУНКЦИЯ ЦЕЛ  SendMsgFloatParamGUI (ЦЕЛ, ПЛАВ);
/* ФУНКЦИЯ ЦЕЛ  SendMsgShortParamPIV (ЦЕЛ, КЦЕЛ); */
/* ФУНКЦИЯ ЦЕЛ  SendMsgIntParamPIV   (ЦЕЛ, ЦЕЛ); */
ФУНКЦИЯ ЦЕЛ  SendMsgLongParamGUI  (ЦЕЛ, ДЦЕЛ);

ФУНКЦИЯ ЦЕЛ  InitChannelStatus_PIV (ПУСТО); /* инициализация статуса,
обнуление числа ошибок */
ФУНКЦИЯ ЦЕЛ  GetChannelStatus_PIV  (ПУСТО);  /* чтение статуса канала 0/1 - не исползуется*/
ФУНКЦИЯ ЦЕЛ  GetErrorNumberCS_PIV  (ПУСТО);  /* чтение числа ошибок КС  - не исползуется*/
ФУНКЦИЯ ЦЕЛ  GetErrorNumberTO_PIV  (ПУСТО);  /* чтение числа ошибок по тайм ауту - не исползуется*/
ФУНКЦИЯ ЦЕЛ  GetInpMsgNumber_PIV (ПУСТО);  /* чтение числа сообщ. во входн. буфере - не исползуется*/
ФУНКЦИЯ ЦЕЛ  GetOutMsgNumber_PIV (ПУСТО); /* чтение числа сообщ. в выходн. буфере -  не исползуется*/

ФУНКЦИЯ ПЛАВ GetFP_P_BAK_COMPRESSOR		(ПУСТО);	/* чтение давления в баке компрессора */ 
ФУНКЦИЯ ПЛАВ GetFP_P_PATRUBKA			(ПУСТО);   /* чтение давления  в патрубке      */
ФУНКЦИЯ ПЛАВ GetFP_T_RUBACHKA			(ПУСТО);	/* чтение температуры воды в рубашке охлаждения */
ФУНКЦИЯ ПЛАВ GetFP_T_BAK				(ПУСТО);	/* чтение температуры воды в баке охлаждения */ 
ФУНКЦИЯ ПЛАВ GetFP_P_TRUBA				(ПУСТО);	/* чтение давления в трубе сидера */


/*###########################################################*/
/*########                                          #########*/
/*########  ОПИСАНИЕ РЕГИСТРОВ МОДУЛЕЙ УСО          #########*/
/*########  (устройство связи с объектом)           #########*/
/*###########################################################*/

ВХОД  ЛОГ_ВХОД_GPIO1  0 0 8; /* имя, базовый адрес, доп. адр. порта, 8бит */
ВХОД  ЛОГ_ВХОД_GPIO2  0 1 8; /* имя, базовый адрес, доп. адр. порта, 8бит */
ВХОД  ЛОГ_ВХОД_GPIO5  0 1 8; /* имя, базовый адрес, доп. адр. порта, 8бит */

/* ВХОД АЦП_T_Вбаке		 			0 66 16; */  /* регистр АЦП */
/*ВХОД АЦП_T_Врубашке	 			0 67 16; */ /* регистр АЦП */
/*ВХОД АЦП_P_ВтрубеСидер	 		0 68 16; */ /* регистр АЦП */
/*ВХОД АЦП_P_Впатрубке 				0 69 16; */ /*   регистр АЦП */
/*ВХОД АЦП_P_ВбакеКомпрессора 		0 70 16; */ /* регистр АЦП */


ВЫХОД  ЛОГ_ВЫХОД_GPIO3  1 2 8; /* имя, базовый адрес, доп. адр. порта, 8бит */
ВЫХОД  ЛОГ_ВЫХОД_GPIO4  1 3 8; /* имя, базовый адрес, доп. адр. порта, 8бит */


/*###########################################################*/
/*########                                          #########*/
/*########  ПРОЦЕССЫ                                #########*/
/*########                                          #########*/
/*###########################################################*/

/*
 * Процесс Инициализация. Служит для
 * развертывания программы. Этот процесс (описанный
 * первым) - единственно активный процесс по запуску.
 * Кроме этого, процесс Инициализация содержит описание 
 * переменных для ссылок из других
 * процессов. Это удобно: описания локализованы в одном
 * месте.
*/

ПРОЦ Инициализация{
/*===========================================================*/
/*=========== ОПИСАНИЕ ПЕРЕМЕННЫХ ===========================*/
/*===========================================================*/

/* ВХОДНЫЕ СИГНАЛЫ: */
/*-----------------
/*ЛОГ К_ВЕНТИЛЯТОР                    = {ЛОГ_ВХОД_GPIO1[1]} ДЛЯ ВСЕХ;  


-------------------*/
ЛОГ К_ОТКР_ЗАТВОР_ВАК             	= {ЛОГ_ВХОД_GPIO1[1]} ДЛЯ ВСЕХ; /*  концевик затвора вакуумного*/
ЛОГ К_ЗАКР_ЗАТВОР_ВАК             	= {ЛОГ_ВХОД_GPIO1[1]} ДЛЯ ВСЕХ; /*  концевик затвора вакуумного*/
ЛОГ К_САПУН                         = {ЛОГ_ВХОД_GPIO1[1]} ДЛЯ ВСЕХ;
ЛОГ К_УСТР_ПЛАВ_ПУСКА				= {ЛОГ_ВХОД_GPIO1[1]} ДЛЯ ВСЕХ;
ЛОГ К_ПИТ_ВАК_НАСОСА			 	= {ЛОГ_ВХОД_GPIO1[1]} ДЛЯ ВСЕХ; 
ЛОГ К_ВЕНТИЛЯТОР                    = {ЛОГ_ВХОД_GPIO1[1]} ДЛЯ ВСЕХ; 
ЛОГ К_ПИТ_РЕГ_Т 			        = {ЛОГ_ВХОД_GPIO1[1]} ДЛЯ ВСЕХ;
ЛОГ К_ПИТ_РЕГ_УР     				= {ЛОГ_ВХОД_GPIO1[1]} ДЛЯ ВСЕХ;

ЛОГ К_УР_ВОДЫ_MIN  					= {ЛОГ_ВХОД_GPIO2[1]} ДЛЯ ВСЕХ;
ЛОГ К_УР_ВОДЫ_MAX   				= {ЛОГ_ВХОД_GPIO2[1]} ДЛЯ ВСЕХ;
/* ЛОГ К_УР_ВОДЫ_АВАРИЯ				= {ЛОГ_ВХОД_GPIO2[1]} ДЛЯ ВСЕХ; */
ЛОГ К_НАСОС_ОХЛ 					= {ЛОГ_ВХОД_GPIO2[1]} ДЛЯ ВСЕХ;


/* ВЫХОДНЫЕ СИГНАЛЫ (т.к. привязаны к модулю выходов): */ 

ЛОГ У_ВКЛ_ПИТ_ВЕНТИЛЯТОР   			= {ЛОГ_ВЫХОД_GPIO3[1]} ДЛЯ ВСЕХ; 
ЛОГ У_ВЫК_ПИТ_ВЕНТИЛЯТОР  			= {ЛОГ_ВЫХОД_GPIO3[1]} ДЛЯ ВСЕХ;	
ЛОГ У_ВКЛ_ПИТ_РЕГ_Т      			= {ЛОГ_ВЫХОД_GPIO3[1]} ДЛЯ ВСЕХ;            
ЛОГ У_ВЫК_ПИТ_РЕГ_Т      			= {ЛОГ_ВЫХОД_GPIO3[1]} ДЛЯ ВСЕХ;  
ЛОГ У_ВКЛ_ПИТ_НАС_ОХЛ				= {ЛОГ_ВЫХОД_GPIO3[1]} ДЛЯ ВСЕХ; 
ЛОГ У_ВЫК_ПИТ_НАС_ОХЛ				= {ЛОГ_ВЫХОД_GPIO3[1]} ДЛЯ ВСЕХ; 	
ЛОГ У_ВКЛ_ПИТ_ВАК_НАСОСА       		= {ЛОГ_ВЫХОД_GPIO3[1]} ДЛЯ ВСЕХ;                    
ЛОГ У_ВЫК_ПИТ_ВАК_НАСОСА           	= {ЛОГ_ВЫХОД_GPIO3[1]} ДЛЯ ВСЕХ;   


ЛОГ У_ОТКР_ЗАТВОР_ВАК				= {ЛОГ_ВЫХОД_GPIO4[1]} ДЛЯ ВСЕХ; 
ЛОГ У_ОТКР_САПУН 					= {ЛОГ_ВЫХОД_GPIO4[1]} ДЛЯ ВСЕХ;
ЛОГ У_ВКЛ_УСТР_ПЛАВ_ПУСКА        	= {ЛОГ_ВЫХОД_GPIO4[1]} ДЛЯ ВСЕХ;                   
ЛОГ У_ВЫК_УСТР_ПЛАВ_ПУСКА      		= {ЛОГ_ВЫХОД_GPIO4[1]} ДЛЯ ВСЕХ;  
ЛОГ У_ВКЛ_ПИТ_РЕГ_УР      			= {ЛОГ_ВЫХОД_GPIO4[1]} ДЛЯ ВСЕХ; 
	
ЛОГ ОШИБКА_ФУНКЦИОНИРОВАНИЯ  		= {ЛОГ_ВЫХОД_GPIO4[1]} ДЛЯ ВСЕХ;  /*резерв */
/* предложение Саши Курочкина: ввести ошибку функционирования для контроля всех устройств, */
/* составляющих вакуумную подсистему. Если Саша не достучится до них, или две единички одновременно */
/* увидит у себя (ВКЛ/ВЫКЛ, например вентилятор). Правда, что в этом случае делать D2 - непонятно*/
/* Предлагается ошибку функционирования обнулять перед каждым запуском соответствующего процесса */ 
/* например: ВКЛ_ПИТ_ВЕНТИЛЯТОР, ВЫК_ПИТ_ВЕНТИЛЯТОР*/
/* И еще, для каждой ошибки функционирования будет int - код ошибки */
/* И еще, Саша говорит, что при обнаружении ош. функционирования надо прекращать вакуумирование ???*/

ЛОГ ОШИБКА_ФУНКЦ_ЗАТВОР_ВАК 			= {ЛОГ_ВХОД_GPIO5[1]} ДЛЯ ВСЕХ;
ЛОГ ОШИБКА_ФУНКЦ_САПУН 					= {ЛОГ_ВХОД_GPIO5[1]} ДЛЯ ВСЕХ;
ЛОГ ОШИБКА_ФУНКЦ_УСТР_ПЛАВ_ПУСКА 		= {ЛОГ_ВХОД_GPIO5[1]} ДЛЯ ВСЕХ;
ЛОГ ОШИБКА_ФУНКЦ_ПИТ_ВАК_НАСОСА 		= {ЛОГ_ВХОД_GPIO5[1]} ДЛЯ ВСЕХ;
ЛОГ ОШИБКА_ФУНКЦ_ВЕНТИЛЯТОР 			= {ЛОГ_ВХОД_GPIO5[1]} ДЛЯ ВСЕХ;
ЛОГ ОШИБКА_ФУНКЦ_ПИТ_РЕГ_Т 				= {ЛОГ_ВХОД_GPIO5[1]} ДЛЯ ВСЕХ;
ЛОГ ОШИБКА_ФУНКЦ_ПИТ_РЕГ_УР 			= {ЛОГ_ВХОД_GPIO5[1]} ДЛЯ ВСЕХ;
ЛОГ ОШИБКА_ФУНКЦ_НАСОС_ОХЛ 				= {ЛОГ_ВХОД_GPIO5[1]} ДЛЯ ВСЕХ;


/*===========================================================*/
/*=========== ЯЧЕЙКИ ИНФОРМАЦИОННЫХ КАНАЛОВ =================*/
/*===========================================================*/
ЦЕЛ КодСобытия_от_ГУИ        ДЛЯ ВСЕХ;
ЦЕЛ КодСобытия_к_ГУИ         ДЛЯ ВСЕХ;

  
  
/*===========================================================*/
/*=========== ЯЧЕЙКИ ОБЩЕГО УПРАВЛЕНИЯ ======================*/
/*===========================================================*/
ЛОГ ФлагГотовУрВодыБО ДЛЯ ВСЕХ;  /* Флаг установлен, если уровень воды в баке охлаждения в норме*/ 
ЛОГ ФлагГотовТемпВодыБО ДЛЯ ВСЕХ;  /* Флаг установлен, если  температура в баке охлаждения в норме*/ 
ЛОГ ФлагГотовTВодыРО ДЛЯ ВСЕХ;  /* Флаг установлен, если  температура в рубашке охлаждения в норме*/ 
ЛОГ ФлагГотовВакуумаВТ ДЛЯ ВСЕХ;
ЛОГ ФлагОткачкиТрубыСидеростата ДЛЯ ВСЕХ;
ЛОГ ФлагДавленияКомпрессора ДЛЯ ВСЕХ;

ДЦЕЛ ЗАДЕРЖКА_НА_РАЗГРУЗКУ_ТРАФИКА ДЛЯ ВСЕХ; /* чтобы в отмену не уходить */


/*===========================================================*/
/*=========== ФАКТИЧЕСКИЕ ПАРАМЕТРЫ =========================*/
/*===========================================================*/
/*ПЛАВ ФП_Pтрубы ДЛЯ ВСЕХ;*/
/* ????????????????????????????????????????????????????????????????? */
/*  Саша Курочкин будет ацепировать */
/*ЦЕЛ	ФП_P_ВбакеКомпрессора 	= {P_ВбакеКомпрессора [16]} 	ДЛЯ ВСЕХ; */
/*ЦЕЛ	ФП_P_Впатрубке	 		= {P_Впатрубке [16]}			ДЛЯ ВСЕХ; 	*/
/*ЦЕЛ	ФП_T_Врубашке 			= {T_Врубашке [16]}				ДЛЯ ВСЕХ; 	*/
/*ЦЕЛ	ФП_T_Вбаке 				= {T_Вбаке [16]}				ДЛЯ ВСЕХ;	*/ 
/*ЦЕЛ	ФП_P_ВтрубеСидер 		= {P_ВтрубеСидер [16]} 			ДЛЯ ВСЕХ; 	*/


ПЛАВ ФП_P_ВбакеКомпрессора ДЛЯ ВСЕХ; 
ПЛАВ ФП_P_Впатрубке ДЛЯ ВСЕХ;
ПЛАВ ФП_T_Врубашке ДЛЯ ВСЕХ;	
ПЛАВ ФП_T_Вбаке ДЛЯ ВСЕХ;

ПЛАВ ЗД_P_ВтрубеСидер ДЛЯ ВСЕХ;	
ПЛАВ ФП_P_ВтрубеСидер ДЛЯ ВСЕХ;


/*########  СОСТОЯНИЯ ПРОЦЕССА  #########*/
 СОСТ Начало{  /* NB: 1-е f-сост. 1-го процесс. - активно по включению питания */
    	ЗАДЕРЖКА_НА_РАЗГРУЗКУ_ТРАФИКА = 1; /* параметр регулирует частоту подачи сообщений в зависимости от заполненности буфера сообщений... */
     ОШИБКА_ФУНКЦИОНИРОВАНИЯ = ВЫК;
	ЗД_P_ВтрубеСидер = 0.02;   /* ????? Уточнить */
	
/* СТАРТ ПРОЦ ЧтениеАЦП; */
	СТАРТ ПРОЦ ЧтениеФактПараметров_с_Датчиков;
	СТАРТ ПРОЦ ЧтениеКольцевогоБуфера;
	СТАРТ ПРОЦ КонтрольИзменения_ФП_P_ВбакеКомпрессора;
	СТАРТ ПРОЦ КонтрольИзменения_ФП_P_Впатрубке;
	СТАРТ ПРОЦ КонтрольИзменения_ФП_T_Врубашке;
	СТАРТ ПРОЦ КонтрольИзменения_ФП_T_Вбаке;
	СТАРТ ПРОЦ КонтрольИзменения_ФП_P_ВтрубеСидер;

	ЕСЛИ (!К_ПИТ_РЕГ_Т) СТАРТ ПРОЦ ВКЛ_ПИТ_РЕГ_Т;   /* питание всегда включено. На всякий случай */
	
	СТАРТ ПРОЦ ВКЛ_ПИТ_РЕГ_УР;

	
	СТАРТ ПРОЦ КонтрольУровняВодыБакаОхлаждения;
	СТАРТ ПРОЦ КонтрольТемпературыРубашкиОхлаждения;
	СТАРТ ПРОЦ КонтрольДавленияВбакеКомпрессора;
	СТАРТ ПРОЦ КонтрольГотовностиВакуума_в_Вакуумной_Трубе;	
	СТАРТ ПРОЦ КонтрольТемпературыВодыБакаОхлаждения;	
/*	СТАРТ ПРОЦ КонтрольОткачкиВауумнойТрубыСидеростата;	*/
/*контроль откачки из вакуумной трубы запускаем в процессе ВКЛ_ВАКУУМИРОВАНИЕ_ТРУБЫ  */
/*  ВЫК_НАСОС_ВАКУУМНЫЙ*/
	СТОП;
   }
}

/*========================================================*/
/*                                                        */
/*==            ЧТЕНИЕ ФАКТИЧЕСКИХ ПАРАМЕТРОВ           ==*/
/*                                                        */
/*========================================================*/
ПРОЦ ЧтениеФактПараметров_с_Датчиков{
ИЗ ПРОЦ Инициализация ФП_P_ВбакеКомпрессора,
                      ФП_P_Впатрубке,
                      ФП_T_Врубашке,
                      ФП_T_Вбаке,
					  ФП_P_ВтрубеСидер;


 СОСТ Начало {
	ФП_P_ВбакеКомпрессора 	= GetFP_P_BAK_COMPRESSOR();     /* чтение давления в баке компрессора */
	ФП_P_Впатрубке 		= GetFP_P_PATRUBKA();     			/* чтение давления  в патрубке      */
	ФП_T_Врубашке 		= GetFP_T_RUBACHKA();     			/* чтение температуры воды в рубашке охлаждения */   
	ФП_T_Вбаке 			= GetFP_T_BAK();     				/* чтение температуры воды в баке охлаждения */ 
	ФП_P_ВтрубеСидер 	= GetFP_P_TRUBA();     				/* чтение давления в трубе сидера */ 
   В СОСТ Начало;
 }
}

/*==============================================*/
/*==============================================*/
/*=                                            =*/
/*=      ПРОЦЕСС ЧтениеКольцевогоБуфера        =*/
/*=                                            =*/
/*==============================================*/
/*==============================================*/
ПРОЦ ЧтениеКольцевогоБуфера{
ИЗ ПРОЦ Инициализация
 /*                        ТекущийРежим, ТекущийЭтап,  Т_НА_СРАБАТЫВАНИЕ_УСТРОЙСТВ_ГВС,*/
                        КодСобытия_от_ГУИ;       /* */

 СОСТ Начало{
    /*  читаем код и параметр события из кольцевого буфера в структуру */
    ЕСЛИ (GetNextMsgGUI()) {
      КодСобытия_от_ГУИ = GetMsgGUICode();
            ЕСЛИ ((КодСобытия_от_ГУИ >= ОТКР_ЗАТВОР_ВАК) &&
                  (КодСобытия_от_ГУИ <= КОМАНДЫ_ОТ_ГУИ)) {
				СТАРТ ПРОЦ ОбработкаКомандУправления_от_ГУИ;
				В СОСТ КонтрольВыполненияК;
             } 
			 ИНАЧЕ В СОСТ НекорректноеСобытие;



      
    } /* конец ЕСЛИ */
 }
 СОСТ КонтрольВыполненияК{
    ЕСЛИ(ПРОЦ ОбработкаКомандУправления_от_ГУИ В СОСТ ПАССИВНОЕ)
    В СОСТ Начало;
 }
  СОСТ НекорректноеСобытие{
     ЕСЛИ (SendMsgLongParamGUI(ПК_ГУИ_ОШИБКА_КОДА_КОМАНДЫ_ОТ_ГУИ, КодСобытия_от_ГУИ))
       В СОСТ Начало;
     ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
  /*===== НЕНОРМАЛЬНЫЕ СИТУАЦИИ === */
 СОСТ НарушениеСвязи {
     СТОП;   /* м.б. в начало?*/
 }

}

/*=========================================*/
/*=                                       =*/
/*=     ОБРАБОТКА КОМАНД ОТ ГУИ           =*/
/*=                                       =*/
/*=========================================*/
ПРОЦ ОбработкаКомандУправления_от_ГУИ{
ИЗ ПРОЦ Инициализация
						/*	ТекущийРежим,*/
                    КодСобытия_от_ГУИ,
					К_УСТР_ПЛАВ_ПУСКА,
					ФлагГотовУрВодыБО,
					ФП_T_Вбаке,
					К_ВЕНТИЛЯТОР,
					К_НАСОС_ОХЛ,
					К_ОТКР_ЗАТВОР_ВАК,
					К_ЗАКР_ЗАТВОР_ВАК,
					К_САПУН,
					ФлагГотовTВодыРО;
ЦЕЛ ЛП_КодСообщенияДляGUI ЛОКАЛ;

 СОСТ Анализ{
    РАЗБОР (КодСобытия_от_ГУИ) {
	    СЛУЧАЙ ОТКР_ЗАТВОР_ВАК:
			СТАРТ ПРОЦ ОТКР_ЗАТВОР_ВАК;
			КОНЕЦ;
	    СЛУЧАЙ ЗАКР_ЗАТВОР_ВАК:
			СТАРТ ПРОЦ ЗАКР_ЗАТВОР_ВАК;
			КОНЕЦ;		  
	    СЛУЧАЙ ОТКР_САПУН:
		  ЕСЛИ (К_УСТР_ПЛАВ_ПУСКА == ВЫК) { /* вакуумирование не включено*/
			СТАРТ ПРОЦ ОТКР_САПУН; 
		  } ИНАЧЕ {
		    ЛП_КодСообщенияДляGUI = ПК_ГУИ_ИДЕТ_ВАКУУМИРОВАНИЕ_ОТКАЗ_ВЫПОЛНЕНИЯ_КОМАНДЫ;
			В СОСТ ПередачаСообщенияСПараметромГУИ;
		  }
		  КОНЕЦ;
	    СЛУЧАЙ ЗАКР_САПУН:
		  ЕСЛИ (К_УСТР_ПЛАВ_ПУСКА == ВЫК) { /* вакуумирование не включено*/
			СТАРТ ПРОЦ ЗАКР_САПУН; 
		  } ИНАЧЕ {
		    ЛП_КодСообщенияДляGUI = ПК_ГУИ_ИДЕТ_ВАКУУМИРОВАНИЕ_ОТКАЗ_ВЫПОЛНЕНИЯ_КОМАНДЫ;
			В СОСТ ПередачаСообщенияСПараметромГУИ;
		  }
		  КОНЕЦ;

		СЛУЧАЙ ВКЛ_УСТР_ПЛАВ_ПУСКА:
			ЕСЛИ (К_УСТР_ПЛАВ_ПУСКА == ВЫК) { /* вакуумирование не включено*/
				ЕСЛИ ((ФлагГотовУрВодыБО == НОРМА) && (К_ВЕНТИЛЯТОР)
						&& (К_НАСОС_ОХЛ)&& (К_ЗАКР_ЗАТВОР_ВАК) && (К_САПУН)
						&& (ФлагГотовTВодыРО == НОРМА)) {   /* можно включать */
						СТАРТ ПРОЦ ВКЛ_УСТР_ПЛАВ_ПУСКА;
				} ИНАЧЕ {
					ЛП_КодСообщенияДляGUI = ПК_ГУИ_НЕТ_УСЛОВИЙ_ДЛЯ_ВЫПОЛНЕНИЯ_КОМАНДЫ;
					В СОСТ ПередачаСообщенияСПараметромГУИ;
				}
			} ИНАЧЕ {
				ЛП_КодСообщенияДляGUI = ПК_ГУИ_ИДЕТ_ВАКУУМИРОВАНИЕ_ОТКАЗ_ВЫПОЛНЕНИЯ_КОМАНДЫ;
				В СОСТ ПередачаСообщенияСПараметромГУИ;
			}			
			КОНЕЦ;
	
			
	    СЛУЧАЙ ВЫК_УСТР_ПЛАВ_ПУСКА:   /* вопрос: надо ли вообще выключать его. М.б. достаточно выкл питание????*/
			СТАРТ ПРОЦ ВЫК_УСТР_ПЛАВ_ПУСКА;
			КОНЕЦ;	
		СЛУЧАЙ ВКЛ_ПИТ_ВАК_НАСОСА:			
          СТАРТ ПРОЦ ВКЛ_ПИТ_ВАК_НАСОСА;
          КОНЕЦ;
	    СЛУЧАЙ ВЫК_ПИТ_ВАК_НАСОСА:
          СТАРТ ПРОЦ ВЫК_ПИТ_ВАК_НАСОСА;
          КОНЕЦ;			  
	    СЛУЧАЙ ВКЛ_ПИТ_ВЕНТИЛЯТОР:
		  ЕСЛИ (К_УСТР_ПЛАВ_ПУСКА == ВЫК) {  /* вакуумирование не включено*/
			СТАРТ ПРОЦ ВКЛ_ПИТ_ВЕНТИЛЯТОР; 
		  } ИНАЧЕ {
		    ЛП_КодСообщенияДляGUI = ПК_ГУИ_ИДЕТ_ВАКУУМИРОВАНИЕ_ОТКАЗ_ВЫПОЛНЕНИЯ_КОМАНДЫ;
			В СОСТ ПередачаСообщенияСПараметромГУИ;
			}
          КОНЕЦ;
	    СЛУЧАЙ ВЫК_ПИТ_ВЕНТИЛЯТОР:
          ЕСЛИ (К_УСТР_ПЛАВ_ПУСКА == ВЫК) { /* вакуумирование не включено*/
			СТАРТ ПРОЦ ВЫК_ПИТ_ВЕНТИЛЯТОР; 
		  } ИНАЧЕ {  /* идет вакуумирование*/
		    ЛП_КодСообщенияДляGUI = ПК_ГУИ_ИДЕТ_ВАКУУМИРОВАНИЕ_ОТКАЗ_ВЫПОЛНЕНИЯ_КОМАНДЫ;
			В СОСТ ПередачаСообщенияСПараметромГУИ;
		  } 
          КОНЕЦ;
	    СЛУЧАЙ ВКЛ_ПИТ_РЕГ_Т:
		  ЕСЛИ(К_УСТР_ПЛАВ_ПУСКА == ВЫК) {   /* вакуумирование не включено*/
		  
/*				ЕСЛИ ((ФлагГотовУрВодыБО = НОРМА) && (ФП_T_Вбаке > 0.0)) СТАРТ ПРОЦ ВКЛ_ПИТ_РЕГ_Т;  */

				ЕСЛИ (ФлагГотовУрВодыБО == НОРМА) {
					ЕСЛИ (ФП_T_Вбаке > 0.0) {
						СТАРТ ПРОЦ ВКЛ_ПИТ_РЕГ_Т;
					} ИНАЧЕ {
					 ЛП_КодСообщенияДляGUI = ПК_ГУИ_ОТРИЦАТЕЛЬНАЯ_ТЕМПЕРАТУРА_ВОДЫ_ОТКАЗ_ВЫПОЛНЕНИЯ_КОМАНДЫ;
					 В СОСТ ПередачаСообщенияСПараметромГУИ;
					}
				ЛП_КодСообщенияДляGUI = ПК_ГУИ_УРОВЕНЬ_ВОДЫ_В_БАКЕ_ОХЛАЖДЕНИЯ_ВНЕ_НОРМЫ_ОТКАЗ_ВЫПОЛНЕНИЯ_КОМАНДЫ;
				В СОСТ ПередачаСообщенияСПараметромГУИ;				
				}
				
		  } ИНАЧЕ {   /* идет вакуумирование*/
		    ЛП_КодСообщенияДляGUI = ПК_ГУИ_ИДЕТ_ВАКУУМИРОВАНИЕ_ОТКАЗ_ВЫПОЛНЕНИЯ_КОМАНДЫ;
			В СОСТ ПередачаСообщенияСПараметромГУИ;
		  }
		  КОНЕЦ;
	    СЛУЧАЙ ВЫК_ПИТ_РЕГ_Т:
		  ЕСЛИ(К_УСТР_ПЛАВ_ПУСКА == ВЫК) {   /* вакуумирование не включено*/
			СТАРТ ПРОЦ ВЫК_ПИТ_РЕГ_Т;
		  } ИНАЧЕ {   /* идет вакуумирование*/
		    ЛП_КодСообщенияДляGUI = ПК_ГУИ_ИДЕТ_ВАКУУМИРОВАНИЕ_ОТКАЗ_ВЫПОЛНЕНИЯ_КОМАНДЫ;
			В СОСТ ПередачаСообщенияСПараметромГУИ;
		  }
          КОНЕЦ;
	    СЛУЧАЙ ВКЛ_ПИТ_РЕГ_УР:
		  ЕСЛИ(К_УСТР_ПЛАВ_ПУСКА == ВЫК) {   /* вакуумирование не включено*/
		  	СТАРТ ПРОЦ ВКЛ_ПИТ_РЕГ_УР;
		  } ИНАЧЕ {   /* идет вакуумирование*/
		    ЛП_КодСообщенияДляGUI = ПК_ГУИ_ИДЕТ_ВАКУУМИРОВАНИЕ_ОТКАЗ_ВЫПОЛНЕНИЯ_КОМАНДЫ;
			В СОСТ ПередачаСообщенияСПараметромГУИ;
		  } 
          КОНЕЦ;
	    СЛУЧАЙ ВЫК_ПИТ_РЕГ_УР:
		  ЕСЛИ(К_УСТР_ПЛАВ_ПУСКА == ВЫК) {   /* вакуумирование не включено*/
		  	СТАРТ ПРОЦ ВЫК_ПИТ_РЕГ_УР;
		  } ИНАЧЕ {   /* идет вакуумирование*/
		    ЛП_КодСообщенияДляGUI = ПК_ГУИ_ИДЕТ_ВАКУУМИРОВАНИЕ_ОТКАЗ_ВЫПОЛНЕНИЯ_КОМАНДЫ;
			В СОСТ ПередачаСообщенияСПараметромГУИ;
		  } 
		  КОНЕЦ;
	    СЛУЧАЙ ВКЛ_ПИТ_НАС_ОХЛ:
		  ЕСЛИ(К_УСТР_ПЛАВ_ПУСКА == ВЫК) {   /* вакуумирование не включено*/
				ЕСЛИ ((ФлагГотовУрВодыБО = НОРМА) && (ФП_T_Вбаке > 0.0)) СТАРТ ПРОЦ ВКЛ_ПИТ_НАС_ОХЛ;
		  } ИНАЧЕ {   /* идет вакуумирование*/
		    ЛП_КодСообщенияДляGUI = ПК_ГУИ_ИДЕТ_ВАКУУМИРОВАНИЕ_ОТКАЗ_ВЫПОЛНЕНИЯ_КОМАНДЫ;
			В СОСТ ПередачаСообщенияСПараметромГУИ;
		  }
		  КОНЕЦ;
	    СЛУЧАЙ ВЫК_ПИТ_НАС_ОХЛ:
		  ЕСЛИ(К_УСТР_ПЛАВ_ПУСКА == ВЫК) {   /* вакуумирование не включено*/
		  	СТАРТ ПРОЦ ВЫК_ПИТ_НАС_ОХЛ;
		  } ИНАЧЕ {   /* идет вакуумирование*/
		    ЛП_КодСообщенияДляGUI = ПК_ГУИ_ИДЕТ_ВАКУУМИРОВАНИЕ_ОТКАЗ_ВЫПОЛНЕНИЯ_КОМАНДЫ;
			В СОСТ ПередачаСообщенияСПараметромГУИ;
		  } 		
		  КОНЕЦ;
		  
/*	    СЛУЧАЙ ВКЛ_НАСОС_ВАКУУМНЫЙ:   */
/*          СТАРТ ПРОЦ ВКЛ_НАСОС_ВАКУУМНЫЙ; */
/*          КОНЕЦ;  */
/*	    СЛУЧАЙ ВЫК_НАСОС_ВАКУУМНЫЙ:  */
/*          СТАРТ ПРОЦ ВЫК_НАСОС_ВАКУУМНЫЙ; */
/*          КОНЕЦ;  */

		  СЛУЧАЙ ВКЛ_ВАКУУМИРОВАНИЕ_ТРУБЫ:   /* достигает вакуума и выключает вак. насос и через 30 минут снова включает, если вакуума недостаточно */
          СТАРТ ПРОЦ ВКЛ_ВАКУУМИРОВАНИЕ_ТРУБЫ;
          КОНЕЦ;
	    СЛУЧАЙ ВЫК_ВАКУУМИРОВАНИЕ_ТРУБЫ:
          СТАРТ ПРОЦ ВЫК_ВАКУУМИРОВАНИЕ_ТРУБЫ;
          КОНЕЦ;
		УМОЛЧАНИЕ:
          В СОСТ НекорректноеСобытие;
          КОНЕЦ;
    } /* конец разбора */
 }


 /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ НекорректноеСобытие {
    ЕСЛИ (SendMsgLongParamGUI(ПК_ГУИ_ОШИБКА_КОДА_КОМАНДЫ_ОТ_ГУИ, КодСобытия_от_ГУИ))
         СТОП;   
    ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
 СОСТ ПередачаСообщенияСПараметромГУИ{
    ЕСЛИ (SendMsgLongParamGUI(ЛП_КодСообщенияДляGUI, КодСобытия_от_ГУИ))
        СТОП;
    ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }

 СОСТ НарушениеСвязи {
    ОШИБКА;
 }
} /* конец ПРОЦ ОбработкаКомандУправленияРегуляторами_от_ПИВ   */

/*====================================================*/
/*					ПРОЦЕСС КОНТРОЛЯ				  */
/*==           изменения ФП_P_ВбакеКомпрессора        */
/*====================================================*/
ПРОЦ КонтрольИзменения_ФП_P_ВбакеКомпрессора{
 ИЗ ПРОЦ Инициализация  ФП_P_ВбакеКомпрессора, ЗАДЕРЖКА_НА_РАЗГРУЗКУ_ТРАФИКА;
 ПЛАВ ЛП_Предыдущее ЛОКАЛ;
 СОСТ Начало{
    ЛП_Предыдущее = (!ФП_P_ВбакеКомпрессора);
    В СЛЕДУЮЩЕЕ;
 }
 СОСТ КонтрольИзменения{
    ЕСЛИ (ЛП_Предыдущее != ФП_P_ВбакеКомпрессора) {
         ЛП_Предыдущее = ФП_P_ВбакеКомпрессора;
         В СЛЕДУЮЩЕЕ;
    }
 }
 СОСТ СообщОбИзменении{
     ЕСЛИ (SendMsgFloatParamGUI(ПК_ГУИ_ФП_P_ВбакеКомпрессора, ЛП_Предыдущее))
         В СОСТ ТрафикПауза;
     ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
 СОСТ ТрафикПауза{
    ТАЙМАУТ ЗАДЕРЖКА_НА_РАЗГРУЗКУ_ТРАФИКА В СОСТ КонтрольИзменения;

 }
/* НЕНОРМАЛЬНЫЕ СИТУАЦИИ */
 СОСТ НарушениеСвязи{
     В СОСТ КонтрольИзменения;
 }
}
/* ПРОЦ КонтрольИзменения_ФП_P_ВбакеКомпрессора  */

/*====================================================*/
/*					ПРОЦЕСС КОНТРОЛЯ				  */
/*==           изменения ИЗМН_ФП_P_Впатрубке     */
/*====================================================*/
ПРОЦ КонтрольИзменения_ФП_P_Впатрубке{
 ИЗ ПРОЦ Инициализация  ФП_P_Впатрубке, ЗАДЕРЖКА_НА_РАЗГРУЗКУ_ТРАФИКА;
 ПЛАВ ЛП_Предыдущее ЛОКАЛ;
 СОСТ Начало{
    ЛП_Предыдущее = (!ФП_P_Впатрубке);
    В СЛЕДУЮЩЕЕ;
 }
 СОСТ КонтрольИзменения{
    ЕСЛИ (ЛП_Предыдущее != ФП_P_Впатрубке) {
         ЛП_Предыдущее = ФП_P_Впатрубке;
         В СЛЕДУЮЩЕЕ;
    }
 }
 СОСТ СообщОбИзменении{
     ЕСЛИ (SendMsgFloatParamGUI(ПК_ГУИ_ФП_P_Впатрубке, ЛП_Предыдущее))
         В СОСТ ТрафикПауза;
     ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
 СОСТ ТрафикПауза{
    ТАЙМАУТ ЗАДЕРЖКА_НА_РАЗГРУЗКУ_ТРАФИКА В СОСТ КонтрольИзменения;

 }
/* НЕНОРМАЛЬНЫЕ СИТУАЦИИ */
 СОСТ НарушениеСвязи{
     В СОСТ КонтрольИзменения;
 }
}
/* ПРОЦ КонтрольИзменения_ФП_P_Впатрубке  */

/*====================================================*/
/*					ПРОЦЕСС КОНТРОЛЯ				  */
/*==           изменения ИЗМН_ФП_T_Врубашке     */
/*====================================================*/
ПРОЦ КонтрольИзменения_ФП_T_Врубашке{
 ИЗ ПРОЦ Инициализация  ФП_T_Врубашке, ЗАДЕРЖКА_НА_РАЗГРУЗКУ_ТРАФИКА;
 ПЛАВ ЛП_Предыдущее ЛОКАЛ;
 СОСТ Начало{
    ЛП_Предыдущее = (!ФП_T_Врубашке);
    В СЛЕДУЮЩЕЕ;
 }
 СОСТ КонтрольИзменения{
    ЕСЛИ (ЛП_Предыдущее != ФП_T_Врубашке) {
         ЛП_Предыдущее = ФП_T_Врубашке;
         В СЛЕДУЮЩЕЕ;
    }
 }
 СОСТ СообщОбИзменении{
     ЕСЛИ (SendMsgFloatParamGUI(ПК_ГУИ_ФП_T_Врубашке, ЛП_Предыдущее))
         В СОСТ ТрафикПауза;
     ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
 СОСТ ТрафикПауза{
    ТАЙМАУТ ЗАДЕРЖКА_НА_РАЗГРУЗКУ_ТРАФИКА В СОСТ КонтрольИзменения;

 }
/* НЕНОРМАЛЬНЫЕ СИТУАЦИИ */
 СОСТ НарушениеСвязи{
     В СОСТ КонтрольИзменения;
 }
}
/* ПРОЦ КонтрольИзменения_ФП_T_Врубашке  */

/*====================================================*/
/*					ПРОЦЕСС КОНТРОЛЯ				  */
/*==           изменения ИЗМН_ФП_T_Вбаке     */
/*====================================================*/
ПРОЦ КонтрольИзменения_ФП_T_Вбаке{
 ИЗ ПРОЦ Инициализация  ФП_T_Вбаке, ЗАДЕРЖКА_НА_РАЗГРУЗКУ_ТРАФИКА;
 ПЛАВ ЛП_Предыдущее ЛОКАЛ;
 СОСТ Начало{
    ЛП_Предыдущее = (!ФП_T_Вбаке);
    В СЛЕДУЮЩЕЕ;
 }
 СОСТ КонтрольИзменения{
    ЕСЛИ (ЛП_Предыдущее != ФП_T_Вбаке) {
         ЛП_Предыдущее = ФП_T_Вбаке;
         В СЛЕДУЮЩЕЕ;
    }
 }
 СОСТ СообщОбИзменении{
     ЕСЛИ (SendMsgFloatParamGUI(ПК_ГУИ_ФП_T_Вбаке, ЛП_Предыдущее))
         В СОСТ ТрафикПауза;
     ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
 СОСТ ТрафикПауза{
    ТАЙМАУТ ЗАДЕРЖКА_НА_РАЗГРУЗКУ_ТРАФИКА В СОСТ КонтрольИзменения;

 }
/* НЕНОРМАЛЬНЫЕ СИТУАЦИИ */
 СОСТ НарушениеСвязи{
     В СОСТ КонтрольИзменения;
 }
}
/* ПРОЦ КонтрольИзменения_ФП_T_Вбаке  */

/*====================================================*/
/*					ПРОЦЕСС КОНТРОЛЯ				  */
/*==           изменения ИЗМН_ФП_P_ВтрубеСидер     */
/*====================================================*/	
 ПРОЦ КонтрольИзменения_ФП_P_ВтрубеСидер{
 ИЗ ПРОЦ Инициализация  ФП_P_ВтрубеСидер, ЗАДЕРЖКА_НА_РАЗГРУЗКУ_ТРАФИКА;
 ПЛАВ ЛП_Предыдущее ЛОКАЛ;
 СОСТ Начало{
    ЛП_Предыдущее = (!ФП_P_ВтрубеСидер);
    В СЛЕДУЮЩЕЕ;
 }
 СОСТ КонтрольИзменения{
    ЕСЛИ (ЛП_Предыдущее != ФП_P_ВтрубеСидер) {
         ЛП_Предыдущее = ФП_P_ВтрубеСидер;
         В СЛЕДУЮЩЕЕ;
    }
 }
 СОСТ СообщОбИзменении{
     ЕСЛИ (SendMsgFloatParamGUI(ПК_ГУИ_ФП_P_ВтрубеСидер, ЛП_Предыдущее))
         В СОСТ ТрафикПауза;
     ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
 СОСТ ТрафикПауза{
    ТАЙМАУТ ЗАДЕРЖКА_НА_РАЗГРУЗКУ_ТРАФИКА В СОСТ КонтрольИзменения;

 }
/* НЕНОРМАЛЬНЫЕ СИТУАЦИИ */
 СОСТ НарушениеСвязи{
     В СОСТ КонтрольИзменения;
 }
}
/* ПРОЦ КонтрольИзменения_ФП_P_ВтрубеСидер  */   


/*====================================================*/
/*					ПРОЦЕСС КОНТРОЛЯ				  */
/*== 		Контроль Откачки в Трубе сидеростата 	==*/
/*====================================================*/
ПРОЦ КонтрольОткачкиВауумнойТрубыСидеростата{
ИЗ ПРОЦ Инициализация 	ФлагОткачкиТрубыСидеростата,
						ФП_P_ВтрубеСидер;
ПЛАВ ЛП_Предыдущее ЛОКАЛ;
					
 СОСТ Начало{
	ЛП_Предыдущее = ФП_P_ВтрубеСидер;
	ТАЙМАУТ ТАЙМАУТ_ЗАДЕРЖКА_НА_ОТКАЧКУ В СЛЕДУЮЩЕЕ;
 }
 СОСТ АнализОткачки{
	ЕСЛИ ((ФП_P_ВтрубеСидер - ЛП_Предыдущее) < -0.05) {    /*  величина откачки  ?????*/
        ЕСЛИ (SendMsgGUICode(СКОРОСТЬ_ОТКАЧКИ_В_НОРМЕ)) {
					ФлагОткачкиТрубыСидеростата = НОРМА;
					В СОСТ Начало;
		} 
    } ИНАЧЕ{
	ЕСЛИ (SendMsgGUICode(СКОРОСТЬ_ОТКАЧКИ_НИЖЕ_НОРМЫ)) {
					ФлагОткачкиТрубыСидеростата = ВНЕ_НОРМЫ;
					В СОСТ Начало;
		} 
	}
 }
}


/*====================================================*/
/*					ПРОЦЕСС КОНТРОЛЯ				  */
/*== Контроль Готовности Вакуума в Вакуумной Трубе  ==*/
/*====================================================*/

ПРОЦ КонтрольГотовностиВакуума_в_Вакуумной_Трубе{
ИЗ ПРОЦ Инициализация  	ФП_P_ВтрубеСидер,
						ЗД_P_ВтрубеСидер,
						ФлагГотовВакуумаВТ;
СОСТ Начало{
   ЕСЛИ (ФП_P_ВтрубеСидер <= ЗД_P_ВтрубеСидер) {
         ЕСЛИ (SendMsgGUICode(ДАВЛЕНИЕ_В_ВАКУУМНОЙ_ТРУБЕ_В_НОРМЕ)) {
					ФлагГотовВакуумаВТ = НОРМА;
					В СОСТ ДавлениеНормальное;
					}
    
    } ИНАЧЕ {
                ЕСЛИ (SendMsgGUICode(ДАВЛЕНИЕ_В_ВАКУУМНОЙ_ТРУБЕ_ВНЕ_НОРМЫ)) {
					ФлагГотовВакуумаВТ = ВНЕ_НОРМЫ;
				   В СОСТ ДавлениеВысокое;
				}
    }
    ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }

СОСТ ДавлениеНормальное{
       ЕСЛИ (ФП_P_ВтрубеСидер > 0.025)  В СОСТ Начало;
 } 
СОСТ ДавлениеВысокое{
       ЕСЛИ (ФП_P_ВтрубеСидер < 0.01)  В СОСТ Начало;
 }  
 
СОСТ Задержка{
      ТАЙМАУТ ОДНА_МИНУТА В СОСТ Начало;
 }
/* НЕНОРМАЛЬНЫЕ СИТУАЦИИ */
 СОСТ НарушениеСвязи{
     В СОСТ Начало;
 }
}

/* конец ПРОЦ КонтрольГотовностиВакуума_в_Вакуумной Трубе

/*====================================================*/
/*					ПРОЦЕСС КОНТРОЛЯ				  */
/*==           Уровень воды в баке охлаждения       ==*/
/*====================================================*/
ПРОЦ КонтрольУровняВодыБакаОхлаждения{
 ИЗ ПРОЦ Инициализация
                        К_УР_ВОДЫ_MIN,
                        К_УР_ВОДЫ_MAX,
						ФлагГотовУрВодыБО;
						
 СОСТ Начало{
     ЕСЛИ (К_УР_ВОДЫ_MIN) {
         ЕСЛИ (SendMsgGUICode(УРОВЕНЬ_ВОДЫ_В_БАКЕ_ОХЛАЖДЕНИЯ_НИЗКИЙ)) {
					ФлагГотовУрВодыБО = ВНЕ_НОРМЫ;
					В СОСТ НизкийУровеньВоды;
					}
    } ИНАЧЕ ЕСЛИ (К_УР_ВОДЫ_MAX) {
              ЕСЛИ (SendMsgGUICode(УРОВЕНЬ_ВОДЫ_В_БАКЕ_ОХЛАЖДЕНИЯ_ВЫСОКИЙ)) {
			   ФлагГотовУрВодыБО = ВНЕ_НОРМЫ;
			   В СОСТ ВысокийУровеньВоды;
			   }
    } ИНАЧЕ {
                ЕСЛИ (SendMsgGUICode(УРОВЕНЬ_ВОДЫ_В_БАКЕ_ОХЛАЖДЕНИЯ_В_НОРМЕ)) {
					ФлагГотовУрВодыБО = НОРМА;
				   В СОСТ УровеньВодыВнорме;
				}
         }
    ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }

 СОСТ НизкийУровеньВоды{
        ЕСЛИ (!К_УР_ВОДЫ_MIN)  В СОСТ Задержка;
 }

 СОСТ ВысокийУровеньВоды{
       ЕСЛИ (!К_УР_ВОДЫ_MAX)  В СОСТ Задержка;
 }
 СОСТ УровеньВодыВнорме{
	   ЕСЛИ (К_УР_ВОДЫ_MIN && К_УР_ВОДЫ_MAX)  В СОСТ Задержка;
 } 
 
СОСТ Задержка{
      ТАЙМАУТ ОДНА_МИНУТА В СОСТ Начало;
 }
 
/*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ НарушениеСвязи {
    В СОСТ Начало;
 }
}   /*   Конец ПРОЦ КонтрольУровняВодыБакаОхлаждения */


/*====================================================*/
/*					ПРОЦЕСС КОНТРОЛЯ				  */
/*==       Температуры воды в баке охлаждения       ==*/
/*====================================================*/
ПРОЦ КонтрольТемпературыВодыБакаОхлаждения{
 ИЗ ПРОЦ Инициализация 	
						ФП_T_Вбаке,	
                        ФлагГотовТемпВодыБО;
						
 СОСТ Начало{
    ЕСЛИ (ФП_T_Вбаке < 50) {
         ЕСЛИ (SendMsgGUICode(ТЕМПЕРАТУРА_ВОДЫ_В_БАКЕ_ОХЛАЖДЕНИЯ_НИЗКАЯ)) {
					ФлагГотовТемпВодыБО = ВНЕ_НОРМЫ;
					В СОСТ НизкаяТемператураВоды;
					}
    } ИНАЧЕ ЕСЛИ (ФП_T_Вбаке > 60) {
              ЕСЛИ (SendMsgGUICode(ТЕМПЕРАТУРА_ВОДЫ_В_БАКЕ_ОХЛАЖДЕНИЯ_ВЫСОКАЯ)) {
			   ФлагГотовТемпВодыБО = ВНЕ_НОРМЫ;
			   В СОСТ ВысокаяТемператураВоды;
			   }
    } ИНАЧЕ {
                ЕСЛИ (SendMsgGUICode(ТЕМПЕРАТУРА_ВОДЫ_В_БАКЕ_ОХЛАЖДЕНИЯ_В_НОРМЕ)) {
					ФлагГотовТемпВодыБО = НОРМА;
				   В СОСТ ТемператураВнорме;
				}
         }
    ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }

 СОСТ НизкаяТемператураВоды{
        ЕСЛИ (ФП_T_Вбаке > (50.0 + Т_ГИСТЕРЕЗИС_ВОДЫ_В_БАКЕ))  В СОСТ Начало;
 }

 СОСТ ВысокаяТемператураВоды{
       ЕСЛИ (ФП_T_Вбаке < (60.0 - Т_ГИСТЕРЕЗИС_ВОДЫ_В_БАКЕ))  В СОСТ Начало;
 }
  СОСТ ТемператураВнорме{
       ЕСЛИ ((ФП_T_Вбаке < (50.0 - Т_ГИСТЕРЕЗИС_ВОДЫ_В_БАКЕ)) && 
	         (ФП_T_Вбаке > (60.0 + Т_ГИСТЕРЕЗИС_ВОДЫ_В_БАКЕ)))  В СОСТ Начало;
 } 
СОСТ Задержка{

      ТАЙМАУТ ОДНА_МИНУТА В СОСТ Начало;
 }
 
/*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ НарушениеСвязи {
    В СОСТ Начало;
 }
}   /*   Конец ПРОЦ КонтрольТемпературыВодыБакаОхлаждения */


/*====================================================*/
/*					ПРОЦЕСС КОНТРОЛЯ				  */
/*==       Температуры воды в рубашке охлаждения    ==*/
/*====================================================*/
ПРОЦ КонтрольТемпературыРубашкиОхлаждения{
 ИЗ ПРОЦ Инициализация 	
						ФП_T_Врубашке,	
                        ФлагГотовTВодыРО;
						
 СОСТ Начало{
    ЕСЛИ (ФП_T_Врубашке < 40) {
         ЕСЛИ (SendMsgGUICode(ТЕМПЕРАТУРА_ВОДЫ_В_РУБАШКЕ_ОХЛАЖДЕНИЯ_НИЗКАЯ)) {
					ФлагГотовTВодыРО = ВНЕ_НОРМЫ;
					В СОСТ НизкаяТемператураВоды;
					}
    } ИНАЧЕ ЕСЛИ (ФП_T_Врубашке > 85) {
              ЕСЛИ (SendMsgGUICode(ТЕМПЕРАТУРА_ВОДЫ_В_РУБАШКЕ_ОХЛАЖДЕНИЯ_ВЫСОКАЯ)) {
			   ФлагГотовTВодыРО = ВНЕ_НОРМЫ;
			   В СОСТ ВысокаяТемператураВоды;
			   }
    } ИНАЧЕ {
                ЕСЛИ (SendMsgGUICode(ТЕМПЕРАТУРА_ВОДЫ_В_РУБАШКЕ_ОХЛАЖДЕНИЯ_В_НОРМЕ)) {
					ФлагГотовTВодыРО = НОРМА;
				   В СОСТ ТемператураВнорме;
				}
         }
    ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }

 СОСТ НизкаяТемператураВоды{
       
		ЕСЛИ (ФП_T_Врубашке > (40.0 + Т_ГИСТЕРЕЗИС_ВОДЫ_В_РУБАХЕ))  В СОСТ Начало;
 }

 СОСТ ВысокаяТемператураВоды{
        ЕСЛИ (ФП_T_Врубашке < (85.0 - Т_ГИСТЕРЕЗИС_ВОДЫ_В_РУБАХЕ))  В СОСТ Начало;
 }
 

 СОСТ ТемператураВнорме{
        ЕСЛИ ((ФП_T_Врубашке < (40.0 - Т_ГИСТЕРЕЗИС_ВОДЫ_В_РУБАХЕ)) && 
	         (ФП_T_Врубашке > (85.0 + Т_ГИСТЕРЕЗИС_ВОДЫ_В_РУБАХЕ)))  В СОСТ Начало;
 } 
СОСТ Задержка{
      ТАЙМАУТ ОДНА_МИНУТА В СОСТ Начало;
 }
 
/*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ НарушениеСвязи {
    В СОСТ Начало;
 }
}   /*   Конец ПРОЦ КонтрольТемпературыВодыВрубашкеОхлаждения */


/*====================================================*/
/*					ПРОЦЕСС КОНТРОЛЯ				  */
/*== 		Контроль давления в баке компрессора ??? 	==*/
/*====================================================*/
ПРОЦ КонтрольДавленияВбакеКомпрессора{
ИЗ ПРОЦ Инициализация  	ФП_P_ВбакеКомпрессора,
					ФлагДавленияКомпрессора;
СОСТ Начало{
   ЕСЛИ (ФП_P_ВбакеКомпрессора > ЗД_P_ВбакеКомпрессора) {
         ЕСЛИ (SendMsgGUICode(ДАВЛЕНИЕ_В_БАКЕ_КОМПРЕССОРА_В_НОРМЕ)) {
					ФлагДавленияКомпрессора = НОРМА;
					В СОСТ ДавлениеНормальное;
					}
    
    } ИНАЧЕ {
                ЕСЛИ (SendMsgGUICode(ДАВЛЕНИЕ_В_БАКЕ_КОМПРЕССОРА_НИЖЕ_НОРМЫ)) {
					ФлагДавленияКомпрессора = ВНЕ_НОРМЫ;
				   В СОСТ ДавлениеНизкое;
				}
    }
    ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }

СОСТ ДавлениеНормальное{
       ЕСЛИ (ФП_P_ВбакеКомпрессора < 9.5)  В СОСТ Начало;
 } 
СОСТ ДавлениеНизкое{
       ЕСЛИ (ФП_P_ВбакеКомпрессора < 10.5)  В СОСТ Начало;
 }  
 
СОСТ Задержка{
      ТАЙМАУТ ОДНА_МИНУТА В СОСТ Начало;
 }
/* НЕНОРМАЛЬНЫЕ СИТУАЦИИ */
 СОСТ НарушениеСвязи{
     В СОСТ Начало;
 }
}


/*=========================================*/
/*=                                       =*/
/*=     ПРОЦЕССЫ  УПРАВЛЕНИЯ ВАКУУМНЫМ    =*/
/*=          ОБОРУДОВАНИЕМ                =*/
/*=========================================*/
/*=======================================================*/
/*=====   Процесс подачи питания на вентилятор		=====*/
/*=======================================================*/
ПРОЦ ВКЛ_ПИТ_ВЕНТИЛЯТОР{
 ИЗ ПРОЦ Инициализация 	К_ВЕНТИЛЯТОР,
						У_ВКЛ_ПИТ_ВЕНТИЛЯТОР,
						ОШИБКА_ФУНКЦ_ВЕНТИЛЯТОР;
					  
 СОСТ Начало{
  ОШИБКА_ФУНКЦ_ВЕНТИЛЯТОР = НЕТ;
  У_ВКЛ_ПИТ_ВЕНТИЛЯТОР = ВКЛ;
  ЕСЛИ (ПРОЦ ВЫК_ПИТ_ВЕНТИЛЯТОР В СОСТ АКТИВНОЕ) ОШИБКА ПРОЦ ВЫК_ПИТ_ВЕНТИЛЯТОР;
  ИНАЧЕ В СЛЕДУЮЩЕЕ;      
 }
 
 СОСТ КонтрольВключения{
  ЕСЛИ (К_ВЕНТИЛЯТОР == ВКЛ) СТОП;
  ТАЙМАУТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_ВЕНТИЛЯТОРА В СОСТ Ошибка;
   }
 /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ Ошибка {
       ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ОТКАЗ_ВКЛЮЧЕНИЯ_ВЕНТИЛЯТОРА))	ОШИБКА;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
  СОСТ НарушениеСвязи {
	   ОШИБКА;
 }   
} /*конец процесса ВКЛ_ПИТ_ВЕНТИЛЯТОР  */

/*=======================================================*/
/*=====   Процесс отключения питания вентилятора    =====*/
/*=======================================================*/
ПРОЦ ВЫК_ПИТ_ВЕНТИЛЯТОР{
 ИЗ ПРОЦ Инициализация 	К_ВЕНТИЛЯТОР,
						У_ВКЛ_ПИТ_ВЕНТИЛЯТОР, 
						ОШИБКА_ФУНКЦ_ВЕНТИЛЯТОР;
 СОСТ Начало{
  ОШИБКА_ФУНКЦ_ВЕНТИЛЯТОР = НЕТ;
  У_ВКЛ_ПИТ_ВЕНТИЛЯТОР = ВЫК;
  ЕСЛИ (ПРОЦ ВКЛ_ПИТ_ВЕНТИЛЯТОР В СОСТ АКТИВНОЕ) ОШИБКА ПРОЦ ВКЛ_ПИТ_ВЕНТИЛЯТОР;
  ИНАЧЕ В СЛЕДУЮЩЕЕ;      
 }
 
 СОСТ КонтрольВыключения{
  ЕСЛИ (К_ВЕНТИЛЯТОР == ВЫК) СТОП;
  ТАЙМАУТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_ВЕНТИЛЯТОРА В СОСТ Ошибка;
  }
    /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ Ошибка {
       ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ОТКАЗ_ВЫКЛЮЧЕНИЯ_ВЕНТИЛЯТОРА))	ОШИБКА;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
  СОСТ НарушениеСвязи {
	   ОШИБКА;
 }   
} /*конец процесса ВЫК_ПИТ_ВЕНТИЛЯТОРА  */					  


/*===========================================================*/
/*=====   Процесс открытия затвора вакуумного         =======*/
/*===========================================================*/
ПРОЦ ОТКР_ЗАТВОР_ВАК{
 ИЗ ПРОЦ Инициализация 	К_ОТКР_ЗАТВОР_ВАК,
						К_ЗАКР_ЗАТВОР_ВАК,
						У_ОТКР_ЗАТВОР_ВАК,
						ОШИБКА_ФУНКЦ_ЗАТВОР_ВАК;
 СОСТ Начало{
  ОШИБКА_ФУНКЦ_ЗАТВОР_ВАК = НЕТ;
  У_ОТКР_ЗАТВОР_ВАК = ОТКР;
  ЕСЛИ (ПРОЦ ЗАКР_ЗАТВОР_ВАК В СОСТ АКТИВНОЕ) ОШИБКА ПРОЦ ЗАКР_ЗАТВОР_ВАК;
  ЕСЛИ ((К_ОТКР_ЗАТВОР_ВАК == ВКЛ) && (К_ЗАКР_ЗАТВОР_ВАК = ВЫК)) СТОП;
  ИНАЧЕ В СЛЕДУЮЩЕЕ;      
 }

 СОСТ Контроль{  
  ЕСЛИ ((К_ОТКР_ЗАТВОР_ВАК == ВКЛ) && (К_ЗАКР_ЗАТВОР_ВАК = ВЫК)) СТОП;
  ТАЙМАУТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_ЗАТ_ВАК В СОСТ Ошибка;
 } 
 /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ Ошибка {
       ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ОТКАЗ_ОТКРЫТИЯ_ЗАТВОРА_ВАКУУМНОГО))	ОШИБКА;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
  СОСТ НарушениеСвязи {
	   ОШИБКА;
 }
}   /*конец процесса ОТКР_ЗАТВОР_ВАК */
/*===========================================================*/
/*=====   Процесс закрытия затвора вакуумного         =======*/
/*===========================================================*/
ПРОЦ ЗАКР_ЗАТВОР_ВАК{
 ИЗ ПРОЦ Инициализация 	К_ОТКР_ЗАТВОР_ВАК,
						К_ЗАКР_ЗАТВОР_ВАК,
						У_ОТКР_ЗАТВОР_ВАК,
						ОШИБКА_ФУНКЦ_ЗАТВОР_ВАК;

 СОСТ Начало{
  ОШИБКА_ФУНКЦ_ЗАТВОР_ВАК = НЕТ;
  У_ОТКР_ЗАТВОР_ВАК = ЗАКР;
  ЕСЛИ (ПРОЦ ОТКР_ЗАТВОР_ВАК В СОСТ АКТИВНОЕ) ОШИБКА ПРОЦ ОТКР_ЗАТВОР_ВАК;
  ЕСЛИ ((К_ОТКР_ЗАТВОР_ВАК == ВЫК) && (К_ЗАКР_ЗАТВОР_ВАК = ВКЛ)) СТОП;
  ИНАЧЕ В СЛЕДУЮЩЕЕ;      
 }
 СОСТ Контроль{  
	ЕСЛИ ((К_ОТКР_ЗАТВОР_ВАК == ВЫК) && (К_ЗАКР_ЗАТВОР_ВАК = ВКЛ)) СТОП;
	ТАЙМАУТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_ЗАТ_ВАК В СОСТ Ошибка;
  }
 /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ Ошибка {
    ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ОТКАЗ_ЗАКРЫТИЯ_ЗАТВОРА_ВАКУУМНОГО))	ОШИБКА;
    ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
  СОСТ НарушениеСвязи {
	   ОШИБКА;
 }
} /*конец процесса ЗАКР_ЗАТВОР_ВАК */



/*===========================================================*/
/*=====   Процесс открытия сапуна                    =======*/
/*===========================================================*/
ПРОЦ ОТКР_САПУН{
 ИЗ ПРОЦ Инициализация 	К_САПУН, 
						У_ОТКР_САПУН,
						ОШИБКА_ФУНКЦ_САПУН; 	
 СОСТ Начало{
  ОШИБКА_ФУНКЦ_САПУН = НЕТ;
  У_ОТКР_САПУН = ОТКР;
  ЕСЛИ (ПРОЦ ЗАКР_САПУН В СОСТ АКТИВНОЕ) ОШИБКА ПРОЦ ЗАКР_САПУН;
/*  ЕСЛИ ((К_САПУН == ОТКР) && (ОШИБКА_ФУНКЦ_САПУН == НЕТ)) СТОП;  */
  ЕСЛИ (К_САПУН == ОТКР)  СТОП;
  ИНАЧЕ В СЛЕДУЮЩЕЕ;      
 }

 СОСТ КонтрольКонцевика{  
/*  ЕСЛИ ((К_САПУН == ОТКР) && (ОШИБКА_ФУНКЦ_САПУН == НЕТ)) СТОП; */
    ЕСЛИ (К_САПУН == ОТКР) СТОП;
  ТАЙМАУТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_САПУНА В СОСТ Ошибка;  /* здесь надо выдавать код ошибки в ГУИ */
 } 
/*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ Ошибка {
       ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ОТКАЗ_ОТКРЫТИЯ_САПУНА))	ОШИБКА;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
  СОСТ НарушениеСвязи {
	   ОШИБКА;
 }
}  /*конец процесса ОТКР_САПУН  */



/*===========================================================*/
/*==!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! ==========================*/
/*=====   Процесс закрытия сапуна                    =======*/
/*===========================================================*/
ПРОЦ ЗАКР_САПУН{
 ИЗ ПРОЦ Инициализация 	К_САПУН,
                      	У_ОТКР_САПУН,
						ОШИБКА_ФУНКЦ_САПУН;

 СОСТ Начало{
  ОШИБКА_ФУНКЦ_САПУН = НЕТ;
  У_ОТКР_САПУН = ЗАКР;
  ЕСЛИ (ПРОЦ ОТКР_САПУН В СОСТ АКТИВНОЕ) ОШИБКА ПРОЦ ОТКР_САПУН;
  ЕСЛИ (К_САПУН == ЗАКР) СТОП;
  ИНАЧЕ В СЛЕДУЮЩЕЕ;      
 }

 СОСТ КонтрольКонцевика{  
  ЕСЛИ (К_САПУН == ЗАКР) СТОП;
  ТАЙМАУТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_САПУНА {
  /*      ОШИБКА_ФУНКЦИОНИРОВАНИЯ = ВКЛ;  ??????? */
	В СОСТ Ошибка;
  }
 }
 /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ Ошибка {
       ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ОТКАЗ_ЗАКРЫТИЯ_САПУНА))	ОШИБКА;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
  СОСТ НарушениеСвязи {
	   ОШИБКА;
 }
} /*конец процесса ЗАКР_САПУН  */




/*================================================================*/
/*              Процесс включить питание вакуумного насоса        */
/*================================================================*/
ПРОЦ ВКЛ_ПИТ_ВАК_НАСОСА{
 ИЗ ПРОЦ Инициализация 	К_ПИТ_ВАК_НАСОСА,
						У_ВКЛ_ПИТ_ВАК_НАСОСА,
						ОШИБКА_ФУНКЦ_ПИТ_ВАК_НАСОСА;
СОСТ Начало{
  ОШИБКА_ФУНКЦ_ПИТ_ВАК_НАСОСА = НЕТ; 
  У_ВКЛ_ПИТ_ВАК_НАСОСА = ВКЛ;
  ЕСЛИ (ПРОЦ ВЫК_ПИТ_ВАК_НАСОСА В СОСТ АКТИВНОЕ) ОШИБКА ПРОЦ ВЫК_ПИТ_ВАК_НАСОСА;
  ЕСЛИ (К_ПИТ_ВАК_НАСОСА == ВКЛ) СТОП;
  ИНАЧЕ В СЛЕДУЮЩЕЕ;      
 }

 СОСТ Контроль{  
  ЕСЛИ (К_ПИТ_ВАК_НАСОСА == ВКЛ) СТОП;
  ТАЙМАУТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_ВАК_НАСОСА 	В СОСТ Ошибка;

 } 
      /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ Ошибка {
       ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ОТКАЗ_ВКЛЮЧЕНИЯ_ПИТАНИЯ_ВАКУУМНОГО_НАСОСА))	ОШИБКА;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
  СОСТ НарушениеСвязи {
	   ОШИБКА;
 } 
} /*конец процесса ВКЛ_ПИТ_ВАК_НАСОСА  */
/*================================================================*/
/*              Процесс выключить питание вакуумного насоса                 */
/*================================================================*/
ПРОЦ ВЫК_ПИТ_ВАК_НАСОСА{
 ИЗ ПРОЦ Инициализация 	К_ПИТ_ВАК_НАСОСА,
						У_ВКЛ_ПИТ_ВАК_НАСОСА,
						ОШИБКА_ФУНКЦ_ПИТ_ВАК_НАСОСА;
 СОСТ Начало{
  ОШИБКА_ФУНКЦ_ПИТ_ВАК_НАСОСА = НЕТ;
  У_ВКЛ_ПИТ_ВАК_НАСОСА = ВЫК;
  ЕСЛИ (ПРОЦ ВКЛ_ПИТ_ВАК_НАСОСА В СОСТ АКТИВНОЕ) ОШИБКА ПРОЦ ВКЛ_ПИТ_ВАК_НАСОСА;
  ЕСЛИ (К_ПИТ_ВАК_НАСОСА == ВЫК) СТОП;
  ИНАЧЕ В СЛЕДУЮЩЕЕ;      
 }

 СОСТ Контроль{  
  ЕСЛИ (К_ПИТ_ВАК_НАСОСА == ВЫК) СТОП;
  ТАЙМАУТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_ВАК_НАСОСА 	В СОСТ Ошибка;
 
 } 
       /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ Ошибка {
       ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ОТКАЗ_ВЫКЛЮЧЕНИЯ_ПИТАНИЯ_ВАКУУМНОГО_НАСОСА))	ОШИБКА;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
  СОСТ НарушениеСвязи {
	   ОШИБКА;
 } 
}  /*конец процесса ВЫК_ПИТ_ВАК_НАСОСА */

/*================================================================*/
/*              Процесс Подать питание на регулятор температуры   */
/*================================================================*/
ПРОЦ ВКЛ_ПИТ_РЕГ_Т{
 ИЗ ПРОЦ Инициализация 	У_ВКЛ_ПИТ_РЕГ_Т,
						К_ПИТ_РЕГ_Т,
						ОШИБКА_ФУНКЦ_ПИТ_РЕГ_Т;
 
 
СОСТ Начало{
  ОШИБКА_ФУНКЦ_ПИТ_РЕГ_Т = НЕТ;
  У_ВКЛ_ПИТ_РЕГ_Т = ВКЛ;
  ЕСЛИ (ПРОЦ ВЫК_ПИТ_РЕГ_Т В СОСТ АКТИВНОЕ) ОШИБКА ПРОЦ ВЫК_ПИТ_РЕГ_Т;
  ЕСЛИ (К_ПИТ_РЕГ_Т == ВКЛ) СТОП;
  ИНАЧЕ В СЛЕДУЮЩЕЕ;      
 }

 СОСТ Контроль{  
  ЕСЛИ (К_ПИТ_РЕГ_Т == ВКЛ) СТОП;
  ТАЙМАУТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_РЕГ_Т 	В СОСТ Ошибка;
 } 
       /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ Ошибка {
       ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ОТКАЗ_ВКЛЮЧЕНИЯ_РЕГУЛЯТОРА_ТЕМПЕРАТУРЫ))	ОШИБКА;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
  СОСТ НарушениеСвязи {
	   ОШИБКА;
 } 

} /*конец процесса ВКЛ_ПИТ_РЕГ_Т */



/*================================================================*/
/*        Процесс Снять питание с регулятора температуры          */
/*================================================================*/
ПРОЦ ВЫК_ПИТ_РЕГ_Т{
 ИЗ ПРОЦ Инициализация 	У_ВКЛ_ПИТ_РЕГ_Т,
						К_ПИТ_РЕГ_Т,
						ОШИБКА_ФУНКЦ_ПИТ_РЕГ_Т;
 
 
СОСТ Начало{
  ОШИБКА_ФУНКЦ_ПИТ_РЕГ_Т = НЕТ;
  У_ВКЛ_ПИТ_РЕГ_Т = ВЫК;
  ЕСЛИ (ПРОЦ ВКЛ_ПИТ_РЕГ_Т В СОСТ АКТИВНОЕ) ОШИБКА ПРОЦ ВКЛ_ПИТ_РЕГ_Т;
  ЕСЛИ (К_ПИТ_РЕГ_Т == ВКЛ) СТОП;
  ИНАЧЕ В СЛЕДУЮЩЕЕ;      
 }

 СОСТ Контроль{  
  ЕСЛИ (К_ПИТ_РЕГ_Т == ВКЛ) СТОП;
  ТАЙМАУТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_РЕГ_Т В СОСТ Ошибка;

 } 
        /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ Ошибка {
       ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ОТКАЗ_ВЫКЛЮЧЕНИЯ_РЕГУЛЯТОРА_ТЕМПЕРАТУРЫ))	ОШИБКА;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
  СОСТ НарушениеСвязи {
	   ОШИБКА;
 } 

}  /*конец процесса ВЫК_ПИТ_РЕГ_Т */
/*================================================================*/
/*        Процесс Подать питание на регулятор уровня воды         */
/*================================================================*/
ПРОЦ ВКЛ_ПИТ_РЕГ_УР{
 ИЗ ПРОЦ Инициализация 	У_ВКЛ_ПИТ_РЕГ_УР,
						К_ПИТ_РЕГ_УР,
						ОШИБКА_ФУНКЦ_ПИТ_РЕГ_УР;

СОСТ Начало{
  ОШИБКА_ФУНКЦ_ПИТ_РЕГ_УР = НЕТ;
  У_ВКЛ_ПИТ_РЕГ_УР = ВКЛ;
  ЕСЛИ (ПРОЦ ВЫК_ПИТ_РЕГ_УР В СОСТ АКТИВНОЕ) ОШИБКА ПРОЦ ВЫК_ПИТ_РЕГ_УР;
  ЕСЛИ (К_ПИТ_РЕГ_УР == ВКЛ) СТОП;
  ИНАЧЕ В СЛЕДУЮЩЕЕ;      
 }

 СОСТ Контроль{  
  ЕСЛИ (К_ПИТ_РЕГ_УР == ВКЛ) СТОП;
  ТАЙМАУТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_РЕГ_УР В СОСТ Ошибка; 
  } 
         /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ Ошибка {
       ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ОТКАЗ_ВКЛЮЧЕНИЯ_РЕГУЛЯТОРА_УРОВНЯ))	ОШИБКА;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
  СОСТ НарушениеСвязи {
	   ОШИБКА;
 } 
}  /*конец процесса ВКЛ_ПИТ_РЕГ_УР */

/*================================================================*/
/*        Процесс Снять питание с регулятора уровня воды          */
/*================================================================*/
ПРОЦ ВЫК_ПИТ_РЕГ_УР{
 ИЗ ПРОЦ Инициализация 	У_ВКЛ_ПИТ_РЕГ_УР,
						К_ПИТ_РЕГ_УР,
						ОШИБКА_ФУНКЦ_ПИТ_РЕГ_УР;

СОСТ Начало{
  ОШИБКА_ФУНКЦ_ПИТ_РЕГ_УР = НЕТ;
  У_ВКЛ_ПИТ_РЕГ_УР = ВЫК;
  ЕСЛИ (ПРОЦ ВКЛ_ПИТ_РЕГ_УР В СОСТ АКТИВНОЕ) ОШИБКА ПРОЦ ВКЛ_ПИТ_РЕГ_УР;
  ЕСЛИ (К_ПИТ_РЕГ_УР == ВЫК) СТОП;
  ИНАЧЕ В СЛЕДУЮЩЕЕ;      
 }

 СОСТ Контроль{  
  ЕСЛИ (К_ПИТ_РЕГ_УР == ВЫК) СТОП;
  ТАЙМАУТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_РЕГ_УР 	В СОСТ Ошибка; 
 } 
          /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
  СОСТ Ошибка {
       ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ОТКАЗ_ВЫКЛЮЧЕНИЯ_РЕГУЛЯТОРА_УРОВНЯ))	ОШИБКА;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
  СОСТ НарушениеСвязи {
	   ОШИБКА;
 } 
}  /*конец процесса ВЫК_ПИТ_РЕГ_УР */


/*================================================================*/
/*              Процесс Подать питание на насос охлаждения        */
/*================================================================*/
ПРОЦ ВКЛ_ПИТ_НАС_ОХЛ{
 ИЗ ПРОЦ Инициализация 	У_ВКЛ_ПИТ_НАС_ОХЛ,
						К_НАСОС_ОХЛ,
						ФлагГотовУрВодыБО,
						ФлагГотовТемпВодыБО,
						ОШИБКА_ФУНКЦ_НАСОС_ОХЛ;
						
 СОСТ Начало{
  ЕСЛИ (ПРОЦ ВЫК_ПИТ_НАС_ОХЛ В СОСТ АКТИВНОЕ) ОШИБКА ПРОЦ ВЫК_ПИТ_НАС_ОХЛ;
  ЕСЛИ (ФлагГотовУрВодыБО == НОРМА)  В СЛЕДУЮЩЕЕ;
  ИНАЧЕ ОШИБКА;      
 }

 СОСТ ВключениеПитания{
  ОШИБКА_ФУНКЦ_НАСОС_ОХЛ = НЕТ;
  У_ВКЛ_ПИТ_НАС_ОХЛ = ВКЛ;
  ЕСЛИ (К_НАСОС_ОХЛ == ВКЛ) СТОП;
  ИНАЧЕ В СЛЕДУЮЩЕЕ;      
 }

 СОСТ Контроль{  
  ЕСЛИ (К_НАСОС_ОХЛ == ВКЛ) СТОП;
  ТАЙМАУТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_НАСОС_ОХЛ В СОСТ Ошибка; 
 } 
           /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
  СОСТ Ошибка {
       ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ОТКАЗ_ВКЛЮЧЕНИЯ_НАСОСА_ОХЛАЖДЕНИЯ))	ОШИБКА;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
  СОСТ НарушениеСвязи {
	   ОШИБКА;
 } 
} /*конец процесса ВКЛ_ПИТ_НАС_ОХЛАЖДЕНИЯ */



/*================================================================*/
/*             Процесс Снять питание с насоса охлаждения          */
/*================================================================*/
ПРОЦ ВЫК_ПИТ_НАС_ОХЛ{
 ИЗ ПРОЦ Инициализация 	У_ВКЛ_ПИТ_НАС_ОХЛ,
						К_НАСОС_ОХЛ,
						ОШИБКА_ФУНКЦ_НАСОС_ОХЛ;

 СОСТ Начало{
  ОШИБКА_ФУНКЦ_НАСОС_ОХЛ = НЕТ;
  У_ВКЛ_ПИТ_НАС_ОХЛ = ВЫК;
  ЕСЛИ (ПРОЦ ВКЛ_ПИТ_НАС_ОХЛ В СОСТ АКТИВНОЕ) ОШИБКА ПРОЦ ВКЛ_ПИТ_НАС_ОХЛ;
  ЕСЛИ (К_НАСОС_ОХЛ == ВЫК) СТОП;
  ИНАЧЕ В СЛЕДУЮЩЕЕ;      
 }

 СОСТ Контроль{  
  ЕСЛИ (К_НАСОС_ОХЛ == ВЫК) СТОП;
  ТАЙМАУТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_НАСОС_ОХЛ В СОСТ Ошибка; 
 } 
            /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
  СОСТ Ошибка {
       ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ОТКАЗ_ВЫКЛЮЧЕНИЯ_НАСОСА_ОХЛАЖДЕНИЯ))	ОШИБКА;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
  СОСТ НарушениеСвязи {
	   ОШИБКА;
 } 

} /*конец процесса ВЫК_ПИТ_НАС_ОХЛАЖДЕНИЯ */
/*=========================================================++++++++++++======*/
/*==   Процесс  ВКЛЮЧИТЬ Устройство плавного пуска (запуск вакуумирования)   */
/*== это означает запуск вакуумного насоса на вакуумирование    			 */
/*==========================================================++++++++++++=====*/

ПРОЦ ВКЛ_УСТР_ПЛАВ_ПУСКА{
 ИЗ ПРОЦ Инициализация 	К_УСТР_ПЛАВ_ПУСКА,
						У_ВКЛ_УСТР_ПЛАВ_ПУСКА,
						ОШИБКА_ФУНКЦ_УСТР_ПЛАВ_ПУСКА;
						
						
						

 СОСТ Начало{
  ОШИБКА_ФУНКЦ_УСТР_ПЛАВ_ПУСКА = НЕТ;
  У_ВКЛ_УСТР_ПЛАВ_ПУСКА = ВКЛ;
  ЕСЛИ (ПРОЦ ВЫК_УСТР_ПЛАВ_ПУСКА В СОСТ АКТИВНОЕ) ОШИБКА ПРОЦ ВЫК_УСТР_ПЛАВ_ПУСКА;
  ЕСЛИ (К_УСТР_ПЛАВ_ПУСКА == ВКЛ) СТОП;
  ИНАЧЕ В СЛЕДУЮЩЕЕ;      
 }

 СОСТ Контроль{  
  ЕСЛИ (К_УСТР_ПЛАВ_ПУСКА == ВКЛ) СТОП;
  ТАЙМАУТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_УСТР_ПЛАВ_ПУСКА  	В СОСТ Ошибка;
   
 } 
     /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ Ошибка {
       ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ОТКАЗ_ВКЛЮЧЕНИЯ_УСТР_ПЛАВ_ПУСКА))	ОШИБКА;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
  СОСТ НарушениеСвязи {
	   ОШИБКА;
 } 
}  /*конец процесса ВКЛ_УСТР_ПЛАВ_ПУСКА  */

/*===============================================================*/
/*==   Процесс  ВЫКЛЮЧИТЬ Устройство плавного пуска                  */
/*===============================================================*/
ПРОЦ ВЫК_УСТР_ПЛАВ_ПУСКА{
 ИЗ ПРОЦ Инициализация 	К_УСТР_ПЛАВ_ПУСКА,
						У_ВКЛ_УСТР_ПЛАВ_ПУСКА,
						ОШИБКА_ФУНКЦ_УСТР_ПЛАВ_ПУСКА;
						
 СОСТ Начало{					
  ОШИБКА_ФУНКЦ_УСТР_ПЛАВ_ПУСКА = НЕТ;                 
  У_ВКЛ_УСТР_ПЛАВ_ПУСКА = ВЫК;
  ЕСЛИ (ПРОЦ ВКЛ_УСТР_ПЛАВ_ПУСКА В СОСТ АКТИВНОЕ) ОШИБКА ПРОЦ ВКЛ_УСТР_ПЛАВ_ПУСКА;
  ЕСЛИ (К_УСТР_ПЛАВ_ПУСКА == ВЫК) СТОП;
  ИНАЧЕ В СЛЕДУЮЩЕЕ;      
 }

 СОСТ КонтрольВключения{  
  ЕСЛИ (К_УСТР_ПЛАВ_ПУСКА == ВЫК) СТОП;
  ТАЙМАУТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_УСТР_ПЛАВ_ПУСКА 	В СОСТ Ошибка;
  
 } 
       /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
  СОСТ Ошибка {
       ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ОТКАЗ_ВЫКЛЮЧЕНИЯ_УСТР_ПЛАВ_ПУСКА))	ОШИБКА;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
  СОСТ НарушениеСвязи {
	   ОШИБКА;
 } 
} /*конец процесса ВЫК_УСТР_ПЛАВ_ПУСКА  */

/*=======================================================================*/
/*              Процесс Включить насос вакуумный                         */
/*   Этапы включения вакуумного насоса:                                  */ 
/*   1. Проверяем допустимое давление в компрессоре. Норма - идем дальше */  
/*   2. Проверяем температуру и уровень воды в баке охлаждения           */ 
/*      Норма - идем дальше   											 */ 
/*   3. Включаем питание вентилятора                                     */ 
/*   4. Включаем питание насоса охлаждения                               */  
/*   5. Закрываем затвор вакуумный                                       */ 
/*   6. Открываем сапун                                                  */ 
/*   7. Контролируем температуру воды в рубашке, если норма              */
/*                                    - идем дальше                      */ 
/*   8. Включаем питание вакуумного насоса                               */ 
/*   9. Включаем устройство плавного пуска                               */ 
/*   10. Выдерживаем одну минуту и закрываем сапун                        */ 
/*   11. Если ФП_P_ВтрубеСидер >= ФП_P_Впатрубке - открываем затвор       */
/*       вакуумный, иначе - ошибка                                       */  
/*       Пошло вакуумирование - насос включен                            */  
/*=======================================================================*/
ПРОЦ ВКЛ_НАСОС_ВАКУУМНЫЙ{
 ИЗ ПРОЦ Инициализация 	У_ВКЛ_ПИТ_НАС_ОХЛ,
						ФП_P_Впатрубке,		
						ФП_P_ВтрубеСидер,
						ФлагГотовTВодыРО,
						ФлагГотовТемпВодыБО,
						
						ФП_P_ВбакеКомпрессора,
						ФП_T_Вбаке,
						ФлагГотовУрВодыБО,
						ФлагДавленияКомпрессора;
 ПЛАВ ЛП_Предыдущее ЛОКАЛ;
 
 СОСТ Начало{
	ЕСЛИ (ПРОЦ ВЫК_НАСОС_ВАКУУМНЫЙ В СОСТ АКТИВНОЕ) ОШИБКА ПРОЦ ВЫК_НАСОС_ВАКУУМНЫЙ;
	/* нельзя включать насос вакуумный, если давление в компрессоре < 5-10 атмосфер*/
		ЕСЛИ (ФлагДавленияКомпрессора != НОРМА) ОШИБКА;
		ИНАЧЕ В СЛЕДУЮЩЕЕ;
	}
	СОСТ КонтрольГотовностиУровняИтемпературы{
		ЕСЛИ ((ФП_T_Вбаке > Т_ПредельнаяВбакеОхлаждения)|| 
			(ФлагГотовУрВодыБО == ВНЕ_НОРМЫ)||
			(ФлагГотовТемпВодыБО == ВНЕ_НОРМЫ )) ОШИБКА;
			ИНАЧЕ 	{
				СТАРТ ПРОЦ ВКЛ_ПИТ_ВЕНТИЛЯТОР;
				В СЛЕДУЮЩЕЕ;
			}
	}
	
		
	СОСТ КонтрольВключенияВентилятора{
		ЕСЛИ ( ПРОЦ ВКЛ_ПИТ_ВЕНТИЛЯТОР В СОСТ ПАССИВНОЕ){ 
				ЕСЛИ (ПРОЦ  ВКЛ_ПИТ_ВЕНТИЛЯТОР В СОСТ ОШИБКА ) ОШИБКА;
				ИНАЧЕ {
					СТАРТ ПРОЦ ВКЛ_ПИТ_НАС_ОХЛ;
					В СЛЕДУЮЩЕЕ;
				}
		}
	}  
	СОСТ КонтрольВключенияПитанияНасосаОхлаждения{
		ЕСЛИ (ПРОЦ ВКЛ_ПИТ_НАС_ОХЛ В СОСТ ПАССИВНОЕ){ 
				ЕСЛИ (ПРОЦ  ВКЛ_ПИТ_НАС_ОХЛ В СОСТ ОШИБКА ) ОШИБКА;
				ИНАЧЕ {
					СТАРТ ПРОЦ ЗАКР_ЗАТВОР_ВАК ;
					В СЛЕДУЮЩЕЕ;
				}
		}
	}  
	СОСТ КонтрольЗакрытияЗатвораВакуумного{
		ЕСЛИ (ПРОЦ ЗАКР_ЗАТВОР_ВАК В СОСТ ПАССИВНОЕ){ 
			ЕСЛИ (ПРОЦ ЗАКР_ЗАТВОР_ВАК В СОСТ ОШИБКА ) ОШИБКА;
			ИНАЧЕ {
				СТАРТ ПРОЦ ОТКР_САПУН;
				В СЛЕДУЮЩЕЕ;
			}
		} 
	} 
	СОСТ КонтрольОткрытияСапуна{
		ЕСЛИ ( ПРОЦ ОТКР_САПУН В СОСТ ПАССИВНОЕ){ 
			ЕСЛИ (ПРОЦ ОТКР_САПУН В СОСТ ОШИБКА ) ОШИБКА;
			ИНАЧЕ В СЛЕДУЮЩЕЕ;
		} 
	} 	
	СОСТ КонтрольТемпературыРубашки{
		ЕСЛИ (ФлагГотовTВодыРО == НОРМА) В СЛЕДУЮЩЕЕ;
/*		ТАЙМАУТ ПЯТЬ_МИНУТ ОШИБКА;  */
		ИНАЧЕ ОШИБКА;
	}
	СОСТ ПодачаПитанияНаВакуумныйНасос{
		СТАРТ ПРОЦ ВКЛ_ПИТ_ВАК_НАСОСА;
		В СЛЕДУЮЩЕЕ;
	}	
	СОСТ КонтрольПитанияВакуумногоНасоса{
		ЕСЛИ ( ПРОЦ ВКЛ_ПИТ_ВАК_НАСОСА В СОСТ ПАССИВНОЕ){ 
			ЕСЛИ (ПРОЦ  ВКЛ_ПИТ_ВАК_НАСОСА В СОСТ ОШИБКА) ОШИБКА;
			ИНАЧЕ  В СЛЕДУЮЩЕЕ;
		} 
	}  
	СОСТ ВключениеУстройстваПлавногоПуска{
		СТАРТ ПРОЦ ВКЛ_УСТР_ПЛАВ_ПУСКА;
		В СЛЕДУЮЩЕЕ;
	}
	СОСТ КонтрольВлюченияПлавногоПуска{
		ЕСЛИ (ПРОЦ ВКЛ_УСТР_ПЛАВ_ПУСКА В СОСТ ПАССИВНОЕ){ 
			ЕСЛИ (ПРОЦ  ВКЛ_УСТР_ПЛАВ_ПУСКА В СОСТ ОШИБКА) ОШИБКА;
			ИНАЧЕ  В СЛЕДУЮЩЕЕ;
		} 
	}  	
	СОСТ ТаймаутПередЗакрытиемСапуна{  
		ТАЙМАУТ ОДНА_МИНУТА В СЛЕДУЮЩЕЕ; 
	}  
	
	СОСТ ЗакрытиеСапуна{  
		СТАРТ ПРОЦ ЗАКР_САПУН;
		В СЛЕДУЮЩЕЕ; 
	} 
	СОСТ КонтрольЗакрытияСапуна{
		ЕСЛИ ( ПРОЦ ЗАКР_САПУН В СОСТ ПАССИВНОЕ){ 
			ЕСЛИ (ПРОЦ ЗАКР_САПУН В СОСТ ОШИБКА ) ОШИБКА;
			ИНАЧЕ В СЛЕДУЮЩЕЕ;
		} 
	} 
	
	СОСТ ЗапоминаемДавлениеВпатрубке{
			ЛП_Предыдущее = ФП_P_Впатрубке;
			В СЛЕДУЮЩЕЕ;
	}
	СОСТ КонтрольДавления{  
		ЕСЛИ (ФП_P_ВтрубеСидер >= ФП_P_Впатрубке) {
			СТАРТ ПРОЦ ОТКР_ЗАТВОР_ВАК; 
			В СОСТ КонтрольОткрытияЗВ;
			}
		ИНАЧЕ В СЛЕДУЮЩЕЕ;
	} 	
	СОСТ ПаузаНаОткачкуВпатрубке{  
		ТАЙМАУТ ДВЕ_МИНУТЫ В СЛЕДУЮЩЕЕ; 
	}  
	СОСТ ПроверкаУменьшенияДавленияВпатрубке{  
		ЕСЛИ(ФП_P_Впатрубке < ЛП_Предыдущее) В СОСТ ЗапоминаемДавлениеВпатрубке;
        ИНАЧЕ В СОСТ Ошибка;		
	} 	
	СОСТ КонтрольОткрытияЗВ{
		ЕСЛИ ( ПРОЦ ОТКР_ЗАТВОР_ВАК В СОСТ ПАССИВНОЕ){ 
			ЕСЛИ (ПРОЦ ОТКР_ЗАТВОР_ВАК В СОСТ ОШИБКА ) ОШИБКА;
			ИНАЧЕ  СТОП;
		} 
	} 
 СОСТ Ошибка {
       ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ДАВЛЕНИЕ_В_ПАТРУБКЕ_НЕ_УМЕНЬШАЕТСЯ))	ОШИБКА;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
  СОСТ НарушениеСвязи {
	   ОШИБКА;
 }    
}
/*================================================================*/
/*              Процесс Выключить насос вакуумный                 */
/*   Этапы выключения вакуумного насоса:                           */
/*    1. Закрывается затвор вакуумный                             */
/*    2. Открывается сапун                                        */
/*    3. Ничего не делаем 20 секунд                               */
/*    4. Выключаем устройство плавного пуска                      */
/*    5. Выключаем питание вакуумного насоса                      */
/*    6. Выключаем питание насоса охлаждения                      */
/*    7. Выключаем питание вентилятора                            */
/*================================================================*/
ПРОЦ ВЫК_НАСОС_ВАКУУМНЫЙ{
 ИЗ ПРОЦ Инициализация 	У_ВКЛ_ПИТ_НАС_ОХЛ;
 
	СОСТ Начало{
 		СТАРТ ПРОЦ ЗАКР_ЗАТВОР_ВАК ;
		В СЛЕДУЮЩЕЕ;
	}  
	
	СОСТ КонтрольЗакрытияЗатвораВакуумного{
		ЕСЛИ (ПРОЦ ЗАКР_ЗАТВОР_ВАК В СОСТ ПАССИВНОЕ){ 
			ЕСЛИ (ПРОЦ ЗАКР_ЗАТВОР_ВАК В СОСТ ОШИБКА ) ОШИБКА;
			ИНАЧЕ {
			СТОП ПРОЦ КонтрольОткачкиВауумнойТрубыСидеростата;
				СТАРТ ПРОЦ ОТКР_САПУН;
				В СЛЕДУЮЩЕЕ;
			}
		} 
	} 
	СОСТ КонтрольОткрытияСапуна{
		ЕСЛИ ( ПРОЦ ОТКР_САПУН В СОСТ ПАССИВНОЕ){ 
			ЕСЛИ (ПРОЦ ОТКР_САПУН В СОСТ ОШИБКА ) ОШИБКА;
			ИНАЧЕ {
				
				В СЛЕДУЮЩЕЕ;
			}
		} 
	} 
	СОСТ ТаймаутПередВыключениемПитанияВакуумногоНасоса{  
		ТАЙМАУТ ДВАДЦАТЬ_СЕКУНД В СЛЕДУЮЩЕЕ; 
	}  

	СОСТ ВыключаемУППВакуумногоНасоса{  
		СТАРТ ПРОЦ ВЫК_УСТР_ПЛАВ_ПУСКА; В СЛЕДУЮЩЕЕ; 
	}	
	СОСТ КонтрольВыключенияПитанияУППВакуумногоНасоса{
		ЕСЛИ ( ПРОЦ ВЫК_УСТР_ПЛАВ_ПУСКА В СОСТ ПАССИВНОЕ){ 
			ЕСЛИ (ПРОЦ  ВЫК_УСТР_ПЛАВ_ПУСКА В СОСТ ОШИБКА) ОШИБКА;
			ИНАЧЕ {
				СТАРТ ПРОЦ ВЫК_ПИТ_ВАК_НАСОСА;
				В СЛЕДУЮЩЕЕ;
			}
		} 
	}   

	СОСТ КонтрольВыключенияПитанияВакуумногоНасоса{
		ЕСЛИ ( ПРОЦ ВЫК_ПИТ_ВАК_НАСОСА В СОСТ ПАССИВНОЕ){ 
			ЕСЛИ (ПРОЦ  ВЫК_ПИТ_ВАК_НАСОСА В СОСТ ОШИБКА) ОШИБКА;
			ИНАЧЕ {
				СТАРТ ПРОЦ ВЫК_ПИТ_НАС_ОХЛ;
				В СЛЕДУЮЩЕЕ;
			}
		} 
	}   	
	
	СОСТ КонтрольВыключенияНасосаОхлаждения{
		ЕСЛИ ( ПРОЦ ВЫК_ПИТ_НАС_ОХЛ В СОСТ ПАССИВНОЕ){ 
				ЕСЛИ (ПРОЦ  ВЫК_ПИТ_НАС_ОХЛ В СОСТ ОШИБКА ) ОШИБКА;
				ИНАЧЕ {
					СТАРТ ПРОЦ ВЫК_ПИТ_ВЕНТИЛЯТОР;
					В СЛЕДУЮЩЕЕ;
				}
		}
	}  
	
	СОСТ КонтрольВыключенияВентилятора{
		ЕСЛИ ( ПРОЦ ВЫК_ПИТ_ВЕНТИЛЯТОР В СОСТ ПАССИВНОЕ){ 
				ЕСЛИ (ПРОЦ  ВЫК_ПИТ_ВЕНТИЛЯТОР В СОСТ ОШИБКА ) ОШИБКА;
				ИНАЧЕ СТОП;
		}
	}   
} /*конец процесса ВЫК_НАСОС_ВАКУУМНЫЙ */



/*====================================================================*/
/*              Процесс Вакуумирование                                */
/* Процесс запускает включение вакуумной подсистемы и смотрит         */
/* величину откачки и готовность вакуума. Если откачка в норме,       */
/* анализируется готовность вакуума и, как только вакуумирование      */
/*  достигнет нормы ( нужный вакуум есть) процесс                     */
/*  заканчивается без ошибки. Причем насос вакуумный продолжает       */
/* работать. Если вакуумирование  не пошло или                        */
/*  откачка не на уровне, данный процесс завершается с ошибкой  и     */
/* насос вакуумный выключается.                                       */
/*====================================================================*/
ПРОЦ ВКЛ_ВАКУУМИРОВАНИЕ_ТРУБЫ{
 ИЗ ПРОЦ Инициализация 	ФлагГотовВакуумаВТ,
						ФлагОткачкиТрубыСидеростата,
						ФП_P_ВтрубеСидер,
						ФП_T_Врубашке,
						К_ВЕНТИЛЯТОР,
						К_НАСОС_ОХЛ;
 ПЛАВ ЛП_Предыдущее ЛОКАЛ;
 СОСТ Начало{
   ЕСЛИ (ФлагГотовВакуумаВТ == НОРМА) СТОП;
   ИНАЧЕ {  
	СТАРТ ПРОЦ ВКЛ_НАСОС_ВАКУУМНЫЙ;
	В СЛЕДУЮЩЕЕ;
	} 
 }
	СОСТ КонтрольВключенияНасоса{
		ЕСЛИ ( ПРОЦ ВКЛ_НАСОС_ВАКУУМНЫЙ В СОСТ ПАССИВНОЕ){ 
			ЕСЛИ (ПРОЦ ВКЛ_НАСОС_ВАКУУМНЫЙ В СОСТ ОШИБКА ) {
				СТАРТ ПРОЦ ВЫК_НАСОС_ВАКУУМНЫЙ; 
				ОШИБКА;
			}
			ИНАЧЕ {
				СТАРТ ПРОЦ КонтрольОткачкиВауумнойТрубыСидеростата;	
				В СЛЕДУЮЩЕЕ;
			}
		} 
	} 
	
	СОСТ ЗапоминаемДавлениеВвакуумнойТрубе{
			ЛП_Предыдущее = ФП_P_ВтрубеСидер;
			В СЛЕДУЮЩЕЕ;
	}
		/* контроль перегрева рубашки, самопроизвольного выключения вентилятора и водяного насоса*/
	СОСТ КонтрольОборудования{
			ЕСЛИ ( (ФП_T_Врубашке <= 85) && (К_ВЕНТИЛЯТОР) && (К_НАСОС_ОХЛ) ) В СЛЕДУЮЩЕЕ;
			ИНАЧЕ {
				СТАРТ ПРОЦ ВЫК_НАСОС_ВАКУУМНЫЙ;
			ОШИБКА;
			}
	}		
	СОСТ КонтрольДавления{  
		ЕСЛИ (ФП_P_ВтрубеСидер <= 0.02) {
			СТАРТ ПРОЦ ВЫК_НАСОС_ВАКУУМНЫЙ; 
			СТОП;
			}
		ИНАЧЕ В СЛЕДУЮЩЕЕ;
	} 	
	СОСТ ПаузаНаОткачкуВвакуумнойТрубе{  
		ТАЙМАУТ ДВЕ_МИНУТЫ В СЛЕДУЮЩЕЕ; 
	}  
	СОСТ ПроверкаУменьшенияДавленияВпатрубке{  
		ЕСЛИ(ФП_P_ВтрубеСидер < ЛП_Предыдущее) В СОСТ ЗапоминаемДавлениеВвакуумнойТрубе;
        ИНАЧЕ ОШИБКА;		/* м.б. сообщение выдавать, что вакуумированию трубы - труба */
	} 	
} /*конец процесса ВКЛ_ВАКУУМИРОВАНИЕ_ТРУБЫ */




/*================================================================*/
/*             Процесс Вакуумирование в вакуумной трубе           */
/*  Процесс выключает вакуумирование в вакуумной трубе            */
/*  может быть этот процесс и не нужен                            */
/*================================================================*/
ПРОЦ ВЫК_ВАКУУМИРОВАНИЕ_ТРУБЫ{
	ИЗ ПРОЦ Инициализация ФлагГотовВакуумаВТ;

 СОСТ Начало{
	ЕСЛИ (ПРОЦ ВКЛ_ВАКУУМИРОВАНИЕ_ТРУБЫ В СОСТ АКТИВНОЕ) СТОП ПРОЦ ВКЛ_ВАКУУМИРОВАНИЕ_ТРУБЫ; 
	СТАРТ ПРОЦ ВЫК_НАСОС_ВАКУУМНЫЙ;
	СТОП;
 }
} /*конец процесса ВЫК_ВАКУУМИРОВАНИЕ_ТРУБЫ */

}  /* \Прогр */

