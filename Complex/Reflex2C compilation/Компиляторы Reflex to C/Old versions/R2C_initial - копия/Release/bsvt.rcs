                        Прогр sapun { 


/*###########################################################*/
/*#########                     #############################*/
/*#########  ОПИСАНИЕ КОНСТАНТ  #############################*/
/*#########                     #############################*/
/*###########################################################*/
/* ?????????????? Таймауты взяты с потолка */
ТАКТ     50;    /* такт равен 500 миллисекундам */
КОНСТ Т0_1                       0;  /* Константа обеспечивающая паузу в 0.5 сек */
КОНСТ ОДНА_СЕКУНДА               2;  /* Константа обеспечивающая паузу в 1 сек */
КОНСТ ДВЕ_СЕКУНДЫ             2*ОДНА_СЕКУНДА;
КОНСТ ТРИ_СЕКУНДЫ             3*ОДНА_СЕКУНДА;
КОНСТ ПЯТЬ_СЕКУНД             5*ОДНА_СЕКУНДА;
КОНСТ ДЕСЯТЬ_СЕКУНД          10*ОДНА_СЕКУНДА;
КОНСТ ДВАДЦАТЬ_СЕКУНД        20*ОДНА_СЕКУНДА;
КОНСТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_ЗАТ_ВАК			10*ОДНА_СЕКУНДА;
КОНСТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_САПУНА           	ДЕСЯТЬ_СЕКУНД;
КОНСТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_УСТР_ПЛАВ_ПУСКА   3*ОДНА_СЕКУНДА;
КОНСТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_ВАК_НАСОСА 		3*ОДНА_СЕКУНДА;
КОНСТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_ВЕНТИЛЯТОРА		ПЯТЬ_СЕКУНД;
КОНСТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_РЕГ_Т				3*ОДНА_СЕКУНДА;
КОНСТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_РЕГ_УР			3*ОДНА_СЕКУНДА;
КОНСТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_НАСОС_ОХЛ			3*ОДНА_СЕКУНДА;
КОНСТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ  ДЕСЯТЬ_СЕКУНД;
КОНСТ ОДНА_МИНУТА          60*ОДНА_СЕКУНДА;
КОНСТ ДВЕ_МИНУТЫ          2*ОДНА_МИНУТА;
КОНСТ ПЯТЬ_МИНУТ          5*60*ОДНА_СЕКУНДА;
/* КОНСТ ТАЙМАУТ_ВАКУУМИРОВАНИЯ_ТРУБЫ  30*ОДНА_МИНУТА;*/
КОНСТ ТРИДЦАТЬ_МИНУТ          30*ОДНА_МИНУТА;
КОНСТ ТАЙМАУТ_ЗАДЕРЖКА_НА_ОТКАЧКУ ПЯТЬ_СЕКУНД;

КОНСТ ВКЛ                1;
КОНСТ ВЫК               0;
КОНСТ ОТКР              1;
КОНСТ ЗАКР          	0;
КОНСТ ЕСТЬ               1;
КОНСТ НЕТ                0;
КОНСТ НОРМА              1;
КОНСТ ВНЕ_НОРМЫ          0;
КОНСТ ДОСТОВЕРНО                1;
КОНСТ НЕДОСТОВЕРНО               0;


КОНСТ ТАЙМАУТ_НА_ДИАГНОСТИКУ_РАЗРЫВА_СВЯЗИ 5*ОДНА_СЕКУНДА;

КОНСТ Т_ПредельнаяВбакеОхлаждения          99.0; 

/*КОНСТ ЗД_P_ВбакеКомпрессора						10.0; */  /*?????  атмосфер */

КОНСТ Т_ГИСТЕРЕЗИС_ВОДЫ_В_БАКЕ  1.0;
КОНСТ Т_ГИСТЕРЕЗИС_ВОДЫ_В_РУБАХЕ  1.0;


/*===========================================================*/
/*=== КОДЫ СООБЩЕНИЙ ПРОЦЕССОВ КОНТРОЛЯ                     =*/
/*===========================================================*/
ПЕРЕЧИСЛЕНИЕ {
    ПК_ГУИ_УРОВЕНЬ_ВОДЫ_В_БАКЕ_ОХЛАЖДЕНИЯ_НИЗКИЙ,
    ПК_ГУИ_УРОВЕНЬ_ВОДЫ_В_БАКЕ_ОХЛАЖДЕНИЯ_ВЫСОКИЙ,
	ПК_ГУИ_УРОВЕНЬ_ВОДЫ_В_БАКЕ_ОХЛАЖДЕНИЯ_В_НОРМЕ,
	ПК_ГУИ_ТЕМПЕРАТУРА_ВОДЫ_В_БАКЕ_ОХЛАЖДЕНИЯ_НИЗКАЯ,
	ПК_ГУИ_ТЕМПЕРАТУРА_ВОДЫ_В_БАКЕ_ОХЛАЖДЕНИЯ_ВЫСОКАЯ,
	ПК_ГУИ_ТЕМПЕРАТУРА_ВОДЫ_В_БАКЕ_ОХЛАЖДЕНИЯ_В_НОРМЕ,

    ПК_ГУИ_ТЕМПЕРАТУРА_ВОДЫ_В_РУБАШКЕ_ОХЛАЖДЕНИЯ_НИЗКАЯ,
	ПК_ГУИ_ТЕМПЕРАТУРА_ВОДЫ_В_РУБАШКЕ_ОХЛАЖДЕНИЯ_ВЫСОКАЯ,
	ПК_ГУИ_ТЕМПЕРАТУРА_ВОДЫ_В_РУБАШКЕ_ОХЛАЖДЕНИЯ_В_НОРМЕ,

	ПК_ГУИ_ДАВЛЕНИЕ_В_ВАКУУМНОЙ_ТРУБЕ_В_НОРМЕ,
	ПК_ГУИ_ДАВЛЕНИЕ_В_ВАКУУМНОЙ_ТРУБЕ_ВНЕ_НОРМЫ,
	
	ПК_ГУИ_СКОРОСТЬ_ОТКАЧКИ_В_НОРМЕ,
	ПК_ГУИ_СКОРОСТЬ_ОТКАЧКИ_НИЖЕ_НОРМЫ,
	
	ПК_ГУИ_ДАВЛЕНИЕ_В_БАКЕ_КОМПРЕССОРА_В_НОРМЕ,
    ПК_ГУИ_ДАВЛЕНИЕ_В_БАКЕ_КОМПРЕССОРА_НИЖЕ_НОРМЫ
	
};
/*===========================================================*/
/*========= КОМАНДЫ ОПЕРАТОРА =============15================*/
/*===========================================================*/
ПЕРЕЧИСЛЕНИЕ {
     КОМ_ГУИ_ОТКР_ЗАТВОР_ВАК= ПК_ГУИ_ДАВЛЕНИЕ_В_БАКЕ_КОМПРЕССОРА_НИЖЕ_НОРМЫ + 1,
     КОМ_ГУИ_ЗАКР_ЗАТВОР_ВАК    	,
     КОМ_ГУИ_ОТКР_САПУН			    ,
     КОМ_ГУИ_ЗАКР_САПУН			    ,
     КОМ_ГУИ_ВКЛ_УСТР_ПЛАВ_ПУСКА    ,
     КОМ_ГУИ_ВЫК_УСТР_ПЛАВ_ПУСКА    ,
	 КОМ_ГУИ_ВКЛ_ПИТ_ВАК_НАСОСА     ,
	 КОМ_ГУИ_ВЫК_ПИТ_ВАК_НАСОСА     ,
	 КОМ_ГУИ_ВКЛ_ПИТ_ВЕНТИЛЯТОР    ,
	 КОМ_ГУИ_ВЫК_ПИТ_ВЕНТИЛЯТОР    ,
     КОМ_ГУИ_ВКЛ_ПИТ_РЕГ_Т    ,
	 КОМ_ГУИ_ВЫК_ПИТ_РЕГ_Т    ,
     КОМ_ГУИ_ВКЛ_ПИТ_РЕГ_УР     ,
	 КОМ_ГУИ_ВЫК_ПИТ_РЕГ_УР     , 
	 КОМ_ГУИ_ВКЛ_ПИТ_НАС_ОХЛ ,
	 КОМ_ГУИ_ВЫК_ПИТ_НАС_ОХЛ    , 
	 КОМ_ГУИ_ВКЛ_ВАКУУМИРОВАНИЕ_ТРУБЫ,
	 КОМ_ГУИ_ВЫК_ВАКУУМИРОВАНИЕ_ТРУБЫ,
	 КОМ_ГУИ_ВКЛ_ВАКУУМНУЮ_ПОДСИСТЕМУ ,
	 КОМ_ГУИ_ВЫК_ВАКУУМНУЮ_ПОДСИСТЕМУ,   /*  35 */
     КОМАНДЫ_ОТ_ГУИ = КОМ_ГУИ_ВЫК_ВАКУУМНУЮ_ПОДСИСТЕМУ 
	
};


/*===========================================================*/
/*========= КОМАНДЫ ИЗМЕНЕНИЯ ФАКТИЧЕСКИХ ПАРАМЕТРОВ ========*/
/*===========================================================*/


ПЕРЕЧИСЛЕНИЕ {
	ИЗМН_ФП_P_ВбакеКомпрессора = КОМАНДЫ_ОТ_ГУИ + 1,
	ИЗМН_ФП_P_Впатрубке,
	ИЗМН_ФП_T_Врубашке,	
	ИЗМН_ФП_T_Вбаке,	
    ИЗМН_ФП_P_ВтрубеСидер,
    КОМАНДЫ_ИЗМ_ФП = ИЗМН_ФП_P_ВтрубеСидер  /*  42 */
};

/*===========================================================*/
/*===========================================================*/
/*============= КОДЫ СООБЩЕНИЙ ВАКУУМНОЙ СИСТЕМЫ ============*/
/*===========================================================*/
/*===========================================================*/
ПЕРЕЧИСЛЕНИЕ {                    
      ПК_ГУИ_ОТКАЗ_ОТКРЫТИЯ_ЗАТВОРА_ВАКУУМНОГО = КОМАНДЫ_ИЗМ_ФП + 1,
      ПК_ГУИ_ОТКАЗ_ЗАКРЫТИЯ_ЗАТВОРА_ВАКУУМНОГО,
      ПК_ГУИ_ОТКАЗ_ОТКРЫТИЯ_САПУНА,
      ПК_ГУИ_ОТКАЗ_ЗАКРЫТИЯ_САПУНА,
	  ПК_ГУИ_ОТКАЗ_ВКЛЮЧЕНИЯ_ВЕНТИЛЯТОРА,
	  ПК_ГУИ_ОТКАЗ_ВЫКЛЮЧЕНИЯ_ВЕНТИЛЯТОРА,
	  ПК_ГУИ_ОТКАЗ_ВКЛЮЧЕНИЯ_УСТР_ПЛАВ_ПУСКА,
	  ПК_ГУИ_ОТКАЗ_ВЫКЛЮЧЕНИЯ_УСТР_ПЛАВ_ПУСКА,
	  ПК_ГУИ_ОТКАЗ_ВКЛЮЧЕНИЯ_РЕГУЛЯТОРА_ТЕМПЕРАТУРЫ,
	  ПК_ГУИ_ОТКАЗ_ВЫКЛЮЧЕНИЯ_РЕГУЛЯТОРА_ТЕМПЕРАТУРЫ,
	  ПК_ГУИ_ОТКАЗ_ВКЛЮЧЕНИЯ_РЕГУЛЯТОРА_УРОВНЯ,
	  ПК_ГУИ_ОТКАЗ_ВЫКЛЮЧЕНИЯ_РЕГУЛЯТОРА_УРОВНЯ,
	  ПК_ГУИ_ОТКАЗ_ВКЛЮЧЕНИЯ_НАСОСА_ОХЛАЖДЕНИЯ,
	  ПК_ГУИ_ОТКАЗ_ВЫКЛЮЧЕНИЯ_НАСОСА_ОХЛАЖДЕНИЯ,
	  ПК_ГУИ_ИДЕТ_ВАКУУМИРОВАНИЕ_ОТКАЗ_ВЫПОЛНЕНИЯ_КОМАНДЫ,
	  ПК_ГУИ_НЕТ_УСЛОВИЙ_ДЛЯ_ВЫПОЛНЕНИЯ_КОМАНДЫ,
	  ПК_ГУИ_УРОВЕНЬ_ВОДЫ_В_БАКЕ_ОХЛАЖДЕНИЯ_ВНЕ_НОРМЫ_ОТКАЗ_ВЫПОЛНЕНИЯ_КОМАНДЫ, 				
	  ПК_ГУИ_ОТРИЦАТЕЛЬНАЯ_ТЕМПЕРАТУРА_ВОДЫ_ОТКАЗ_ВЫПОЛНЕНИЯ_КОМАНДЫ,
	  ПК_ГУИ_ДАВЛЕНИЕ_В_ПАТРУБКЕ_НЕ_УМЕНЬШАЕТСЯ,
	  ПК_ГУИ_ДАВЛЕНИЕ_В_ВАК_ТРУБЕ_НЕ_УМЕНЬШАЕТСЯ,
	  ПК_ГУИ_ВАКУУМИРОВАНИЕ_УСПЕШНО_ЗАВЕРШЕНО,	  
	  ПК_ГУИ_ОТКАЗ_ВКЛЮЧЕНИЯ_ПИТАНИЯ_ВАКУУМНОГО_НАСОСА,   /*   */
	  ПК_ГУИ_ОТКАЗ_ВЫКЛЮЧЕНИЯ_ПИТАНИЯ_ВАКУУМНОГО_НАСОСА,
	  ПК_ГУИ_ВЕНТИЛЯТОР_ВКЛЮЧЕН,
	  ПК_ГУИ_ВЕНТИЛЯТОР_ВЫКЛЮЧЕН,
	  ПК_ГУИ_СПОНТАННОЕ_ВЫКЛЮЧЕНИЕ_ВЕНТИЛЯТОРА,
	  ПК_ГУИ_СПОНТАННОЕ_ВКЛЮЧЕНИЕ_ВЕНТИЛЯТОРА,
	  ПК_ГУИ_ЗАТВОР_ВАКУУМНЫЙ_ОТКРЫТ,
	  ПК_ГУИ_ЗАТВОР_ВАКУУМНЫЙ_ЗАКРЫТ,
	  ПК_ГУИ_СПОНТАННОЕ_ОТКРЫТИЕ_ЗАТ_ВАКУУМНОГО,
	  ПК_ГУИ_СПОНТАННОЕ_ЗАКРЫТИЕ_ЗАТ_ВАКУУМНОГО,
	  ПК_ГУИ_САПУН_ОТКРЫТ,
	  ПК_ГУИ_САПУН_ЗАКРЫТ,
	  ПК_ГУИ_СПОНТАННОЕ_ОТКРЫТИЕ_САПУНА,
	  ПК_ГУИ_СПОНТАННОЕ_ЗАКРЫТИЕ_САПУНА,
	  ПК_ГУИ_ПИТАНИЕ_ВАКУУМНОГО_НАСОСА_ВКЛЮЧЕНО,
	  ПК_ГУИ_ПИТАНИЕ_ВАКУУМНОГО_НАСОСА_ВЫКЛЮЧЕНО,
	  ПК_ГУИ_СПОНТАННОЕ_ВКЛЮЧЕНИЕ_ВАКУУМНОГО_НАСОСА,
	  ПК_ГУИ_СПОНТАННОЕ_ВЫКЛЮЧЕНИЕ_ВАКУУМНОГО_НАСОСА,
	  ПК_ГУИ_ПИТАНИЕ_РЕГУЛЯТОРА_ТЕМПЕРАТУРЫ_ВКЛЮЧЕНО,
	  ПК_ГУИ_ПИТАНИЕ_РЕГУЛЯТОРА_ТЕМПЕРАТУРЫ_ВЫКЛЮЧЕНО,
  	  ПК_ГУИ_СПОНТАННОЕ_ВКЛЮЧЕНИЕ_РЕГУЛЯТОРА_ТЕМПЕРАТУРЫ,
	  ПК_ГУИ_СПОНТАННОЕ_ВЫКЛЮЧЕНИЕ_РЕГУЛЯТОРА_ТЕМПЕРАТУРЫ,
	  ПК_ГУИ_ПИТАНИЕ_РЕГУЛЯТОРА_УРОВНЯ_ВКЛЮЧЕНО,
	  ПК_ГУИ_ПИТАНИЕ_РЕГУЛЯТОРА_УРОВНЯ_ВЫКЛЮЧЕНО,
	  ПК_ГУИ_СПОНТАННОЕ_ВКЛЮЧЕНИЕ_РЕГУЛЯТОРА_УРОВНЯ,
	  ПК_ГУИ_СПОНТАННОЕ_ВЫКЛЮЧЕНИЕ_РЕГУЛЯТОРА_УРОВНЯ,
	  ПК_ГУИ_ПИТАНИЕ_НАСОСА_ОХЛАЖДЕНИЯ_ВКЛЮЧЕНО,
	  ПК_ГУИ_ПИТАНИЕ_НАСОСА_ОХЛАЖДЕНИЯ_ВЫКЛЮЧЕНО,
	  ПК_ГУИ_СПОНТАННОЕ_ВКЛЮЧЕНИЕ_НАСОСА_ОХЛАЖДЕНИЯ,
	  ПК_ГУИ_СПОНТАННОЕ_ВЫКЛЮЧЕНИЕ_НАСОСА_ОХЛАЖДЕНИЯ,
	  ПК_ГУИ_УСТРОЙСТВО_ПЛАВНОГО_ПУСКА_ВКЛЮЧЕНО,
	  ПК_ГУИ_УСТРОЙСТВО_ПЛАВНОГО_ПУСКА_ВЫКЛЮЧЕНО,
	  ПК_ГУИ_СПОНТАННОЕ_ВКЛЮЧЕНИЕ_УСТРОЙСТВА_ПЛАВНОГО_ПУСКА,
	  ПК_ГУИ_СПОНТАННОЕ_ВЫКЛЮЧЕНИЕ_УСТРОЙСТВА_ПЛАВНОГО_ПУСКА,
	  ПК_ГУИ_ОТКАЗ_ВАКУУМИРОВАНИЯ,
	  ПК_ГУИ_ОТКАЗ_ВКЛЮЧЕНИЯ_ВАКУУМНОЙ_ПОДСИСТЕМЫ,
	  ПК_ГУИ_ОТКАЗ_ВЫКЛЮЧЕНИЯ_ВАКУУМНОЙ_ПОДСИСТЕМЫ,
	  ПК_ГУИ_ЕСТЬ_ДОСТОВЕРНОСТЬ_КОНТРОЛЯ_ОТКРЫТИЯ_ЗАТВОРА_ВАКУУМНОГО,
      ПК_ГУИ_НЕТ_ДОСТОВЕРНОСТИ_КОНТРОЛЯ_ОТКРЫТИЯ_ЗАТВОРА_ВАКУУМНОГО,
	  ПК_ГУИ_ЕСТЬ_ДОСТОВЕРНОСТЬ_КОНТРОЛЯ_ЗАКРЫТИЯ_ЗАТВОРА_ВАКУУМНОГО,
	  ПК_ГУИ_НЕТ_ДОСТОВЕРНОСТИ_КОНТРОЛЯ_ЗАКРЫТИЯ_ЗАТВОРА_ВАКУУМНОГО,
      ПК_ГУИ_ЕСТЬ_ДОСТОВЕРНОСТЬ_КОНТРОЛЯ_САПУНА,
	  ПК_ГУИ_НЕТ_ДОСТОВЕРНОСТИ_КОНТРОЛЯ_САПУНА,
	  ПК_ГУИ_ЕСТЬ_ДОСТОВЕРНОСТЬ_КОНТРОЛЯ_УСТР_ПЛАВ_ПУСКА,
	  ПК_ГУИ_НЕТ_ДОСТОВЕРНОСТИ_КОНТРОЛЯ_УСТР_ПЛАВ_ПУСКА,
	  ПК_ГУИ_ЕСТЬ_ДОСТОВЕРНОСТЬ_КОНТРОЛЯ_ПИТ_ВАК_НАСОСА,
	  ПК_ГУИ_НЕТ_ДОСТОВЕРНОСТИ_КОНТРОЛЯ_ПИТ_ВАК_НАСОСА,
	  ПК_ГУИ_ЕСТЬ_ДОСТОВЕРНОСТЬ_КОНТРОЛЯ_ПИТ_ВЕНТИЛЯТОРА,
	  ПК_ГУИ_НЕТ_ДОСТОВЕРНОСТИ_КОНТРОЛЯ_ПИТ_ВЕНТИЛЯТОРА
};


/*===========================================================*/
/*========= КОДЫ СООБЩЕНИЙ ИЗМЕНЕНИЙ ФП         =============*/
/*===========================================================*/
ПЕРЕЧИСЛЕНИЕ {                    
      ПК_ГУИ_ФП_P_ВбакеКомпрессора = ПК_ГУИ_НЕТ_ДОСТОВЕРНОСТИ_КОНТРОЛЯ_ПИТ_ВЕНТИЛЯТОРА + 1,
      ПК_ГУИ_ФП_P_Впатрубке,
      ПК_ГУИ_ФП_T_Врубашке,
      ПК_ГУИ_ФП_T_Вбаке,
      ПК_ГУИ_ФП_P_ВтрубеСидер
      
};
/*===========================================================*/
/*= КОДЫ КОМАНД ИЗМЕНЕНИЯ НАСТРОЕЧНЫХ ПАРАМЕТРОВ   ==========*/
/*===========================================================*/
ПЕРЕЧИСЛЕНИЕ {
ИЗМН_НП_1 = ПК_ГУИ_ФП_P_ВтрубеСидер + 1,
ИЗМН_НП_P_ВбакеКомпрессора = ИЗМН_НП_1,
ИЗМН_НП_Pатмосф,
ИЗМН_НП_ГлубокийВакуумТрубы,

ИЗМН_НП_N
};



/*===========================================================*/
/*========= КОДЫ СООБЩЕНИЙ  О НЕВЕРНОЙ КОМАНДЕ ==============*/
/*===========================================================*/


ПЕРЕЧИСЛЕНИЕ {
     ПК_ГУИ_ОШИБКА_КОДА_КОМАНДЫ_ОТ_ГУИ = ИЗМН_НП_N + 1  
}; 

/*###########################################################*/
/*###########################################################*/
/*########                                          #########*/
/*########  ОПИСАНИЕ ФУНКЦИЙ                        #########*/
/*########                                          #########*/
/*########                                          #########*/
/*###########################################################*/

/*:::::::::Функции для работы  с ГУИ :::::::::::::::::::::   */

/*чтение кода и параметра события от ПИВ из кольцевого буфера в память */
ФУНКЦИЯ ЦЕЛ GetNextMsgGUI(ПУСТО);

ФУНКЦИЯ ЦЕЛ  GetMsgGUICode        (ПУСТО);  /*чтение из памяти кода события */
ФУНКЦИЯ ПЛАВ GetFloatParamGUI     (ПУСТО);  /*чтение из памяти параметра события  используется */
/* ФУНКЦИЯ КЦЕЛ GetShortParamGUI     (ПУСТО); */
ФУНКЦИЯ ЦЕЛ  GetIntParamGUI       (ПУСТО);  /*чтение с экрана гуи - не исползуется */
ФУНКЦИЯ ДЦЕЛ GetLongParamGUI      (ПУСТО);  /*чтение с экрана гуи - исползуется */

ФУНКЦИЯ ЦЕЛ  SendMsgGUICode       (ЦЕЛ);
ФУНКЦИЯ ЦЕЛ  SendMsgFloatParamGUI (ЦЕЛ, ПЛАВ);
/* ФУНКЦИЯ ЦЕЛ  SendMsgShortParamGUI (ЦЕЛ, КЦЕЛ); */
/* ФУНКЦИЯ ЦЕЛ  SendMsgIntParamGUI   (ЦЕЛ, ЦЕЛ); */
ФУНКЦИЯ ЦЕЛ  SendMsgLongParamGUI  (ЦЕЛ, ДЦЕЛ);

ФУНКЦИЯ ЦЕЛ  InitChannelStatus_GUI (ПУСТО); /* инициализация статуса,
обнуление числа ошибок */
ФУНКЦИЯ ЦЕЛ  GetChannelStatus_GUI  (ПУСТО);  /* чтение статуса канала 0/1 - не исползуется*/
ФУНКЦИЯ ЦЕЛ  GetErrorNumberCS_GUI  (ПУСТО);  /* чтение числа ошибок КС  - не исползуется*/
ФУНКЦИЯ ЦЕЛ  GetErrorNumberTO_GUI  (ПУСТО);  /* чтение числа ошибок по тайм ауту - не исползуется*/
ФУНКЦИЯ ЦЕЛ  GetInpMsgNumber_GUI (ПУСТО);  /* чтение числа сообщ. во входн. буфере - не исползуется*/
ФУНКЦИЯ ЦЕЛ  GetOutMsgNumber_GUI (ПУСТО); /* чтение числа сообщ. в выходн. буфере -  не исползуется*/

ФУНКЦИЯ ПЛАВ GetFP_P_BAK_COMPRESSOR		(ПУСТО);	/* чтение давления в баке компрессора */ 
ФУНКЦИЯ ПЛАВ GetFP_P_PATRUBKA			(ПУСТО);   /* чтение давления  в патрубке      */
ФУНКЦИЯ ПЛАВ GetFP_T_RUBACHKA			(ПУСТО);	/* чтение температуры воды в рубашке охлаждения */
ФУНКЦИЯ ПЛАВ GetFP_T_BAK				(ПУСТО);	/* чтение температуры воды в баке охлаждения */ 
ФУНКЦИЯ ПЛАВ GetFP_P_TRUBA				(ПУСТО);	/* чтение давления в трубе сидера */


/*###########################################################*/
/*########                                          #########*/
/*########  ОПИСАНИЕ РЕГИСТРОВ МОДУЛЕЙ УСО          #########*/
/*########  (устройство связи с объектом)           #########*/
/*###########################################################*/

ВХОД  ЛОГ_ВХОД_ОТКР_ЗАТВОР_ВАК  0 0 8; /* имя, базовый адрес, доп. адр. порта, 8бит */
ВХОД  ЛОГ_ВХОД_ЗАКР_ЗАТВОР_ВАК  0 1 8; /* имя, базовый адрес, доп. адр. порта, 8бит */
ВХОД  ЛОГ_ВХОД_САПУН  			0 2 8; /* имя, базовый адрес, доп. адр. порта, 8бит */
ВХОД  ЛОГ_ВХОД_УСТР_ПЛАВ_ПУСКА  0 3 8; /* имя, базовый адрес, доп. адр. порта, 8бит */
ВХОД  ЛОГ_ВХОД_ПИТ_ВАК_НАСОСА  	0 4 8; /* имя, базовый адрес, доп. адр. порта, 8бит */
ВХОД  ЛОГ_ВХОД_ПИТ_ВЕНТИЛЯТОРА  0 5 8; /* имя, базовый адрес, доп. адр. порта, 8бит */
ВХОД  ЛОГ_ВХОД_ПИТ_РЕГ_Т  		0 6 8; /* имя, базовый адрес, доп. адр. порта, 8бит */
ВХОД  ЛОГ_ВХОД_ПИТ_РЕГ_УР  		0 7 8; /* имя, базовый адрес, доп. адр. порта, 8бит */
ВХОД  ЛОГ_ВХОД_УР_ВОДЫ_MIN  	0 8 8; /* имя, базовый адрес, доп. адр. порта, 8бит */
ВХОД  ЛОГ_ВХОД_УР_ВОДЫ_MAX  	0 9 8; /* имя, базовый адрес, доп. адр. порта, 8бит */
ВХОД  ЛОГ_ВХОД_ПИТ_НАСОС_ОХЛ  	0 10 8; /* имя, базовый адрес, доп. адр. порта, 8бит */


/* ВХОД АЦП_T_Вбаке		 			0 66 16; */  /* регистр АЦП */
/*ВХОД АЦП_T_Врубашке	 			0 67 16; */ /* регистр АЦП */
/*ВХОД АЦП_P_ВтрубеСидер	 		0 68 16; */ /* регистр АЦП */
/*ВХОД АЦП_P_Впатрубке 				0 69 16; */ /*   регистр АЦП */
/*ВХОД АЦП_P_ВбакеКомпрессора 		0 70 16; */ /* регистр АЦП */


ВЫХОД  ЛОГ_ВЫХОД_GPIO20  1 0 8; /* имя, базовый адрес, доп. адр. порта, 8бит */
/*ВЫХОД  ЛОГ_ВЫХОД_GPIO21  1 1 8; */ /* имя, базовый адрес, доп. адр. порта, 8бит */


/*###########################################################*/
/*########                                          #########*/
/*########  ПРОЦЕССЫ                                #########*/
/*########                                          #########*/
/*###########################################################*/

/*
 * Процесс Инициализация. Служит для
 * развертывания программы. Этот процесс (описанный
 * первым) - единственно активный процесс по запуску.
 * Кроме этого, процесс Инициализация содержит описание 
 * переменных для ссылок из других
 * процессов. Это удобно: описания локализованы в одном
 * месте.
*/

ПРОЦ Инициализация{
/*===========================================================*/
/*=========== ОПИСАНИЕ ПЕРЕМЕННЫХ ===========================*/
/*===========================================================*/

/* ВХОДНЫЕ СИГНАЛЫ: */
/*-----------------*/


ЛОГ К_ОТКР_ЗАТВОР_ВАК             	= {ЛОГ_ВХОД_ОТКР_ЗАТВОР_ВАК[1]} ДЛЯ ВСЕХ; /*  концевик затвора вакуумного*/
ЛОГ Д_ОТКР_ЗАТВОР_ВАК             	= {ЛОГ_ВХОД_ОТКР_ЗАТВОР_ВАК[1]} ДЛЯ ВСЕХ; /*  достоверность*/

ЛОГ К_ЗАКР_ЗАТВОР_ВАК             	= {ЛОГ_ВХОД_ЗАКР_ЗАТВОР_ВАК[1]} ДЛЯ ВСЕХ; /*  концевик затвора вакуумного*/
ЛОГ Д_ЗАКР_ЗАТВОР_ВАК             	= {ЛОГ_ВХОД_ЗАКР_ЗАТВОР_ВАК[1]} ДЛЯ ВСЕХ;

ЛОГ К_САПУН                         = {ЛОГ_ВХОД_САПУН[1]} ДЛЯ ВСЕХ;
ЛОГ Д_САПУН                         = {ЛОГ_ВХОД_САПУН[1]} ДЛЯ ВСЕХ;

ЛОГ К_УСТР_ПЛАВ_ПУСКА				= {ЛОГ_ВХОД_УСТР_ПЛАВ_ПУСКА[1]} ДЛЯ ВСЕХ;
ЛОГ Д_УСТР_ПЛАВ_ПУСКА				= {ЛОГ_ВХОД_УСТР_ПЛАВ_ПУСКА[1]} ДЛЯ ВСЕХ;

ЛОГ К_ПИТ_ВАК_НАСОСА			 	= {ЛОГ_ВХОД_ПИТ_ВАК_НАСОСА[1]} ДЛЯ ВСЕХ; 
ЛОГ Д_ПИТ_ВАК_НАСОСА			 	= {ЛОГ_ВХОД_ПИТ_ВАК_НАСОСА[1]} ДЛЯ ВСЕХ; 

ЛОГ К_ПИТ_ВЕНТИЛЯТОРА               = {ЛОГ_ВХОД_ПИТ_ВЕНТИЛЯТОРА[1]} ДЛЯ ВСЕХ; 
ЛОГ Д_ПИТ_ВЕНТИЛЯТОРА               = {ЛОГ_ВХОД_ПИТ_ВЕНТИЛЯТОРА[1]} ДЛЯ ВСЕХ; 

ЛОГ К_ПИТ_РЕГ_Т 			        = {ЛОГ_ВХОД_ПИТ_РЕГ_Т[1]} ДЛЯ ВСЕХ;
ЛОГ Д_ПИТ_РЕГ_Т 			        = {ЛОГ_ВХОД_ПИТ_РЕГ_Т[1]} ДЛЯ ВСЕХ;

ЛОГ К_ПИТ_РЕГ_УР     				= {ЛОГ_ВХОД_ПИТ_РЕГ_УР[1]} ДЛЯ ВСЕХ;
ЛОГ Д_ПИТ_РЕГ_УР     				= {ЛОГ_ВХОД_ПИТ_РЕГ_УР[1]} ДЛЯ ВСЕХ;

ЛОГ К_УР_ВОДЫ_MIN  					= {ЛОГ_ВХОД_УР_ВОДЫ_MIN[1]} ДЛЯ ВСЕХ;
ЛОГ Д_УР_ВОДЫ_MIN  					= {ЛОГ_ВХОД_УР_ВОДЫ_MIN[1]} ДЛЯ ВСЕХ;

ЛОГ К_УР_ВОДЫ_MAX   				= {ЛОГ_ВХОД_УР_ВОДЫ_MAX[1]} ДЛЯ ВСЕХ;
ЛОГ Д_УР_ВОДЫ_MAX   				= {ЛОГ_ВХОД_УР_ВОДЫ_MAX[1]} ДЛЯ ВСЕХ;

ЛОГ К_ПИТ_НАСОС_ОХЛ 				= {ЛОГ_ВХОД_ПИТ_НАСОС_ОХЛ[1]} ДЛЯ ВСЕХ;
ЛОГ Д_ПИТ_НАСОС_ОХЛ 				= {ЛОГ_ВХОД_ПИТ_НАСОС_ОХЛ[1]} ДЛЯ ВСЕХ;


/* ВЫХОДНЫЕ СИГНАЛЫ (т.к. привязаны к модулю выходов): */ 

ЛОГ У_ПИТ_ВЕНТИЛЯТОРА   			= {ЛОГ_ВЫХОД_GPIO20[1]} ДЛЯ ВСЕХ; 
ЛОГ У_ПИТ_РЕГ_Т      				= {ЛОГ_ВЫХОД_GPIO20[1]} ДЛЯ ВСЕХ;            
ЛОГ У_ПИТ_НАС_ОХЛ					= {ЛОГ_ВЫХОД_GPIO20[1]} ДЛЯ ВСЕХ; 
ЛОГ У_ПИТ_ВАК_НАСОСА       			= {ЛОГ_ВЫХОД_GPIO20[1]} ДЛЯ ВСЕХ;                    
ЛОГ У_ЗАТВОР_ВАК					= {ЛОГ_ВЫХОД_GPIO20[1]} ДЛЯ ВСЕХ; 
ЛОГ У_САПУН 						= {ЛОГ_ВЫХОД_GPIO20[1]} ДЛЯ ВСЕХ;
ЛОГ У_УСТР_ПЛАВ_ПУСКА        		= {ЛОГ_ВЫХОД_GPIO20[1]} ДЛЯ ВСЕХ;                   
ЛОГ У_ПИТ_РЕГ_УР      				= {ЛОГ_ВЫХОД_GPIO20[1]} ДЛЯ ВСЕХ; 
	
/* ЛОГ ОШИБКА_ФУНКЦИОНИРОВАНИЯ  		= {ЛОГ_ВЫХОД_GPIO5[1]} ДЛЯ ВСЕХ; */ /*резерв */
/* предложение Саши Курочкина: ввести ошибку функционирования для контроля всех устройств, */
/* составляющих вакуумную подсистему. Если Саша не достучится до них, или две единички одновременно */
/* увидит у себя (ВКЛ/ВЫКЛ, например вентилятор). Правда, что в этом случае делать D2 - непонятно*/
/* Предлагается ошибку функционирования обнулять перед каждым запуском соответствующего процесса */ 
/* например: ВКЛ_ПИТ_ВЕНТИЛЯТОР, ВЫК_ПИТ_ВЕНТИЛЯТОР*/
/* И еще, для каждой ошибки функционирования будет int - код ошибки */
/* И еще, Саша говорит, что при обнаружении ош. функционирования надо прекращать вакуумирование ???*/

/*ЛОГ ОШИБКА_ФУНКЦ_ЗАТВОР_ВАК 			= {ЛОГ_ВХОД_GPIO6[1]} ДЛЯ ВСЕХ;*/
/*ЛОГ ОШИБКА_ФУНКЦ_САПУН 					= {ЛОГ_ВХОД_GPIO6[1]} ДЛЯ ВСЕХ;*/
/*ЛОГ ОШИБКА_ФУНКЦ_УСТР_ПЛАВ_ПУСКА 		= {ЛОГ_ВХОД_GPIO6[1]} ДЛЯ ВСЕХ;*/
/*ЛОГ ОШИБКА_ФУНКЦ_ПИТ_ВАК_НАСОСА 		= {ЛОГ_ВХОД_GPIO6[1]} ДЛЯ ВСЕХ;*/
/*ЛОГ ОШИБКА_ФУНКЦ_ВЕНТИЛЯТОР 			= {ЛОГ_ВХОД_GPIO6[1]} ДЛЯ ВСЕХ;*/
/*ЛОГ ОШИБКА_ФУНКЦ_ПИТ_РЕГ_Т 				= {ЛОГ_ВХОД_GPIO6[1]} ДЛЯ ВСЕХ;*/
/*ЛОГ ОШИБКА_ФУНКЦ_ПИТ_РЕГ_УР 			= {ЛОГ_ВХОД_GPIO6[1]} ДЛЯ ВСЕХ;*/
/*ЛОГ ОШИБКА_ФУНКЦ_НАСОС_ОХЛ 				= {ЛОГ_ВХОД_GPIO6[1]} ДЛЯ ВСЕХ;*/


/*===========================================================*/
/*=========== ЯЧЕЙКИ ИНФОРМАЦИОННЫХ КАНАЛОВ =================*/
/*===========================================================*/
ЦЕЛ КодСобытия_от_ГУИ        ДЛЯ ВСЕХ;
ЦЕЛ КодСобытия_к_ГУИ         ДЛЯ ВСЕХ;

  
  
/*===========================================================*/
/*=========== ЯЧЕЙКИ ОБЩЕГО УПРАВЛЕНИЯ ======================*/
/*===========================================================*/
ЛОГ ФлагГотовУрВодыБО ДЛЯ ВСЕХ;  /* Флаг установлен, если уровень воды в баке охлаждения в норме*/ 
ЛОГ ФлагГотовТемпВодыБО ДЛЯ ВСЕХ;  /* Флаг установлен, если  температура в баке охлаждения в норме*/ 
ЛОГ ФлагГотовTВодыРО ДЛЯ ВСЕХ;  /* Флаг установлен, если  температура в рубашке охлаждения в норме*/ 
ЛОГ ФлагГотовВакуумаВТ ДЛЯ ВСЕХ;
ЛОГ ФлагОткачкиТрубыСидеростата ДЛЯ ВСЕХ;
ЛОГ ФлагДавленияКомпрессора ДЛЯ ВСЕХ;

ДЦЕЛ ЗАДЕРЖКА_НА_РАЗГРУЗКУ_ТРАФИКА ДЛЯ ВСЕХ; /* ????????чтобы в отмену не уходить */


/*===========================================================*/
/*=========== ФАКТИЧЕСКИЕ ПАРАМЕТРЫ =========================*/
/*===========================================================*/
/*ПЛАВ ФП_Pтрубы ДЛЯ ВСЕХ;*/
/* ????????????????????????????????????????????????????????????????? */
/*  Саша Курочкин будет ацепировать */
/*ЦЕЛ	ФП_P_ВбакеКомпрессора 	= {P_ВбакеКомпрессора [16]} 	ДЛЯ ВСЕХ; */
/*ЦЕЛ	ФП_P_Впатрубке	 		= {P_Впатрубке [16]}			ДЛЯ ВСЕХ; 	*/
/*ЦЕЛ	ФП_T_Врубашке 			= {T_Врубашке [16]}				ДЛЯ ВСЕХ; 	*/
/*ЦЕЛ	ФП_T_Вбаке 				= {T_Вбаке [16]}				ДЛЯ ВСЕХ;	*/ 
/*ЦЕЛ	ФП_P_ВтрубеСидер 		= {P_ВтрубеСидер [16]} 			ДЛЯ ВСЕХ; 	*/


ПЛАВ ФП_P_ВбакеКомпрессора ДЛЯ ВСЕХ; 
ПЛАВ ФП_P_Впатрубке ДЛЯ ВСЕХ;
ПЛАВ ФП_T_Врубашке ДЛЯ ВСЕХ;	
ПЛАВ ФП_T_Вбаке ДЛЯ ВСЕХ;

/*ПЛАВ ЗД_P_ВтрубеСидер ДЛЯ ВСЕХ;	*/
ПЛАВ ФП_P_ВтрубеСидер ДЛЯ ВСЕХ;
/*===========================================================*/
/*============       НАСТРОЕЧНЫЕ ПАРАМЕТРЫ        ===========*/
/*===========================================================*/
ПЛАВ НП_P_ВбакеКомпрессора  ДЛЯ ВСЕХ;
ПЛАВ НП_Pатмосф             ДЛЯ ВСЕХ;
ПЛАВ НП_ГлубокийВакуумТрубы ДЛЯ ВСЕХ;


/*########  СОСТОЯНИЯ ПРОЦЕССА  #########*/
 СОСТ Начало{  
	ЕСЛИ (Д_ОТКР_ЗАТВОР_ВАК)  В СЛЕДУЮЩЕЕ;  /* проверяю один параметр. Все будут достоверны или наоборот*/
 }

СОСТ НачалоРаботы{	
/*    ОШИБКА_ФУНКЦИОНИРОВАНИЯ = ВЫК;  */
    ЗАДЕРЖКА_НА_РАЗГРУЗКУ_ТРАФИКА = 1; /* параметр регулирует частоту подачи */
	                                   /* сообщений в зависимости от заполненности буфера сообщений... */
/*===========================================================*/
/*=================== ИНИЦИАЛИЗАЦИЯ ФЛАГОВ   ================*/
/*===========================================================*/	
/*	ФлагГотовУрВодыБО  = ВНЕ_НОРМЫ;  */
/*	ФлагГотовТемпВодыБО = ВНЕ_НОРМЫ; */
/*	ФлагГотовTВодыРО = ВНЕ_НОРМЫ;  */
/*	ФлагГотовВакуумаВТ = ВНЕ_НОРМЫ; */
/*	ФлагОткачкиТрубыСидеростата = ВНЕ_НОРМЫ; */
/*	ФлагДавленияКомпрессора = ВНЕ_НОРМЫ; */
/*===========================================================*/


/*	У_ПИТ_ВЕНТИЛЯТОРА   			= ВЫК; */
/*	У_ПИТ_РЕГ_Т      				= ВЫК;  */          
/*	У_ПИТ_НАС_ОХЛ					= ВЫК; */
/*	У_ПИТ_ВАК_НАСОСА       			= ВЫК;  */                  
/*	У_ЗАТВОР_ВАК					= ЗАКР; */
/*	У_САПУН 						= ЗАКР; */
/*	У_УСТР_ПЛАВ_ПУСКА        		= ВЫК;  */                 
/*	У_ПИТ_РЕГ_УР      				= ВЫК; */

/*===========================================================*/
/*========= ИНИЦИАЛИЗАЦИЯ НАСТРОЕЧНЫХ ПАРАМЕТРОВ   ==========*/
/*===========================================================*/

	НП_P_ВбакеКомпрессора = 8; /*атмосфер (бар) */
	НП_Pатмосф              = 760.00;
	НП_ГлубокийВакуумТрубы =  0.02; /*мм. рт. ст*/



/* СТАРТ ПРОЦ ЧтениеАЦП; */
    СТАРТ ПРОЦ РЕГУЛЯТОР_ТРАФИКА;
	СТАРТ ПРОЦ ЧтениеФактПараметров_с_Датчиков;
	СТАРТ ПРОЦ ЧтениеКольцевогоБуфера;
	СТАРТ ПРОЦ КонтрольИзменения_ФП_P_ВбакеКомпрессора;
	СТАРТ ПРОЦ КонтрольИзменения_ФП_P_Впатрубке;
	СТАРТ ПРОЦ КонтрольИзменения_ФП_T_Врубашке;
	СТАРТ ПРОЦ КонтрольИзменения_ФП_T_Вбаке;
	СТАРТ ПРОЦ КонтрольИзменения_ФП_P_ВтрубеСидер;

	ЕСЛИ (!К_ПИТ_РЕГ_Т) СТАРТ ПРОЦ ВКЛ_ПИТ_РЕГ_Т;   /* питание всегда включено. На всякий случай */
	
	СТАРТ ПРОЦ ВКЛ_ПИТ_РЕГ_УР;

	
	СТАРТ ПРОЦ КонтрольУровняВодыБакаОхлаждения;
	СТАРТ ПРОЦ КонтрольТемпературыРубашкиОхлаждения;
	СТАРТ ПРОЦ КонтрольДавленияВбакеКомпрессора;
	СТАРТ ПРОЦ КонтрольГотовностиВакуума_в_Вакуумной_Трубе;	
	СТАРТ ПРОЦ КонтрольТемпературыВодыБакаОхлаждения;	
/*	СТАРТ ПРОЦ КонтрольОткачкиВауумнойТрубыСидеростата;	*/
/*контроль откачки из вакуумной трубы запускаем в процессе ВКЛ_ВАКУУМНУЮ_ПОДСИСТЕМУ  */
/*  останавливаем в процессе ВЫК_ВАКУУМНУЮ_ПОДСИСТЕМУ, если затвор вакуумный закрылся*/
	СТОП;
   }
}
/*==============не используется   представлен как вариант===*/
/*== Процессы контроля достоверности параметров К       ==*/
/*      от Курочкина  (от девайсиков)  непрерывно выдает сообщения    */
/*========================================================*/
ПРОЦ КонтрольДостоверностиОткрытияЗатвораВакуумного{
 ИЗ ПРОЦ Инициализация  ЗАДЕРЖКА_НА_РАЗГРУЗКУ_ТРАФИКА,
                        Д_ОТКР_ЗАТВОР_ВАК;

 СОСТ Начало{
    ЕСЛИ (Д_ОТКР_ЗАТВОР_ВАК) {
         ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ЕСТЬ_ДОСТОВЕРНОСТЬ_КОНТРОЛЯ_ОТКРЫТИЯ_ЗАТВОРА_ВАКУУМНОГО))  В СЛЕДУЮЩЕЕ;
    } ИНАЧЕ {
         ЕСЛИ (SendMsgGUICode(ПК_ГУИ_НЕТ_ДОСТОВЕРНОСТИ_КОНТРОЛЯ_ОТКРЫТИЯ_ЗАТВОРА_ВАКУУМНОГО))  В СОСТ СменаСостоянияВЫКЛ;
    }
    ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }

 СОСТ СменаСостоянияВКЛ{
    ЕСЛИ (!Д_ОТКР_ЗАТВОР_ВАК)  В СОСТ ТрафикПауза;
 }

 СОСТ СменаСостоянияВЫКЛ{
    ЕСЛИ (Д_ОТКР_ЗАТВОР_ВАК)  В СОСТ ТрафикПауза;
 }
 СОСТ ТрафикПауза{
    ТАЙМАУТ ЗАДЕРЖКА_НА_РАЗГРУЗКУ_ТРАФИКА В СОСТ Начало;
 }
/*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ НарушениеСвязи {
      В СОСТ Начало;
 }
}
/*========================================================*/
/*== Процессы контроля достоверности параметров К       ==*/
/*      от Курочкина  (от девайсиков)                    */
/*========================================================*/
ПРОЦ КонтрольДостоверностиОткрЗатВакуумного{
 ИЗ ПРОЦ Инициализация  Д_ОТКР_ЗАТВОР_ВАК;

 СОСТ Начало {
      ЕСЛИ (Д_ОТКР_ЗАТВОР_ВАК == ДОСТОВЕРНО) В СЛЕДУЮЩЕЕ;
      ИНАЧЕ В СОСТ СообК_ГУИ_НЕТ_ДОСТОВЕРНОСТИ;
 }
 СОСТ СообК_ГУИ_ЕСТЬ_ДОСТОВЕРНОСТЬ {
     ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ЕСТЬ_ДОСТОВЕРНОСТЬ_КОНТРОЛЯ_ОТКРЫТИЯ_ЗАТВОРА_ВАКУУМНОГО)) В СЛЕДУЮЩЕЕ;
     ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
 СОСТ ОжиданиеНедостоверности {
      ЕСЛИ (Д_ОТКР_ЗАТВОР_ВАК == НЕДОСТОВЕРНО) В СЛЕДУЮЩЕЕ;
 }
 СОСТ СообК_ГУИ_НЕТ_ДОСТОВЕРНОСТИ {
     ЕСЛИ (SendMsgGUICode(ПК_ГУИ_НЕТ_ДОСТОВЕРНОСТИ_КОНТРОЛЯ_ОТКРЫТИЯ_ЗАТВОРА_ВАКУУМНОГО)) В СЛЕДУЮЩЕЕ;
     ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
 СОСТ ОжиданиеЕстьДостоверность {
      ЕСЛИ (Д_ОТКР_ЗАТВОР_ВАК == ДОСТОВЕРНО) В СОСТ СообК_ГУИ_ЕСТЬ_ДОСТОВЕРНОСТЬ;
 }
/*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ НарушениеСвязи {
	   В СОСТ Начало;
 }
}
/*===========================================ЗАКР_ЗАТВОР_ВАК=============*/
ПРОЦ КонтрольДостоверностиЗакрЗатВакуумного{
 ИЗ ПРОЦ Инициализация  Д_ЗАКР_ЗАТВОР_ВАК;

 СОСТ Начало {
      ЕСЛИ (Д_ЗАКР_ЗАТВОР_ВАК == ДОСТОВЕРНО) В СЛЕДУЮЩЕЕ;
      ИНАЧЕ В СОСТ СообК_ГУИ_НЕТ_ДОСТОВЕРНОСТИ;
 }
 СОСТ СообК_ГУИ_ЕСТЬ_ДОСТОВЕРНОСТЬ {
     ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ЕСТЬ_ДОСТОВЕРНОСТЬ_КОНТРОЛЯ_ЗАКРЫТИЯ_ЗАТВОРА_ВАКУУМНОГО)) В СЛЕДУЮЩЕЕ;
     ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
 СОСТ ОжиданиеНедостоверности {
      ЕСЛИ (Д_ЗАКР_ЗАТВОР_ВАК == НЕДОСТОВЕРНО) В СЛЕДУЮЩЕЕ;
 }
 СОСТ СообК_ГУИ_НЕТ_ДОСТОВЕРНОСТИ {
     ЕСЛИ (SendMsgGUICode(ПК_ГУИ_НЕТ_ДОСТОВЕРНОСТИ_КОНТРОЛЯ_ЗАКРЫТИЯ_ЗАТВОРА_ВАКУУМНОГО)) В СЛЕДУЮЩЕЕ;
     ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
 СОСТ ОжиданиеЕстьДостоверность {
      ЕСЛИ (Д_ЗАКР_ЗАТВОР_ВАК == ДОСТОВЕРНО) В СОСТ СообК_ГУИ_ЕСТЬ_ДОСТОВЕРНОСТЬ;
 }
/*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ НарушениеСвязи {
	   В СОСТ Начало;
 }
}
/*===========================================Д_САПУН=============*/
ПРОЦ КонтрольДостоверностиСостоянияСапуна{
 ИЗ ПРОЦ Инициализация  Д_САПУН;

 СОСТ Начало {
      ЕСЛИ (Д_САПУН == ДОСТОВЕРНО) В СЛЕДУЮЩЕЕ;
      ИНАЧЕ В СОСТ СообК_ГУИ_НЕТ_ДОСТОВЕРНОСТИ;
 }
 СОСТ СообК_ГУИ_ЕСТЬ_ДОСТОВЕРНОСТЬ {
     ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ЕСТЬ_ДОСТОВЕРНОСТЬ_КОНТРОЛЯ_САПУНА)) В СЛЕДУЮЩЕЕ;
     ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
 СОСТ ОжиданиеНедостоверности {
      ЕСЛИ (Д_САПУН == НЕДОСТОВЕРНО) В СЛЕДУЮЩЕЕ;
 }
 СОСТ СообК_ГУИ_НЕТ_ДОСТОВЕРНОСТИ {
     ЕСЛИ (SendMsgGUICode(ПК_ГУИ_НЕТ_ДОСТОВЕРНОСТИ_КОНТРОЛЯ_САПУНА)) В СЛЕДУЮЩЕЕ;
     ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
 СОСТ ОжиданиеЕстьДостоверность {
      ЕСЛИ (Д_САПУН == ДОСТОВЕРНО) В СОСТ СообК_ГУИ_ЕСТЬ_ДОСТОВЕРНОСТЬ;
 }
/*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ НарушениеСвязи {
	   В СОСТ Начало;
 }
}

/*===========================================Д_УСТР_ПЛАВ_ПУСКА=============*/
ПРОЦ КонтрольДостоверностиСостоянияУстройстваПлавПуска{
 ИЗ ПРОЦ Инициализация  Д_УСТР_ПЛАВ_ПУСКА;

 СОСТ Начало {
      ЕСЛИ (Д_УСТР_ПЛАВ_ПУСКА == ДОСТОВЕРНО) В СЛЕДУЮЩЕЕ;
      ИНАЧЕ В СОСТ СообК_ГУИ_НЕТ_ДОСТОВЕРНОСТИ;
 }
 СОСТ СообК_ГУИ_ЕСТЬ_ДОСТОВЕРНОСТЬ {
     ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ЕСТЬ_ДОСТОВЕРНОСТЬ_КОНТРОЛЯ_УСТР_ПЛАВ_ПУСКА)) В СЛЕДУЮЩЕЕ;
     ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
 СОСТ ОжиданиеНедостоверности {
      ЕСЛИ (Д_УСТР_ПЛАВ_ПУСКА == НЕДОСТОВЕРНО) В СЛЕДУЮЩЕЕ;
 }
 СОСТ СообК_ГУИ_НЕТ_ДОСТОВЕРНОСТИ {
     ЕСЛИ (SendMsgGUICode(ПК_ГУИ_НЕТ_ДОСТОВЕРНОСТИ_КОНТРОЛЯ_УСТР_ПЛАВ_ПУСКА)) В СЛЕДУЮЩЕЕ;
     ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
 СОСТ ОжиданиеЕстьДостоверность {
      ЕСЛИ (Д_УСТР_ПЛАВ_ПУСКА == ДОСТОВЕРНО) В СОСТ СообК_ГУИ_ЕСТЬ_ДОСТОВЕРНОСТЬ;
 }
/*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ НарушениеСвязи {
	   В СОСТ Начало;
 }
}
/*===========================================Д_ПИТ_ВАК_НАСОСА=============*/
ПРОЦ КонтрольДостоверностиСостоянияПитанияВакуумногоНасоса{
 ИЗ ПРОЦ Инициализация  Д_ПИТ_ВАК_НАСОСА;

 СОСТ Начало {
      ЕСЛИ (Д_ПИТ_ВАК_НАСОСА == ДОСТОВЕРНО) В СЛЕДУЮЩЕЕ;
      ИНАЧЕ В СОСТ СообК_ГУИ_НЕТ_ДОСТОВЕРНОСТИ;
 }
 СОСТ СообК_ГУИ_ЕСТЬ_ДОСТОВЕРНОСТЬ {
     ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ЕСТЬ_ДОСТОВЕРНОСТЬ_КОНТРОЛЯ_ПИТ_ВАК_НАСОСА)) В СЛЕДУЮЩЕЕ;
     ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
 СОСТ ОжиданиеНедостоверности {
      ЕСЛИ (Д_ПИТ_ВАК_НАСОСА == НЕДОСТОВЕРНО) В СЛЕДУЮЩЕЕ;
 }
 СОСТ СообК_ГУИ_НЕТ_ДОСТОВЕРНОСТИ {
     ЕСЛИ (SendMsgGUICode(ПК_ГУИ_НЕТ_ДОСТОВЕРНОСТИ_КОНТРОЛЯ_ПИТ_ВАК_НАСОСА)) В СЛЕДУЮЩЕЕ;
     ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
 СОСТ ОжиданиеЕстьДостоверность {
      ЕСЛИ (Д_ПИТ_ВАК_НАСОСА == ДОСТОВЕРНО) В СОСТ СообК_ГУИ_ЕСТЬ_ДОСТОВЕРНОСТЬ;
 }
/*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ НарушениеСвязи {
	   В СОСТ Начало;
 }
}
/*===========================================Д_ПИТ_ВЕНТИЛЯТОРА=============*/
ПРОЦ КонтрольДостоверностиСостоянияПитанияВентилятора{
 ИЗ ПРОЦ Инициализация  Д_ПИТ_ВЕНТИЛЯТОРА;

 СОСТ Начало {
      ЕСЛИ (Д_ПИТ_ВЕНТИЛЯТОРА == ДОСТОВЕРНО) В СЛЕДУЮЩЕЕ;
      ИНАЧЕ В СОСТ СообК_ГУИ_НЕТ_ДОСТОВЕРНОСТИ;
 }
 СОСТ СообК_ГУИ_ЕСТЬ_ДОСТОВЕРНОСТЬ {
     ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ЕСТЬ_ДОСТОВЕРНОСТЬ_КОНТРОЛЯ_ПИТ_ВЕНТИЛЯТОРА)) В СЛЕДУЮЩЕЕ;
     ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
 СОСТ ОжиданиеНедостоверности {
      ЕСЛИ (Д_ПИТ_ВЕНТИЛЯТОРА == НЕДОСТОВЕРНО) В СЛЕДУЮЩЕЕ;
 }
 СОСТ СообК_ГУИ_НЕТ_ДОСТОВЕРНОСТИ {
     ЕСЛИ (SendMsgGUICode(ПК_ГУИ_НЕТ_ДОСТОВЕРНОСТИ_КОНТРОЛЯ_ПИТ_ВЕНТИЛЯТОРА)) В СЛЕДУЮЩЕЕ;
     ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
 СОСТ ОжиданиеЕстьДостоверность {
      ЕСЛИ (Д_ПИТ_ВЕНТИЛЯТОРА == ДОСТОВЕРНО) В СОСТ СообК_ГУИ_ЕСТЬ_ДОСТОВЕРНОСТЬ;
 }
/*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ НарушениеСвязи {
	   В СОСТ Начало;
 }
}



/*Д_ЗАКР_ЗАТВОР_ВАК*/
/*Д_САПУН */
/*Д_УСТР_ПЛАВ_ПУСКА*/
/*Д_ПИТ_ВАК_НАСОСА*/
/*Д_ПИТ_ВЕНТИЛЯТОРА*/
/*Д_ПИТ_РЕГ_Т  (дописать бы надо)
Д_ПИТ_РЕГ_УР
Д_УР_ВОДЫ_MIN
Д_УР_ВОДЫ_MAX
Д_ПИТ_НАСОС_ОХЛ */


/*========================================================*/
/*                                                        */
/*==            ЧТЕНИЕ ФАКТИЧЕСКИХ ПАРАМЕТРОВ           ==*/
/*                                                        */
/*========================================================*/
ПРОЦ ЧтениеФактПараметров_с_Датчиков{
ИЗ ПРОЦ Инициализация ФП_P_ВбакеКомпрессора,
                      ФП_P_Впатрубке,
                      ФП_T_Врубашке,
                      ФП_T_Вбаке,
					  ФП_P_ВтрубеСидер;

 СОСТ Начало {
	ФП_P_ВбакеКомпрессора 	= GetFP_P_BAK_COMPRESSOR();     /* чтение давления в баке компрессора */
	ФП_P_Впатрубке 		= GetFP_P_PATRUBKA();     			/* чтение давления  в патрубке      */
	ФП_T_Врубашке 		= GetFP_T_RUBACHKA();     			/* чтение температуры воды в рубашке охлаждения */   
	ФП_T_Вбаке 			= GetFP_T_BAK();     				/* чтение температуры воды в баке охлаждения */ 
	ФП_P_ВтрубеСидер 	= GetFP_P_TRUBA();     				/* чтение давления в трубе сидера */ 
   В СОСТ Начало;
 }
}

/*=============================================================*/
/*=         РЕГУЛЯТОР ТРАФИКА                    ==============*/
/*=         ЕСЛИ КАНАЛЫ ЗАБИТЫ, ТО                           ==*/
/*=         УВЕЛИЧИВАЕТСЯ ВРЕМЯ МЕЖДУ                        ==*/
/*=         СЕРВЕРОМ И ВЫДАЧЕЙ В GUI                         ==*/
/*=         ФАКТИЧЕСКИХ ПАРАМЕТРОВ                           ==*/
/*= GetOutMsgNumber_GUI читает число сообщ. в выходн. буфере ==*/
/*=============================================================*/
ПРОЦ РЕГУЛЯТОР_ТРАФИКА{
 ИЗ ПРОЦ Инициализация ЗАДЕРЖКА_НА_РАЗГРУЗКУ_ТРАФИКА;
 СОСТ Начало{
    ЗАДЕРЖКА_НА_РАЗГРУЗКУ_ТРАФИКА = GetOutMsgNumber_GUI()/5 + 1;  /*динамический таймаут*/
    ЗАЦИКЛИТЬ;
 }
}






/*==============================================*/
/*==============================================*/
/*=                                            =*/
/*=      ПРОЦЕСС ЧтениеКольцевогоБуфера        =*/
/*=                                            =*/
/*==============================================*/
/*==============================================*/
ПРОЦ ЧтениеКольцевогоБуфера{
ИЗ ПРОЦ Инициализация
                        КодСобытия_от_ГУИ;       /* */

 СОСТ Начало{
    /*  читаем код и параметр события из кольцевого буфера в структуру */
    ЕСЛИ (GetNextMsgGUI()) {
      КодСобытия_от_ГУИ = GetMsgGUICode();
            ЕСЛИ ((КодСобытия_от_ГУИ >= КОМ_ГУИ_ОТКР_ЗАТВОР_ВАК) &&
                  (КодСобытия_от_ГУИ <= КОМАНДЫ_ОТ_ГУИ)) {
				СТАРТ ПРОЦ ОбработкаКомандУправления_от_ГУИ;
				В СОСТ КонтрольВыполненияК;
            } 
			ИНАЧЕ ЕСЛИ ((КодСобытия_от_ГУИ >= ИЗМН_НП_1) &&
                         (КодСобытия_от_ГУИ <= ИЗМН_НП_N)) {
                     СТАРТ ПРОЦ ОбработкаКомандИзмененияНП_от_ГУИ;
                     В СОСТ КонтрольИзмененияНП;
			}
			ИНАЧЕ В СОСТ НекорректноеСобытие;
    }
 } 
 
 СОСТ КонтрольВыполненияК{
    ЕСЛИ(ПРОЦ ОбработкаКомандУправления_от_ГУИ В СОСТ ПАССИВНОЕ)
    В СОСТ Начало;
 }
  СОСТ КонтрольИзмененияНП{
    ЕСЛИ(ПРОЦ ОбработкаКомандИзмененияНП_от_ГУИ В СОСТ ПАССИВНОЕ)
    В СОСТ Начало;
 }
  /*===== НЕНОРМАЛЬНЫЕ СИТУАЦИИ === */
  СОСТ НекорректноеСобытие{
     ЕСЛИ (SendMsgLongParamGUI(ПК_ГУИ_ОШИБКА_КОДА_КОМАНДЫ_ОТ_ГУИ, КодСобытия_от_ГУИ))
       В СОСТ Начало;
     ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }

 СОСТ НарушениеСвязи {
	В СОСТ Начало;
     СТОП;   /* м.б. в начало?!!!!!!!!!!!!!! что я и сделал*/
 }
}
/*===================================================*/
/*==                                               ==*/
/*==  ПРОЦЕСС ИЗМЕНЕНИЯ НАСТРОЕЧНЫХ ПАРАМЕТРОВ     ==*/
/*==                                               ==*/
/*===================================================*/
ПРОЦ ОбработкаКомандИзмененияНП_от_ГУИ{
ИЗ ПРОЦ Инициализация 	КодСобытия_от_ГУИ,
						КодСобытия_к_ГУИ,
						НП_Pатмосф,
						НП_ГлубокийВакуумТрубы,
						НП_P_ВбакеКомпрессора;
ПЛАВ ЛП_Параметр_к_ПИВ ЛОКАЛ;
 СОСТ Анализ{
    РАЗБОР (КодСобытия_от_ГУИ) {
	    СЛУЧАЙ ИЗМН_НП_Pатмосф:
          НП_Pатмосф  = GetFloatParamGUI();
          КодСобытия_к_ГУИ = ИЗМН_НП_Pатмосф;
          В СОСТ ПодтверждениеИзмененияНП;
			КОНЕЦ;
	    СЛУЧАЙ ИЗМН_НП_P_ВбакеКомпрессора:
		   НП_P_ВбакеКомпрессора  = GetFloatParamGUI();
          КодСобытия_к_ГУИ = НП_P_ВбакеКомпрессора;
          В СОСТ ПодтверждениеИзмененияНП;		   
			КОНЕЦ;
	    СЛУЧАЙ ИЗМН_НП_ГлубокийВакуумТрубы:
		   НП_ГлубокийВакуумТрубы  = GetFloatParamGUI();
          КодСобытия_к_ГУИ = НП_ГлубокийВакуумТрубы;
          В СОСТ ПодтверждениеИзмененияНП;		   
			КОНЕЦ;
		УМОЛЧАНИЕ:
          В СОСТ НекорректноеСобытие;
          КОНЕЦ;
    } /* конец разбора */
	СТОП;	

 }
 СОСТ ПодтверждениеИзмененияНП{
    ЕСЛИ (SendMsgLongParamGUI(КодСобытия_к_ГУИ, GetLongParamGUI())) СТОП;
    ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
 /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ НекорректноеСобытие {
    ЕСЛИ (SendMsgLongParamGUI(ПК_ГУИ_ОШИБКА_КОДА_КОМАНДЫ_ОТ_ГУИ, КодСобытия_от_ГУИ))
         СТОП;
    ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
  СОСТ НарушениеСвязи {
    ОШИБКА;
 }
}
/*=========================================*/
/*=                                       =*/
/*=     ОБРАБОТКА КОМАНД ОТ ГУИ           =*/
/*=                                       =*/
/*=========================================*/
ПРОЦ ОбработкаКомандУправления_от_ГУИ{
ИЗ ПРОЦ Инициализация
						/*	ТекущийРежим,*/
                    КодСобытия_от_ГУИ,
					К_УСТР_ПЛАВ_ПУСКА,
					ФлагГотовУрВодыБО,
					ФП_T_Вбаке,
					К_ПИТ_ВЕНТИЛЯТОРА,
					К_ПИТ_НАСОС_ОХЛ,
					К_ОТКР_ЗАТВОР_ВАК,
					К_ЗАКР_ЗАТВОР_ВАК,
					К_САПУН,
					ФлагГотовTВодыРО,
					ФП_P_ВтрубеСидер,
					ФП_P_Впатрубке,
					НП_Pатмосф;
ЦЕЛ ЛП_КодСообщенияДляGUI ЛОКАЛ;

 СОСТ Анализ{
    РАЗБОР (КодСобытия_от_ГУИ) {
	
	    СЛУЧАЙ КОМ_ГУИ_ОТКР_ЗАТВОР_ВАК:
		  ЕСЛИ (К_УСТР_ПЛАВ_ПУСКА == ВЫК) { /* вакуумирования нет */
				ЕСЛИ ((ФП_P_ВтрубеСидер >= ФП_P_Впатрубке)
					|| (ФП_P_ВтрубеСидер >= НП_Pатмосф))   /* можно открывать */
					СТАРТ ПРОЦ ОТКР_ЗАТВОР_ВАК; 
				ИНАЧЕ {
					ЛП_КодСообщенияДляGUI = ПК_ГУИ_НЕТ_УСЛОВИЙ_ДЛЯ_ВЫПОЛНЕНИЯ_КОМАНДЫ;
					В СОСТ ПередачаСообщенияСПараметромГУИ;
				}			
		  } ИНАЧЕ {
		    ЛП_КодСообщенияДляGUI = ПК_ГУИ_ИДЕТ_ВАКУУМИРОВАНИЕ_ОТКАЗ_ВЫПОЛНЕНИЯ_КОМАНДЫ;
			В СОСТ ПередачаСообщенияСПараметромГУИ;
		  }		
		КОНЕЦ;

	    СЛУЧАЙ КОМ_ГУИ_ЗАКР_ЗАТВОР_ВАК:
		  ЕСЛИ (К_УСТР_ПЛАВ_ПУСКА == ВЫК) { /* вакуумирование не включено*/
			СТАРТ ПРОЦ ЗАКР_ЗАТВОР_ВАК; 
		  } ИНАЧЕ {
		    ЛП_КодСообщенияДляGUI = ПК_ГУИ_ИДЕТ_ВАКУУМИРОВАНИЕ_ОТКАЗ_ВЫПОЛНЕНИЯ_КОМАНДЫ;
			В СОСТ ПередачаСообщенияСПараметромГУИ;
		  }		
			КОНЕЦ;		  
	    СЛУЧАЙ КОМ_ГУИ_ОТКР_САПУН:
		  ЕСЛИ (К_УСТР_ПЛАВ_ПУСКА == ВЫК) { /* вакуумирование не включено*/
			СТАРТ ПРОЦ ОТКР_САПУН; 
		  } ИНАЧЕ {
		    ЛП_КодСообщенияДляGUI = ПК_ГУИ_ИДЕТ_ВАКУУМИРОВАНИЕ_ОТКАЗ_ВЫПОЛНЕНИЯ_КОМАНДЫ;
			В СОСТ ПередачаСообщенияСПараметромГУИ;
		  }
		  КОНЕЦ;
	    СЛУЧАЙ КОМ_ГУИ_ЗАКР_САПУН:
		  ЕСЛИ (К_УСТР_ПЛАВ_ПУСКА == ВЫК) { /* вакуумирование не включено*/
			СТАРТ ПРОЦ ЗАКР_САПУН; 
		  } ИНАЧЕ {
		    ЛП_КодСообщенияДляGUI = ПК_ГУИ_ИДЕТ_ВАКУУМИРОВАНИЕ_ОТКАЗ_ВЫПОЛНЕНИЯ_КОМАНДЫ;
			В СОСТ ПередачаСообщенияСПараметромГУИ;
		  }
		  КОНЕЦ;

		СЛУЧАЙ КОМ_ГУИ_ВКЛ_УСТР_ПЛАВ_ПУСКА:
			ЕСЛИ (К_УСТР_ПЛАВ_ПУСКА == ВЫК) { /* вакуумирование не включено*/
				ЕСЛИ ((ФлагГотовУрВодыБО == НОРМА) && (К_ПИТ_ВЕНТИЛЯТОРА)
						&& (К_ПИТ_НАСОС_ОХЛ)&& (К_ЗАКР_ЗАТВОР_ВАК) && (К_САПУН)
						&& (ФлагГотовTВодыРО == НОРМА)) {   /* можно включать */
						СТАРТ ПРОЦ ВКЛ_УСТР_ПЛАВ_ПУСКА;
				} ИНАЧЕ {
					ЛП_КодСообщенияДляGUI = ПК_ГУИ_НЕТ_УСЛОВИЙ_ДЛЯ_ВЫПОЛНЕНИЯ_КОМАНДЫ;
					В СОСТ ПередачаСообщенияСПараметромГУИ;
				}
			} ИНАЧЕ {
				ЛП_КодСообщенияДляGUI = ПК_ГУИ_ИДЕТ_ВАКУУМИРОВАНИЕ_ОТКАЗ_ВЫПОЛНЕНИЯ_КОМАНДЫ;
				В СОСТ ПередачаСообщенияСПараметромГУИ;
			}			
			КОНЕЦ;
	
			
	    СЛУЧАЙ КОМ_ГУИ_ВЫК_УСТР_ПЛАВ_ПУСКА:   /* вопрос: надо ли вообще выключать его. М.б. достаточно выкл питание????*/
			СТАРТ ПРОЦ ВЫК_УСТР_ПЛАВ_ПУСКА;
			КОНЕЦ;	
		СЛУЧАЙ КОМ_ГУИ_ВКЛ_ПИТ_ВАК_НАСОСА:			
          СТАРТ ПРОЦ ВКЛ_ПИТ_ВАК_НАСОСА;
          КОНЕЦ;
	    СЛУЧАЙ КОМ_ГУИ_ВЫК_ПИТ_ВАК_НАСОСА:
          СТАРТ ПРОЦ ВЫК_ПИТ_ВАК_НАСОСА;
          КОНЕЦ;

/*----------------------------------------------------------------------*/
	    СЛУЧАЙ КОМ_ГУИ_ВКЛ_ПИТ_ВЕНТИЛЯТОР:
			СТАРТ ПРОЦ ВКЛ_ПИТ_ВЕНТИЛЯТОР;
          КОНЕЦ;
	    СЛУЧАЙ КОМ_ГУИ_ВЫК_ПИТ_ВЕНТИЛЯТОР:
			СТАРТ ПРОЦ ВЫК_ПИТ_ВЕНТИЛЯТОР;	
          КОНЕЦ;			
/*----------------------------------------------------------------------*/		  
/*	    СЛУЧАЙ КОМ_ГУИ_ВКЛ_ПИТ_ВЕНТИЛЯТОР:*/
/*		  ЕСЛИ (К_УСТР_ПЛАВ_ПУСКА == ВЫК) {  *//* вакуумирование не включено*/
/*			СТАРТ ПРОЦ ВКЛ_ПИТ_ВЕНТИЛЯТОР; */
/*		  } ИНАЧЕ {*/
/*		    ЛП_КодСообщенияДляGUI = ПК_ГУИ_ИДЕТ_ВАКУУМИРОВАНИЕ_ОТКАЗ_ВЫПОЛНЕНИЯ_КОМАНДЫ;*/
/*			В СОСТ ПередачаСообщенияСПараметромГУИ;*/
/*			}*/
/*          КОНЕЦ;*/
/*	    СЛУЧАЙ КОМ_ГУИ_ВЫК_ПИТ_ВЕНТИЛЯТОР:*/
/*          ЕСЛИ (К_УСТР_ПЛАВ_ПУСКА == ВЫК) { *//* вакуумирование не включено*/
/*			СТАРТ ПРОЦ ВЫК_ПИТ_ВЕНТИЛЯТОР; */
/*		  } ИНАЧЕ { */ /* идет вакуумирование*/
/*		    ЛП_КодСообщенияДляGUI = ПК_ГУИ_ИДЕТ_ВАКУУМИРОВАНИЕ_ОТКАЗ_ВЫПОЛНЕНИЯ_КОМАНДЫ;*/
/*			В СОСТ ПередачаСообщенияСПараметромГУИ;*/
/*		  } */
/*          КОНЕЦ;   */
/*----------------------------------------------------------------------*/		  
	    СЛУЧАЙ КОМ_ГУИ_ВКЛ_ПИТ_РЕГ_Т:
		  ЕСЛИ(К_УСТР_ПЛАВ_ПУСКА == ВЫК) {   /* вакуумирование не включено*/
		  
/*				ЕСЛИ ((ФлагГотовУрВодыБО = НОРМА) && (ФП_T_Вбаке > 0.0)) СТАРТ ПРОЦ ВКЛ_ПИТ_РЕГ_Т;  */

				ЕСЛИ (ФлагГотовУрВодыБО == НОРМА) {
					ЕСЛИ (ФП_T_Вбаке > 0.0) {
						СТАРТ ПРОЦ ВКЛ_ПИТ_РЕГ_Т;
					} ИНАЧЕ {
					 ЛП_КодСообщенияДляGUI = ПК_ГУИ_ОТРИЦАТЕЛЬНАЯ_ТЕМПЕРАТУРА_ВОДЫ_ОТКАЗ_ВЫПОЛНЕНИЯ_КОМАНДЫ;
					 В СОСТ ПередачаСообщенияСПараметромГУИ;
					}
				ЛП_КодСообщенияДляGUI = ПК_ГУИ_УРОВЕНЬ_ВОДЫ_В_БАКЕ_ОХЛАЖДЕНИЯ_ВНЕ_НОРМЫ_ОТКАЗ_ВЫПОЛНЕНИЯ_КОМАНДЫ;
				В СОСТ ПередачаСообщенияСПараметромГУИ;				
				}
				
		  } ИНАЧЕ {   /* идет вакуумирование*/
		    ЛП_КодСообщенияДляGUI = ПК_ГУИ_ИДЕТ_ВАКУУМИРОВАНИЕ_ОТКАЗ_ВЫПОЛНЕНИЯ_КОМАНДЫ;
			В СОСТ ПередачаСообщенияСПараметромГУИ;
		  }
		  КОНЕЦ;
	    СЛУЧАЙ КОМ_ГУИ_ВЫК_ПИТ_РЕГ_Т:
		  ЕСЛИ(К_УСТР_ПЛАВ_ПУСКА == ВЫК) {   /* вакуумирование не включено*/
			СТАРТ ПРОЦ ВЫК_ПИТ_РЕГ_Т;
		  } ИНАЧЕ {   /* идет вакуумирование*/
		    ЛП_КодСообщенияДляGUI = ПК_ГУИ_ИДЕТ_ВАКУУМИРОВАНИЕ_ОТКАЗ_ВЫПОЛНЕНИЯ_КОМАНДЫ;
			В СОСТ ПередачаСообщенияСПараметромГУИ;
		  }
          КОНЕЦ;
	    СЛУЧАЙ КОМ_ГУИ_ВКЛ_ПИТ_РЕГ_УР:
		  ЕСЛИ(К_УСТР_ПЛАВ_ПУСКА == ВЫК) {   /* вакуумирование не включено*/
		  	СТАРТ ПРОЦ ВКЛ_ПИТ_РЕГ_УР;
		  } ИНАЧЕ {   /* идет вакуумирование*/
		    ЛП_КодСообщенияДляGUI = ПК_ГУИ_ИДЕТ_ВАКУУМИРОВАНИЕ_ОТКАЗ_ВЫПОЛНЕНИЯ_КОМАНДЫ;
			В СОСТ ПередачаСообщенияСПараметромГУИ;
		  } 
          КОНЕЦ;
	    СЛУЧАЙ КОМ_ГУИ_ВЫК_ПИТ_РЕГ_УР:
		  ЕСЛИ(К_УСТР_ПЛАВ_ПУСКА == ВЫК) {   /* вакуумирование не включено*/
		  	СТАРТ ПРОЦ ВЫК_ПИТ_РЕГ_УР;
		  } ИНАЧЕ {   /* идет вакуумирование*/
		    ЛП_КодСообщенияДляGUI = ПК_ГУИ_ИДЕТ_ВАКУУМИРОВАНИЕ_ОТКАЗ_ВЫПОЛНЕНИЯ_КОМАНДЫ;
			В СОСТ ПередачаСообщенияСПараметромГУИ;
		  } 
		  КОНЕЦ;
	    СЛУЧАЙ КОМ_ГУИ_ВКЛ_ПИТ_НАС_ОХЛ:
		  ЕСЛИ(К_УСТР_ПЛАВ_ПУСКА == ВЫК) {   /* вакуумирование не включено*/
				ЕСЛИ ((ФлагГотовУрВодыБО = НОРМА) && (ФП_T_Вбаке > 0.0)) СТАРТ ПРОЦ ВКЛ_ПИТ_НАС_ОХЛ;
		  } ИНАЧЕ {   /* идет вакуумирование*/
		    ЛП_КодСообщенияДляGUI = ПК_ГУИ_ИДЕТ_ВАКУУМИРОВАНИЕ_ОТКАЗ_ВЫПОЛНЕНИЯ_КОМАНДЫ;
			В СОСТ ПередачаСообщенияСПараметромГУИ;
		  }
		  КОНЕЦ;
	    СЛУЧАЙ КОМ_ГУИ_ВЫК_ПИТ_НАС_ОХЛ:
		  ЕСЛИ(К_УСТР_ПЛАВ_ПУСКА == ВЫК) {   /* вакуумирование не включено*/
		  	СТАРТ ПРОЦ ВЫК_ПИТ_НАС_ОХЛ;
		  } ИНАЧЕ {   /* идет вакуумирование*/
		    ЛП_КодСообщенияДляGUI = ПК_ГУИ_ИДЕТ_ВАКУУМИРОВАНИЕ_ОТКАЗ_ВЫПОЛНЕНИЯ_КОМАНДЫ;
			В СОСТ ПередачаСообщенияСПараметромГУИ;
		  } 		
		  КОНЕЦ;
		  
	    СЛУЧАЙ КОМ_ГУИ_ВКЛ_ВАКУУМНУЮ_ПОДСИСТЕМУ:   
          СТАРТ ПРОЦ ВКЛ_ВАКУУМНУЮ_ПОДСИСТЕМУ; 
          КОНЕЦ;  
	    СЛУЧАЙ КОМ_ГУИ_ВЫК_ВАКУУМНУЮ_ПОДСИСТЕМУ:  
          СТАРТ ПРОЦ ВЫК_ВАКУУМНУЮ_ПОДСИСТЕМУ; 
          КОНЕЦ;  

		  СЛУЧАЙ КОМ_ГУИ_ВКЛ_ВАКУУМИРОВАНИЕ_ТРУБЫ:   /* достигает вакуума и выключает вак. насос и через 30 минут снова включает, если вакуума недостаточно */
          СТАРТ ПРОЦ ВКЛ_ВАКУУМИРОВАНИЕ_ТРУБЫ;
          КОНЕЦ;
		  
		  СЛУЧАЙ КОМ_ГУИ_ВЫК_ВАКУУМИРОВАНИЕ_ТРУБЫ:   
		  СТАРТ ПРОЦ ВКЛ_ВАКУУМИРОВАНИЕ_ТРУБЫ;
          КОНЕЦ;
		УМОЛЧАНИЕ:
          В СОСТ НекорректноеСобытие;
          КОНЕЦ;
    } /* конец разбора */
	СТОП;
 }


 /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ НекорректноеСобытие {
    ЕСЛИ (SendMsgLongParamGUI(ПК_ГУИ_ОШИБКА_КОДА_КОМАНДЫ_ОТ_ГУИ, КодСобытия_от_ГУИ))
         СТОП;   
    ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
 СОСТ ПередачаСообщенияСПараметромГУИ{
    ЕСЛИ (SendMsgLongParamGUI(ЛП_КодСообщенияДляGUI, КодСобытия_от_ГУИ))
        СТОП;
    ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }

 СОСТ НарушениеСвязи {
    ОШИБКА;
 }
} /* конец ПРОЦ ОбработкаКомандУправленияРегуляторами_от_ПИВ   */

/*====================================================*/
/*					ПРОЦЕСС КОНТРОЛЯ				  */
/*==           изменения ФП_P_ВбакеКомпрессора        */
/*====================================================*/
ПРОЦ КонтрольИзменения_ФП_P_ВбакеКомпрессора{
 ИЗ ПРОЦ Инициализация  ФП_P_ВбакеКомпрессора, ЗАДЕРЖКА_НА_РАЗГРУЗКУ_ТРАФИКА;
 ПЛАВ ЛП_Предыдущее ЛОКАЛ;
 СОСТ Начало{
    ЛП_Предыдущее = (!ФП_P_ВбакеКомпрессора);
    В СЛЕДУЮЩЕЕ;
 }
 СОСТ КонтрольИзменения{
    ЕСЛИ (ЛП_Предыдущее != ФП_P_ВбакеКомпрессора) {
         ЛП_Предыдущее = ФП_P_ВбакеКомпрессора;
         В СЛЕДУЮЩЕЕ;
    }
 }
 СОСТ СообщОбИзменении{
     ЕСЛИ (SendMsgFloatParamGUI(ПК_ГУИ_ФП_P_ВбакеКомпрессора, ЛП_Предыдущее))
         В СОСТ ТрафикПауза;
     ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
 СОСТ ТрафикПауза{
    ТАЙМАУТ ЗАДЕРЖКА_НА_РАЗГРУЗКУ_ТРАФИКА В СОСТ КонтрольИзменения;

 }
/* НЕНОРМАЛЬНЫЕ СИТУАЦИИ */
 СОСТ НарушениеСвязи{
     В СОСТ КонтрольИзменения;
 }
}
/* ПРОЦ КонтрольИзменения_ФП_P_ВбакеКомпрессора  */

/*====================================================*/
/*					ПРОЦЕСС КОНТРОЛЯ				  */
/*==           изменения ИЗМН_ФП_P_Впатрубке     */
/*====================================================*/
ПРОЦ КонтрольИзменения_ФП_P_Впатрубке{
 ИЗ ПРОЦ Инициализация  ФП_P_Впатрубке, ЗАДЕРЖКА_НА_РАЗГРУЗКУ_ТРАФИКА;
 ПЛАВ ЛП_Предыдущее ЛОКАЛ;
 СОСТ Начало{
    ЛП_Предыдущее = (!ФП_P_Впатрубке);
    В СЛЕДУЮЩЕЕ;
 }
 СОСТ КонтрольИзменения{
    ЕСЛИ (ЛП_Предыдущее != ФП_P_Впатрубке) {
         ЛП_Предыдущее = ФП_P_Впатрубке;
         В СЛЕДУЮЩЕЕ;
    }
 }
 СОСТ СообщОбИзменении{
     ЕСЛИ (SendMsgFloatParamGUI(ПК_ГУИ_ФП_P_Впатрубке, ЛП_Предыдущее))
         В СОСТ ТрафикПауза;
     ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
 СОСТ ТрафикПауза{
    ТАЙМАУТ ЗАДЕРЖКА_НА_РАЗГРУЗКУ_ТРАФИКА В СОСТ КонтрольИзменения;

 }
/* НЕНОРМАЛЬНЫЕ СИТУАЦИИ */
 СОСТ НарушениеСвязи{
     В СОСТ КонтрольИзменения;
 }
}
/* ПРОЦ КонтрольИзменения_ФП_P_Впатрубке  */

/*====================================================*/
/*					ПРОЦЕСС КОНТРОЛЯ				  */
/*==           изменения ИЗМН_ФП_T_Врубашке     */
/*====================================================*/
ПРОЦ КонтрольИзменения_ФП_T_Врубашке{
 ИЗ ПРОЦ Инициализация  ФП_T_Врубашке, ЗАДЕРЖКА_НА_РАЗГРУЗКУ_ТРАФИКА;
 ПЛАВ ЛП_Предыдущее ЛОКАЛ;
 СОСТ Начало{
    ЛП_Предыдущее = (!ФП_T_Врубашке);
    В СЛЕДУЮЩЕЕ;
 }
 СОСТ КонтрольИзменения{
    ЕСЛИ (ЛП_Предыдущее != ФП_T_Врубашке) {
         ЛП_Предыдущее = ФП_T_Врубашке;
         В СЛЕДУЮЩЕЕ;
    }
 }
 СОСТ СообщОбИзменении{
     ЕСЛИ (SendMsgFloatParamGUI(ПК_ГУИ_ФП_T_Врубашке, ЛП_Предыдущее))
         В СОСТ ТрафикПауза;
     ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
 СОСТ ТрафикПауза{
    ТАЙМАУТ ЗАДЕРЖКА_НА_РАЗГРУЗКУ_ТРАФИКА В СОСТ КонтрольИзменения;

 }
/* НЕНОРМАЛЬНЫЕ СИТУАЦИИ */
 СОСТ НарушениеСвязи{
     В СОСТ КонтрольИзменения;
 }
}
/* ПРОЦ КонтрольИзменения_ФП_T_Врубашке  */

/*====================================================*/
/*					ПРОЦЕСС КОНТРОЛЯ				  */
/*==           изменения ИЗМН_ФП_T_Вбаке     */
/*====================================================*/
ПРОЦ КонтрольИзменения_ФП_T_Вбаке{
 ИЗ ПРОЦ Инициализация  ФП_T_Вбаке, ЗАДЕРЖКА_НА_РАЗГРУЗКУ_ТРАФИКА;
 ПЛАВ ЛП_Предыдущее ЛОКАЛ;
 СОСТ Начало{
    ЛП_Предыдущее = (!ФП_T_Вбаке);
    В СЛЕДУЮЩЕЕ;
 }
 СОСТ КонтрольИзменения{
    ЕСЛИ (ЛП_Предыдущее != ФП_T_Вбаке) {
         ЛП_Предыдущее = ФП_T_Вбаке;
         В СЛЕДУЮЩЕЕ;
    }
 }
 СОСТ СообщОбИзменении{
     ЕСЛИ (SendMsgFloatParamGUI(ПК_ГУИ_ФП_T_Вбаке, ЛП_Предыдущее))
         В СОСТ ТрафикПауза;
     ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
 СОСТ ТрафикПауза{
    ТАЙМАУТ ЗАДЕРЖКА_НА_РАЗГРУЗКУ_ТРАФИКА В СОСТ КонтрольИзменения;

 }
/* НЕНОРМАЛЬНЫЕ СИТУАЦИИ */
 СОСТ НарушениеСвязи{
     В СОСТ КонтрольИзменения;
 }
}
/* ПРОЦ КонтрольИзменения_ФП_T_Вбаке  */

/*====================================================*/
/*					ПРОЦЕСС КОНТРОЛЯ				  */
/*==           изменения ИЗМН_ФП_P_ВтрубеСидер     */
/*====================================================*/	
 ПРОЦ КонтрольИзменения_ФП_P_ВтрубеСидер{
 ИЗ ПРОЦ Инициализация  ФП_P_ВтрубеСидер, ЗАДЕРЖКА_НА_РАЗГРУЗКУ_ТРАФИКА;
 ПЛАВ ЛП_Предыдущее ЛОКАЛ;
 СОСТ Начало{
    ЛП_Предыдущее = (!ФП_P_ВтрубеСидер);
    В СЛЕДУЮЩЕЕ;
 }
 СОСТ КонтрольИзменения{
    ЕСЛИ (ЛП_Предыдущее != ФП_P_ВтрубеСидер) {
         ЛП_Предыдущее = ФП_P_ВтрубеСидер;
         В СЛЕДУЮЩЕЕ;
    }
 }
 СОСТ СообщОбИзменении{
     ЕСЛИ (SendMsgFloatParamGUI(ПК_ГУИ_ФП_P_ВтрубеСидер, ЛП_Предыдущее))
         В СОСТ ТрафикПауза;
     ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
 СОСТ ТрафикПауза{
    ТАЙМАУТ ЗАДЕРЖКА_НА_РАЗГРУЗКУ_ТРАФИКА В СОСТ КонтрольИзменения;

 }
/* НЕНОРМАЛЬНЫЕ СИТУАЦИИ */
 СОСТ НарушениеСвязи{
     В СОСТ КонтрольИзменения;
 }
}
/* ПРОЦ КонтрольИзменения_ФП_P_ВтрубеСидер  */   


/*====================================================*/
/*					ПРОЦЕСС КОНТРОЛЯ				  */
/*== 		Контроль Откачки в Трубе сидеростата 	==*/
/*====================================================*/
ПРОЦ КонтрольОткачкиВауумнойТрубыСидеростата{
ИЗ ПРОЦ Инициализация 	ФлагОткачкиТрубыСидеростата,
						ФП_P_ВтрубеСидер;
ПЛАВ ЛП_Предыдущее ЛОКАЛ;
					
 СОСТ Начало{
	ЛП_Предыдущее = ФП_P_ВтрубеСидер;
	ТАЙМАУТ ТАЙМАУТ_ЗАДЕРЖКА_НА_ОТКАЧКУ В СЛЕДУЮЩЕЕ;
 }
 СОСТ АнализОткачки{
	ЕСЛИ ((ФП_P_ВтрубеСидер - ЛП_Предыдущее) < -0.01) {    /*  величина откачки  ?????* м.б. настр параметр ввести*/
        ЕСЛИ (SendMsgGUICode(ПК_ГУИ_СКОРОСТЬ_ОТКАЧКИ_В_НОРМЕ)) {
					ФлагОткачкиТрубыСидеростата = НОРМА;
					В СОСТ Начало;
		} 
    } ИНАЧЕ{
	ЕСЛИ (SendMsgGUICode(ПК_ГУИ_СКОРОСТЬ_ОТКАЧКИ_НИЖЕ_НОРМЫ)) {
					ФлагОткачкиТрубыСидеростата = ВНЕ_НОРМЫ;
					В СОСТ Начало;
		} 
	}
 }
}
/*====================================================*/
/*					ПРОЦЕСС КОНТРОЛЯ				  */
/*== Контроль Готовности Вакуума в Вакуумной Трубе  ==*/
/*====================================================*/

ПРОЦ КонтрольГотовностиВакуума_в_Вакуумной_Трубе{
ИЗ ПРОЦ Инициализация  	ФП_P_ВтрубеСидер,
						НП_ГлубокийВакуумТрубы,
						ФлагГотовВакуумаВТ;
СОСТ Начало{
   ЕСЛИ (ФП_P_ВтрубеСидер <= НП_ГлубокийВакуумТрубы) {
         ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ДАВЛЕНИЕ_В_ВАКУУМНОЙ_ТРУБЕ_В_НОРМЕ)) {
					ФлагГотовВакуумаВТ = НОРМА;
					В СОСТ ДавлениеНормальное;
					}
    
    } ИНАЧЕ {
                ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ДАВЛЕНИЕ_В_ВАКУУМНОЙ_ТРУБЕ_ВНЕ_НОРМЫ)) {
					ФлагГотовВакуумаВТ = ВНЕ_НОРМЫ;
				   В СОСТ ДавлениеВысокое;
				}
    }
    ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }

СОСТ ДавлениеНормальное{
       ЕСЛИ (ФП_P_ВтрубеСидер > 0.025)  В СОСТ Начало;
 } 
СОСТ ДавлениеВысокое{
       ЕСЛИ (ФП_P_ВтрубеСидер < 0.01)  В СОСТ Начало;
 }  
 
СОСТ Задержка{
      ТАЙМАУТ ОДНА_МИНУТА В СОСТ Начало;
 }
/* НЕНОРМАЛЬНЫЕ СИТУАЦИИ */
 СОСТ НарушениеСвязи{
     В СОСТ Начало;
 }
}

/* конец ПРОЦ КонтрольГотовностиВакуума_в_Вакуумной Трубе

/*====================================================*/
/*					ПРОЦЕСС КОНТРОЛЯ				  */
/*==           Уровень воды в баке охлаждения       ==*/
/*====================================================*/
ПРОЦ КонтрольУровняВодыБакаОхлаждения{
 ИЗ ПРОЦ Инициализация
                        К_УР_ВОДЫ_MIN,
                        К_УР_ВОДЫ_MAX,
						ФлагГотовУрВодыБО;
						
 СОСТ Начало{
     ЕСЛИ (К_УР_ВОДЫ_MIN) {
         ЕСЛИ (SendMsgGUICode(ПК_ГУИ_УРОВЕНЬ_ВОДЫ_В_БАКЕ_ОХЛАЖДЕНИЯ_НИЗКИЙ)) {
					ФлагГотовУрВодыБО = ВНЕ_НОРМЫ;
					В СОСТ НизкийУровеньВоды;
					}
    } ИНАЧЕ ЕСЛИ (К_УР_ВОДЫ_MAX) {
              ЕСЛИ (SendMsgGUICode(ПК_ГУИ_УРОВЕНЬ_ВОДЫ_В_БАКЕ_ОХЛАЖДЕНИЯ_ВЫСОКИЙ)) {
			   ФлагГотовУрВодыБО = ВНЕ_НОРМЫ;
			   В СОСТ ВысокийУровеньВоды;
			   }
    } ИНАЧЕ {
                ЕСЛИ (SendMsgGUICode(ПК_ГУИ_УРОВЕНЬ_ВОДЫ_В_БАКЕ_ОХЛАЖДЕНИЯ_В_НОРМЕ)) {
					ФлагГотовУрВодыБО = НОРМА;
				   В СОСТ УровеньВодыВнорме;
				}
         }
    ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }

 СОСТ НизкийУровеньВоды{
        ЕСЛИ (!К_УР_ВОДЫ_MIN)  В СОСТ Задержка;
 }

 СОСТ ВысокийУровеньВоды{
       ЕСЛИ (!К_УР_ВОДЫ_MAX)  В СОСТ Задержка;
 }
 СОСТ УровеньВодыВнорме{
	   ЕСЛИ (К_УР_ВОДЫ_MIN && К_УР_ВОДЫ_MAX)  В СОСТ Задержка;
 } 
 
СОСТ Задержка{
      ТАЙМАУТ ОДНА_МИНУТА В СОСТ Начало;
 }
 
/*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ НарушениеСвязи {
    В СОСТ Начало;
 }
}   /*   Конец ПРОЦ КонтрольУровняВодыБакаОхлаждения */


/*====================================================*/
/*					ПРОЦЕСС КОНТРОЛЯ				  */
/*==       Температуры воды в баке охлаждения       ==*/
/*====================================================*/
ПРОЦ КонтрольТемпературыВодыБакаОхлаждения{
 ИЗ ПРОЦ Инициализация 	
						ФП_T_Вбаке,	
                        ФлагГотовТемпВодыБО;
						
 СОСТ Начало{
    ЕСЛИ (ФП_T_Вбаке < 50) {
         ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ТЕМПЕРАТУРА_ВОДЫ_В_БАКЕ_ОХЛАЖДЕНИЯ_НИЗКАЯ)) {
					ФлагГотовТемпВодыБО = ВНЕ_НОРМЫ;
					В СОСТ НизкаяТемператураВоды;
					}
    } ИНАЧЕ ЕСЛИ (ФП_T_Вбаке > 60) {
              ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ТЕМПЕРАТУРА_ВОДЫ_В_БАКЕ_ОХЛАЖДЕНИЯ_ВЫСОКАЯ)) {
			   ФлагГотовТемпВодыБО = ВНЕ_НОРМЫ;
			   В СОСТ ВысокаяТемператураВоды;
			   }
    } ИНАЧЕ {
                ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ТЕМПЕРАТУРА_ВОДЫ_В_БАКЕ_ОХЛАЖДЕНИЯ_В_НОРМЕ)) {
					ФлагГотовТемпВодыБО = НОРМА;
				   В СОСТ ТемператураВнорме;
				}
         }
		 
    ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
 

 СОСТ НизкаяТемператураВоды{
        ЕСЛИ (ФП_T_Вбаке > (50.0 + Т_ГИСТЕРЕЗИС_ВОДЫ_В_БАКЕ))  В СОСТ Начало;
 }

 СОСТ ВысокаяТемператураВоды{
       ЕСЛИ (ФП_T_Вбаке < (60.0 - Т_ГИСТЕРЕЗИС_ВОДЫ_В_БАКЕ))  В СОСТ Начало;
 }
  СОСТ ТемператураВнорме{
       ЕСЛИ ((ФП_T_Вбаке < (50.0 - Т_ГИСТЕРЕЗИС_ВОДЫ_В_БАКЕ)) && 
	         (ФП_T_Вбаке > (60.0 + Т_ГИСТЕРЕЗИС_ВОДЫ_В_БАКЕ)))  В СОСТ Начало;
 } 
СОСТ Задержка{

      ТАЙМАУТ ОДНА_МИНУТА В СОСТ Начало;
 }
 
/*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ НарушениеСвязи {
    В СОСТ Начало;
 }
}   /*   Конец ПРОЦ КонтрольТемпературыВодыБакаОхлаждения */


/*====================================================*/
/*					ПРОЦЕСС КОНТРОЛЯ				  */
/*==       Температуры воды в рубашке охлаждения    ==*/
/*====================================================*/
ПРОЦ КонтрольТемпературыРубашкиОхлаждения{
 ИЗ ПРОЦ Инициализация 	
						ФП_T_Врубашке,	
                        ФлагГотовTВодыРО;
						
 СОСТ Начало{
    ЕСЛИ (ФП_T_Врубашке < 40) {
         ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ТЕМПЕРАТУРА_ВОДЫ_В_РУБАШКЕ_ОХЛАЖДЕНИЯ_НИЗКАЯ)) {
					ФлагГотовTВодыРО = ВНЕ_НОРМЫ;
					В СОСТ НизкаяТемператураВоды;
					}
    } ИНАЧЕ ЕСЛИ (ФП_T_Врубашке > 85) {
              ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ТЕМПЕРАТУРА_ВОДЫ_В_РУБАШКЕ_ОХЛАЖДЕНИЯ_ВЫСОКАЯ)) {
			   ФлагГотовTВодыРО = ВНЕ_НОРМЫ;
			   В СОСТ ВысокаяТемператураВоды;
			   }
    } ИНАЧЕ {
                ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ТЕМПЕРАТУРА_ВОДЫ_В_РУБАШКЕ_ОХЛАЖДЕНИЯ_В_НОРМЕ)) {
					ФлагГотовTВодыРО = НОРМА;
				   В СОСТ ТемператураВнорме;
				}
         }
    ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }

 СОСТ НизкаяТемператураВоды{
       
		ЕСЛИ (ФП_T_Врубашке > (40.0 + Т_ГИСТЕРЕЗИС_ВОДЫ_В_РУБАХЕ))  В СОСТ Начало;
 }

 СОСТ ВысокаяТемператураВоды{
        ЕСЛИ (ФП_T_Врубашке < (85.0 - Т_ГИСТЕРЕЗИС_ВОДЫ_В_РУБАХЕ))  В СОСТ Начало;
 }
 

 СОСТ ТемператураВнорме{
        ЕСЛИ ((ФП_T_Врубашке < (40.0 - Т_ГИСТЕРЕЗИС_ВОДЫ_В_РУБАХЕ)) && 
	         (ФП_T_Врубашке > (85.0 + Т_ГИСТЕРЕЗИС_ВОДЫ_В_РУБАХЕ)))  В СОСТ Начало;
 } 
СОСТ Задержка{
      ТАЙМАУТ ОДНА_МИНУТА В СОСТ Начало;
 }
 
/*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ НарушениеСвязи {
    В СОСТ Начало;
 }
}   /*   Конец ПРОЦ КонтрольТемпературыВодыВрубашкеОхлаждения */


/*====================================================*/
/*					ПРОЦЕСС КОНТРОЛЯ				  */
/*== 		Контроль давления в баке компрессора ??? 	==*/
/*====================================================*/
ПРОЦ КонтрольДавленияВбакеКомпрессора{
ИЗ ПРОЦ Инициализация  	ФП_P_ВбакеКомпрессора,
					ФлагДавленияКомпрессора,
					НП_P_ВбакеКомпрессора;
СОСТ Начало{
   ЕСЛИ (ФП_P_ВбакеКомпрессора > НП_P_ВбакеКомпрессора) {
         ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ДАВЛЕНИЕ_В_БАКЕ_КОМПРЕССОРА_В_НОРМЕ)) {
					ФлагДавленияКомпрессора = НОРМА;
					В СОСТ ДавлениеНормальное;
					}
    
    } ИНАЧЕ {
                ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ДАВЛЕНИЕ_В_БАКЕ_КОМПРЕССОРА_НИЖЕ_НОРМЫ)) {
					ФлагДавленияКомпрессора = ВНЕ_НОРМЫ;
				   В СОСТ ДавлениеНизкое;
				}
    }
    ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }

СОСТ ДавлениеНормальное{
       ЕСЛИ (ФП_P_ВбакеКомпрессора < 9.5)  В СОСТ Начало;
 } 
СОСТ ДавлениеНизкое{
       ЕСЛИ (ФП_P_ВбакеКомпрессора < 10.5)  В СОСТ Начало;
 }  
 
СОСТ Задержка{
      ТАЙМАУТ ОДНА_МИНУТА В СОСТ Начало;
 }
/* НЕНОРМАЛЬНЫЕ СИТУАЦИИ */
 СОСТ НарушениеСвязи{
     В СОСТ Начало;
 }
}


/*=========================================*/
/*=                                       =*/
/*=     ПРОЦЕССЫ  УПРАВЛЕНИЯ ВАКУУМНЫМ    =*/
/*=          ОБОРУДОВАНИЕМ                =*/
/*=========================================*/
/*=======================================================*/
/*=====   Процесс подачи питания на вентилятор		=====*/
/*=======================================================*/
ПРОЦ ВКЛ_ПИТ_ВЕНТИЛЯТОР{
 ИЗ ПРОЦ Инициализация 	К_ПИТ_ВЕНТИЛЯТОРА,
						У_ПИТ_ВЕНТИЛЯТОРА,
						Д_ПИТ_ВЕНТИЛЯТОРА
					/*	ОШИБКА_ФУНКЦ_ВЕНТИЛЯТОР */
						;
					  
 СОСТ Начало{
   ЕСЛИ (ПРОЦ ВЫК_ПИТ_ВЕНТИЛЯТОР В СОСТ АКТИВНОЕ) ОШИБКА ПРОЦ ВЫК_ПИТ_ВЕНТИЛЯТОР;
   СТОП ПРОЦ КонтрольСпонтанногоВключенияВентилятора;
/*	ОШИБКА_ФУНКЦ_ВЕНТИЛЯТОР = НЕТ; */
	У_ПИТ_ВЕНТИЛЯТОРА = ВКЛ;
	В СЛЕДУЮЩЕЕ;      
 }
 СОСТ КонтрольВключения{
	ЕСЛИ ((К_ПИТ_ВЕНТИЛЯТОРА == ВКЛ) && (Д_ПИТ_ВЕНТИЛЯТОРА)) {
		СТАРТ ПРОЦ КонтрольСпонтанногоВыключенияВентилятора;
		В СОСТ ПередачаСообщения;
	}
  ТАЙМАУТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_ВЕНТИЛЯТОРА В СОСТ Ошибка;
  }
 СОСТ ПередачаСообщения {
    ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ВЕНТИЛЯТОР_ВКЛЮЧЕН)) СТОП;
    ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
  
/*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ Ошибка {
       ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ОТКАЗ_ВКЛЮЧЕНИЯ_ВЕНТИЛЯТОРА))	ОШИБКА;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
  СОСТ НарушениеСвязи {
	   ОШИБКА;
 }   
} /*конец процесса ВКЛ_ПИТ_ВЕНТИЛЯТОР  */

/*=======================================================*/
/*=====   Процесс отключения питания вентилятора    =====*/
/*=======================================================*/
ПРОЦ ВЫК_ПИТ_ВЕНТИЛЯТОР{
 ИЗ ПРОЦ Инициализация 	К_ПИТ_ВЕНТИЛЯТОРА,
						У_ПИТ_ВЕНТИЛЯТОРА,
						Д_ПИТ_ВЕНТИЛЯТОРА
					/*	ОШИБКА_ФУНКЦ_ВЕНТИЛЯТОР  */
						;
 СОСТ Начало{
	ЕСЛИ (ПРОЦ ВКЛ_ПИТ_ВЕНТИЛЯТОР В СОСТ АКТИВНОЕ) ОШИБКА ПРОЦ ВКЛ_ПИТ_ВЕНТИЛЯТОР;
	СТОП ПРОЦ КонтрольСпонтанногоВыключенияВентилятора;
/*	ОШИБКА_ФУНКЦ_ВЕНТИЛЯТОР = НЕТ; */
	У_ПИТ_ВЕНТИЛЯТОРА = ВЫК;
	В СЛЕДУЮЩЕЕ;      
 }
 
 СОСТ КонтрольВыключения{
	ЕСЛИ ((К_ПИТ_ВЕНТИЛЯТОРА == ВЫК) && (Д_ПИТ_ВЕНТИЛЯТОРА)) {
		СТАРТ ПРОЦ КонтрольСпонтанногоВключенияВентилятора;
		В СОСТ ПередачаСообщения;
	} 
  ТАЙМАУТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_ВЕНТИЛЯТОРА В СОСТ Ошибка;
 }
   
 СОСТ ПередачаСообщения {
    ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ВЕНТИЛЯТОР_ВЫКЛЮЧЕН)) СТОП;
    ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
  
/*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ Ошибка {
       ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ОТКАЗ_ВЫКЛЮЧЕНИЯ_ВЕНТИЛЯТОРА))	ОШИБКА;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
  СОСТ НарушениеСвязи {
	   ОШИБКА;
 }   
} /*конец процесса ВЫК_ПИТ_ВЕНТИЛЯТОРА  */					  

/*========================================================*/
/*=====   Процесс контроля включенности вентилятора  =====*/
/*========================================================*/
ПРОЦ КонтрольСпонтанногоВыключенияВентилятора{
	ИЗ ПРОЦ Инициализация 	К_ПИТ_ВЕНТИЛЯТОРА;

	СОСТ Контроль {
		ЕСЛИ (К_ПИТ_ВЕНТИЛЯТОРА == ВЫК) В СОСТ Ошибка;
	}
	СОСТ Ошибка {
		ЕСЛИ (SendMsgGUICode(ПК_ГУИ_СПОНТАННОЕ_ВЫКЛЮЧЕНИЕ_ВЕНТИЛЯТОРА)) {
			СТАРТ ПРОЦ ВЫК_ВАКУУМИРОВАНИЕ_ТРУБЫ;  		/* прекращаем вакуумирование */
			ОШИБКА;}
		ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
	}
	
    /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/	
	СОСТ НарушениеСвязи {
		ОШИБКА;
	}
}	
/*=========================================================*/
/*=====   Процесс контроля выключенности вентилятора  =====*/
/*=========================================================*/
ПРОЦ КонтрольСпонтанногоВключенияВентилятора{
	ИЗ ПРОЦ Инициализация 	К_ПИТ_ВЕНТИЛЯТОРА;
	
	СОСТ Контроль {
		ЕСЛИ (К_ПИТ_ВЕНТИЛЯТОРА == ВКЛ) В СОСТ Ошибка;
	}
	
	СОСТ Ошибка {
		ЕСЛИ (SendMsgGUICode(ПК_ГУИ_СПОНТАННОЕ_ВКЛЮЧЕНИЕ_ВЕНТИЛЯТОРА)) ОШИБКА;
				/*  если включился, то и ладно, вакуумирования-то нет !!!!!!!!!!!!!!!!!*/
		ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
	}

	
    /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/		
	СОСТ НарушениеСвязи {
		ОШИБКА;
	}
}			  
/*=========================================================*/


/*===========================================================*/
/*=====   Процесс открытия затвора вакуумного         =======*/
/*===========================================================*/
ПРОЦ ОТКР_ЗАТВОР_ВАК{
 ИЗ ПРОЦ Инициализация 	К_ОТКР_ЗАТВОР_ВАК,
						К_ЗАКР_ЗАТВОР_ВАК,
						У_ЗАТВОР_ВАК
					/*	ОШИБКА_ФУНКЦ_ЗАТВОР_ВАК*/
						;
 СОСТ Начало{
	ЕСЛИ (ПРОЦ ЗАКР_ЗАТВОР_ВАК В СОСТ АКТИВНОЕ) ОШИБКА ПРОЦ ЗАКР_ЗАТВОР_ВАК;
	СТОП ПРОЦ КонтрольСпонтанногоОткрытияЗатвораВакуумного;
/*	ОШИБКА_ФУНКЦ_ЗАТВОР_ВАК = НЕТ;*/
	У_ЗАТВОР_ВАК = ОТКР;
	В СЛЕДУЮЩЕЕ;      
 }

 СОСТ Контроль{  
	ЕСЛИ ((К_ОТКР_ЗАТВОР_ВАК == ВКЛ) && (К_ЗАКР_ЗАТВОР_ВАК = ВЫК)) {
		СТАРТ ПРОЦ КонтрольСпонтанногоЗакрытияЗатвораВакуумного;		
		В СОСТ ПередачаСообщения;  
	}
  ТАЙМАУТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_ЗАТ_ВАК В СОСТ Ошибка;
 }
 СОСТ ПередачаСообщения {
    ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ЗАТВОР_ВАКУУМНЫЙ_ОТКРЫТ)) СТОП;
    ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
 /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ Ошибка {
       ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ОТКАЗ_ОТКРЫТИЯ_ЗАТВОРА_ВАКУУМНОГО))	ОШИБКА;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
  СОСТ НарушениеСвязи {
	   ОШИБКА;
 }
}   /*конец процесса ОТКР_ЗАТВОР_ВАК */
/*===========================================================*/
/*=====   Процесс закрытия затвора вакуумного         =======*/
/*===========================================================*/
ПРОЦ ЗАКР_ЗАТВОР_ВАК{
 ИЗ ПРОЦ Инициализация 	К_ОТКР_ЗАТВОР_ВАК,
						К_ЗАКР_ЗАТВОР_ВАК,
						У_ЗАТВОР_ВАК  
					/*	,ОШИБКА_ФУНКЦ_ЗАТВОР_ВАК  */
						;

 СОСТ Начало{
	ЕСЛИ (ПРОЦ ОТКР_ЗАТВОР_ВАК В СОСТ АКТИВНОЕ) ОШИБКА ПРОЦ ОТКР_ЗАТВОР_ВАК;
	СТОП ПРОЦ КонтрольСпонтанногоЗакрытияЗатвораВакуумного;
/*	ОШИБКА_ФУНКЦ_ЗАТВОР_ВАК = НЕТ; */
	У_ЗАТВОР_ВАК = ЗАКР;
	В СЛЕДУЮЩЕЕ;      
 }
 СОСТ Контроль{  
	ЕСЛИ ((К_ОТКР_ЗАТВОР_ВАК == ВЫК) && (К_ЗАКР_ЗАТВОР_ВАК = ВКЛ)) {
	СТАРТ ПРОЦ КонтрольСпонтанногоОткрытияЗатвораВакуумного;	
	В СОСТ ПередачаСообщения;
	} 
	ТАЙМАУТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_ЗАТ_ВАК В СОСТ Ошибка;
  }
 СОСТ ПередачаСообщения {
    ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ЗАТВОР_ВАКУУМНЫЙ_ЗАКРЫТ)) СТОП;
    ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }  

 /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ Ошибка {
    ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ОТКАЗ_ЗАКРЫТИЯ_ЗАТВОРА_ВАКУУМНОГО))	ОШИБКА;
    ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
  СОСТ НарушениеСвязи {
	   ОШИБКА;
 }
} /*конец процесса ЗАКР_ЗАТВОР_ВАК */


/*========================================================*/
/*=====  Процесс контроля включенности затвора вак.  =====*/
/*========================================================*/
/*  затвор был открыт, но вдруг закрылся */
ПРОЦ КонтрольСпонтанногоЗакрытияЗатвораВакуумного{
	ИЗ ПРОЦ Инициализация 	К_ОТКР_ЗАТВОР_ВАК,
							К_ЗАКР_ЗАТВОР_ВАК;

	СОСТ КонтрольЗакрытия {
		ЕСЛИ ((К_ОТКР_ЗАТВОР_ВАК == ВЫК) && (К_ЗАКР_ЗАТВОР_ВАК = ВКЛ)) В СОСТ Ошибка;
	}
	СОСТ Ошибка {
		ЕСЛИ (SendMsgGUICode(ПК_ГУИ_СПОНТАННОЕ_ЗАКРЫТИЕ_ЗАТ_ВАКУУМНОГО)) {
			СТАРТ ПРОЦ ВЫК_ВАКУУМИРОВАНИЕ_ТРУБЫ;  		/* прекращаем вакуумирование */
			ОШИБКА; }
		/*  здесь надо выключать вакуумирование !!!!!!!!!!!!!????*/
		ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
	}
	
    /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/	
	СОСТ НарушениеСвязи {
		ОШИБКА;
	}
}	
/*=========================================================*/
/*=====   Процесс контроля выключенности затвора вак  =====*/
/*=========================================================*/
/*  затвор был закрыт, но вдруг открылся */
ПРОЦ КонтрольСпонтанногоОткрытияЗатвораВакуумного{
	ИЗ ПРОЦ Инициализация 	К_ОТКР_ЗАТВОР_ВАК,
							К_ЗАКР_ЗАТВОР_ВАК;
	
	СОСТ Контроль {
		ЕСЛИ ((К_ОТКР_ЗАТВОР_ВАК == ВКЛ) && (К_ЗАКР_ЗАТВОР_ВАК = ВЫК)) В СОСТ Ошибка;
	}
	
	СОСТ Ошибка {
		/*  если открылся, то вроде бы его надо попытаться закрыть и прекратить вакуумирование !!!!!!!!!!!!!!!!!*/	
		ЕСЛИ (SendMsgGUICode(ПК_ГУИ_СПОНТАННОЕ_ОТКРЫТИЕ_ЗАТ_ВАКУУМНОГО)) { ОШИБКА;}

		ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
	}
    /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/		
	СОСТ НарушениеСвязи {
		ОШИБКА;
	}
}			  

/*===========================================================*/
/*=====   Процесс открытия сапуна                    =======*/
/*===========================================================*/
ПРОЦ ОТКР_САПУН{
 ИЗ ПРОЦ Инициализация 	К_САПУН, 
						У_САПУН
					/*	ОШИБКА_ФУНКЦ_САПУН */
						; 	
 СОСТ Начало{
	ЕСЛИ (ПРОЦ ЗАКР_САПУН В СОСТ АКТИВНОЕ) ОШИБКА ПРОЦ ЗАКР_САПУН;
	СТОП ПРОЦ КонтрольСпонтанногоОткрытияСапуна;	
/*  ЕСЛИ ((К_САПУН == ОТКР) && (ОШИБКА_ФУНКЦ_САПУН == НЕТ)) СТОП;  */ 
/*	ОШИБКА_ФУНКЦ_САПУН = НЕТ; */
	У_САПУН = ОТКР;
	В СЛЕДУЮЩЕЕ;      
 }

 СОСТ КонтрольОткрытия{  
/*  ЕСЛИ ((К_САПУН == ОТКР) && (ОШИБКА_ФУНКЦ_САПУН == НЕТ)) СТОП; */
    ЕСЛИ (К_САПУН == ОТКР) {
		СТАРТ ПРОЦ КонтрольСпонтанногоЗакрытияСапуна;	
		В СОСТ ПередачаСообщения;	
	
	}В СОСТ ПередачаСообщения;
  ТАЙМАУТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_САПУНА В СОСТ Ошибка;  /* здесь надо выдавать код ошибки в ГУИ */
 }
 СОСТ ПередачаСообщения {
    ЕСЛИ (SendMsgGUICode(ПК_ГУИ_САПУН_ОТКРЫТ)) СТОП;
    ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 } 
/*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ Ошибка {
       ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ОТКАЗ_ОТКРЫТИЯ_САПУНА))	ОШИБКА;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
  СОСТ НарушениеСвязи {
	   ОШИБКА;
 }
}  /*конец процесса ОТКР_САПУН  */



/*===========================================================*/
/*==!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! ==========================*/
/*=====   Процесс закрытия сапуна                    =======*/
/*===========================================================*/
ПРОЦ ЗАКР_САПУН{
 ИЗ ПРОЦ Инициализация 	К_САПУН,
                      	У_САПУН
					/*	ОШИБКА_ФУНКЦ_САПУН */
						;

 СОСТ Начало{
   ЕСЛИ (ПРОЦ ОТКР_САПУН В СОСТ АКТИВНОЕ) ОШИБКА ПРОЦ ОТКР_САПУН;
	СТОП ПРОЦ КонтрольСпонтанногоЗакрытияСапуна;   
/*	ОШИБКА_ФУНКЦ_САПУН = НЕТ;  */
	У_САПУН = ЗАКР;
	В СЛЕДУЮЩЕЕ;      
 }

 СОСТ КонтрольЗакрытия{  
	ЕСЛИ (К_САПУН == ЗАКР) {
		СТАРТ ПРОЦ КонтрольСпонтанногоОткрытияСапуна;	
		В СОСТ ПередачаСообщения; 
	}
  ТАЙМАУТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_САПУНА {
  /*      ОШИБКА_ФУНКЦИОНИРОВАНИЯ = ВКЛ;  ??????? */
	В СОСТ Ошибка;
  }
 }
 СОСТ ПередачаСообщения {
    ЕСЛИ (SendMsgGUICode(ПК_ГУИ_САПУН_ЗАКРЫТ)) СТОП;
    ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 } 
 /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ Ошибка {
       ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ОТКАЗ_ЗАКРЫТИЯ_САПУНА))	ОШИБКА;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
  СОСТ НарушениеСвязи {
	   ОШИБКА;
 }
} /*конец процесса ЗАКР_САПУН  */
/*========================================================*/
/*=====  Процесс контроля включенности сапуна        =====*/
/*========================================================*/
ПРОЦ КонтрольСпонтанногоЗакрытияСапуна{
	ИЗ ПРОЦ Инициализация 	К_САПУН;

	СОСТ КонтрольЗакрытия {
		ЕСЛИ (К_САПУН == ВЫК)  В СОСТ Ошибка;
	}
	СОСТ Ошибка {
		ЕСЛИ (SendMsgGUICode(ПК_ГУИ_СПОНТАННОЕ_ЗАКРЫТИЕ_САПУНА)) ОШИБКА;
		/*  здесь надо выключать вакуумирование !!!!!!!!!!!!!????*/
		ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
	}
	
    /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/	
	СОСТ НарушениеСвязи {
		ОШИБКА;
	}
}	
/*=========================================================*/
/*=====   Процесс контроля выключенности сапуна       =====*/
/*=========================================================*/
ПРОЦ КонтрольСпонтанногоОткрытияСапуна{
	ИЗ ПРОЦ Инициализация 	К_САПУН;
	
	СОСТ Контроль {
		ЕСЛИ (К_САПУН == ВКЛ) В СОСТ Ошибка;
	}
	
	СОСТ Ошибка {
		ЕСЛИ (SendMsgGUICode(ПК_ГУИ_СПОНТАННОЕ_ОТКРЫТИЕ_САПУНА)) ОШИБКА;
				/*  если включился, то и ладно, вакуумирования-то нет !!!!!!!!!!!!!!!!!*/
		ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
	}
    /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/		
	СОСТ НарушениеСвязи {
		ОШИБКА;
	}
}			  

/*================================================================*/
/*              Процесс включить питание вакуумного насоса        */
/*================================================================*/
ПРОЦ ВКЛ_ПИТ_ВАК_НАСОСА{
 ИЗ ПРОЦ Инициализация 	К_ПИТ_ВАК_НАСОСА,
						У_ПИТ_ВАК_НАСОСА
					/*	ОШИБКА_ФУНКЦ_ПИТ_ВАК_НАСОСА */
						;
СОСТ Начало{
	ЕСЛИ (ПРОЦ ВЫК_ПИТ_ВАК_НАСОСА В СОСТ АКТИВНОЕ) ОШИБКА ПРОЦ ВЫК_ПИТ_ВАК_НАСОСА;
	СТОП ПРОЦ КонтрольСпонтанногоВключенияВакНасоса;
/*	ОШИБКА_ФУНКЦ_ПИТ_ВАК_НАСОСА = НЕТ;  */
	У_ПИТ_ВАК_НАСОСА = ВКЛ;
	В СЛЕДУЮЩЕЕ;      
 }
 СОСТ КонтрольВключения{  
  ЕСЛИ (К_ПИТ_ВАК_НАСОСА == ВКЛ) {
		СТАРТ ПРОЦ КонтрольСпонтанногоВыключенияВакНасоса;
		В СОСТ ПередачаСообщения;
  }
  ТАЙМАУТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_ВАК_НАСОСА 	В СОСТ Ошибка;
 } 
  СОСТ ПередачаСообщения {
    ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ПИТАНИЕ_ВАКУУМНОГО_НАСОСА_ВКЛЮЧЕНО)) СТОП;
    ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 } 
 
      /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ Ошибка {
       ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ОТКАЗ_ВКЛЮЧЕНИЯ_ПИТАНИЯ_ВАКУУМНОГО_НАСОСА))	ОШИБКА;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
  СОСТ НарушениеСвязи {
	   ОШИБКА;
 } 
} /*конец процесса ВКЛ_ПИТ_ВАК_НАСОСА  */
/*================================================================*/
/*              Процесс выключить питание вакуумного насоса       */
/*================================================================*/
ПРОЦ ВЫК_ПИТ_ВАК_НАСОСА{
 ИЗ ПРОЦ Инициализация 	К_ПИТ_ВАК_НАСОСА,
						У_ПИТ_ВАК_НАСОСА
					/*	ОШИБКА_ФУНКЦ_ПИТ_ВАК_НАСОСА */
						;
 СОСТ Начало{
	ЕСЛИ (ПРОЦ ВКЛ_ПИТ_ВАК_НАСОСА В СОСТ АКТИВНОЕ) ОШИБКА ПРОЦ ВКЛ_ПИТ_ВАК_НАСОСА;
	СТОП ПРОЦ КонтрольСпонтанногоВыключенияВакНасоса;
/*	ОШИБКА_ФУНКЦ_ПИТ_ВАК_НАСОСА = НЕТ; */
	У_ПИТ_ВАК_НАСОСА = ВЫК;
	В СЛЕДУЮЩЕЕ;      
 }
 СОСТ КонтрольВыключения{  
  ЕСЛИ (К_ПИТ_ВАК_НАСОСА == ВЫК) {
  		СТАРТ ПРОЦ КонтрольСпонтанногоВключенияВакНасоса;
		В СОСТ ПередачаСообщения;
  }
  ТАЙМАУТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_ВАК_НАСОСА 	В СОСТ Ошибка;
 } 
       /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ Ошибка {
       ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ОТКАЗ_ВЫКЛЮЧЕНИЯ_ПИТАНИЯ_ВАКУУМНОГО_НАСОСА))	ОШИБКА;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
  СОСТ НарушениеСвязи {
	   ОШИБКА;
 }
  СОСТ ПередачаСообщения {
    ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ПИТАНИЕ_ВАКУУМНОГО_НАСОСА_ВЫКЛЮЧЕНО)) СТОП;
    ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 } 
}  /*конец процесса ВЫК_ПИТ_ВАК_НАСОСА */
/*========================================================*/
/*=====   Процесс контроля спонтанного выключения  =====*/
/*========================================================*/
ПРОЦ КонтрольСпонтанногоВыключенияВакНасоса{
	ИЗ ПРОЦ Инициализация 	К_ПИТ_ВАК_НАСОСА;

	СОСТ Контроль {
		ЕСЛИ (К_ПИТ_ВАК_НАСОСА == ВЫК) В СОСТ Ошибка;
	}
	СОСТ Ошибка {
		ЕСЛИ (SendMsgGUICode(ПК_ГУИ_СПОНТАННОЕ_ВЫКЛЮЧЕНИЕ_ВАКУУМНОГО_НАСОСА)) ОШИБКА;
		/*  здесь надо выключать вакуумирование !!!!!!!!!!!!!!!!!*/
		ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
	}
	
    /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/	
	СОСТ НарушениеСвязи {
		ОШИБКА;
	}
}	
/*=========================================================*/
/*=====   Процесс контроля спонтанного включения  =====*/
/*=========================================================*/
ПРОЦ КонтрольСпонтанногоВключенияВакНасоса{
	ИЗ ПРОЦ Инициализация 	К_ПИТ_ВАК_НАСОСА;
	
	СОСТ Контроль {
		ЕСЛИ (К_ПИТ_ВАК_НАСОСА == ВКЛ) В СОСТ Ошибка;
	}
	СОСТ Ошибка {
		ЕСЛИ (SendMsgGUICode(ПК_ГУИ_СПОНТАННОЕ_ВКЛЮЧЕНИЕ_ВАКУУМНОГО_НАСОСА)) ОШИБКА;
				/*  если включился, то и ладно, вакуумирования-то нет !!!!!!!!!!!!!!!!!*/
		ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
	}
    /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/		
	СОСТ НарушениеСвязи {
		ОШИБКА;
	}
}			  

/*================================================================*/
/*              Процесс Подать питание на регулятор температуры   */
/*================================================================*/
ПРОЦ ВКЛ_ПИТ_РЕГ_Т{
 ИЗ ПРОЦ Инициализация 	У_ПИТ_РЕГ_Т,					
						К_ПИТ_РЕГ_Т
					/*	ОШИБКА_ФУНКЦ_ПИТ_РЕГ_Т */
						;
 
 
 СОСТ Начало{
	ЕСЛИ (ПРОЦ ВЫК_ПИТ_РЕГ_Т В СОСТ АКТИВНОЕ) ОШИБКА ПРОЦ ВЫК_ПИТ_РЕГ_Т;
		СТОП ПРОЦ КонтрольСпонтанногоВключенияРЕГ_Т;
/*		ОШИБКА_ФУНКЦ_ПИТ_РЕГ_Т = НЕТ; */
		У_ПИТ_РЕГ_Т = ВКЛ;
		В СЛЕДУЮЩЕЕ;      
 }

 СОСТ КонтрольВключения{  
	ЕСЛИ (К_ПИТ_РЕГ_Т == ВКЛ) {
		СТАРТ ПРОЦ КонтрольСпонтанногоВыключенияРЕГ_Т;
		В СОСТ ПередачаСообщения;
	}
	ТАЙМАУТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_РЕГ_Т В СОСТ Ошибка;
 } 
   СОСТ ПередачаСообщения {
    ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ПИТАНИЕ_РЕГУЛЯТОРА_ТЕМПЕРАТУРЫ_ВКЛЮЧЕНО)) СТОП;
    ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 } 
       /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ Ошибка {
       ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ОТКАЗ_ВКЛЮЧЕНИЯ_РЕГУЛЯТОРА_ТЕМПЕРАТУРЫ))	ОШИБКА;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
  СОСТ НарушениеСвязи {
	   ОШИБКА;
 } 
} /*конец процесса ВКЛ_ПИТ_РЕГ_Т */

/*================================================================*/
/*        Процесс Снять питание с регулятора температуры          */
/*================================================================*/
ПРОЦ ВЫК_ПИТ_РЕГ_Т{
 ИЗ ПРОЦ Инициализация 	У_ПИТ_РЕГ_Т,
						К_ПИТ_РЕГ_Т
					/*	ОШИБКА_ФУНКЦ_ПИТ_РЕГ_Т */
						;
 
 
 СОСТ Начало{
	ЕСЛИ (ПРОЦ ВКЛ_ПИТ_РЕГ_Т В СОСТ АКТИВНОЕ) ОШИБКА ПРОЦ ВКЛ_ПИТ_РЕГ_Т;
	СТОП ПРОЦ КонтрольСпонтанногоВыключенияРЕГ_Т;
/*	ОШИБКА_ФУНКЦ_ПИТ_РЕГ_Т = НЕТ; */
	У_ПИТ_РЕГ_Т  = ВЫК;
	В СЛЕДУЮЩЕЕ;      
 }
 СОСТ КонтрольВыключения{  
	ЕСЛИ (К_ПИТ_РЕГ_Т == ВКЛ) {
		СТАРТ ПРОЦ КонтрольСпонтанногоВключенияРЕГ_Т;  
		В СОСТ ПередачаСообщения;
	} 
	ТАЙМАУТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_РЕГ_Т В СОСТ Ошибка;
 }
  СОСТ ПередачаСообщения {
		ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ПИТАНИЕ_РЕГУЛЯТОРА_ТЕМПЕРАТУРЫ_ВЫКЛЮЧЕНО)) СТОП;
		ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }  
        /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ Ошибка {
       ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ОТКАЗ_ВЫКЛЮЧЕНИЯ_РЕГУЛЯТОРА_ТЕМПЕРАТУРЫ))	ОШИБКА;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
  СОСТ НарушениеСвязи {
	   ОШИБКА;
 } 
}  /*конец процесса ВЫК_ПИТ_РЕГ_Т */

/*========================================================*/
/*=====   Процесс контроля спонтанного выключения  =====*/
/*========================================================*/
ПРОЦ КонтрольСпонтанногоВыключенияРЕГ_Т{
	ИЗ ПРОЦ Инициализация 	К_ПИТ_РЕГ_Т;

	СОСТ Контроль {
		ЕСЛИ (К_ПИТ_РЕГ_Т == ВЫК) В СОСТ Ошибка;
	}
	СОСТ Ошибка {
		ЕСЛИ (SendMsgGUICode(ПК_ГУИ_СПОНТАННОЕ_ВЫКЛЮЧЕНИЕ_РЕГУЛЯТОРА_ТЕМПЕРАТУРЫ)) ОШИБКА;
		/*  здесь надо выключать вакуумирование !!!!!!!!!!!!!!!!!*/
		ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
	}
	
    /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/	
	СОСТ НарушениеСвязи {
		ОШИБКА;
	}
}	
/*=========================================================*/
/*=====   Процесс контроля спонтанного включения  =====*/
/*=========================================================*/
ПРОЦ КонтрольСпонтанногоВключенияРЕГ_Т{
	ИЗ ПРОЦ Инициализация 	К_ПИТ_РЕГ_Т;
	
	СОСТ Контроль {
		ЕСЛИ (К_ПИТ_РЕГ_Т == ВКЛ) В СОСТ Ошибка;
	}
	СОСТ Ошибка {
		ЕСЛИ (SendMsgGUICode(ПК_ГУИ_СПОНТАННОЕ_ВКЛЮЧЕНИЕ_РЕГУЛЯТОРА_ТЕМПЕРАТУРЫ)) ОШИБКА;
				/*  если включился, то и ладно, вакуумирования-то нет !!!!!!!!!!!!!!!!!*/
		ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
	}
    /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/		
	СОСТ НарушениеСвязи {
		ОШИБКА;
	}
}			  

/*================================================================*/
/*        Процесс Подать питание на регулятор уровня воды         */
/*================================================================*/
ПРОЦ ВКЛ_ПИТ_РЕГ_УР{
 ИЗ ПРОЦ Инициализация 	У_ПИТ_РЕГ_УР,
						К_ПИТ_РЕГ_УР
					/*	ОШИБКА_ФУНКЦ_ПИТ_РЕГ_УР */
						;

 СОСТ Начало{
	ЕСЛИ (ПРОЦ ВЫК_ПИТ_РЕГ_УР В СОСТ АКТИВНОЕ) ОШИБКА ПРОЦ ВЫК_ПИТ_РЕГ_УР;
	СТОП ПРОЦ КонтрольСпонтанногоВключенияРЕГ_УР;
/*	ОШИБКА_ФУНКЦ_ПИТ_РЕГ_УР = НЕТ; */
	У_ПИТ_РЕГ_УР = ВКЛ;
	В СЛЕДУЮЩЕЕ;      
 }

 СОСТ Контроль{  
	ЕСЛИ (К_ПИТ_РЕГ_УР == ВКЛ){
		СТАРТ ПРОЦ КонтрольСпонтанногоВыключенияРЕГ_УР;
		В СОСТ ПередачаСообщения;
	} 
  ТАЙМАУТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_РЕГ_УР В СОСТ Ошибка; 
  }
 СОСТ ПередачаСообщения {
  ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ПИТАНИЕ_РЕГУЛЯТОРА_УРОВНЯ_ВКЛЮЧЕНО)) СТОП;
  ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }  
  
         /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ Ошибка {
       ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ОТКАЗ_ВКЛЮЧЕНИЯ_РЕГУЛЯТОРА_УРОВНЯ))	ОШИБКА;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
  СОСТ НарушениеСвязи {
	   ОШИБКА;
 } 
}  /*конец процесса ВКЛ_ПИТ_РЕГ_УР */

/*================================================================*/
/*        Процесс Снять питание с регулятора уровня воды          */
/*================================================================*/
ПРОЦ ВЫК_ПИТ_РЕГ_УР{
 ИЗ ПРОЦ Инициализация 	У_ПИТ_РЕГ_УР,
						К_ПИТ_РЕГ_УР
					/*	ОШИБКА_ФУНКЦ_ПИТ_РЕГ_УР */
						;

 СОСТ Начало{
	ЕСЛИ (ПРОЦ ВКЛ_ПИТ_РЕГ_УР В СОСТ АКТИВНОЕ) ОШИБКА ПРОЦ ВКЛ_ПИТ_РЕГ_УР;
	СТОП ПРОЦ КонтрольСпонтанногоВыключенияРЕГ_УР;
/*	ОШИБКА_ФУНКЦ_ПИТ_РЕГ_УР = НЕТ; */
	У_ПИТ_РЕГ_УР = ВЫК;
	В СЛЕДУЮЩЕЕ;      
 }

 СОСТ Контроль{  
	ЕСЛИ (К_ПИТ_РЕГ_УР == ВЫК) {
		СТАРТ ПРОЦ КонтрольСпонтанногоВключенияРЕГ_УР;  
		В СОСТ ПередачаСообщения;
	} 
  ТАЙМАУТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_РЕГ_УР 	В СОСТ Ошибка; 
 }
 СОСТ ПередачаСообщения {
  ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ПИТАНИЕ_РЕГУЛЯТОРА_УРОВНЯ_ВЫКЛЮЧЕНО)) СТОП;
  ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }  
 
          /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
  СОСТ Ошибка {
       ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ОТКАЗ_ВЫКЛЮЧЕНИЯ_РЕГУЛЯТОРА_УРОВНЯ))	ОШИБКА;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
  СОСТ НарушениеСвязи {
	   ОШИБКА;
 } 
}  /*конец процесса ВЫК_ПИТ_РЕГ_УР */

/*========================================================*/
/*=====   Процесс контроля спонтанного выключения  =====*/
/*========================================================*/
ПРОЦ КонтрольСпонтанногоВыключенияРЕГ_УР{
	ИЗ ПРОЦ Инициализация 	К_ПИТ_РЕГ_УР;

	СОСТ Контроль {
		ЕСЛИ (К_ПИТ_РЕГ_УР == ВЫК) В СОСТ Ошибка;
	}
	СОСТ Ошибка {
		ЕСЛИ (SendMsgGUICode(ПК_ГУИ_СПОНТАННОЕ_ВЫКЛЮЧЕНИЕ_РЕГУЛЯТОРА_УРОВНЯ)) ОШИБКА;
		/*  здесь надо выключать вакуумирование !!!!!!!!!!!!!!!!!*/
		ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
	}
	
    /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/	
	СОСТ НарушениеСвязи {
		ОШИБКА;
	}
}	
/*=========================================================*/
/*=====   Процесс контроля спонтанного включения  =====*/
/*=========================================================*/
ПРОЦ КонтрольСпонтанногоВключенияРЕГ_УР{
	ИЗ ПРОЦ Инициализация 	К_ПИТ_РЕГ_УР;
	
	СОСТ Контроль {
		ЕСЛИ (К_ПИТ_РЕГ_УР == ВКЛ) В СОСТ Ошибка;
	}
	СОСТ Ошибка {
		ЕСЛИ (SendMsgGUICode(ПК_ГУИ_СПОНТАННОЕ_ВКЛЮЧЕНИЕ_РЕГУЛЯТОРА_УРОВНЯ)) ОШИБКА;
				/*  если включился, то и ладно, вакуумирования-то нет !!!!!!!!!!!!!!!!!*/
		ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
	}
    /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/		
	СОСТ НарушениеСвязи {
		ОШИБКА;
	}
}	

/*================================================================*/
/*              Процесс Подать питание на насос охлаждения        */
/*================================================================*/
ПРОЦ ВКЛ_ПИТ_НАС_ОХЛ{
 ИЗ ПРОЦ Инициализация 	У_ПИТ_НАС_ОХЛ,
						К_ПИТ_НАСОС_ОХЛ,
						ФлагГотовУрВодыБО,
						ФлагГотовТемпВодыБО
					/*	ОШИБКА_ФУНКЦ_НАСОС_ОХЛ */
						;
						
 СОСТ Начало{
  ЕСЛИ (ФлагГотовУрВодыБО == НОРМА)  В СЛЕДУЮЩЕЕ;
  ИНАЧЕ ОШИБКА;      
 }

 СОСТ ВключениеПитания{
	ЕСЛИ (ПРОЦ ВЫК_ПИТ_НАС_ОХЛ В СОСТ АКТИВНОЕ) ОШИБКА ПРОЦ ВЫК_ПИТ_НАС_ОХЛ;
	СТОП ПРОЦ КонтрольСпонтанногоВключенияНасосаОхлаждения;   
/*	ОШИБКА_ФУНКЦ_НАСОС_ОХЛ = НЕТ; */
	У_ПИТ_НАС_ОХЛ = ВКЛ;
	В СЛЕДУЮЩЕЕ;      
 }

 СОСТ Контроль{  
  ЕСЛИ (К_ПИТ_НАСОС_ОХЛ == ВКЛ) {
  	СТАРТ ПРОЦ КонтрольСпонтанногоВыключенияНасосаОхлаждения;
	В СОСТ ПередачаСообщения;
  } 
  ТАЙМАУТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_НАСОС_ОХЛ В СОСТ Ошибка;
 }    
 СОСТ ПередачаСообщения {
  ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ПИТАНИЕ_НАСОСА_ОХЛАЖДЕНИЯ_ВКЛЮЧЕНО)) СТОП;
  ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }   

           /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
  СОСТ Ошибка {
       ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ОТКАЗ_ВКЛЮЧЕНИЯ_НАСОСА_ОХЛАЖДЕНИЯ))	ОШИБКА;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
  СОСТ НарушениеСвязи {
	   ОШИБКА;
 } 
} /*конец процесса ВКЛ_ПИТ_НАС_ОХЛАЖДЕНИЯ */

/*================================================================*/
/*             Процесс Снять питание с насоса охлаждения          */
/*================================================================*/
ПРОЦ ВЫК_ПИТ_НАС_ОХЛ{
 ИЗ ПРОЦ Инициализация 	У_ПИТ_НАС_ОХЛ,
						К_ПИТ_НАСОС_ОХЛ
					/*	ОШИБКА_ФУНКЦ_НАСОС_ОХЛ */
						;

 СОСТ Начало{
	ЕСЛИ (ПРОЦ ВКЛ_ПИТ_НАС_ОХЛ В СОСТ АКТИВНОЕ) ОШИБКА ПРОЦ ВКЛ_ПИТ_НАС_ОХЛ;
   	СТОП ПРОЦ КонтрольСпонтанногоВыключенияНасосаОхлаждения;
/*	ОШИБКА_ФУНКЦ_НАСОС_ОХЛ = НЕТ; */
	У_ПИТ_НАС_ОХЛ = ВЫК;
	В СЛЕДУЮЩЕЕ;      
 }

 СОСТ Контроль{  
  ЕСЛИ (К_ПИТ_НАСОС_ОХЛ == ВЫК) {
  		СТАРТ ПРОЦ КонтрольСпонтанногоВключенияНасосаОхлаждения;
		В СОСТ ПередачаСообщения;
  }
  ТАЙМАУТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_НАСОС_ОХЛ В СОСТ Ошибка; 
 }
 СОСТ ПередачаСообщения {
  ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ПИТАНИЕ_НАСОСА_ОХЛАЖДЕНИЯ_ВЫКЛЮЧЕНО)) СТОП;
  ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }  
            /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
  СОСТ Ошибка {
       ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ОТКАЗ_ВЫКЛЮЧЕНИЯ_НАСОСА_ОХЛАЖДЕНИЯ))	ОШИБКА;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
  СОСТ НарушениеСвязи {
	   ОШИБКА;
 } 
} /*конец процесса ВЫК_ПИТ_НАС_ОХЛАЖДЕНИЯ */

/*========================================================*/
/*=====   Процесс контроля спонтанного выключения  =====*/
/*========================================================*/
ПРОЦ КонтрольСпонтанногоВыключенияНасосаОхлаждения{
	ИЗ ПРОЦ Инициализация 	К_ПИТ_НАСОС_ОХЛ;

	СОСТ Контроль {
		ЕСЛИ (К_ПИТ_НАСОС_ОХЛ == ВЫК) В СОСТ Ошибка;
	}
	СОСТ Ошибка {
		ЕСЛИ (SendMsgGUICode(ПК_ГУИ_СПОНТАННОЕ_ВЫКЛЮЧЕНИЕ_НАСОСА_ОХЛАЖДЕНИЯ)) ОШИБКА;
		/*  здесь надо выключать вакуумирование !!!!!!!!!!!!!!!!!*/
		ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
	}
	
    /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/	
	СОСТ НарушениеСвязи {
		ОШИБКА;
	}
}	
/*=========================================================*/
/*=====   Процесс контроля спонтанного включения  =====*/
/*=========================================================*/
ПРОЦ КонтрольСпонтанногоВключенияНасосаОхлаждения{
	ИЗ ПРОЦ Инициализация 	К_ПИТ_НАСОС_ОХЛ;
	
	СОСТ Контроль {
		ЕСЛИ (К_ПИТ_НАСОС_ОХЛ == ВКЛ) В СОСТ Ошибка;
	}
	СОСТ Ошибка {
		ЕСЛИ (SendMsgGUICode(ПК_ГУИ_СПОНТАННОЕ_ВКЛЮЧЕНИЕ_НАСОСА_ОХЛАЖДЕНИЯ)) ОШИБКА;
				/*  если включился, то и ладно, вакуумирования-то нет !!!!!!!!!!!!!!!!!*/
		ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
	}
    /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/		
	СОСТ НарушениеСвязи {
		ОШИБКА;
	}
}	


/*=========================================================++++++++++++======*/
/*==   Процесс  ВКЛЮЧИТЬ Устройство плавного пуска (запуск вакуумирования)   */
/*== это означает что вакуумного насос начнет работать          			 */
/*==========================================================++++++++++++=====*/

ПРОЦ ВКЛ_УСТР_ПЛАВ_ПУСКА{
 ИЗ ПРОЦ Инициализация 	К_УСТР_ПЛАВ_ПУСКА,
						У_УСТР_ПЛАВ_ПУСКА
					/*	ОШИБКА_ФУНКЦ_УСТР_ПЛАВ_ПУСКА */
						;
						

 СОСТ Начало{
	ЕСЛИ (ПРОЦ ВЫК_УСТР_ПЛАВ_ПУСКА В СОСТ АКТИВНОЕ) ОШИБКА ПРОЦ ВЫК_УСТР_ПЛАВ_ПУСКА;
    СТОП ПРОЦ КонтрольСпонтанногоВключенияПлавнПуска;
/*	ОШИБКА_ФУНКЦ_УСТР_ПЛАВ_ПУСКА = НЕТ;  */
	У_УСТР_ПЛАВ_ПУСКА = ВКЛ;
	В СЛЕДУЮЩЕЕ;      
 }

 СОСТ Контроль{  
	ЕСЛИ (К_УСТР_ПЛАВ_ПУСКА == ВКЛ) {
		СТАРТ ПРОЦ КонтрольСпонтанногоВыключенияПлавнПуска;  
		В СОСТ ПередачаСообщения;
	}
  ТАЙМАУТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_УСТР_ПЛАВ_ПУСКА  	В СОСТ Ошибка;
 } 
 СОСТ ПередачаСообщения {
  ЕСЛИ (SendMsgGUICode(ПК_ГУИ_УСТРОЙСТВО_ПЛАВНОГО_ПУСКА_ВКЛЮЧЕНО)) СТОП;
  ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }  
     /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ Ошибка {
       ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ОТКАЗ_ВКЛЮЧЕНИЯ_УСТР_ПЛАВ_ПУСКА))	ОШИБКА;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
  СОСТ НарушениеСвязи {
	   ОШИБКА;
 } 
}  /*конец процесса ВКЛ_УСТР_ПЛАВ_ПУСКА  */

/*===============================================================*/
/*==   Процесс  ВЫКЛЮЧИТЬ Устройство плавного пуска                  */
/*===============================================================*/
ПРОЦ ВЫК_УСТР_ПЛАВ_ПУСКА{
 ИЗ ПРОЦ Инициализация 	К_УСТР_ПЛАВ_ПУСКА,
						У_УСТР_ПЛАВ_ПУСКА
					/*	ОШИБКА_ФУНКЦ_УСТР_ПЛАВ_ПУСКА */
						;
						
 СОСТ Начало{	
	ЕСЛИ (ПРОЦ ВКЛ_УСТР_ПЛАВ_ПУСКА В СОСТ АКТИВНОЕ) ОШИБКА ПРОЦ ВКЛ_УСТР_ПЛАВ_ПУСКА;
	СТОП ПРОЦ КонтрольСпонтанногоВыключенияПлавнПуска;	
/*	ОШИБКА_ФУНКЦ_УСТР_ПЛАВ_ПУСКА = НЕТ; */
	У_УСТР_ПЛАВ_ПУСКА = ВЫК;	
	В СЛЕДУЮЩЕЕ;      
 }
 СОСТ КонтрольВыключения{  
	ЕСЛИ (К_УСТР_ПЛАВ_ПУСКА == ВЫК) {
		СТАРТ ПРОЦ КонтрольСпонтанногоВключенияПлавнПуска;
		В СОСТ ПередачаСообщения;
	}
	ТАЙМАУТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_УСТР_ПЛАВ_ПУСКА 	В СОСТ Ошибка;
 }
 СОСТ ПередачаСообщения {
  ЕСЛИ (SendMsgGUICode(ПК_ГУИ_УСТРОЙСТВО_ПЛАВНОГО_ПУСКА_ВЫКЛЮЧЕНО)) СТОП;
  ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи; 
  }
  
       /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
  СОСТ Ошибка {
       ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ОТКАЗ_ВЫКЛЮЧЕНИЯ_УСТР_ПЛАВ_ПУСКА))	ОШИБКА;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
  СОСТ НарушениеСвязи {
	   ОШИБКА;
 } 
} /*конец процесса ВЫК_УСТР_ПЛАВ_ПУСКА  */

/*========================================================*/
/*=====   Процесс контроля спонтанного выключения  =====*/
/*========================================================*/
ПРОЦ КонтрольСпонтанногоВыключенияПлавнПуска{
	ИЗ ПРОЦ Инициализация 	К_УСТР_ПЛАВ_ПУСКА;

	СОСТ Контроль {
		ЕСЛИ (К_УСТР_ПЛАВ_ПУСКА == ВЫК) В СОСТ Ошибка;
	}
	СОСТ Ошибка {
		ЕСЛИ (SendMsgGUICode(ПК_ГУИ_СПОНТАННОЕ_ВЫКЛЮЧЕНИЕ_УСТРОЙСТВА_ПЛАВНОГО_ПУСКА)) ОШИБКА;
		/*  здесь надо выключать вакуумирование !!!!!!!!!!!!!!!!!*/
		ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
	}
	
    /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/	
	СОСТ НарушениеСвязи {
		ОШИБКА;
	}
}	
/*=========================================================*/
/*=====   Процесс контроля спонтанного включения  =====*/
/*=========================================================*/
ПРОЦ КонтрольСпонтанногоВключенияПлавнПуска{
	ИЗ ПРОЦ Инициализация 	К_УСТР_ПЛАВ_ПУСКА;
	
	СОСТ Контроль {
		ЕСЛИ (К_УСТР_ПЛАВ_ПУСКА == ВКЛ) В СОСТ Ошибка;
	}
	СОСТ Ошибка {
		ЕСЛИ (SendMsgGUICode(ПК_ГУИ_СПОНТАННОЕ_ВКЛЮЧЕНИЕ_УСТРОЙСТВА_ПЛАВНОГО_ПУСКА)) ОШИБКА;
				/*  если включился, то и ладно, вакуумирования-то нет !!!!!!!!!!!!!!!!!*/
		ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
	}
    /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/		
	СОСТ НарушениеСвязи {
		ОШИБКА;
	}
}	





/*======================================================================*/
/*              Процесс Включить вакуумную подсистему                   */
/*   Этапы включения вакуумного насоса:                                 */ 
/*   1. Проверяем допустимое давление в компрессоре. Норма - идем дальше */  
/*   2. Проверяем температуру и уровень воды в баке охлаждения          */ 
/*      норма - идем дальше   											*/ 
/*   3. Включаем питание вентилятора                                    */ 
/*   4. Включаем питание насоса охлаждения                              */  
/*   5. Закрываем затвор вакуумный                                      */ 
/*   6. Открываем сапун                                                 */ 
/*   7. Контролируем температуру воды в рубашке, если норма             */
/*                                    - идем дальше                     */ 
/*   8. Включаем питание вакуумного насоса                              */ 
/*   9. Включаем устройство плавного пуска                              */ 
/*   10. Выдерживаем одну минуту и закрываем сапун                      */ 
/*   11. Если ФП_P_ВтрубеСидер >= ФП_P_Впатрубке - открываем затвор     */
/*       вакуумный. Пошло вакуумирование. Иначе - даем 2 минуты  		*/
/*		на откачку из патрубка, и      									*/  
/*       если откачки нет, то стартуем проц выключения вак. подсистемы  */
/*       и уходим в ошибку .  											*/
/*======================================================================*/
ПРОЦ ВКЛ_ВАКУУМНУЮ_ПОДСИСТЕМУ{
 ИЗ ПРОЦ Инициализация 	У_ПИТ_НАС_ОХЛ,
						ФП_P_Впатрубке,		
						ФП_P_ВтрубеСидер,
						ФлагГотовTВодыРО,
						ФлагГотовТемпВодыБО,
						ФП_P_ВбакеКомпрессора,
						ФП_T_Вбаке,
						ФлагГотовУрВодыБО,
						ФлагДавленияКомпрессора;
 ПЛАВ ЛП_Предыдущее ЛОКАЛ;
 
 СОСТ Начало{
	ЕСЛИ (ПРОЦ ВЫК_ВАКУУМНУЮ_ПОДСИСТЕМУ В СОСТ АКТИВНОЕ) ОШИБКА ПРОЦ ВЫК_ВАКУУМНУЮ_ПОДСИСТЕМУ;
	/* нельзя включать насос вакуумный, если давление в компрессоре < 5-10 атмосфер*/
		ЕСЛИ (ФлагДавленияКомпрессора != НОРМА) В СОСТ ВыключениеПоОшибке;
		ИНАЧЕ В СЛЕДУЮЩЕЕ;
	}
	
	/*м.б. здесь надо подождать, пока температура достигнет нормы?  ????????????*/
	
	СОСТ КонтрольГотовностиУровняИтемпературы{
		ЕСЛИ ((ФП_T_Вбаке > Т_ПредельнаяВбакеОхлаждения)|| 
			(ФлагГотовУрВодыБО == ВНЕ_НОРМЫ)||
			(ФлагГотовТемпВодыБО == ВНЕ_НОРМЫ )) В СОСТ ВыключениеПоОшибке;
			ИНАЧЕ 	{
				СТАРТ ПРОЦ ВКЛ_ПИТ_ВЕНТИЛЯТОР;
				В СЛЕДУЮЩЕЕ;
			}
	}
	СОСТ КонтрольВключенияВентилятора{
		ЕСЛИ ( ПРОЦ ВКЛ_ПИТ_ВЕНТИЛЯТОР В СОСТ ПАССИВНОЕ){ 
				ЕСЛИ (ПРОЦ  ВКЛ_ПИТ_ВЕНТИЛЯТОР В СОСТ ОШИБКА ) В СОСТ ВыключениеПоОшибке;
				ИНАЧЕ {
					СТАРТ ПРОЦ ВКЛ_ПИТ_НАС_ОХЛ;
					В СЛЕДУЮЩЕЕ;
				}
		}
	}  
	СОСТ КонтрольВключенияПитанияНасосаОхлаждения{
		ЕСЛИ (ПРОЦ ВКЛ_ПИТ_НАС_ОХЛ В СОСТ ПАССИВНОЕ){ 
				ЕСЛИ (ПРОЦ  ВКЛ_ПИТ_НАС_ОХЛ В СОСТ ОШИБКА) В СОСТ ВыключениеПоОшибке;
				ИНАЧЕ {
					СТАРТ ПРОЦ ЗАКР_ЗАТВОР_ВАК ;
					В СЛЕДУЮЩЕЕ;
				}
		}
	}  
	СОСТ КонтрольЗакрытияЗатвораВакуумного{
		ЕСЛИ (ПРОЦ ЗАКР_ЗАТВОР_ВАК В СОСТ ПАССИВНОЕ){ 
			ЕСЛИ (ПРОЦ ЗАКР_ЗАТВОР_ВАК В СОСТ ОШИБКА ) В СОСТ ВыключениеПоОшибке;
			ИНАЧЕ {
				СТАРТ ПРОЦ ОТКР_САПУН;
				В СЛЕДУЮЩЕЕ;
			}
		} 
	} 
	СОСТ КонтрольОткрытияСапуна{
		ЕСЛИ ( ПРОЦ ОТКР_САПУН В СОСТ ПАССИВНОЕ){ 
			ЕСЛИ (ПРОЦ ОТКР_САПУН В СОСТ ОШИБКА ) В СОСТ ВыключениеПоОшибке;
			ИНАЧЕ В СЛЕДУЮЩЕЕ;
		} 
	}
 /*м.б. здесь надо подождать, пока температура достигнет нормы?  ????????????*/ 	
	СОСТ КонтрольТемпературыРубашки{
		ЕСЛИ (ФлагГотовTВодыРО == НОРМА) В СЛЕДУЮЩЕЕ;
/*		ТАЙМАУТ ПЯТЬ_МИНУТ ОШИБКА;  */
		ИНАЧЕ В СОСТ ВыключениеПоОшибке;
	}
	СОСТ ПодачаПитанияНаВакуумныйНасос{
		СТАРТ ПРОЦ ВКЛ_ПИТ_ВАК_НАСОСА;
		В СЛЕДУЮЩЕЕ;
	}	
	СОСТ КонтрольПитанияВакуумногоНасоса{
		ЕСЛИ ( ПРОЦ ВКЛ_ПИТ_ВАК_НАСОСА В СОСТ ПАССИВНОЕ){ 
			ЕСЛИ (ПРОЦ  ВКЛ_ПИТ_ВАК_НАСОСА В СОСТ ОШИБКА) В СОСТ ВыключениеПоОшибке;
			ИНАЧЕ  В СЛЕДУЮЩЕЕ;
		} 
	}  
	СОСТ ВключениеУстройстваПлавногоПуска{
		СТАРТ ПРОЦ ВКЛ_УСТР_ПЛАВ_ПУСКА;
		В СЛЕДУЮЩЕЕ;
	}
	СОСТ КонтрольВлюченияПлавногоПуска{
		ЕСЛИ (ПРОЦ ВКЛ_УСТР_ПЛАВ_ПУСКА В СОСТ ПАССИВНОЕ){ 
			ЕСЛИ (ПРОЦ  ВКЛ_УСТР_ПЛАВ_ПУСКА В СОСТ ОШИБКА) В СОСТ ВыключениеПоОшибке;
			ИНАЧЕ  В СЛЕДУЮЩЕЕ;
		} 
	}  	
	СОСТ ТаймаутПередЗакрытиемСапуна{  /*насос должен разогнаться*/
		ТАЙМАУТ ОДНА_МИНУТА В СЛЕДУЮЩЕЕ; 
	}  
	
	СОСТ ЗакрытиеСапуна{  
		СТАРТ ПРОЦ ЗАКР_САПУН;
		В СЛЕДУЮЩЕЕ; 
	} 
	СОСТ КонтрольЗакрытияСапуна{
		ЕСЛИ ( ПРОЦ ЗАКР_САПУН В СОСТ ПАССИВНОЕ){ 
			ЕСЛИ (ПРОЦ ЗАКР_САПУН В СОСТ ОШИБКА ) В СОСТ ВыключениеПоОшибке;
			ИНАЧЕ В СЛЕДУЮЩЕЕ;
		} 
	} 
	
	СОСТ ЗапоминаемДавлениеВпатрубке{
			ЛП_Предыдущее = ФП_P_Впатрубке;
			В СЛЕДУЮЩЕЕ;
	}
	СОСТ КонтрольДавления{  
		ЕСЛИ (ФП_P_ВтрубеСидер >= ФП_P_Впатрубке) {
			СТАРТ ПРОЦ ОТКР_ЗАТВОР_ВАК; 
			В СОСТ КонтрольОткрытияЗВ;
			}
		ИНАЧЕ В СЛЕДУЮЩЕЕ;
	} 	
	СОСТ ПаузаНаОткачкуВпатрубке{  
		ТАЙМАУТ ДВЕ_МИНУТЫ В СЛЕДУЮЩЕЕ; 
	}  
	СОСТ ПроверкаУменьшенияДавленияВпатрубке{  
		ЕСЛИ(ФП_P_Впатрубке < ЛП_Предыдущее) В СОСТ ЗапоминаемДавлениеВпатрубке;
        ИНАЧЕ В СОСТ Ошибка;
	} 	
	СОСТ КонтрольОткрытияЗВ{
		ЕСЛИ ( ПРОЦ ОТКР_ЗАТВОР_ВАК В СОСТ ПАССИВНОЕ){ 
			ЕСЛИ (ПРОЦ ОТКР_ЗАТВОР_ВАК В СОСТ ОШИБКА ) В СОСТ ВыключениеПоОшибке;
			ИНАЧЕ { 
				СТАРТ ПРОЦ КонтрольОткачкиВауумнойТрубыСидеростата;
				СТОП;
			}
		} 
	}

    /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ Ошибка {
       ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ДАВЛЕНИЕ_В_ПАТРУБКЕ_НЕ_УМЕНЬШАЕТСЯ))	В СЛЕДУЮЩЕЕ;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
 
 СОСТ ВыключениеПоОшибке{
     ЕСЛИ (!(ПРОЦ ВЫК_ВАКУУМНУЮ_ПОДСИСТЕМУ В СОСТ АКТИВНОЕ)) СТАРТ ПРОЦ ВЫК_ВАКУУМНУЮ_ПОДСИСТЕМУ;
    В СЛЕДУЮЩЕЕ;
 }
/*	СОСТ КонтрольВыключенияВакПодсистемы{ */
/*		ЕСЛИ ( ПРОЦ ВЫК_ВАКУУМНУЮ_ПОДСИСТЕМУ В СОСТ ПАССИВНОЕ){ */
/*			ЕСЛИ (ПРОЦ  ВЫК_ВАКУУМНУЮ_ПОДСИСТЕМУ В СОСТ ОШИБКА) ОШИБКА; */
/*			ИНАЧЕ  В СОСТ СообщениеОбОтказеВакуумирования; */
/*		} */
/*	} */
 СОСТ СообщениеОбОтказеВакуумирования{
       ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ОТКАЗ_ВКЛЮЧЕНИЯ_ВАКУУМНОЙ_ПОДСИСТЕМЫ))	ОШИБКА;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи; 
 } 
 СОСТ НарушениеСвязи {
	   ОШИБКА;
 }    
}
/*================================================================*/
/*          Процесс Выключить вакуумную подсистему вакуумный      */
/*   Этапы выключения вакуумного насоса:                          */
/*    1. Закрывается затвор вакуумный                             */
/*    2. Открывается сапун                                        */
/*    3. Ничего не делаем 20 секунд                               */
/*    4. Выключаем устройство плавного пуска                      */
/*    5. Выключаем питание вакуумного насоса                      */
/*    6. Выключаем питание насоса охлаждения                      */
/*    7. Выключаем питание вентилятора                            */
/*================================================================*/
ПРОЦ ВЫК_ВАКУУМНУЮ_ПОДСИСТЕМУ{
 ИЗ ПРОЦ Инициализация 	У_ПИТ_НАС_ОХЛ;
 
	СОСТ Начало{
 		СТАРТ ПРОЦ ЗАКР_ЗАТВОР_ВАК;
		СТОП ПРОЦ КонтрольОткачкиВауумнойТрубыСидеростата;
		В СЛЕДУЮЩЕЕ;
	}  
	СОСТ КонтрольЗакрытияЗатвораВакуумного{
		ЕСЛИ (ПРОЦ ЗАКР_ЗАТВОР_ВАК В СОСТ ПАССИВНОЕ){ 
			ЕСЛИ (ПРОЦ ЗАКР_ЗАТВОР_ВАК В СОСТ ОШИБКА ) В СОСТ СообщениеОбОтказеВыклВакСистемы;
			ИНАЧЕ {
				СТАРТ ПРОЦ ОТКР_САПУН;
				В СЛЕДУЮЩЕЕ;
			}
		} 
	} 
	СОСТ КонтрольОткрытияСапуна{
		ЕСЛИ ( ПРОЦ ОТКР_САПУН В СОСТ ПАССИВНОЕ){ 
			ЕСЛИ (ПРОЦ ОТКР_САПУН В СОСТ ОШИБКА ) В СОСТ СообщениеОбОтказеВыклВакСистемы;
			ИНАЧЕ {
				
				В СЛЕДУЮЩЕЕ;
			}
		} 
	} 
	СОСТ ТаймаутПередВыключениемПитанияВакуумногоНасоса{  
		ТАЙМАУТ ДВАДЦАТЬ_СЕКУНД В СЛЕДУЮЩЕЕ; 
	}  
	СОСТ ВыключаемУППВакуумногоНасоса{  
		СТАРТ ПРОЦ ВЫК_УСТР_ПЛАВ_ПУСКА; В СЛЕДУЮЩЕЕ; 
	}	
	СОСТ КонтрольВыключенияПитанияУППВакуумногоНасоса{
		ЕСЛИ ( ПРОЦ ВЫК_УСТР_ПЛАВ_ПУСКА В СОСТ ПАССИВНОЕ){ 
			ЕСЛИ (ПРОЦ  ВЫК_УСТР_ПЛАВ_ПУСКА В СОСТ ОШИБКА) В СОСТ СообщениеОбОтказеВыклВакСистемы;
			ИНАЧЕ {
				СТАРТ ПРОЦ ВЫК_ПИТ_ВАК_НАСОСА;
				В СЛЕДУЮЩЕЕ;
			}
		} 
	}   
	СОСТ КонтрольВыключенияПитанияВакуумногоНасоса{
		ЕСЛИ ( ПРОЦ ВЫК_ПИТ_ВАК_НАСОСА В СОСТ ПАССИВНОЕ){ 
			ЕСЛИ (ПРОЦ  ВЫК_ПИТ_ВАК_НАСОСА В СОСТ ОШИБКА) В СОСТ СообщениеОбОтказеВыклВакСистемы;
			ИНАЧЕ {
				СТАРТ ПРОЦ ВЫК_ПИТ_НАС_ОХЛ;
				В СЛЕДУЮЩЕЕ;
			}
		} 
	}   	
	СОСТ КонтрольВыключенияНасосаОхлаждения{
		ЕСЛИ ( ПРОЦ ВЫК_ПИТ_НАС_ОХЛ В СОСТ ПАССИВНОЕ){ 
				ЕСЛИ (ПРОЦ  ВЫК_ПИТ_НАС_ОХЛ В СОСТ ОШИБКА ) В СОСТ СообщениеОбОтказеВыклВакСистемы;
				ИНАЧЕ {
					СТАРТ ПРОЦ ВЫК_ПИТ_ВЕНТИЛЯТОР;
					В СЛЕДУЮЩЕЕ;
				}
		}
	}  
	СОСТ КонтрольВыключенияВентилятора{
		ЕСЛИ ( ПРОЦ ВЫК_ПИТ_ВЕНТИЛЯТОР В СОСТ ПАССИВНОЕ){ 
				ЕСЛИ (ПРОЦ ВЫК_ПИТ_ВЕНТИЛЯТОР В СОСТ ОШИБКА ) В СОСТ СообщениеОбОтказеВыклВакСистемы;

				ИНАЧЕ СТОП;
		}
	} 
  /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ СообщениеОбОтказеВыклВакСистемы{
      ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ОТКАЗ_ВЫКЛЮЧЕНИЯ_ВАКУУМНОЙ_ПОДСИСТЕМЫ))	ОШИБКА;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи; 
 } 
 СОСТ НарушениеСвязи {
	   ОШИБКА;
 }  
	
} /*конец процесса ВЫК_ВАКУУМНУЮ_ПОДСИСТЕМУ */

/*====================================================================*/
/*              Процесс ВКЛ_ВАКУУМИРОВАНИЕ_ТРУБЫ 				      */
/* Процесс смотрит готовность вакуума, если в норме, то стопует себя. */
/* иначе запускает процесс ВКЛ_ВАКУУМНУЮ_ПОДСИСТЕМУ, и контролирует        */
/* глубину откачки вак-ной трубы , как только вакуумирование          */
/*  достигнет нормы ( нужный вакуум есть) процесс                     */
/*  заканчивается без ошибки. Насос вакуумный выключается             */
/*  Если вакуумирование  не пошло или                                 */
/*  откачка не на уровне, данный процесс завершается с ошибкой  и     */
/* насос вакуумный выключается.                                       */
/*====================================================================*/
ПРОЦ ВКЛ_ВАКУУМИРОВАНИЕ_ТРУБЫ{
 ИЗ ПРОЦ Инициализация 	ФлагГотовВакуумаВТ,
						ФлагОткачкиТрубыСидеростата,
						ФП_P_ВтрубеСидер,
						ФП_T_Врубашке,
						НП_ГлубокийВакуумТрубы,
						К_ПИТ_ВЕНТИЛЯТОРА,
						К_ПИТ_НАСОС_ОХЛ;
 ПЛАВ ЛП_Предыдущее ЛОКАЛ;
 
 СОСТ Начало{
   ЕСЛИ (ФлагГотовВакуумаВТ == НОРМА) В СОСТ ПаузаНаРазвакуумированиеТрубы;
   ИНАЧЕ {  
	СТАРТ ПРОЦ ВКЛ_ВАКУУМНУЮ_ПОДСИСТЕМУ;
	В СЛЕДУЮЩЕЕ;
	} 
 }
	СОСТ КонтрольВключенияВакПодсистемы{
		ЕСЛИ ( ПРОЦ ВКЛ_ВАКУУМНУЮ_ПОДСИСТЕМУ В СОСТ ПАССИВНОЕ){ 
			ЕСЛИ (ПРОЦ ВКЛ_ВАКУУМНУЮ_ПОДСИСТЕМУ В СОСТ ОШИБКА ) {
				ОШИБКА;  /* ?  ВКЛ_ВАКУУМНУЮ_ПОДСИСТЕМУ сам выключает насос и сообщает об ошибке*/
			}
			ИНАЧЕ В СЛЕДУЮЩЕЕ;
		} 
	} 
	СОСТ ЗапоминаемДавлениеВвакуумнойТрубе{
			ЛП_Предыдущее = ФП_P_ВтрубеСидер;
			В СЛЕДУЮЩЕЕ;
	}
		/* контроль перегрева рубашки, самопроизвольного выключения вентилятора и водяного насоса*/
	СОСТ КонтрольОборудования{  
			ЕСЛИ ( (ФП_T_Врубашке <= 85) && (К_ПИТ_ВЕНТИЛЯТОРА) && (К_ПИТ_НАСОС_ОХЛ) ) В СЛЕДУЮЩЕЕ;
			ИНАЧЕ {
				В СОСТ ВыключениеВакуумнойПодсистемы;
			}
	}
	СОСТ КонтрольДавления{  
		ЕСЛИ (ФП_P_ВтрубеСидер <= НП_ГлубокийВакуумТрубы) {  /*достигли нужного давления*/
			СТАРТ ПРОЦ ВЫК_ВАКУУМНУЮ_ПОДСИСТЕМУ; 
			В СЛЕДУЮЩЕЕ;
		}
		ИНАЧЕ В СОСТ ПаузаНаОткачкуВвакуумнойТрубе;
	}
	СОСТ СообщениеОКонцеВакуумирования{
		ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ВАКУУМИРОВАНИЕ_УСПЕШНО_ЗАВЕРШЕНО)) В СЛЕДУЮЩЕЕ; 
		ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ ОШИБКА;
	}
	 СОСТ ПаузаНаРазвакуумированиеТрубы{
      ТАЙМАУТ ОДНА_МИНУТА В СОСТ Начало;       /*   НП надо сделать ???????*/
	}
	СОСТ ПаузаНаОткачкуВвакуумнойТрубе{  
		ТАЙМАУТ ДВЕ_МИНУТЫ В СЛЕДУЮЩЕЕ; 
	}  
	СОСТ ПроверкаУменьшенияДавленияВпатрубке{  
		ЕСЛИ(ФП_P_ВтрубеСидер < ЛП_Предыдущее) В СОСТ ЗапоминаемДавлениеВвакуумнойТрубе;
        ИНАЧЕ В СОСТ ОткачкаОтсутствует;	
	} 
/*====== НЕНОРМАЛЬНЫЕ СИТУАЦИИ =========*/
 СОСТ ОткачкаОтсутствует {
       ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ДАВЛЕНИЕ_В_ВАК_ТРУБЕ_НЕ_УМЕНЬШАЕТСЯ))	В СЛЕДУЮЩЕЕ;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
 СОСТ ВыключениеВакуумнойПодсистемы{
     ЕСЛИ (!(ПРОЦ ВЫК_ВАКУУМНУЮ_ПОДСИСТЕМУ В СОСТ АКТИВНОЕ)) СТАРТ ПРОЦ ВЫК_ВАКУУМНУЮ_ПОДСИСТЕМУ;
    В СЛЕДУЮЩЕЕ;
 }
 СОСТ СообщениеОбОтказеВакуумирования{
       ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ОТКАЗ_ВАКУУМИРОВАНИЯ))	ОШИБКА;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи; 
 } 
	СОСТ НарушениеСвязи {
	   ОШИБКА;
	}
 } /*конец процесса ВКЛ_ВАКУУМИРОВАНИЕ_ТРУБЫ */
/*====================================================================*/
/*              Процесс ВЫК_ВАКУУМИРОВАНИЕ_ТРУБЫ 				     */
/*                                    								*/
/*====================================================================*/
 ПРОЦ ВЫК_ВАКУУМИРОВАНИЕ_ТРУБЫ{
	СОСТ Начало{
		СТОП ПРОЦ ВКЛ_ВАКУУМИРОВАНИЕ_ТРУБЫ;
/*		ЕСЛИ (ПРОЦ ВЫК_ВАКУУМНУЮ_ПОДСИСТЕМУ В СОСТ ПАССИВНОЕ) СТАРТ ПРОЦ ВЫК_ВАКУУМНУЮ_ПОДСИСТЕМУ;*/
	СТАРТ ПРОЦ ВЫК_ВАКУУМНУЮ_ПОДСИСТЕМУ; /*этот процесс может уже и отработать. флаг надо ввести*/
	СТОП;
	}
 }  /*конец процесса ВЫК_ВАКУУМИРОВАНИЕ_ТРУБЫ */
}  /* \Прогр */

