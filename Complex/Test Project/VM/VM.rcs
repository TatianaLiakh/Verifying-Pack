Прогр ВЕР{

ТАКТ 50;
КОНСТ ВКЛ 1;
КОНСТ ВЫКЛ 0;
КОНСТ ОжиданиеОтветаОтАлгоритма 10;
КОНСТ ЗадержкаОбъекта 10;

/*Коды сообщений от оператора  к Алгоритму*/ 
ПЕРЕЧИСЛЕНИЕ 
{
	АУ2ГУИ_В_РУЧНОЕ_УПРАВЛЕНИЕ,
	АУ2ГУИ_ОТКЛЮЧИТЬ_РУЧНОЕ_УПРАВЛЕНИЕ,
	АУ2ГУИ_ВКЛЮЧИТЬ_СУШИЛКУ,
	АУ2ГУИ_ВЫКЛЮЧИТЬ_СУШИЛКУ
};

/*Коды сообщений к оператору */ 
ПЕРЕЧИСЛЕНИЕ 
{
	КОМ2АУ_ЕСТЬ_СУШИЛКА_В_РУЧНОЕ_УПРАВЛЕНИЕ,
	КОМ2АУ_ЕСТЬ_ОТКЛЮЧИТЬ_РУЧНОЕ_УПРАВЛЕНИЕ,
	КОМ2АУ_СУШИЛКА_ВЫКЛЮЧЕНА,
	КОМ2АУ_СУШИЛКА_ВКЛЮЧЕНА
};

 /*Набор команд для БВ*/
 ПЕРЕЧИСЛЕНИЕ 
{
	БУС2БВ_РУЧНОЕ_УПРАВЛЕНИЕ_ВКЛЮЧЕНИЕ,
	БУС2БВ_РУЧНОЕ_УПРАВЛЕНИЕ_ВЫКЛЮЧЕНИЕ,
	БУС2БВ_АВТОМАТИЧЕСКОЕ_УПРАВЛЕНИЕ_РУКИ_ПОМЕЩЕНЫ,
	БУС2БВ_АВТОМАТИЧЕСКОЕ_УПРАВЛЕНИЕ_РУКИ_УБРАНЫ,
	БУС2БВ_КОНЕЦ_ШТАНОГО_РЕЖИМА	
};

/*Набор команд для БУС от БВ*/
ПЕРЕЧИСЛЕНИЕ
{
	БВ2БУС_СЛЕДУЮЩИЙ_СЦЕНАРИЙ,
	БВ2БУС_ОШИБКА_ОСТАНОВ
};

/*Набор Команд для ГОО */
ПЕРЕЧИСЛЕНИЕ
{
	БВ2ГОО_ВключениеРучногоУправленияУспешно,
	БВ2ГОО_ВыключениеРучногоУправленияУспешно,
	БВ2ГОО_ВключениеАвтоматическоеУспешно,
	БВ2ГОО_ВыключениеАвтоматическоеУспешно,
	БВ2ГОО_
};

ПЕРЕЧИСЛЕНИЕ
{
	БВ2ГОО_ВключениеРучногоУправленияНЕУспешно = БВ2ГОО_ + 1,
	БВ2ГОО_ВыключениеРучногоУправленияНЕУспешно,
	БВ2ГОО_СамопроизвольноеОтключениеРежимаРучногоУправления,
	БВ2ГОО_СамопроизвольноеВключениеРежимаРучногоУправления,
	БВ2ГОО_ОШИБКА_СамопроизвольноеВыключениеВавтоматРежиме,
	БВ2ГОО_ОШИБКА_СамопроизвольноеВключениеВавтоматРежиме
};

ФУНКЦИЯ ЦЕЛ  SendMsgGUICode       (ЦЕЛ);


/*CA Input msg queue*/
ФУНКЦИЯ ЦЕЛ GetNextMsgFromCA(ПУСТО);
ФУНКЦИЯ ЦЕЛ GetMsgGUIFromCA(ПУСТО);  /*чтение из памяти кода события */

/*SCM  Input msg queue*/
ФУНКЦИЯ ЦЕЛ GetNextMsgFromSCM(ПУСТО);
ФУНКЦИЯ ЦЕЛ GetMsgGUIFromSCM(ПУСТО);  /*чтение из памяти кода события */

/*SCM  Output msg queue*/
ФУНКЦИЯ ЦЕЛ SendMsgCodeToSCM(ЦЕЛ);

ВХОД  ЛОГ_ВЫХОДЫ_ОТ_АУ1  0 0 8; /* имя, базовый адрес, доп. адр. порта, 8бит */
ВХОД  ЛОГ_ВЫХОДЫ_ОТ_АУ2  1 0 8; /* имя, базовый адрес, доп. адр. порта, 8бит */
ВХОД  ЛОГ_ВЫХОДЫ_ОТ_ВОУ1  2 0 8; /* имя, базовый адрес, доп. адр. порта, 8бит */
ВХОД  ЛОГ_ВЫХОДЫ_ОТ_ВОУ2  3 0 8; /* имя, базовый адрес, доп. адр. порта, 8бит */

ПРОЦ Инициализация
{
	ЛОГ К_РУКИ_ПОД_СУШИЛКОЙ    = {ЛОГ_ВЫХОДЫ_ОТ_ВОУ1[1]} ДЛЯ ВСЕХ;

/* ВЫХОДНЫЕ СИГНАЛЫ (т.к. привязаны к модулю выходов): */ 
	ЛОГ У_ВКЛ_СУШИЛКУ = {ЛОГ_ВЫХОДЫ_ОТ_АУ1[1]} ДЛЯ ВСЕХ;

	ЛОГ ФлагРучноеУправление ДЛЯ ВСЕХ;
	ЛОГ ФлагРучноеУправлениеПоддверждено  ДЛЯ ВСЕХ;
	ЛОГ ФлагРукиПодСушилкой  ДЛЯ ВСЕХ;

/*########  СОСТОЯНИЯ ПРОЦЕССА  #########*/
 СОСТ Начало
 {  /* NB: 1-е f-сост. 1-го проц. - активно по включению питания */
	ФлагРучноеУправление = ВЫКЛ;
	ФлагРучноеУправлениеПоддверждено = ВЫКЛ;
	ФлагРукиПодСушилкой = ВЫКЛ;
	/* СТАРТ ПРОЦ ЧтениеСообщенийОтАУ; */
	СТАРТ ПРОЦ ЧтениеСообщенийОтБУС;
	СТАРТ ПРОЦ КонтрольОтключенияРучногоУправления;
	СТОП;
 }

} /* \ПРОЦ */

ПРОЦ ЧтениеСообщенийОтБУС{  
ИЗ ПРОЦ Инициализация ФлагРучноеУправление, ФлагРукиПодСушилкой; 
/* */
ЦЕЛ КодСообщенияОтБУС ЛОКАЛ;
 СОСТ Начало{
    /*  читаем код и параметр события из кольцевого буфера в структуру */
    ЕСЛИ (GetNextMsgFromSCM()) 
	{
		КодСообщенияОтБУС = GetMsgGUIFromCA();
		ЕСЛИ (КодСообщенияОтБУС == БУС2БВ_РУЧНОЕ_УПРАВЛЕНИЕ_ВКЛЮЧЕНИЕ)
		{
			/* ФлагРучноеУправление = ВКЛ; */
			СТОП ПРОЦ КонтрольАвтоматическойРаботыСушилки;
			СТОП ПРОЦ КонтрольОтключенияРучногоУправления;
			СТАРТ ПРОЦ КонтрольВключенияРучногоУправления;
		}
		ИНАЧЕ ЕСЛИ (КодСообщенияОтБУС == БУС2БВ_РУЧНОЕ_УПРАВЛЕНИЕ_ВЫКЛЮЧЕНИЕ)
		{
			/*ФлагРучноеУправление = ВЫКЛ; */
			СТАРТ ПРОЦ КонтрольОтключенияРучногоУправления;
			СТОП ПРОЦ КонтрольВключенияРучногоУправления;
		}
		ИНАЧЕ ЕСЛИ (КодСообщенияОтБУС == БУС2БВ_АВТОМАТИЧЕСКОЕ_УПРАВЛЕНИЕ_РУКИ_ПОМЕЩЕНЫ ||
					КодСообщенияОтБУС == БУС2БВ_АВТОМАТИЧЕСКОЕ_УПРАВЛЕНИЕ_РУКИ_УБРАНЫ)
		{
		 /*	ФлагРучноеУправление = ВЫКЛ; */
			ФлагРукиПодСушилкой = ВКЛ;
			СТАРТ ПРОЦ КонтрольАвтоматическойРаботыСушилки;
		}
	}
	ЗАЦИКЛИТЬ;
}
}

ПРОЦ КонтрольАвтоматическойРаботыСушилки
{
ИЗ ПРОЦ Инициализация К_РУКИ_ПОД_СУШИЛКОЙ, У_ВКЛ_СУШИЛКУ;

	СОСТ Начало
	{
		ЕСЛИ (К_РУКИ_ПОД_СУШИЛКОЙ == ВКЛ) В СОСТ ОжиданиеВключения;
		ИНАЧЕ В СОСТ ОжиданиеОтключения; 
	}
	СОСТ ОжиданиеВключения
	{
		ЕСЛИ (У_ВКЛ_СУШИЛКУ == ВКЛ)
		{
			В СОСТ СушилкаВключена;
			SendMsgGUICode(БВ2ГОО_ВключениеАвтоматическоеУспешно);
		}
			
		ТАЙМАУТ ОжиданиеОтветаОтАлгоритма В СОСТ ОшибкаВключения;
	}
	СОСТ ОжиданиеОтключения
	{
		ЕСЛИ (У_ВКЛ_СУШИЛКУ == ВЫКЛ) 
		{
			В СОСТ СушилкаВыключена;
			SendMsgGUICode(БВ2ГОО_ВыключениеАвтоматическоеУспешно);
		}
		ТАЙМАУТ ОжиданиеОтветаОтАлгоритма В СОСТ ОшибкаВыключения;		
	}
	СОСТ СушилкаВключена 
	{
		ЕСЛИ (У_ВКЛ_СУШИЛКУ == ВЫКЛ  && К_РУКИ_ПОД_СУШИЛКОЙ == ВКЛ)
		{
			SendMsgGUICode(БВ2ГОО_ОШИБКА_СамопроизвольноеВыключениеВавтоматРежиме);
			СТОП;
		}
		ИНАЧЕ ЕСЛИ (К_РУКИ_ПОД_СУШИЛКОЙ == ВЫКЛ) В СОСТ ОжиданиеПеремен;
	}
	СОСТ ОжиданиеПеремен
	{
		ЕСЛИ (К_РУКИ_ПОД_СУШИЛКОЙ == ВКЛ) В СОСТ СушилкаВключена;
		ТАЙМАУТ ЗадержкаОбъекта В СОСТ Начало;
	}	
	СОСТ ОшибкаВключения
	{
			SendMsgGUICode(БВ2ГОО_ВключениеРучногоУправленияНЕУспешно);
			СТОП;
	}
	СОСТ ОшибкаВыключения
	{
		СТОП;
	}
	СОСТ СушилкаВыключена 
	{
		ЕСЛИ (У_ВКЛ_СУШИЛКУ == ВКЛ  && К_РУКИ_ПОД_СУШИЛКОЙ == ВЫКЛ)
		{
			SendMsgGUICode(БВ2ГОО_ОШИБКА_СамопроизвольноеВыключениеВавтоматРежиме);
			СТОП;
		}
		ИНАЧЕ ЕСЛИ (К_РУКИ_ПОД_СУШИЛКОЙ == ВЫКЛ) В СОСТ Начало;
	}
}

ПРОЦ КонтрольОтключенияРучногоУправления
{
ИЗ ПРОЦ Инициализация ФлагРучноеУправлениеПоддверждено;
	СОСТ Старт
	{
		ЕСЛИ (ФлагРучноеУправлениеПоддверждено == ВЫКЛ)
		{
			SendMsgGUICode(БВ2ГОО_ВыключениеРучногоУправленияУспешно);
			В СОСТ Контроль;
		}
		ТАЙМАУТ ОжиданиеОтветаОтАлгоритма В СЛЕДУЮЩЕЕ;
	}
	СОСТ Беда
	{
		SendMsgGUICode(БВ2ГОО_ВыключениеРучногоУправленияНЕУспешно);
		СТОП;
	}
	СОСТ Контроль
	{
		ЕСЛИ (ФлагРучноеУправлениеПоддверждено != ВЫКЛ)
		{
			SendMsgGUICode(БВ2ГОО_СамопроизвольноеВключениеРежимаРучногоУправления);
			СТОП;
		}
	}
}

ПРОЦ КонтрольВключенияРучногоУправления
{
ИЗ ПРОЦ Инициализация ФлагРучноеУправлениеПоддверждено;

	СОСТ Старт
	{
		ЕСЛИ (ФлагРучноеУправлениеПоддверждено == ВКЛ)
		{
			SendMsgGUICode(БВ2ГОО_ВключениеРучногоУправленияУспешно);
			В СОСТ Контроль;
		}
		ТАЙМАУТ ОжиданиеОтветаОтАлгоритма В СЛЕДУЮЩЕЕ;
	}	
	СОСТ Беда
	{
		SendMsgGUICode(БВ2ГОО_ВключениеРучногоУправленияНЕУспешно);
		СТОП;
	}
	СОСТ Контроль
	{
		ЕСЛИ (ФлагРучноеУправлениеПоддверждено != ВКЛ)
		{
			SendMsgGUICode(БВ2ГОО_СамопроизвольноеОтключениеРежимаРучногоУправления);
			СТОП;
		}
	}
}


}  /* \Прогр */



