/*===== ПРОГРАММА УПРАВЛЕНИЯ УГЛОИЗМЕРИТЕЛЬНОЙ МАШИНОЙ НОНИУС ====*/
/*===== ДАТА СОЗДАНИЯ: 09 октября 2015 г ====*/
/*===== АВТОРЫ:
/*=====     Зюбин В. Е.
/*=====		Лях Т. В.
/*=====		Петухов А. Д.
/*
                                               _
   Ш   Ш   ШШШ   Ш   Ш  Ш    Ш  Ш   Ш   ШШШ   
   Ш   Ш  Ш   Ш  Ш   Ш  Ш    Ш  Ш   Ш  Ш   Ш  
   ШШШшш  Ш   Ш  ШШШШШ  Ш  Ш Ш   ШШШШ  Ш   
   Ш   Ш  Ш   Ш  Ш   Ш  Ш Ш  Ш      Ш  Ш   Ш  
   Ш   Ш   ШШШ   Ш   Ш  Ш    Ш  ШШШШ    ШШШ   
АННОТАЦИЯ ПРОГРАММЫ:

ИСТОРИЯ РЕДАКТИРОВАНИЯ ПРОГРАММЫ:
09.10.15 Начало

*/

Прогр Нониус {
/*=========================================================================*/
/*=========================================================================*/
/*========================== КОНСТАНТЫ И СООБЩЕНИЯ ========================*/
/*=========================================================================*/
/*=========================================================================*/
/*###########################################################*/
/*#########                     #############################*/
/*#########  ОПИСАНИЕ КОНСТАНТ  #############################*/
/*#########                     #############################*/
/*###########################################################*/
ТАКТ     50;    /* такт равен 500 миллисекундам */
КОНСТ ОДНА_СЕКУНДА               10;  /* Константа обеспечивающая паузу в 1 сек */
/* вызов рефлексовской программ 10 раз в секунду  */
/* если в программе определена константа 1 сек = 2. это говорит о том, что вызов программы */
/* рефлексовской производиться будет 2 раза в секунду. */
/* Если поставит 4, то четыре раза в секунду. Тогда счет секунд в рефлексе будет верным.*/
КОНСТ ДВЕ_СЕКУНДЫ             2*ОДНА_СЕКУНДА;
КОНСТ ТРИ_СЕКУНДЫ             3*ОДНА_СЕКУНДА;
КОНСТ ПЯТЬ_СЕКУНД             5*ОДНА_СЕКУНДА;
КОНСТ ДЕСЯТЬ_СЕКУНД          10*ОДНА_СЕКУНДА;
КОНСТ ДВАДЦАТЬ_СЕКУНД        20*ОДНА_СЕКУНДА;
КОНСТ ТРИДЦАТЬ_СЕКУНД        30*ОДНА_СЕКУНДА;
КОНСТ ОДНА_МИНУТА            60*ОДНА_СЕКУНДА;
КОНСТ ДВЕ_МИНУТЫ             2*ОДНА_МИНУТА;
КОНСТ ПЯТЬ_МИНУТ             5*60*ОДНА_СЕКУНДА;
КОНСТ ДЕСЯТЬ_МИНУТ			 10*ОДНА_МИНУТА;
КОНСТ ПЯТНАДЦАТЬ_МИНУТ       15*ОДНА_МИНУТА;
КОНСТ ТРИДЦАТЬ_МИНУТ         30*ОДНА_МИНУТА;

КОНСТ ТАЙМАУТ_ВОЗД_ПОДУШКИ		ТРИ_СЕКУНДЫ /*ДВАДЦАТЬ_СЕКУНД*/ ;
КОНСТ ТАЙМАУТ_НАСОСА_ВАКУУМНОГО	ДВАДЦАТЬ_СЕКУНД;
КОНСТ ТАЙМАУТ_ЗАЖИМА_ДИСКА		ДВАДЦАТЬ_СЕКУНД;
КОНСТ ТАЙМАУТ_РАЗЖИМА_ДИСКА		ДВАДЦАТЬ_СЕКУНД;

КОНСТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_КЛАПАНА_КАРЕТКИ          	ТРИ_СЕКУНДЫ;   /*+ */
КОНСТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_КЛАПАНА_ШПИНДЕЛЯ_Д       	ТРИ_СЕКУНДЫ;   /*+*/

КОНСТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГИО  ДЕСЯТЬ_СЕКУНД;

КОНСТ ВКЛ                1;
КОНСТ ВЫК               0;
КОНСТ ВЫКЛ               0;
КОНСТ ОТКР              1;
КОНСТ ЗАКР          	0;
КОНСТ ЕСТЬ               1;
КОНСТ НЕТ                0;
КОНСТ НИ_ДА_НИ_НЕТ       2;


/*===========================================================*/
/*========= ДИАПАЗОН ИЗМЕНЕНИЯ ЗАДАНИЙ            ===========*/
/*===========================================================*/
КОНСТ МИНИМАЛЬНОЕ_ЗАДАНИЕ_R_КАРЕТКИ            0.0;   /* мм  */ 
КОНСТ МАКСИМАЛЬНОЕ_ЗАДАНИЕ_R_КАРЕТКИ        200.0;    /* мм  */

КОНСТ МИНИМАЛЬНОЕ_ЗАДАНИЕ_W_ПРЕДМЕТНОГО_СТОЛИКА            0.0;   /* ??????  */ 
КОНСТ МАКСИМАЛЬНОЕ_ЗАДАНИЕ_W_ПРЕДМЕТНОГО_СТОЛИКА        200.0;    /* ?????  */ 

/*===========================================================*/
/*===========================================================*/
/*========= КОДЫ КОМАНД ПОСТУПАЮЩИЕ ОТ GUI ==================*/
/*===========================================================*/
/*===========================================================*/

/*===========================================================*/
/*========= РЕЖИМЫ РАБОТЫ			 ========================*/
/*===========================================================*/
ПЕРЕЧИСЛЕНИЕ {
    РЕЖИМ_ИНИЦИАЛИЗАЦИИ = 1,
    РЕЖИМ_РР,
    РЕЖИМ_ОТМЕНА        ,
    РЕЖИМ_КОНЕЦ_РАБОТЫ,  
	КОНЕЦ_КОДОВ_РЕЖИМОВ       
};

/*=========================================================*/
/*========= КОМАНДЫ ОТ ГИО (граф. интерф. оператора) ======*/
/*=========================================================*/
ПЕРЕЧИСЛЕНИЕ {
    ГИО2СУАБ_РЕЖИМ_ОТМЕНА = КОНЕЦ_КОДОВ_РЕЖИМОВ,
    ГИО2СУАБ_РЕЖИМ_КОНЕЦ_РАБОТЫ,  
	ГИО2СУАБ_ЗАЖАТЬ_ДИСК , /* сквозная нумерация */
	ГИО2СУАБ_РАЗЖАТЬ_ДИСК,
	ГИО2СУАБ_КО_ИСПРАВНОСТЬ_ВОЗДУШНЫХ_ПОДУШЕК,
	ГИО2СУАБ_ОТКАЗ_ВОЗДУШНЫХ_ПОДУШЕК,
	ГИО2СУАБ_КАРЕТКУ_В_ПОЛОЖЕНИЕ_УСТАНОВКИ_ДИСКА,
   	ГИО2СУАБ_КАРЕТКУ_В_ЦЕНТР_ВРАЩЕНИЯ,
	ГИО2СУАБ_КАРЕТКУ_В_ЗАДАННЫЙ_РАДИУС,
	ГИО2СУАБ_СТОП_КАРЕТКИ,
	ГИО2СУАБ_ВКЛ_АКТИВНОЕ_ПОЗИЦИОНИРОВАНИЕ_ПО_ВРАЩЕНИЮ,
	ГИО2СУАБ_ВЫКЛ_АКТИВНОЕ_ПОЗИЦИОНИРОВАНИЕ_ПО_ВРАЩЕНИЮ,
	ГИО2СУАБ_ВКЛ_ВРАЩЕНИЕ_ПРЕД_СТОЛИКА_В_ЗД_W,  
	ГИО2СУАБ_СТОП_ВРАЩЕНИЯ_ПРЕДМЕТНОГО_СТОЛИКА,
	ГИО2СУАБ_ВКЛ_ПОДСВЕТКУ_ДИСКА,  
	ГИО2СУАБ_ВЫКЛ_ПОДСВЕТКУ_ДИСКА,
	
	ФИК2СУАБ_ЕСТЬ_СВЯЗЬ_С_МОХА1212,
	ФИК2СУАБ_НЕТ_СВЯЗИ_С_МОХА1212,
	ФИК2СУАБ_ЕСТЬ_СВЯЗЬ_С_МОХА1214,
	ФИК2СУАБ_НЕТ_СВЯЗИ_С_МОХА1214,

/*========= КОДЫ КОМАНД ИЗМЕНЕНИЯ ЗАДАНИЯ ===================*/
	ГИО2СУАБ_ИЗМН_ЗД_R_КАРЕТКИ,
    ГИО2СУАБ_ИЗМН_ЗД_W_ПРЕДМЕТНОГО_СТОЛИКА,	
	ГИО2СУАБ_КОНЕЦ_КОДОВ_ФИК	
	
};

/*===========================================================*/
/*=== КОДЫ СООБЩЕНИЙ ПРОЦЕССОВ ОТ СУАБ К ФИК                =*/
/*===========================================================*/
ПЕРЕЧИСЛЕНИЕ {
	
	СУАБ2ФИК_ДАВЛЕНИЕ_В_НОРМЕ = 0,
	СУАБ2ФИК_ДАВЛЕНИЕ_НИЖЕ_НОРМЫ,
	СУАБ2ФИК_ДАВЛЕНИЕ_АВАРИЙНОЕ,
	
	СУАБ2ФИК_ПРОВЕРЬ_ВОЗДУШНУЮ_ПОДУШКУ,
	СУАБ2ФИК_БЛОКИРОВКА_КОМАНД_ПЕРЕМЕЩЕНИЯ,
	СУАБ2ФИК_РАЗБЛОКИРОВКА_КОМАНД_ПЕРЕМЕЩЕНИЯ,
	СУАБ2ФИК_ИЗМН_ЗД_R_КАРЕТКИ,
	СУАБ2ФИК_ИЗМН_ЗД_W_ПРЕДМЕТНОГО_СТОЛИКА,

	/* сообщения о положении технических средств */
	СУАБ2ФИК_ДИСК_ЗАЖАТ,
	СУАБ2ФИК_ДИСК_РАЗЖАТ,
	СУАБ2ФИК_АВАРИЙНОЕ_РАЗЖАТИЕ_ДИСКА,
	СУАБ2ФИК_ОТКАЗ_ЗАЖИМА_ДИСКА,
	СУАБ2ФИК_ОТКАЗ_РАЗЖИМА_ДИСКА,
	СУАБ2ФИК_ОСВЕЩЕНИЕ_ДИСКА_ВКЛЮЧЕНО,
	СУАБ2ФИК_ОСВЕЩЕНИЕ_ДИСКА_ВЫКЛЮЧЕНО,
	
	
	СУАБ2ФИК_ЕСТЬ_СВЯЗЬ_С_МОХА1212,
	СУАБ2ФИК_НЕТ_СВЯЗИ_С_МОХА1212,
	СУАБ2ФИК_ЕСТЬ_СВЯЗЬ_С_МОХА1214,
	СУАБ2ФИК_НЕТ_СВЯЗИ_С_МОХА1214,
	
	СУАБ2ФИК_ЕСТЬ_ДИСТАНЦИОННОЕ_УПРАВЛЕНИЕ_1212,
	СУАБ2ФИК_НЕТ_ДИСТАНЦИОННОГО_УПРАВЛЕНИЯ_1212,
	СУАБ2ФИК_ЕСТЬ_ДИСТАНЦИОННОЕ_УПРАВЛЕНИЕ_1214,
	СУАБ2ФИК_НЕТ_ДИСТАНЦИОННОГО_УПРАВЛЕНИЯ_1214,
	СУАБ2ФИК_РЕЖИМ_ИНИЦИАЛИЗАЦИЯ,
	СУАБ2ФИК_РЕЖИМ_РР,
	СУАБ2ФИК_РЕЖИМ_ОТМЕНА,
	СУАБ2ФИК_РЕЖИМ_КОНЕЦ_РАБОТЫ,
	СУАБ2ФИК_УСТАНОВКА_ГОТОВА_К_ВЫКЛЮЧЕНИЮ,
	СУАБ2ФИК_КОНЕЦ_КОДОВ_СПК 
};	


/*==========================================================*/
/*====КОДЫ СООБЩЕНИЙ ОБ ИЗМЕНЕНИИ ФАКТИЧЕСКИХ ПАРАМЕТРОВ ========*/
/*===========================================================*/
ПЕРЕЧИСЛЕНИЕ {
	СУАБ2ФИК_ИЗМН_ФП_R_КАРЕТКИ = СУАБ2ФИК_КОНЕЦ_КОДОВ_СПК + 1,
	СУАБ2ФИК_ИЗМН_ФП_W_ПРЕДМЕТНОГО_СТОЛИКА,
	КОМАНДЫ_ИЗМ_ФП = СУАБ2ФИК_ИЗМН_ФП_W_ПРЕДМЕТНОГО_СТОЛИКА 	
};

/*=====================================================*/
/*=== КОДЫ СООБЩЕНИЙ ОБ ИЗМЕНЕНИИИ ЗАДАНИЙ  ===========*/
/*=====================================================*/

ПЕРЕЧИСЛЕНИЕ {
	СУАБ2ФИК_ЗД_R_КАРЕТКИ_ВНЕ_ДИАПАЗОНА = КОМАНДЫ_ИЗМ_ФП + 1,
	СУАБ2ФИК_ЗД_W_ПРЕДМЕТНОГО_СТОЛИКА_ВНЕ_ДИАПАЗОНА
};

ПЕРЕЧИСЛЕНИЕ {                    
   
	
	СУАБ2ФИК_ВКЛЮЧИТЕ_КОМПРЕССОР = СУАБ2ФИК_ЗД_W_ПРЕДМЕТНОГО_СТОЛИКА_ВНЕ_ДИАПАЗОНА +1

};	


/*===========================================================*/
/*= КОДЫ КОМАНД ИЗМЕНЕНИЯ НАСТРОЕЧНЫХ ПАРАМЕТРОВ   ???????????==========*/
/*===========================================================*/
ПЕРЕЧИСЛЕНИЕ { 
	ИЗМН_НП_1 = СУАБ2ФИК_ВКЛЮЧИТЕ_КОМПРЕССОР + 1,
	ИЗМН_НП_N
};
/*===========================================================*/
/*========= КОДЫ СООБЩЕНИЙ  О НЕВЕРНОЙ КОМАНДЕ ==============*/
/*===========================================================*/


ПЕРЕЧИСЛЕНИЕ {
    СУАБ2ФИК_ОШИБКА_КОДА_КОМАНДЫ_ОТ_ГИО = ИЗМН_НП_N + 1,
	СУАБ2ФИК_ОШИБКА_ЗАПРЕЩЕННАЯ_КОМАНДА_РЕЖИМА_РР,
	СУАБ2ФИК_ОШИБКА_ЗАПРЕЩЕННАЯ_КОМАНДА_РЕЖИМА_ИНИЦИАЛИЗАЦИИ	 
	 
}; 

/*=========================================================================*/
/*=========================================================================*/
/*========================== ФУНКЦИИ ======================================*/
/*=========================================================================*/
/*=========================================================================*/
/*:::::::::Функции для работы  с ГИО :::::::::::::::::::::   */

/*чтение кода и параметра события от ПИВ из кольцевого буфера в память */
ФУНКЦИЯ ЦЕЛ GetNextMsgGUI(ПУСТО);
ФУНКЦИЯ ЦЕЛ  GetMsgGUICode        		(ПУСТО);  /*чтение из памяти кода события */
ФУНКЦИЯ ПЛАВ GetFloatParamGUI     		(ПУСТО);  /*чтение из памяти параметра события */
ФУНКЦИЯ ЦЕЛ  SendMsgLongParamGUI  		(ЦЕЛ, ДЦЕЛ);
ФУНКЦИЯ ДЦЕЛ GetLongParamGUI      		(ПУСТО);  
ФУНКЦИЯ ЦЕЛ  SendMsgGUICode       		(ЦЕЛ);
ФУНКЦИЯ ЦЕЛ  SendMsgFloatParamGUI       (ЦЕЛ, ПЛАВ);

/*=========================================================================*/
/*=========================================================================*/
/*========================== ПОРТЫ ВВОДА-ВЫВОДА ===========================*/
/*=========================================================================*/
/*=========================================================================*/
ВХОД  ВХОД_МОХА_1214_ДАВЛ_ВАКУУМ  			0 0 8; 
ВХОД  ВХОД_МОХА_1212_ЛАЗЕР		  			0 1 8; 
ВЫХОД ВЫХОД_МОХА_1214_ДАВЛ_ВАКУУМ		  	1 0 8; 
ВЫХОД ВЫХОД_МОХА_1212_ЛАЗЕР		  			1 1 8; 


/*=========================================================================*/
/*=========================================================================*/
/*========================== ПРОЦЕССЫ =====================================*/
/*=========================================================================*/
/*=========================================================================*/
/*
/ * Процесс Инициализация. Служит для
/* развертывания программы. Этот процесс (описанный
/* первым) - единственно активный процесс по запуску.
/* Кроме этого, процесс Инициализация содержит описание 
/* переменных для ссылок из других
/* процессов. Это удобно: описания локализованы в одном
/* месте.
*/
ПРОЦ Инициализация{
/*===========================================================*/
/*=========== ОПИСАНИЕ ПЕРЕМЕННЫХ ===========================*/
/*===========================================================*/

/*=========== МОХА 1214 (ДАВЛЕНИЕ И ВАКУУМ) =======*/
/* ВЫХОДНЫЕ СИГНАЛЫ 1214 "ВОЗД_ПОД" -- воздушная подушка, "ЛУ" -- локальное управление */
ЛОГ У_ВОЗД_ПОД_КАРЕТКИ			= {ВЫХОД_МОХА_1214_ДАВЛ_ВАКУУМ[1]} ДЛЯ ВСЕХ; 	/* К1 */
ЛОГ У_ВОЗД_ПОД_ШПИНДЕЛЯ			= {ВЫХОД_МОХА_1214_ДАВЛ_ВАКУУМ[1]} ДЛЯ ВСЕХ;	/* К2 */
ЛОГ У_ВОЗД_ПОД_СТОЛА			= {ВЫХОД_МОХА_1214_ДАВЛ_ВАКУУМ[1]} ДЛЯ ВСЕХ;	/* К3 */  /* ноги*/
ЛОГ У_ЗАЖИМ_ДИСКА				= {ВЫХОД_МОХА_1214_ДАВЛ_ВАКУУМ[1]} ДЛЯ ВСЕХ;	/* К4 */   /* клапан натекания вакуума*/
ЛОГ У_ВКЛ_НАСОС					= {ВЫХОД_МОХА_1214_ДАВЛ_ВАКУУМ[1]} ДЛЯ ВСЕХ;	/* питание насоса */ 
ЛОГ У_РАЗЖИМ_ДИСКА				= {ВЫХОД_МОХА_1214_ДАВЛ_ВАКУУМ[1]} ДЛЯ ВСЕХ;	/* К5 */  /* клапан атмосферы */  

/* ВХОДНЫЕ ДАВЛЕНИЕ И ВАКУУМ */  
ЛОГ К_ВОЗД_ПОД_Р_НЕ_КРИТИЧЕСКОЕ		= {ВХОД_МОХА_1214_ДАВЛ_ВАКУУМ[1]} ДЛЯ ВСЕХ;	/*вход МОХИ  	0*/  
ЛОГ К_ВОЗД_ПОД_Р_РАБОЧЕЕ			= {ВХОД_МОХА_1214_ДАВЛ_ВАКУУМ[1]} ДЛЯ ВСЕХ;	/*вход МОХИ  	1*/ 
ЛОГ К_ДИСК_РАЗЖАТ					= {ВХОД_МОХА_1214_ДАВЛ_ВАКУУМ[1]} ДЛЯ ВСЕХ; /*вход МОХИ  	2*/     /* если 0, то вакуум есть */ /* норма*/
ЛОГ К_1214_ДИСТАНЦИОННОЕ			= {ВХОД_МОХА_1214_ДАВЛ_ВАКУУМ[1]} ДЛЯ ВСЕХ; /*вход МОХИ  	3*/		/* true - от программы, означает, что все управление false на оборудовании*/
/* ЛОГ К_ВОЗДУШНОЙ_ПОДУШКИ_ШПИНДЕЛЯ	= {ВХОД_МОХА_1214_ДАВЛ_ВАКУУМ[1]} ДЛЯ ВСЕХ; /*вход МОХИ  	4*/
/* ЛОГ К_ДАВЛЕНИЕ_НА_КАРЕТКЕ			= {ВХОД_МОХА_1214_ДАВЛ_ВАКУУМ[1]} ДЛЯ ВСЕХ; /*вход МОХИ  	5*/


/*=========== МОХА 1212 =======*/
/* ВЫХОДНЫЕ СИГНАЛЫ 1212 "ВОЗД_ПОД" -- воздушная подушка, "ЛУ" -- локальное управление */
ЛОГ У_ПИТАНИЕ_ЛАЗЕРА				= {ВЫХОД_МОХА_1212_ЛАЗЕР[1]} ДЛЯ ВСЕХ; /*выход МОХИ  0*/
ЛОГ У_ВКЛ_ЛАЗЕРА					= {ВЫХОД_МОХА_1212_ЛАЗЕР[1]} ДЛЯ ВСЕХ; /*выход МОХИ  1*/
ЛОГ У_ИНТЕНСИВНОСТЬ_ЛАЗЕРА			= {ВЫХОД_МОХА_1212_ЛАЗЕР[1]} ДЛЯ ВСЕХ;  /*выход МОХИ 2*/	/*МОДУЛЯТОР_ЛАЗЕРА*/
ЛОГ У_ВКЛ_СВЕТОДИОДА				= {ВЫХОД_МОХА_1212_ЛАЗЕР[1]} ДЛЯ ВСЕХ;  /*освещение диска*/
ЛОГ У_ВЫКЛ_СВЕТОДИОДА				= {ВЫХОД_МОХА_1212_ЛАЗЕР[1]} ДЛЯ ВСЕХ; /*освещение диска*/


/*-----от  ЖЕЛЕЗА --ЛАЗЕР---------*/	/* читаются входы с железа через МОХУ функцией inputf2  */
ЛОГ К_1212_ДИСТАНЦИОННОЕ				= {ВХОД_МОХА_1212_ЛАЗЕР[1]} ДЛЯ ВСЕХ;	/*вход МОХИ  	0*/
ЛОГ К_ИНТЕНСИВНОСТЬ_ЛАЗЕРА				= {ВХОД_МОХА_1212_ЛАЗЕР[1]} ДЛЯ ВСЕХ;	/*вход МОХИ  	1*/ /* Перебейнос думает о значении сигнала */


															/* записываются в МОХУ функцией outf15*/
													
															
/*===========================================================*/
/*=========== ЯЧЕЙКИ ОБЩЕГО УПРАВЛЕНИЯ ======================*/
/*===========================================================*/
ЦЕЛ ТекущийРежим          ДЛЯ ВСЕХ;



/*===========================================================*/
/*=========== ЯЧЕЙКИ ИНФОРМАЦИОННЫХ КАНАЛОВ =================*/
/*===========================================================*/
ЦЕЛ КодСобытия_от_ГИO        ДЛЯ ВСЕХ;
ЦЕЛ КодСобытия_к_ГИO         ДЛЯ ВСЕХ;

/*===========================================================*/
/*=========== ЯЧЕЙКИ ФАКТИЧЕСКИХ ЗНАЧЕНИЙ ===================*/
/*===========================================================*/
ПЛАВ   	ЗД_R_КАРЕТКИ			ДЛЯ ВСЕХ;
ПЛАВ 	ЗД_W_ПРЕДМЕТНОГО_СТОЛИКА	ДЛЯ ВСЕХ;

ПЛАВ   	ФП_R_КАРЕТКИ			ДЛЯ ВСЕХ;
ПЛАВ 	ФП_W_ПРЕДМЕТНОГО_СТОЛИКА	ДЛЯ ВСЕХ;

ЛОГ ФлагКонтроляВоздушнойПодушки    ДЛЯ ВСЕХ;
ЛОГ ФлагСвязиМоха1212			 	ДЛЯ ВСЕХ;
ЛОГ ФлагСвязиМоха1214			 	ДЛЯ ВСЕХ;

 СОСТ Начало{
	ФлагСвязиМоха1212 = НИ_ДА_НИ_НЕТ;
	ФлагСвязиМоха1214 = НИ_ДА_НИ_НЕТ;
 
	СТАРТ ПРОЦ ВКЛ_ВОЗД_ПОДУШКУ;
	СТАРТ ПРОЦ ИНИЦИАЛИЗАЦИЯ_ЛАЗЕРА;
	СТАРТ ПРОЦ ВКЛ_НАСОС;
/*	СТАРТ ПРОЦ ИНИЦИАЛИЗАЦИЯ_ПРИВОДОВ;
*/
	СТАРТ ПРОЦ Режим_ИНИЦИАЛИЗАЦИЯ;
	ТекущийРежим = РЕЖИМ_ИНИЦИАЛИЗАЦИИ;
    В СЛЕДУЮЩЕЕ;
 }
 СОСТ КонтрольКонцаИнициализации{ 
	ЕСЛИ (
		(ПРОЦ ВКЛ_ВОЗД_ПОДУШКУ В СОСТ ПАССИВНОЕ) &&
		(ПРОЦ ВКЛ_НАСОС В СОСТ ПАССИВНОЕ) &&
		(ПРОЦ ИНИЦИАЛИЗАЦИЯ_ЛАЗЕРА В СОСТ ПАССИВНОЕ) &&
		(ПРОЦ ИНИЦИАЛИЗАЦИЯ_ПРИВОДОВ В СОСТ ПАССИВНОЕ)) {
		ЕСЛИ (
			(ПРОЦ ВКЛ_ВОЗД_ПОДУШКУ В СОСТ СТОП) &&
			(ПРОЦ ВКЛ_НАСОС В СОСТ СТОП) &&
			(ПРОЦ ИНИЦИАЛИЗАЦИЯ_ЛАЗЕРА В СОСТ СТОП) &&
			(ПРОЦ ИНИЦИАЛИЗАЦИЯ_ПРИВОДОВ В СОСТ СТОП)) {
				ТекущийРежим = РЕЖИМ_РР;
				СТОП ПРОЦ Режим_ИНИЦИАЛИЗАЦИЯ;

				СТАРТ ПРОЦ Режим_РР;
				СТАРТ ПРОЦ КонтрольВакуума;
				СТАРТ ПРОЦ КонтрольДавления;
				СТАРТ ПРОЦ КонтрольСвязи_сМОХА1212;
				СТАРТ ПРОЦ КонтрольСвязи_сМОХА1214;
				СТАРТ ПРОЦ КонтрольДистанционного1212;
				СТАРТ ПРОЦ КонтрольДистанционного1214;
				СТАРТ ПРОЦ КонтрольИзмененияЗаданий_ЗД_R_КАРЕТКИ;
				СТАРТ ПРОЦ КонтрольИзмененияЗаданий_ЗД_W_ПРЕДМЕТНОГО_СТОЛИКА;
				СТАРТ ПРОЦ КонтрольИзменения_ФП_R_КАРЕТКИ;
				СТАРТ ПРОЦ КонтрольИзменения_ФП_W_ПРЕДМЕТНОГО_СТОЛИКА;
				СТОП;
	    } ИНАЧЕ {
			ОШИБКА;		
		}
	}
 }

} /* конец ПРОЦ Инициализация*/  


/*==============================================*/
/*==============================================*/
/*=                                            =*/
/*=         РЕЖИМ НАЧАЛЬНАЯ ИНИЦИАЛИЗАЦИЯ      =*/
/*=                                            =*/
/*==============================================*/
/*==============================================*/
ПРОЦ Режим_ИНИЦИАЛИЗАЦИЯ{
ИЗ ПРОЦ Инициализация 	КодСобытия_от_ГИO,
						ФлагКонтроляВоздушнойПодушки,
						ТекущийРежим;

 СОСТ Начало{
    ТекущийРежим = РЕЖИМ_ИНИЦИАЛИЗАЦИИ;
	СТАРТ ПРОЦ КонтрольРежимов;
    В СЛЕДУЮЩЕЕ;
 }
     /*  читаем код и параметр события из кольцевого буфера в структуру */
 СОСТ ЧтениеКольцевогоБуфера{
    ЕСЛИ (GetNextMsgGUI()) {
		КодСобытия_от_ГИO = GetMsgGUICode(); 
		РАЗБОР (КодСобытия_от_ГИO) {
			СЛУЧАЙ ГИО2СУАБ_РЕЖИМ_ОТМЕНА:
				СТАРТ ПРОЦ ОТМЕНА;
				КОНЕЦ;			
			СЛУЧАЙ ГИО2СУАБ_РЕЖИМ_КОНЕЦ_РАБОТЫ:
				СТАРТ ПРОЦ КОНЕЦ_РАБОТЫ;
				КОНЕЦ;
			СЛУЧАЙ ГИО2СУАБ_КО_ИСПРАВНОСТЬ_ВОЗДУШНЫХ_ПОДУШЕК:
				ФлагКонтроляВоздушнойПодушки = ВКЛ;
				СТАРТ ПРОЦ РАЗБЛОКИРОВКА_КОМАНД_ПЕРЕМЕЩЕНИЯ;
				КОНЕЦ;
			СЛУЧАЙ ГИО2СУАБ_ОТКАЗ_ВОЗДУШНЫХ_ПОДУШЕК:
				ФлагКонтроляВоздушнойПодушки = ВЫКЛ;
				СТАРТ ПРОЦ БЛОКИРОВКА_КОМАНД_ПЕРЕМЕЩЕНИЯ;
				КОНЕЦ;
			УМОЛЧАНИЕ:
				ЕСЛИ ((КодСобытия_от_ГИO >= ГИО2СУАБ_ЗАЖАТЬ_ДИСК) &&
					(КодСобытия_от_ГИO <= ГИО2СУАБ_КОНЕЦ_КОДОВ_ФИК)) {
						В СОСТ ЗапрещеннаяКоманда;
					} ИНАЧЕ	В СОСТ НекорректноеСобытие;		
 
			КОНЕЦ;  /* конец для УМОЛЧАНИЯ */
		  
		} /* конец разбора */
	} /* конец ЕСЛИ */
	
 }	
/*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ ЗапрещеннаяКоманда{
     ЕСЛИ (SendMsgLongParamGUI(СУАБ2ФИК_ОШИБКА_ЗАПРЕЩЕННАЯ_КОМАНДА_РЕЖИМА_ИНИЦИАЛИЗАЦИИ, КодСобытия_от_ГИO))
       В СОСТ Начало;
     ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГИО В СОСТ НарушениеСвязи;
 }
 СОСТ НекорректноеСобытие {
    ЕСЛИ (SendMsgLongParamGUI(СУАБ2ФИК_ОШИБКА_КОДА_КОМАНДЫ_ОТ_ГИО, КодСобытия_от_ГИO))
         СТОП;   
    ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГИО В СОСТ НарушениеСвязи;
 }					
 СОСТ НарушениеСвязи {
    В СОСТ ЧтениеКольцевогоБуфера;
 }
} /* конец ПРОЦ Режим_ИНИЦИАЛИЗАЦИЯ   */	

/*==============================================*/
/*=         КОНТРОЛЬ РЕЖИМОВ                   =*/
/*==============================================*/
ПРОЦ КонтрольРежимов {
ИЗ ПРОЦ Инициализация ТекущийРежим;
ЦЕЛ ЛП_Предыдущее ЛОКАЛ;
ЦЕЛ ЛП_Сообщение ЛОКАЛ;

	СОСТ Начало {
		ЛП_Предыдущее = (!ТекущийРежим);
		В СЛЕДУЮЩЕЕ;
	}
	СОСТ КонтрольИзменения{
		ЕСЛИ (ЛП_Предыдущее != ТекущийРежим) {
			ЛП_Предыдущее = ТекущийРежим;
			РАЗБОР (ЛП_Предыдущее) {
				СЛУЧАЙ РЕЖИМ_ИНИЦИАЛИЗАЦИИ:
					ЛП_Сообщение = СУАБ2ФИК_РЕЖИМ_ИНИЦИАЛИЗАЦИЯ;
				КОНЕЦ;	
				СЛУЧАЙ РЕЖИМ_РР:
					ЛП_Сообщение = СУАБ2ФИК_РЕЖИМ_РР;
				КОНЕЦ;	
				СЛУЧАЙ РЕЖИМ_ОТМЕНА:
					ЛП_Сообщение = СУАБ2ФИК_РЕЖИМ_ОТМЕНА;
				КОНЕЦ;	
				СЛУЧАЙ РЕЖИМ_КОНЕЦ_РАБОТЫ:
					ЛП_Сообщение = СУАБ2ФИК_РЕЖИМ_КОНЕЦ_РАБОТЫ;
				КОНЕЦ;	
				УМОЛЧАНИЕ: 
				КОНЕЦ;	
			}
			В СЛЕДУЮЩЕЕ;
		}
	}
 СОСТ СообщОбИзменении_Режима {
     ЕСЛИ (SendMsgGUICode(ЛП_Сообщение)) В СОСТ КонтрольИзменения;
     ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГИО В СОСТ НарушениеСвязи;
 }

 /*======= Ненормальные ситуации ===========*/
 СОСТ НарушениеСвязи{
     В СОСТ Начало;
 }
}  /* конец ПРОЦ КонтрольРежимов   */
/*==============================================*/
/*==============================================*/
/*=                                            =*/
/*=         РЕЖИМ РР     					   =*/
/*=                                            =*/
/*==============================================*/
/*==============================================*/
ПРОЦ Режим_РР{
ИЗ ПРОЦ Инициализация 	КодСобытия_от_ГИO,
						ТекущийРежим;

 СОСТ Начало{
    ТекущийРежим = РЕЖИМ_РР;
    В СЛЕДУЮЩЕЕ;
 }
     /*  читаем код и параметр события из кольцевого буфера в структуру */

 СОСТ ЧтениеКольцевогоБуфера{
    ЕСЛИ (GetNextMsgGUI()) {
		КодСобытия_от_ГИO = GetMsgGUICode(); 
		РАЗБОР (КодСобытия_от_ГИO) {
			СЛУЧАЙ ГИО2СУАБ_РЕЖИМ_ОТМЕНА:
				СТАРТ ПРОЦ ОТМЕНА;
				КОНЕЦ;			
			СЛУЧАЙ ГИО2СУАБ_РЕЖИМ_КОНЕЦ_РАБОТЫ:
				СТАРТ ПРОЦ КОНЕЦ_РАБОТЫ;
				КОНЕЦ;
			УМОЛЧАНИЕ:
				ЕСЛИ ((КодСобытия_от_ГИO >= ГИО2СУАБ_ЗАЖАТЬ_ДИСК) &&
					(КодСобытия_от_ГИO <= ГИО2СУАБ_КОНЕЦ_КОДОВ_ФИК)) {
						СТАРТ ПРОЦ ОбработкаКомандОтГИО;
						В СОСТ КонтрольВыполненияКомандОтГИО;
				} ИНАЧЕ 		

				В СОСТ НекорректноеСобытие;		
				КОНЕЦ;  /* конец для УМОЛЧАНИЯ */
		  
		} /* конец разбора */
	} /* конец ЕСЛИ */
 }

 
 СОСТ КонтрольВыполненияКомандОтГИО{
    ЕСЛИ(!(ПРОЦ ОбработкаКомандОтГИО В СОСТ АКТИВНОЕ))
    В СОСТ ЧтениеКольцевогоБуфера;
 }
 
 
/*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ ЗапрещеннаяКоманда{
     ЕСЛИ (SendMsgLongParamGUI(СУАБ2ФИК_ОШИБКА_ЗАПРЕЩЕННАЯ_КОМАНДА_РЕЖИМА_РР, КодСобытия_от_ГИO))
       В СОСТ Начало;
     ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГИО В СОСТ НарушениеСвязи;
 }
 СОСТ НекорректноеСобытие {
    ЕСЛИ (SendMsgLongParamGUI(СУАБ2ФИК_ОШИБКА_КОДА_КОМАНДЫ_ОТ_ГИО, КодСобытия_от_ГИO))
        В СОСТ Начало;   
    ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГИО В СОСТ НарушениеСвязи;
 }					
 СОСТ НарушениеСвязи {
    В СОСТ ЧтениеКольцевогоБуфера;
 }
} /* конец ПРОЦ Режим_ИНИЦИАЛИЗАЦИЯ   */



/*=========================================*/
/*=                                       =*/
/*=     ОБРАБОТКА КОМАНД УПРАВЛЕНИЯ       =*/
/*=     ОТ ГИО                 			 =*/
/*=========================================*/
ПРОЦ ОбработкаКомандОтГИО{
ИЗ ПРОЦ Инициализация
                      КодСобытия_от_ГИO,
					  КодСобытия_к_ГИO,
					  ЗД_R_КАРЕТКИ,
					  ЗД_W_ПРЕДМЕТНОГО_СТОЛИКА,
					  ФлагКонтроляВоздушнойПодушки,
					  ФлагСвязиМоха1212,
  					  ФлагСвязиМоха1214;
ПЛАВ ЛП_ЗД ЛОКАЛ;					  
ЦЕЛ ЛП_КодСообщения ЛОКАЛ;

/* СОБЫТИЯ УПРАВЛЕНИЯ ПРОЦЕССАМИ */
 СОСТ Анализ{
    В СЛЕДУЮЩЕЕ; 
    РАЗБОР (КодСобытия_от_ГИO) {
       СЛУЧАЙ ГИО2СУАБ_ЗАЖАТЬ_ДИСК:
          СТАРТ ПРОЦ ЗАЖАТЬ_ДИСК;
          КОНЕЦ;
       СЛУЧАЙ ГИО2СУАБ_РАЗЖАТЬ_ДИСК:
          СТАРТ ПРОЦ РАЗЖАТЬ_ДИСК;
          КОНЕЦ;
       СЛУЧАЙ ГИО2СУАБ_ВКЛ_ПОДСВЕТКУ_ДИСКА:
          СТАРТ ПРОЦ ВКЛ_ПОДСВЕТКУ_ДИСКА;
          КОНЕЦ;		  
       СЛУЧАЙ ГИО2СУАБ_ВЫКЛ_ПОДСВЕТКУ_ДИСКА:
          СТАРТ ПРОЦ ВЫКЛ_ПОДСВЕТКУ_ДИСКА;
          КОНЕЦ;		  
		СЛУЧАЙ ГИО2СУАБ_КО_ИСПРАВНОСТЬ_ВОЗДУШНЫХ_ПОДУШЕК:
			ФлагКонтроляВоздушнойПодушки = ВКЛ;
			СТАРТ ПРОЦ РАЗБЛОКИРОВКА_КОМАНД_ПЕРЕМЕЩЕНИЯ;
			КОНЕЦ;
		СЛУЧАЙ ГИО2СУАБ_ОТКАЗ_ВОЗДУШНЫХ_ПОДУШЕК:
			ФлагКонтроляВоздушнойПодушки = ВЫКЛ;
			СТАРТ ПРОЦ БЛОКИРОВКА_КОМАНД_ПЕРЕМЕЩЕНИЯ;
			КОНЕЦ;
       СЛУЧАЙ ГИО2СУАБ_КАРЕТКУ_В_ПОЛОЖЕНИЕ_УСТАНОВКИ_ДИСКА:
          СТАРТ ПРОЦ КАРЕТКУ_В_ПОЛОЖЕНИЕ_УСТАНОВКИ_ДИСКА;
          КОНЕЦ;		  
       СЛУЧАЙ ГИО2СУАБ_КАРЕТКУ_В_ЦЕНТР_ВРАЩЕНИЯ:
          СТАРТ ПРОЦ КАРЕТКУ_В_ЦЕНТР_ВРАЩЕНИЯ;
          КОНЕЦ;
       СЛУЧАЙ ГИО2СУАБ_КАРЕТКУ_В_ЗАДАННЫЙ_РАДИУС:
         СТАРТ ПРОЦ КАРЕТКУ_В_ЗАДАННЫЙ_РАДИУС;  
          КОНЕЦ;
       СЛУЧАЙ ГИО2СУАБ_СТОП_КАРЕТКИ:
          СТАРТ ПРОЦ СТОП_КАРЕТКИ;  
          КОНЕЦ;
       СЛУЧАЙ ГИО2СУАБ_ВКЛ_АКТИВНОЕ_ПОЗИЦИОНИРОВАНИЕ_ПО_ВРАЩЕНИЮ:
          СТАРТ ПРОЦ ВКЛ_АКТИВНОЕ_ПОЗИЦИОНИРОВАНИЕ_ПО_ВРАЩЕНИЮ;
          КОНЕЦ;
       СЛУЧАЙ ГИО2СУАБ_ВЫКЛ_АКТИВНОЕ_ПОЗИЦИОНИРОВАНИЕ_ПО_ВРАЩЕНИЮ:
          СТАРТ ПРОЦ ВЫКЛ_АКТИВНОЕ_ПОЗИЦИОНИРОВАНИЕ_ПО_ВРАЩЕНИЮ;
          КОНЕЦ;
       СЛУЧАЙ ГИО2СУАБ_ВКЛ_ВРАЩЕНИЕ_ПРЕД_СТОЛИКА_В_ЗД_W:
          СТАРТ ПРОЦ ВКЛ_ВРАЩЕНИЕ_ПРЕД_СТОЛИКА_В_ЗД_W;
          КОНЕЦ;
       СЛУЧАЙ ГИО2СУАБ_СТОП_ВРАЩЕНИЯ_ПРЕДМЕТНОГО_СТОЛИКА:
          СТАРТ ПРОЦ СТОП_ВРАЩЕНИЯ_ПРЕДМЕТНОГО_СТОЛИКА;
          КОНЕЦ;
       СЛУЧАЙ ФИК2СУАБ_ЕСТЬ_СВЯЗЬ_С_МОХА1212:
          ФлагСвязиМоха1212 = ВКЛ;
          КОНЕЦ;
       СЛУЧАЙ ФИК2СУАБ_НЕТ_СВЯЗИ_С_МОХА1212:
          ФлагСвязиМоха1212 = ВЫКЛ;
          КОНЕЦ;
       СЛУЧАЙ ФИК2СУАБ_ЕСТЬ_СВЯЗЬ_С_МОХА1214:
          ФлагСвязиМоха1214 = ВКЛ;
          КОНЕЦ;
       СЛУЧАЙ ФИК2СУАБ_НЕТ_СВЯЗИ_С_МОХА1214:
          ФлагСвязиМоха1214 = ВЫКЛ;
          КОНЕЦ;
       СЛУЧАЙ ГИО2СУАБ_ИЗМН_ЗД_R_КАРЕТКИ:
          ЛП_ЗД = GetFloatParamGUI();
          ЕСЛИ ((ЛП_ЗД < МИНИМАЛЬНОЕ_ЗАДАНИЕ_R_КАРЕТКИ)
                  || (ЛП_ЗД > МАКСИМАЛЬНОЕ_ЗАДАНИЕ_R_КАРЕТКИ)) {
               ЛП_КодСообщения = СУАБ2ФИК_ЗД_R_КАРЕТКИ_ВНЕ_ДИАПАЗОНА;
               В СОСТ ЗД_ВнеДиапазона;
          } ИНАЧЕ {
               ЗД_R_КАРЕТКИ = ЛП_ЗД;
/*			   КодСобытия_к_ГИO = СУАБ2ФИК_ИЗМН_ЗД_R_КАРЕТКИ;
			   В СОСТ ПодтверждениеИзмененияЗД;   */
			   СТОП;
           }
        КОНЕЦ;
		  
       СЛУЧАЙ ГИО2СУАБ_ИЗМН_ЗД_W_ПРЕДМЕТНОГО_СТОЛИКА:
          ЛП_ЗД = GetFloatParamGUI();
          ЕСЛИ ((ЛП_ЗД < МИНИМАЛЬНОЕ_ЗАДАНИЕ_W_ПРЕДМЕТНОГО_СТОЛИКА)
                  || (ЛП_ЗД > МАКСИМАЛЬНОЕ_ЗАДАНИЕ_W_ПРЕДМЕТНОГО_СТОЛИКА)) {
               ЛП_КодСообщения = СУАБ2ФИК_ЗД_W_ПРЕДМЕТНОГО_СТОЛИКА_ВНЕ_ДИАПАЗОНА;
               В СОСТ ЗД_ВнеДиапазона;
          } ИНАЧЕ {
               ЗД_W_ПРЕДМЕТНОГО_СТОЛИКА = ЛП_ЗД;
/*			   КодСобытия_к_ГИO = СУАБ2ФИК_ИЗМН_ЗД_W_ПРЕДМЕТНОГО_СТОЛИКА;
			   В СОСТ ПодтверждениеИзмененияЗД;  */
			   СТОП;	
          }
        КОНЕЦ;

       УМОЛЧАНИЕ:
          В СОСТ НекорректноеСобытие; /* отменит СТОП и все будет хорошо */
        КОНЕЦ;
    } /* конец разбора */
 }
 СОСТ ОстановПоЗапускуКоманды {
    СТОП;
 }
 /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ НекорректноеСобытие {
    ЕСЛИ (SendMsgLongParamGUI(СУАБ2ФИК_ОШИБКА_КОДА_КОМАНДЫ_ОТ_ГИО, КодСобытия_от_ГИO))
         СТОП;
    ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГИО В СОСТ НарушениеСвязи;
 }
 СОСТ ЗД_ВнеДиапазона{
    ЕСЛИ (SendMsgGUICode(ЛП_КодСообщения)) СТОП;
     ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГИО В СОСТ НарушениеСвязи;
 }
 СОСТ ПодтверждениеИзмененияЗД{
 	 ЕСЛИ (SendMsgLongParamGUI(КодСобытия_к_ГИO, GetLongParamGUI())) СТОП;
    ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГИО В СОСТ НарушениеСвязи;
 } 
 СОСТ НарушениеСвязи {
    ОШИБКА;
 }
} /* конец ПРОЦ ОбработкаКомандОтГИО   */


/*================================================*/
/*=  Контроль изменения заданий   R_КАРЕТКИ      =*/
/*================================================*/
ПРОЦ КонтрольИзмененияЗаданий_ЗД_R_КАРЕТКИ{
 ИЗ ПРОЦ Инициализация   ЗД_R_КАРЕТКИ;
 ПЛАВ ЛП_Предыдущее ЛОКАЛ;
 СОСТ Начало{
    ЛП_Предыдущее = (!ЗД_R_КАРЕТКИ);
    В СЛЕДУЮЩЕЕ;
 }
 СОСТ КонтрольИзменения{
    ЕСЛИ (ЛП_Предыдущее != ЗД_R_КАРЕТКИ) {
         ЛП_Предыдущее = ЗД_R_КАРЕТКИ;
         В СЛЕДУЮЩЕЕ;
    }
 }
 СОСТ СообщОбИзменении_ГИО{
     ЕСЛИ (SendMsgFloatParamGUI(СУАБ2ФИК_ИЗМН_ЗД_R_КАРЕТКИ, ЛП_Предыдущее)) В СОСТ КонтрольИзменения;
     ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГИО В СОСТ НарушениеСвязи;
 }

 /*======= Ненормальные ситуации ===========*/
 СОСТ НарушениеСвязи{
     В СОСТ КонтрольИзменения;
 }
}  /* конец ПРОЦ КонтрольИзмененияЗаданий_ЗД_R_КАРЕТКИ   */

/*================================================*/
/*=  Контроль изменения заданий   ЗД_W_ПРЕДМЕТНОГО_СТОЛИКА      =*/
/*================================================*/
ПРОЦ КонтрольИзмененияЗаданий_ЗД_W_ПРЕДМЕТНОГО_СТОЛИКА{
 ИЗ ПРОЦ Инициализация   ЗД_W_ПРЕДМЕТНОГО_СТОЛИКА;
 ПЛАВ ЛП_Предыдущее ЛОКАЛ;
 СОСТ Начало{
    ЛП_Предыдущее = (!ЗД_W_ПРЕДМЕТНОГО_СТОЛИКА);
    В СЛЕДУЮЩЕЕ;
 }
 СОСТ КонтрольИзменения{
    ЕСЛИ (ЛП_Предыдущее != ЗД_W_ПРЕДМЕТНОГО_СТОЛИКА) {
         ЛП_Предыдущее = ЗД_W_ПРЕДМЕТНОГО_СТОЛИКА;
         В СЛЕДУЮЩЕЕ;
    }
 }
 СОСТ СообщОбИзменении_ГИО{
     ЕСЛИ (SendMsgFloatParamGUI(СУАБ2ФИК_ИЗМН_ЗД_W_ПРЕДМЕТНОГО_СТОЛИКА, ЛП_Предыдущее)) В СОСТ КонтрольИзменения;
     ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГИО В СОСТ НарушениеСвязи;
 }

 /*======= Ненормальные ситуации ===========*/
 СОСТ НарушениеСвязи{
     В СОСТ КонтрольИзменения;
 }
}  /* конец ПРОЦ КонтрольИзмененияЗаданий ЗД_W_ПРЕДМЕТНОГО_СТОЛИКА   */
	

/*================================================*/
/*=  Контроль изменения ФП   R_КАРЕТКИ      =*/
/*================================================*/
ПРОЦ КонтрольИзменения_ФП_R_КАРЕТКИ{
 ИЗ ПРОЦ Инициализация   ФП_R_КАРЕТКИ;
 ПЛАВ ЛП_Предыдущее ЛОКАЛ;
 СОСТ Начало{
    ЛП_Предыдущее = (!ФП_R_КАРЕТКИ);
    В СЛЕДУЮЩЕЕ;
 }
 СОСТ КонтрольИзменения{
    ЕСЛИ (ЛП_Предыдущее != ФП_R_КАРЕТКИ) {
         ЛП_Предыдущее = ФП_R_КАРЕТКИ;
         В СЛЕДУЮЩЕЕ;
    }
 }
 СОСТ СообщОбИзменении_ГИО{
     ЕСЛИ (SendMsgFloatParamGUI(СУАБ2ФИК_ИЗМН_ФП_R_КАРЕТКИ, ЛП_Предыдущее)) В СОСТ КонтрольИзменения;
     ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГИО В СОСТ НарушениеСвязи;
 }

 /*======= Ненормальные ситуации ===========*/
 СОСТ НарушениеСвязи{
     В СОСТ КонтрольИзменения;
 }
}  /* конец ПРОЦ КонтрольИзмененияЗаданий_ФП_R_КАРЕТКИ   */

/*================================================*/
/*=  Контроль изменения ФП_W_ПРЕДМЕТНОГО_СТОЛИКА     =*/
/*================================================*/
ПРОЦ КонтрольИзменения_ФП_W_ПРЕДМЕТНОГО_СТОЛИКА{
 ИЗ ПРОЦ Инициализация   ФП_W_ПРЕДМЕТНОГО_СТОЛИКА;
 ПЛАВ ЛП_Предыдущее ЛОКАЛ;
 СОСТ Начало{
    ЛП_Предыдущее = (!ФП_W_ПРЕДМЕТНОГО_СТОЛИКА);
    В СЛЕДУЮЩЕЕ;
 }
 СОСТ КонтрольИзменения{
    ЕСЛИ (ЛП_Предыдущее != ФП_W_ПРЕДМЕТНОГО_СТОЛИКА) {
         ЛП_Предыдущее = ФП_W_ПРЕДМЕТНОГО_СТОЛИКА;
         В СЛЕДУЮЩЕЕ;
    }
 }
 СОСТ СообщОбИзменении_ГИО{
     ЕСЛИ (SendMsgFloatParamGUI(СУАБ2ФИК_ИЗМН_ФП_W_ПРЕДМЕТНОГО_СТОЛИКА, ЛП_Предыдущее)) В СОСТ КонтрольИзменения;
     ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГИО В СОСТ НарушениеСвязи;
 }

 /*======= Ненормальные ситуации ===========*/
 СОСТ НарушениеСвязи{
     В СОСТ КонтрольИзменения;
 }
}  /* конец ПРОЦ КонтрольИзмененияЗаданий_ФП_W_ПРЕДМЕТНОГО_СТОЛИКА   */


/*=================================================*/
/*=                                               =*/
/*=     Создание воздушной подушки установки      =*/
/*=                                               =*/
/*=================================================*/
ПРОЦ ВКЛ_ВОЗД_ПОДУШКУ{
ИЗ ПРОЦ Инициализация  	К_ВОЗД_ПОД_Р_РАБОЧЕЕ,
						У_ВОЗД_ПОД_СТОЛА,
						У_ВОЗД_ПОД_КАРЕТКИ,
						У_ВОЗД_ПОД_ШПИНДЕЛЯ;
	СОСТ Начало{
    	ЕСЛИ (К_ВОЗД_ПОД_Р_РАБОЧЕЕ) В СОСТ ПодачаВоздухаНаОпоруСтола;
		ИНАЧЕ В СОСТ СообщениеВключитьКомпрессор; 
	}
	СОСТ СообщениеВключитьКомпрессор{
		ЕСЛИ (SendMsgGUICode(СУАБ2ФИК_ВКЛЮЧИТЕ_КОМПРЕССОР))	В СОСТ ОжиданиеДавления;
		ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГИО В СОСТ НарушениеСвязи;
	}
	СОСТ ОжиданиеДавления{
		ЕСЛИ (К_ВОЗД_ПОД_Р_РАБОЧЕЕ) В СЛЕДУЮЩЕЕ;	
		ТАЙМАУТ ПЯТЬ_МИНУТ В СОСТ СообщениеВключитьКомпрессор;
	}
	СОСТ ПодачаВоздухаНаОпоруСтола{
		У_ВОЗД_ПОД_СТОЛА = ВКЛ; В СЛЕДУЮЩЕЕ;
	}
	СОСТ ПаузаДляОпорыСтола{
		ТАЙМАУТ ТАЙМАУТ_ВОЗД_ПОДУШКИ В СЛЕДУЮЩЕЕ;
	}	
	СОСТ ПодачаВоздухаНаКаретку{
		У_ВОЗД_ПОД_КАРЕТКИ = ВКЛ; В СЛЕДУЮЩЕЕ;
	}
	СОСТ ПаузаДляКаретки{
		ТАЙМАУТ ТАЙМАУТ_ВОЗД_ПОДУШКИ В СЛЕДУЮЩЕЕ;
	}
	СОСТ ПодачаВоздухаНаШпиндель{
		У_ВОЗД_ПОД_ШПИНДЕЛЯ = ВКЛ; В СЛЕДУЮЩЕЕ;
	}	
	СОСТ ПаузаДляШпинделя{
		ТАЙМАУТ ТАЙМАУТ_ВОЗД_ПОДУШКИ {
			СТАРТ ПРОЦ ПроверкаИсправностиВоздушнойПодушки;
			В СЛЕДУЮЩЕЕ;
		}
	} 
	СОСТ АнализРезультатовПроверки{
		ЕСЛИ (ПРОЦ ПроверкаИсправностиВоздушнойПодушки В СОСТ СТОП) СТОП;
		ЕСЛИ (ПРОЦ ПроверкаИсправностиВоздушнойПодушки В СОСТ ОШИБКА) ОШИБКА;
	}	
	
	/*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
	СОСТ НарушениеСвязи {
		В СОСТ Начало;	
	}
}  /* конец ПРОЦ ВКЛ_ВОЗД_ПОДУШКУ   */	

/*=================================================*/
/*=                                               =*/
/*= Проверка оператором наличия воздушных подушек =*/
/*=                                               =*/
/*=================================================*/
ПРОЦ ПроверкаИсправностиВоздушнойПодушки {
ИЗ ПРОЦ Инициализация  	ФлагКонтроляВоздушнойПодушки;
	СОСТ ОператоруПроконтролируйГотовностьВоздушныхПодушек{
		ФлагКонтроляВоздушнойПодушки = НИ_ДА_НИ_НЕТ;
		ЕСЛИ (SendMsgGUICode(СУАБ2ФИК_ПРОВЕРЬ_ВОЗДУШНУЮ_ПОДУШКУ)) 	
			В СЛЕДУЮЩЕЕ;
		ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГИО В СОСТ НарушениеСвязи;
	}
	СОСТ АнализГотовностьВоздПодушек{	
		ЕСЛИ (ФлагКонтроляВоздушнойПодушки == ВКЛ) СТОП;
		ЕСЛИ (ФлагКонтроляВоздушнойПодушки == ВЫКЛ) ОШИБКА;
	}	
	СОСТ НарушениеСвязи {
		ОШИБКА;	
	}
}	


/*=================================================*/
/*=                                               =*/
/*=     Включени насоса вакуумной системы         =*/
/*=                                               =*/
/*=================================================*/
ПРОЦ ВКЛ_НАСОС{
ИЗ ПРОЦ Инициализация  	У_ЗАЖИМ_ДИСКА,
						У_РАЗЖИМ_ДИСКА,
						У_ВКЛ_НАСОС;

	СОСТ ЗакрываемОбаКлапана{
		У_ЗАЖИМ_ДИСКА = ВЫКЛ;	/* закрываем клапан натекания вакуума*/
		У_РАЗЖИМ_ДИСКА = ВЫКЛ;	/* закрываем клапан атмосферы */
		В СЛЕДУЮЩЕЕ;
	}
	СОСТ ПаузаДляЗакрываемОбоихКлапанов{
		ТАЙМАУТ ДВЕ_СЕКУНДЫ В СЛЕДУЮЩЕЕ;
	}	
	СОСТ ВключениеНасоса{
		У_ВКЛ_НАСОС = ВКЛ; В СЛЕДУЮЩЕЕ;
	}

	СОСТ ПаузаДляВключенияНасоса{
		ТАЙМАУТ ТАЙМАУТ_НАСОСА_ВАКУУМНОГО СТОП;
	}	
}   /* конец ПРОЦ ВКЛ_НАСОС   */	

/*================================================*/
/*=                                              =*/
/*=     Зажим диска        						 =*/
/*=                                              =*/
/*================================================*/
ПРОЦ ЗАЖАТЬ_ДИСК{
ИЗ ПРОЦ Инициализация  	У_ЗАЖИМ_ДИСКА,
						У_РАЗЖИМ_ДИСКА,
						К_ДИСК_РАЗЖАТ;  /* норма была*/
						
	СОСТ Начало{
		ЕСЛИ (К_ДИСК_РАЗЖАТ == ВЫКЛ) СТАРТ ПРОЦ КонтрольВакуума; /* если уже зажат, то выдастся сообщение о зажатии */
	    ЕСЛИ (ПРОЦ РАЗЖАТЬ_ДИСК В СОСТ АКТИВНОЕ)  ОШИБКА ПРОЦ РАЗЖАТЬ_ДИСК;
		У_РАЗЖИМ_ДИСКА = ВЫКЛ;	/* закрываем клапан атмосферы */
		В СЛЕДУЮЩЕЕ;
	}
	СОСТ ЖдемЗакрытияКлапанаАтмосферы{
		ТАЙМАУТ ОДНА_СЕКУНДА В СЛЕДУЮЩЕЕ;
	}
	СОСТ ОткрКлапанНатеканияВакуума{
		У_ЗАЖИМ_ДИСКА = ВКЛ;	/* открываем клапан натекания вакуума */
		В СЛЕДУЮЩЕЕ;
	}
	СОСТ ЕстьЛиНормальноеДавление{	
		ЕСЛИ (К_ДИСК_РАЗЖАТ == ВЫКЛ){
			СТАРТ ПРОЦ КонтрольАварииВакуума;
			СТОП;  /* прижали диск*/
		}
		ТАЙМАУТ ТАЙМАУТ_ЗАЖИМА_ДИСКА В СОСТ Ошибка; 					
	}
/*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
	СОСТ Ошибка {
		ЕСЛИ (SendMsgGUICode(СУАБ2ФИК_ОТКАЗ_ЗАЖИМА_ДИСКА))	ОШИБКА;
		ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГИО В СОСТ НарушениеСвязи;
	}
	СОСТ НарушениеСвязи {
	   ОШИБКА;
	}
}   /* конец ПРОЦ ЗАЖАТЬ_ДИСК   */	

/*================================================*/
/*=                                              =*/
/*=     Зажим диска        						 =*/
/*=                                              =*/
/*================================================*/
ПРОЦ РАЗЖАТЬ_ДИСК{
ИЗ ПРОЦ Инициализация  	У_ЗАЖИМ_ДИСКА,
						У_РАЗЖИМ_ДИСКА,
						К_ДИСК_РАЗЖАТ;	/* норма была*/
	СОСТ Начало{
		ЕСЛИ (К_ДИСК_РАЗЖАТ == ВКЛ) СТАРТ ПРОЦ КонтрольВакуума; /* если уже зажат, то выдастся сообщение о разжатии */
		ЕСЛИ (ПРОЦ ЗАЖАТЬ_ДИСК В СОСТ АКТИВНОЕ)  ОШИБКА ПРОЦ ЗАЖАТЬ_ДИСК;
		СТАРТ ПРОЦ ОТМЕНА;   /* останавливаем движение и вращение*/
		СТОП ПРОЦ КонтрольАварииВакуума;
		В СЛЕДУЮЩЕЕ;
	}
	СОСТ КонтрольЗавершенияОтмены{
      ЕСЛИ (ПРОЦ ОТМЕНА В СОСТ ПАССИВНОЕ) {
         ЕСЛИ (ПРОЦ ОТМЕНА В СОСТ ОШИБКА) ОШИБКА;
         ИНАЧЕ В СЛЕДУЮЩЕЕ;
      }
	}
	СОСТ ЗакрываемНатекание{
		У_ЗАЖИМ_ДИСКА = ВЫКЛ;	/* закрываем клапан натекания вакуума */	
		В СЛЕДУЮЩЕЕ;
	}
	СОСТ ЖдемЗакрытияКлапанаНатекания{
		ТАЙМАУТ ОДНА_СЕКУНДА В СЛЕДУЮЩЕЕ;
	}	
	СОСТ ОткрываемКлапанАтмосферы{
		У_РАЗЖИМ_ДИСКА = ВКЛ;	/* открываем клапан атмосферы */
		В СЛЕДУЮЩЕЕ;
	}	
	СОСТ НетЛиДавления{	
		ЕСЛИ (К_ДИСК_РАЗЖАТ == ВКЛ) 	СТОП;  /* отжали диск*/
		ТАЙМАУТ ТАЙМАУТ_РАЗЖИМА_ДИСКА В СОСТ Ошибка; 					
	}

/*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
	СОСТ Ошибка {
		ЕСЛИ (SendMsgGUICode(СУАБ2ФИК_ОТКАЗ_РАЗЖИМА_ДИСКА))	ОШИБКА;
		ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГИО В СОСТ НарушениеСвязи;
	}
	СОСТ НарушениеСвязи {
	   ОШИБКА;
	}
}   /* конец ПРОЦ РАЗЖАТЬ_ДИСК   */	

/*================================================*/
/*=                                              =*/
/*=     ИНИЦИАЛИЗАЦИЯ_ЛАЗЕРА   					 =*/
/*=                                              =*/
/*================================================*/
ПРОЦ ИНИЦИАЛИЗАЦИЯ_ЛАЗЕРА{
ИЗ ПРОЦ Инициализация  	У_ПИТАНИЕ_ЛАЗЕРА;
	СОСТ Начало{
		ТАЙМАУТ ТРИДЦАТЬ_СЕКУНД В СЛЕДУЮЩЕЕ;
	}	
	СОСТ ВключаемЛазер{
		У_ПИТАНИЕ_ЛАЗЕРА = ВКЛ;	
		СТАРТ ПРОЦ КонтрольОсвещенияДиска;
		СТОП;
	}
}   /* конец ПРОЦ ИНИЦИАЛИЗАЦИЯ_ЛАЗЕРА   */	



/*================================================*/
/*=                                              =*/
/*=     Включение  ЛАЗЕРА   					     =*/
/*=                                              =*/
/*================================================*/
ПРОЦ ВКЛ_ЛАЗЕРА{
ИЗ ПРОЦ Инициализация  	У_ВКЛ_ЛАЗЕРА,
						У_ИНТЕНСИВНОСТЬ_ЛАЗЕРА;
	СОСТ Начало{
		У_ВКЛ_ЛАЗЕРА = ВКЛ;	
		У_ИНТЕНСИВНОСТЬ_ЛАЗЕРА = ВКЛ;
		СТОП;
	}	
}   /* конец ПРОЦ ВКЛ_ЛАЗЕРА   */	

/*================================================*/
/*=                                              =*/
/*=     Выключение ЛАЗЕРА   					     =*/
/*=                                              =*/
/*================================================*/
ПРОЦ ВЫКЛ_ЛАЗЕРА{
ИЗ ПРОЦ Инициализация  	У_ВКЛ_ЛАЗЕРА,
						У_ИНТЕНСИВНОСТЬ_ЛАЗЕРА;
	СОСТ Начало{
		У_ВКЛ_ЛАЗЕРА = ВЫКЛ;	
		У_ИНТЕНСИВНОСТЬ_ЛАЗЕРА = ВЫКЛ;
		СТОП;
	}	
}   /* конец ПРОЦ ВЫКЛ_ЛАЗЕРА   */	

/*================================================*/
/*=                                              =*/
/*=     КОНТРОЛЬ_ЛАЗЕРА   					 =*/
/*=                                              =*/
/*================================================*/
ПРОЦ КонтрольОсвещенияДиска{
ИЗ ПРОЦ Инициализация  	У_ВКЛ_СВЕТОДИОДА,
						У_ВЫКЛ_СВЕТОДИОДА;
 СОСТ Начало{
	В СОСТ ОсвещениеВыключено; /*по умолчанию */
	ЕСЛИ (У_ВКЛ_СВЕТОДИОДА == ВКЛ)  В СОСТ ОсвещениеВключено;
 }	
 СОСТ ОсвещениеВыключено{
    ЕСЛИ (SendMsgGUICode(СУАБ2ФИК_ОСВЕЩЕНИЕ_ДИСКА_ВЫКЛЮЧЕНО)) 	В СОСТ ОжиданиеВключения;
    ИНАЧЕ В СОСТ Начало;
 }	
 СОСТ ОжиданиеВключения{
	ЕСЛИ (У_ВКЛ_СВЕТОДИОДА == ВКЛ) В СОСТ Начало;	
 }
 СОСТ ОсвещениеВключено{
    ЕСЛИ (SendMsgGUICode(СУАБ2ФИК_ОСВЕЩЕНИЕ_ДИСКА_ВКЛЮЧЕНО)) 	В СОСТ ОжиданиеВыключения;
    ИНАЧЕ В СОСТ Начало;
 }	
 СОСТ ОжиданиеВыключения{
	ЕСЛИ (У_ВКЛ_СВЕТОДИОДА == ВЫКЛ) В СОСТ Начало;	
 }
}   /* конец ПРОЦ "КонтрольДавления   */	


/*================================================*/
/*=                                              =*/
/*=     Включение освещения диска			     =*/
/*=                                              =*/
/*================================================*/
ПРОЦ ВКЛ_ПОДСВЕТКУ_ДИСКА{
ИЗ ПРОЦ Инициализация  	У_ВКЛ_СВЕТОДИОДА,
						У_ВЫКЛ_СВЕТОДИОДА;
	СОСТ Начало{
		ЕСЛИ (У_ВКЛ_СВЕТОДИОДА == ВКЛ) СТАРТ ПРОЦ КонтрольОсвещенияДиска;	
		У_ВКЛ_СВЕТОДИОДА = ВКЛ;	
		У_ВЫКЛ_СВЕТОДИОДА = ВЫКЛ;
		СТОП;
	}	
}   /* конец ПРОЦ ВКЛ_ПОДСВЕТКУ_ДИСКА   */	

/*================================================*/
/*=                                              =*/
/*=     Выключение освещения диска			     =*/
/*=                                              =*/
/*================================================*/
ПРОЦ ВЫКЛ_ПОДСВЕТКУ_ДИСКА{
ИЗ ПРОЦ Инициализация  	У_ВКЛ_СВЕТОДИОДА,
						У_ВЫКЛ_СВЕТОДИОДА;
	СОСТ Начало{
		ЕСЛИ (У_ВКЛ_СВЕТОДИОДА == ВЫКЛ) СТАРТ ПРОЦ КонтрольОсвещенияДиска;	
		У_ВКЛ_СВЕТОДИОДА = ВЫКЛ;	
		У_ВЫКЛ_СВЕТОДИОДА = ВКЛ;
		СТОП;
	}	
}   /* конец ПРОЦ ВЫКЛ_ПОДСВЕТКУ_ДИСКА   */	
/*================================================*/
/*=                                              =*/
/*=     Инициализация приводов				     =*/
/*=                                              =*/
/*================================================*/
ПРОЦ ИНИЦИАЛИЗАЦИЯ_ПРИВОДОВ{
/* ИЗ ПРОЦ Инициализация  	;  */
	СОСТ Начало{
/*		?????????????		*/
		СТОП;
	}	
}   /* конец ПРОЦ ИНИЦИАЛИЗАЦИЯ_ПРИВОДОВ   */	

/*================================================*/
/*=                                              =*/
/*=      контроль давления  				     =*/
/*=                                              =*/
/*================================================*/
ПРОЦ КонтрольДавления{
 ИЗ ПРОЦ Инициализация 	К_ВОЗД_ПОД_Р_НЕ_КРИТИЧЕСКОЕ,		/*		было критическое		*/
						К_ВОЗД_ПОД_Р_РАБОЧЕЕ;  				/*		было норма		*/
 СОСТ Начало{
	В СОСТ НормальноеДавление; /*по умолчанию */
	ЕСЛИ ((К_ВОЗД_ПОД_Р_НЕ_КРИТИЧЕСКОЕ == ВКЛ) &&
				(К_ВОЗД_ПОД_Р_РАБОЧЕЕ != ВКЛ)) В СОСТ НизкоеДавление;	/*		было критическое		*/
	ЕСЛИ (К_ВОЗД_ПОД_Р_НЕ_КРИТИЧЕСКОЕ == ВЫКЛ) В СОСТ Авария;		/*	давление ниже критического	-- авария */
 }	
 СОСТ НормальноеДавление{
    ЕСЛИ (SendMsgGUICode(СУАБ2ФИК_ДАВЛЕНИЕ_В_НОРМЕ)) 	В СОСТ ОжиданиеНеНормальногоДавления;
    ИНАЧЕ В СОСТ Начало;
 }	
 СОСТ ОжиданиеНеНормальногоДавления{
	ЕСЛИ ((К_ВОЗД_ПОД_Р_НЕ_КРИТИЧЕСКОЕ == ВЫКЛ) ||
				(К_ВОЗД_ПОД_Р_РАБОЧЕЕ != ВКЛ)) В СОСТ Начало;	
 }
 СОСТ НизкоеДавление{
    ЕСЛИ (SendMsgGUICode(СУАБ2ФИК_ДАВЛЕНИЕ_НИЖЕ_НОРМЫ)) 	В СОСТ ОжиданиеНеНизкогоДавления;
    ИНАЧЕ В СОСТ Начало;
 }	
 СОСТ ОжиданиеНеНизкогоДавления{
	ЕСЛИ ((К_ВОЗД_ПОД_Р_НЕ_КРИТИЧЕСКОЕ == ВЫКЛ) ||
				(К_ВОЗД_ПОД_Р_РАБОЧЕЕ == ВКЛ)) В СОСТ Начало;	
	ТАЙМАУТ ОДНА_МИНУТА В СОСТ Начало;			
 }
 СОСТ Авария{
    ЕСЛИ (SendMsgGUICode(СУАБ2ФИК_БЛОКИРОВКА_КОМАНД_ПЕРЕМЕЩЕНИЯ) &&
	       SendMsgGUICode(СУАБ2ФИК_ДАВЛЕНИЕ_АВАРИЙНОЕ)) {	
		В СОСТ ОжиданиеНеАварии;
	} ИНАЧЕ В СОСТ Начало;    
 }	
 СОСТ ОжиданиеНеАварии{
	ЕСЛИ (К_ВОЗД_ПОД_Р_НЕ_КРИТИЧЕСКОЕ == ВКЛ) {
		ЕСЛИ (ПРОЦ ПроверкаИсправностиВоздушнойПодушки В СОСТ ПАССИВНОЕ) 
			СТАРТ ПРОЦ ПроверкаИсправностиВоздушнойПодушки;
		В СОСТ Начало;	
	}	
 }
}   /* конец ПРОЦ "КонтрольДавления   */	

	
/*================================================*/
/*=                                              =*/
/*=      контроль давления  				     =*/
/*=                                              =*/
/*================================================*/
ПРОЦ КонтрольАварииДавления{
 ИЗ ПРОЦ Инициализация 	К_ВОЗД_ПОД_Р_НЕ_КРИТИЧЕСКОЕ,		/*		было критическое		*/
						К_ВОЗД_ПОД_Р_РАБОЧЕЕ;  				/*		было норма		*/
 СОСТ Начало{
	В СОСТ ОжиданиеНеНормальногоДавления; /*по умолчанию */
	ЕСЛИ ((К_ВОЗД_ПОД_Р_НЕ_КРИТИЧЕСКОЕ == ВКЛ) &&
				(К_ВОЗД_ПОД_Р_РАБОЧЕЕ != ВКЛ)) В СОСТ ОжиданиеНеНизкогоДавления;	/*		было критическое		*/
	ЕСЛИ (К_ВОЗД_ПОД_Р_НЕ_КРИТИЧЕСКОЕ == ВЫКЛ) В СОСТ Авария;		/*	давление ниже критического	-- авария */
 }	
  СОСТ ОжиданиеНеНормальногоДавления{
	ЕСЛИ ((К_ВОЗД_ПОД_Р_НЕ_КРИТИЧЕСКОЕ == ВЫКЛ) ||
				(К_ВОЗД_ПОД_Р_РАБОЧЕЕ != ВКЛ)) В СОСТ Начало;	
 }
 СОСТ ОжиданиеНеНизкогоДавления{
	ЕСЛИ ((К_ВОЗД_ПОД_Р_НЕ_КРИТИЧЕСКОЕ == ВЫКЛ) ||
				(К_ВОЗД_ПОД_Р_РАБОЧЕЕ == ВКЛ)) В СОСТ Начало;	
	ТАЙМАУТ ОДНА_МИНУТА В СОСТ Начало;			
 }
 СОСТ Авария{
	ЕСЛИ (ПРОЦ ОТМЕНА В СОСТ ПАССИВНОЕ) СТАРТ ПРОЦ ОТМЕНА; /* останов вращения/перемещения */
    ЕСЛИ (SendMsgGUICode(СУАБ2ФИК_ДАВЛЕНИЕ_АВАРИЙНОЕ)) {	
		В СОСТ ОжиданиеНеАварии;
	} ИНАЧЕ В СОСТ Начало;
 }	
 СОСТ ОжиданиеНеАварии{
	ЕСЛИ (К_ВОЗД_ПОД_Р_НЕ_КРИТИЧЕСКОЕ == ВКЛ) В СОСТ Начало;	
 }
}   /* конец ПРОЦ "КонтрольДавления   */	

/*================================================*/
/*=                                              =*/
/*=      контроль вакуума  	    			     =*/
/*=                                              =*/
/*================================================*/
ПРОЦ КонтрольВакуума{		
 ИЗ ПРОЦ Инициализация 	К_ДИСК_РАЗЖАТ;  				/*		было норма		*/
	СОСТ Начало{
      ЕСЛИ (К_ДИСК_РАЗЖАТ == ВЫКЛ) В СОСТ ВакуумНорма;      /* есть давление*/
      ИНАЧЕ                В СОСТ НедостаточныйВакуум;
	}
 СОСТ ВакуумНорма{
      ЕСЛИ (SendMsgGUICode(СУАБ2ФИК_ДИСК_ЗАЖАТ)) 	В СОСТ ОжиданиеНедостаточногоВакуума;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГИО В СОСТ НарушениеСвязи;
 }
 СОСТ НедостаточныйВакуум{
      ЕСЛИ (SendMsgGUICode(СУАБ2ФИК_ДИСК_РАЗЖАТ)) 	В СОСТ ОжиданиеНормальногоВакуума;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГИО В СОСТ НарушениеСвязи;
 }

 СОСТ ОжиданиеНедостаточногоВакуума{
      ЕСЛИ (К_ДИСК_РАЗЖАТ == ВКЛ) В СОСТ НедостаточныйВакуум;
 }
 СОСТ ОжиданиеНормальногоВакуума {
      ЕСЛИ (К_ДИСК_РАЗЖАТ == ВЫКЛ) В СОСТ ВакуумНорма;
 } 
/*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ НарушениеСвязи {
    В СОСТ Начало;
 } 
}   /* конец ПРОЦ "КонтрольВакуума   */	

/*================================================*/
/*=                                              =*/
/*=      контроль вакуума  	    			     =*/
/*=                                              =*/
/*================================================*/
ПРОЦ КонтрольАварииВакуума{		
 ИЗ ПРОЦ Инициализация 	К_ДИСК_РАЗЖАТ;  				/*		было норма		*/
	СОСТ Начало{
      ЕСЛИ (К_ДИСК_РАЗЖАТ == ВЫКЛ) В СОСТ КонтрольАварииВакуума;    /* есть вакуум*/
      ИНАЧЕ В СЛЕДУЮЩЕЕ;
	}
 
 СОСТ АварияВакуума{
      ЕСЛИ (ПРОЦ ОТМЕНА В СОСТ ПАССИВНОЕ) СТАРТ ПРОЦ ОТМЕНА; /* останов вращения/перемещения */
      ЕСЛИ (SendMsgGUICode(СУАБ2ФИК_АВАРИЙНОЕ_РАЗЖАТИЕ_ДИСКА)) 	ОШИБКА;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГИО ОШИБКА;
 }

 СОСТ КонтрольАварииВакуума{
      ЕСЛИ (К_ДИСК_РАЗЖАТ == ВКЛ) В СОСТ АварияВакуума;
 }
}   /* конец ПРОЦ "КонтрольАварииВакуума   */	


/*================================================*/
/*=                                              =*/
/*=      КонтрольСвязи_сМОХА1212  			     =*/
/*=     МОХА отвечает за лазер                   =*/
/*================================================*/
ПРОЦ КонтрольСвязи_сМОХА1212{
 ИЗ ПРОЦ Инициализация 	ФлагСвязиМоха1212;	
	СОСТ Начало{
		ЕСЛИ (ФлагСвязиМоха1212 == ВКЛ) В СОСТ ЕстьСвязьМоха1212;
		ЕСЛИ (ФлагСвязиМоха1212 == ВЫКЛ) В СОСТ НетСвязиМоха1212;	
	}
	СОСТ ЕстьСвязьМоха1212{
      ЕСЛИ (SendMsgGUICode(СУАБ2ФИК_ЕСТЬ_СВЯЗЬ_С_МОХА1212)) 	В СОСТ ОжиданиеРазрываСвязи;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГИО В СОСТ НарушениеСвязи;		
	}
	СОСТ НетСвязиМоха1212{
	  ЕСЛИ (ПРОЦ ОТМЕНА В СОСТ ПАССИВНОЕ) СТАРТ ПРОЦ ОТМЕНА;      /* останов вращения/перемещения */	
      ЕСЛИ (SendMsgGUICode(СУАБ2ФИК_НЕТ_СВЯЗИ_С_МОХА1212)) 	В СОСТ ОжиданиеВосстановленияСвязи;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГИО В СОСТ НарушениеСвязи;		
	}	
 СОСТ ОжиданиеРазрываСвязи{
      ЕСЛИ (ФлагСвязиМоха1212 == ВЫКЛ) В СОСТ НетСвязиМоха1212;
 }	
 СОСТ ОжиданиеВосстановленияСвязи{
      ЕСЛИ (ФлагСвязиМоха1212 == ВКЛ) В СОСТ ЕстьСвязьМоха1212;
 } 
 СОСТ НарушениеСвязи {
    В СОСТ Начало;
 } 	
}   /* конец ПРОЦ КонтрольСвязи_сМОХА1212   */	
/*================================================*/
/*=                                              =*/
/*=      КонтрольСвязи_сМОХА1214  			     =*/
/*=     отвечает за давление и вакуум            =*/
/*================================================*/
ПРОЦ КонтрольСвязи_сМОХА1214{
 ИЗ ПРОЦ Инициализация 	ФлагСвязиМоха1214;  				
	СОСТ Начало{
		ЕСЛИ (ФлагСвязиМоха1214 == ВКЛ) В СОСТ ЕстьСвязьМоха1214;
		ЕСЛИ (ФлагСвязиМоха1214 == ВЫКЛ) В СОСТ НетСвязиМоха1214;	
	}
	СОСТ ЕстьСвязьМоха1214{
      ЕСЛИ (SendMsgGUICode(СУАБ2ФИК_ЕСТЬ_СВЯЗЬ_С_МОХА1214)) 	В СОСТ ОжиданиеРазрываСвязи;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГИО В СОСТ НарушениеСвязи;		
	}
	СОСТ НетСвязиМоха1214{
	  ЕСЛИ (ПРОЦ ОТМЕНА В СОСТ ПАССИВНОЕ) СТАРТ ПРОЦ ОТМЕНА;      /* останов вращения/перемещения */	
      ЕСЛИ (SendMsgGUICode(СУАБ2ФИК_НЕТ_СВЯЗИ_С_МОХА1214)) 	В СОСТ ОжиданиеВосстановленияСвязи;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГИО В СОСТ НарушениеСвязи;		
	}	
 СОСТ ОжиданиеРазрываСвязи{
      ЕСЛИ (ФлагСвязиМоха1214 == ВЫКЛ) В СОСТ НетСвязиМоха1214;
 }	
 СОСТ ОжиданиеВосстановленияСвязи{
      ЕСЛИ (ФлагСвязиМоха1214 == ВКЛ) В СОСТ ЕстьСвязьМоха1214;
 } 
 СОСТ НарушениеСвязи {
    В СОСТ Начало;
 } 		
}   /* конец ПРОЦ КонтрольСвязи_сМОХА1214   */	
/*================================================*/
/*=                                              =*/
/*=      КонтрольДистанционного  			     =*/
/*=     управления  1212 (через программу)       =*/
/*================================================*/
ПРОЦ КонтрольДистанционного1212{
 ИЗ ПРОЦ Инициализация 	К_1212_ДИСТАНЦИОННОЕ;  				
	СОСТ Начало{
		ЕСЛИ (К_1212_ДИСТАНЦИОННОЕ) В СОСТ ЕстьДист1212;
		ИНАЧЕ В СОСТ НетДист1212;
	}	
	СОСТ ЕстьДист1212{
      ЕСЛИ (SendMsgGUICode(СУАБ2ФИК_ЕСТЬ_ДИСТАНЦИОННОЕ_УПРАВЛЕНИЕ_1212)) 	В СОСТ ОжиданиеНедистанционного;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГИО В СОСТ НарушениеСвязи;		
	}
	СОСТ НетДист1212{
	  ЕСЛИ (ПРОЦ ОТМЕНА В СОСТ ПАССИВНОЕ) СТАРТ ПРОЦ ОТМЕНА;      /* останов вращения/перемещения */	
      ЕСЛИ (SendMsgGUICode(СУАБ2ФИК_НЕТ_ДИСТАНЦИОННОГО_УПРАВЛЕНИЯ_1212)) 	В СОСТ ОжиданиеДистанционного;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГИО В СОСТ НарушениеСвязи;		
	}	
 СОСТ ОжиданиеНедистанционного{
      ЕСЛИ (К_1212_ДИСТАНЦИОННОЕ == ВЫКЛ) В СОСТ НетДист1212;
 }	
 СОСТ ОжиданиеДистанционного{
      ЕСЛИ (К_1212_ДИСТАНЦИОННОЕ == ВКЛ) В СОСТ ЕстьДист1212;
 } 
 СОСТ НарушениеСвязи {
    В СОСТ Начало;
 } 
}   /* конец ПРОЦ КонтрольДистанционного1212   */	


/*================================================*/
/*=                                              =*/
/*=      КонтрольДистанционного  			     =*/
/*=     управления  1214 (через программу)       =*/
/*================================================*/
ПРОЦ КонтрольДистанционного1214{
 ИЗ ПРОЦ Инициализация 	К_1214_ДИСТАНЦИОННОЕ; 
	СОСТ Начало{
		ЕСЛИ (К_1214_ДИСТАНЦИОННОЕ) В СОСТ ЕстьДист1214;
		ИНАЧЕ В СОСТ НетДист1214;
	}	
	СОСТ ЕстьДист1214{
      ЕСЛИ (SendMsgGUICode(СУАБ2ФИК_ЕСТЬ_ДИСТАНЦИОННОЕ_УПРАВЛЕНИЕ_1214)) 	В СОСТ ОжиданиеНедистанционного;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГИО В СОСТ НарушениеСвязи;		
	}
	СОСТ НетДист1214{
	  ЕСЛИ (ПРОЦ ОТМЕНА В СОСТ ПАССИВНОЕ) СТАРТ ПРОЦ ОТМЕНА;      /* останов вращения/перемещения */	
      ЕСЛИ (SendMsgGUICode(СУАБ2ФИК_НЕТ_ДИСТАНЦИОННОГО_УПРАВЛЕНИЯ_1214)) 	В СОСТ ОжиданиеДистанционного;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГИО В СОСТ НарушениеСвязи;		
	}	
 СОСТ ОжиданиеНедистанционного{
      ЕСЛИ (К_1214_ДИСТАНЦИОННОЕ == ВЫКЛ) В СОСТ НетДист1214;
 }	
 СОСТ ОжиданиеДистанционного{
      ЕСЛИ (К_1214_ДИСТАНЦИОННОЕ == ВКЛ) В СОСТ ЕстьДист1214;
 } 
  СОСТ НарушениеСвязи {
    В СОСТ Начало;
 } 
}   /* конец ПРОЦ КонтрольДистанционного1214   */

/*================================================*/
/*=                                              =*/
/*=      РАЗБЛОКИРОВКА_КОМАНД_ПЕРЕМЕЩЕНИЯ		     =*/
/*=   на ГПИ и ОСТАНОВ перемещений 			     =*/
/*================================================*/
ПРОЦ РАЗБЛОКИРОВКА_КОМАНД_ПЕРЕМЕЩЕНИЯ{
	СОСТ Начало{
		СТАРТ ПРОЦ ОТМЕНА;
		В СЛЕДУЮЩЕЕ;
	}	
СОСТ СообщениеГИО{
    ЕСЛИ (SendMsgGUICode(СУАБ2ФИК_РАЗБЛОКИРОВКА_КОМАНД_ПЕРЕМЕЩЕНИЯ)) В СЛЕДУЮЩЕЕ;
     ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГИО В СОСТ Конец;
 }

 СОСТ Конец{
    СТОП;
 }

}/*================================================*/
/*=                                              =*/
/*=      БЛОКИРОВКА_КОМАНД_ПЕРЕМЕЩЕНИЯ		     =*/
/*=   на ГПИ и ОСТАНОВ перемещений 			     =*/
/*================================================*/
ПРОЦ БЛОКИРОВКА_КОМАНД_ПЕРЕМЕЩЕНИЯ{
	СОСТ Начало{
		СТАРТ ПРОЦ ОТМЕНА;
		В СЛЕДУЮЩЕЕ;
	}	
СОСТ СообщениеГИО{
    ЕСЛИ (SendMsgGUICode(СУАБ2ФИК_БЛОКИРОВКА_КОМАНД_ПЕРЕМЕЩЕНИЯ)) В СЛЕДУЮЩЕЕ;
     ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГИО В СОСТ Конец;
 }

 СОСТ Конец{
    СТОП;
 }
}
/*================================================*/
/*=                                              =*/
/*=      ОТМЕНА 							     =*/
/*=   останов вращения/перемещения 			     =*/
/*================================================*/
ПРОЦ ОТМЕНА{
/* ИЗ ПРОЦ Инициализация 	;  	*/			
	СОСТ Начало{
/*		?????????????		*/
		СТОП;
	}	
}   /* конец ПРОЦ ОТМЕНА   */

/*================================================*/
/*=                                              =*/
/*=      КОНЕЦ_РАБОТЫ						     =*/
/*=     									     =*/
/*================================================*/
ПРОЦ КОНЕЦ_РАБОТЫ{
ИЗ ПРОЦ Инициализация ТекущийРежим;
	СОСТ Начало{
		ТекущийРежим = РЕЖИМ_КОНЕЦ_РАБОТЫ;
		СТОП ПРОЦ Режим_РР;
			
		СТАРТ ПРОЦ ВЫКЛ_ЛАЗЕРА;
		СТАРТ ПРОЦ ВЫКЛ_ПОДСВЕТКУ_ДИСКА;
		СТАРТ ПРОЦ КАРЕТКУ_В_ПОЛОЖЕНИЕ_УСТАНОВКИ_ДИСКА;
		СТАРТ ПРОЦ СТОП_ВРАЩЕНИЯ_ПРЕДМЕТНОГО_СТОЛИКА;

/*		ВЫКЛ_АКТИВНОЕ_ПОЗИЦИОНИРОВАНИЕ_ПО_ВРАЩЕНИЮ   ???????	*/
		
			
		СТОП ПРОЦ КонтрольСвязи_сМОХА1212;
		СТОП ПРОЦ КонтрольСвязи_сМОХА1214;
		СТОП ПРОЦ КонтрольДистанционного1212;
		СТОП ПРОЦ КонтрольДистанционного1214;
		СТОП ПРОЦ КонтрольИзмененияЗаданий_ЗД_R_КАРЕТКИ;
		СТОП ПРОЦ КонтрольИзмененияЗаданий_ЗД_W_ПРЕДМЕТНОГО_СТОЛИКА;
		СТОП ПРОЦ КонтрольИзменения_ФП_R_КАРЕТКИ;
		СТОП ПРОЦ КонтрольИзменения_ФП_W_ПРЕДМЕТНОГО_СТОЛИКА;
		
		В СЛЕДУЮЩЕЕ;
	}
 СОСТ ПроверкаПриходаВИсходное{
    ЕСЛИ (
	
        !( ПРОЦ ВЫКЛ_ЛАЗЕРА     В СОСТ АКТИВНОЕ) &&
		!( ПРОЦ ВЫКЛ_ПОДСВЕТКУ_ДИСКА   В СОСТ АКТИВНОЕ)  &&
		!( ПРОЦ КАРЕТКУ_В_ПОЛОЖЕНИЕ_УСТАНОВКИ_ДИСКА     В СОСТ АКТИВНОЕ) &&
		!( ПРОЦ СТОП_ВРАЩЕНИЯ_ПРЕДМЕТНОГО_СТОЛИКА   В СОСТ АКТИВНОЕ)  
          ) В СЛЕДУЮЩЕЕ;
 }

 СОСТ СообщениеГИО{
    ЕСЛИ (SendMsgGUICode(СУАБ2ФИК_УСТАНОВКА_ГОТОВА_К_ВЫКЛЮЧЕНИЮ)) В СЛЕДУЮЩЕЕ;
     ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГИО В СОСТ Конец;
 }

 СОСТ Конец{
    СТОП;
 }

	
}   /* конец ПРОЦ КОНЕЦ_РАБОТЫ   */

/*==============================================================*/
/*=                                                            =*/
/*=      Перевести каретку в центр вращения					    =*/
/*=     									                   =*/
/*==============================================================*/
ПРОЦ КАРЕТКУ_В_ЦЕНТР_ВРАЩЕНИЯ{
 ИЗ ПРОЦ Инициализация 	ЗД_R_КАРЕТКИ,
						ФП_R_КАРЕТКИ;  				
	СОСТ Начало{
/*		?????????????		*/
		СТОП;
	}	
}   /* конец ПРОЦ КАРЕТКУ_В_ЦЕНТР_ВРАЩЕНИЯ   */

/*===========================================================*/
/*=                                                         =*/
/*=      Перевести каретку в ЗАДАННЫЙ РАДИУС			    =*/
/*=     									                =*/
/*===========================================================*/
ПРОЦ КАРЕТКУ_В_ЗАДАННЫЙ_РАДИУС{
 ИЗ ПРОЦ Инициализация 	ЗД_R_КАРЕТКИ,
						ФП_R_КАРЕТКИ;  				
	СОСТ Начало{
/*		?????????????		*/
		СТОП;
	}	
}   /* конец ПРОЦ КАРЕТКУ_В_ЗАДАННЫЙ_РАДИУС   */

/*===========================================================*/
/*=                                                         =*/
/*=      Перевести каретку в ЗАДАННЫЙ РАДИУС			    =*/
/*=     									                =*/
/*===========================================================*/
ПРОЦ СТОП_КАРЕТКИ{
 ИЗ ПРОЦ Инициализация 	ЗД_R_КАРЕТКИ,
						ФП_R_КАРЕТКИ;  				
	СОСТ Начало{
/*		?????????????		*/
		СТОП;
	}	
}   /* конец ПРОЦ СТОП_КАРЕТКИ   */


/*==============================================================*/
/*=                                                            =*/
/*=      Перевести каретку в положение установки диска         =*/
/*=     									                   =*/
/*==============================================================*/
ПРОЦ КАРЕТКУ_В_ПОЛОЖЕНИЕ_УСТАНОВКИ_ДИСКА{
 ИЗ ПРОЦ Инициализация 	ЗД_R_КАРЕТКИ,
						ФП_R_КАРЕТКИ;  				
	СОСТ Начало{
/*		?????????????		*/
		СТОП;
	}	
}   /* конец ПРОЦ КАРЕТКУ_В_ПОЛОЖЕНИЕ_УСТАНОВКИ_ДИСКА   */

/*==============================================================*/
/*=                                                            =*/
/*=      Включить активное позиционирование по вращению        =*/
/*=     									                   =*/
/*==============================================================*/
ПРОЦ ВКЛ_АКТИВНОЕ_ПОЗИЦИОНИРОВАНИЕ_ПО_ВРАЩЕНИЮ{
 ИЗ ПРОЦ Инициализация 	К_1214_ДИСТАНЦИОННОЕ;  				
	СОСТ Начало{
/*		?????????????	Команда F????	*/
		СТОП;
	}	
}   /* конец ПРОЦ ВКЛ_АКТИВНОЕ_ПОЗИЦИОНИРОВАНИЕ_ПО_ВРАЩЕНИЮ   */

/*==============================================================*/
/*=                                                            =*/
/*=      Выключить активное позиционирование по вращению        =*/
/*=     									                   =*/
/*==============================================================*/
ПРОЦ ВЫКЛ_АКТИВНОЕ_ПОЗИЦИОНИРОВАНИЕ_ПО_ВРАЩЕНИЮ{
 ИЗ ПРОЦ Инициализация 	К_1214_ДИСТАНЦИОННОЕ;  				
	СОСТ Начало{
/*		?????????????		*/
		СТОП;
	}	
}   /* конец ПРОЦ "ВЫКЛ_АКТИВНОЕ_ПОЗИЦИОНИРОВАНИЕ_ПО_ВРАЩЕНИЮ   */

/*==============================================================*/
/*=                                                            =*/
/*=      Включить вращение предметного столика        =*/
/*=     									                   =*/
/*==============================================================*/
ПРОЦ ВКЛ_ВРАЩЕНИЕ_ПРЕД_СТОЛИКА_В_ЗД_W{
 ИЗ ПРОЦ Инициализация 	ЗД_W_ПРЕДМЕТНОГО_СТОЛИКА,
						ФП_W_ПРЕДМЕТНОГО_СТОЛИКА;  				
	СОСТ Начало{
/*		?????????????		*/
		СТОП;
	}	
}   /* конец ПРОЦ ВКЛ_ВРАЩЕНИЕ_ПРЕД_СТОЛИКА_В_ЗД_W   */

/*==============================================================*/
/*=                                                            =*/
/*=      Выключить вращение предметного столика        =*/
/*=     									                   =*/
/*==============================================================*/
ПРОЦ СТОП_ВРАЩЕНИЯ_ПРЕДМЕТНОГО_СТОЛИКА{
 ИЗ ПРОЦ Инициализация 	ЗД_W_ПРЕДМЕТНОГО_СТОЛИКА,
						ФП_W_ПРЕДМЕТНОГО_СТОЛИКА;  				
	СОСТ Начало{
/*		?????????????		*/
		СТОП;
	}	
}   /* конец ПРОЦ СТОП_ВРАЩЕНИЯ_ПРЕДМЕТНОГО_СТОЛИКА   */

} /* конец программы */