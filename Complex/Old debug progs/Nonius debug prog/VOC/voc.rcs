 Прогр VOC 

 { 

/*###########################################################*/
/*ПРОГРАММА ИМИТАЦИИ УГЛОИЗМЕРИТЕЛЬНОЙ МАШИНЫ  НОНИУС*/
/*###########################################################*/

/*###########################################################*/
/*#########                     #############################*/
/*#########  ОПИСАНИЕ КОНСТАНТ  #############################*/
/*#########                     #############################*/
/*###########################################################*/

ТАКТ     50;    /* такт равен 500 миллисекундам */

КОНСТ ВКЛ                1;
КОНСТ ВЫКЛ               0;
/*===========================================================*/
/*========= ДИАПАЗОН ИЗМЕНЕНИЯ ЗАДАНИЙ            ===========*/
/*===========================================================*/
КОНСТ МИНИМАЛЬНОЕ_ЗАДАНИЕ_R_КАРЕТКИ            0.0;   /* мм  */ 
КОНСТ МАКСИМАЛЬНОЕ_ЗАДАНИЕ_R_КАРЕТКИ        200.0;    /* мм  */

КОНСТ МИНИМАЛЬНОЕ_ЗАДАНИЕ_W_УгловойСкорости            0.0;   /* ??????  */ 
КОНСТ МАКСИМАЛЬНОЕ_ЗАДАНИЕ_W_УгловойСкорости        200.0;    /* ?????  */ 


КОНСТ R_ПО_УМОЛЧАНИЮ 0.0;
КОНСТ W_ПО_УМОЛЧАНИЮ 0.0;
КОНСТ R_ФП_ПО_УМОЛЧАНИЮ 0.0;
КОНСТ ФП_W_ПО_УМОЛЧАНИЮ 0.0;

КОНСТ ТАЙМАУТ_ВОЗД_ПОДУШКИ 10;
КОНСТ ТАЙМАУТ_КЛАПАНОВ 10;


/*===========================================================*/
/*========= КОДЫ КОМАНД ПОСТУПАЮЩИХ ОТ GUI ==================*/
/*===========================================================*/
/*КОДЫ НАЧАЛЬНЫХ СООБЩЕНИЙ*/
ПЕРЕЧИСЛЕНИЕ 
{
КОМ_ВХОДНАЯ_ТЕСТ_ДАВЛЕНИЕ_В_НОРМУ,
КОМ_ВХОДНАЯ_ТЕСТ_ДАВЛЕНИЕ_В_НИЗКОЕ,
КОМ_ВХОДНАЯ_ТЕСТ_ДАВЛЕНИЕ_В_АВАРИНОЕ,
КОМ_ВХОДНАЯ_ТЕСТ_ОПОРА_ИСПРАВНА,
КОМ_ВХОДНАЯ_ТЕСТ_ОПОРА_ОТКАЗ_ВКЛЮЧЕНИЯ,
КОМ_ВХОДНАЯ_ТЕСТ_ОПОРА_ОТКАЗ_ВЫКЛЮЧЕНИЯ,
КОМ_ВХОДНАЯ_ТЕСТ_КАРЕТКА_ИСПРАВНА,
КОМ_ВХОДНАЯ_ТЕСТ_КАРЕТКА_ОТКАЗ_ВКЛЮЧЕНИЯ,
КОМ_ВХОДНАЯ_ТЕСТ_КАРЕТКА_ОТКАЗ_ВЫКЛЮЧЕНИЯ,
КОМ_ВХОДНАЯ_ТЕСТ_ШПИНДЕЛЬ_ИСПРАВЕН,
КОМ_ВХОДНАЯ_ТЕСТ_ШПИНДЕЛЬ_ОТКАЗ_ВКЛЮЧЕНИЯ,
КОМ_ВХОДНАЯ_ТЕСТ_ШПИНДЕЛЬ_ОТКАЗ_ВЫКЛЮЧЕНИЯ,
КОМ_ВХОДНАЯ_ТЕСТ_СВЕТОДИОД_ИСПРАВЕН,
КОМ_ВХОДНАЯ_ТЕСТ_СВЕТОДИОД_ОТКАЗ_ВКЛЮЧЕНИЯ,
КОМ_ВХОДНАЯ_ТЕСТ_СВЕТОДИОД_ОТКАЗ_ВЫКЛЮЧЕНИЯ,
КОМ_ВХОДНАЯ_ТЕСТ_КЛАПАН_АТМОСФЕРЫ_ИСПРАВЕН,
КОМ_ВХОДНАЯ_ТЕСТ_КЛАПАН_АТМОСФЕРЫ_ОТКАЗ_ВКЛЮЧЕНИЯ,
КОМ_ВХОДНАЯ_ТЕСТ_КЛАПАН_АТМОСФЕРЫ_ОТКАЗ_ВЫКЛЮЧЕНИЯ,
КОМ_ВХОДНАЯ_ТЕСТ_КЛАПАН_ВАКУУМА_ИСПРАВЕН,
КОМ_ВХОДНАЯ_ТЕСТ_КЛАПАН_ВАКУУМА_ОТКАЗ_ВКЛЮЧЕНИЯ,
КОМ_ВХОДНАЯ_ТЕСТ_КЛАПАН_ВАКУУМА_ОТКАЗ_ВЫКЛЮЧЕНИЯ,

КОМ_ВХОДНАЯ_ТЕСТ_КОНТРОЛЬНОЕ_СООБЩЕНИЕ
};

/*===========================================================*/
/*========= КОДЫ КОМАНД ПОСТУПАЮЩИХ ОТ АУ  ==================*/
/*===========================================================*/

/*===========================================================*/
/*========= КОДЫ СООБЩЕНИЙ ВЫХОДНЫХ ==============*/
/*===========================================================*/
ПЕРЕЧИСЛЕНИЕ {
	КОМ_ВЫХОДНАЯ_ТЕСТ_ИЗМН_R_КАРЕТКИ = КОМ_ВХОДНАЯ_ТЕСТ_КОНТРОЛЬНОЕ_СООБЩЕНИЕ + 1, 
	КОМ_ВЫХОДНАЯ_ТЕСТ_ИЗМН_W_КАРЕТКИ,  		
	КОМ_ВЫХОДНАЯ_ТЕСТ_ИЗМН_R_ФП_КАРЕТКИ,
	КОМ_ВЫХОДНАЯ_ТЕСТ_ИЗМН_W_ФП_КАРЕТКИ,
	КОМ_ВЫХОДНАЯ_ТЕСТ_ДАВЛЕНИЕ_В_НОРМЕ,
	КОМ_ВЫХОДНАЯ_ТЕСТ_ДАВЛЕНИЕ_НИЗКОЕ,
	КОМ_ВЫХОДНАЯ_ТЕСТ_ДАВЕЛЕНИЕ_АВАРИЙНОЕ,
	КОМ_ВЫХОДНАЯ_ОШИБКА_КОДА_КОМАНДЫ_ОТ_ГУИ,
	КОМ_ВЫХОДНАЯ_ТЕСТ_ВОЗДУХ_ПОДАН_НА_ОПОРУ,
	КОМ_ВЫХОДНАЯ_ТЕСТ_НА_ОПОРЕ_СТОЛА_АВАРИЙНОЕ_ДАВЛЕНИЕ,
	КОМ_ВЫХОДНАЯ_ТЕСТ_ВЫКЛЮЧЕНА_ПОДАЧА_ВОЗДУХА_НА_ОПОРУ,
	КОМ_ВЫХОДНАЯ_ТЕСТ_ВОЗДУХ_ПОДАН_НА_КАРЕТКУ,
	КОМ_ВЫХОДНАЯ_ТЕСТ_НА_КАРЕТКЕ_АВАРИЙНОЕ_ДАВЛЕНИЕ,
	КОМ_ВЫХОДНАЯ_ТЕСТ_ВЫКЛЮЧЕНА_ПОДАЧА_ВОЗДУХА_НА_КАРЕТКУ,
	КОМ_ВЫХОДНАЯ_ТЕСТ_ВОЗДУХ_ПОДАН_НА_ШПИНДЕЛЬ,
	КОМ_ВЫХОДНАЯ_ТЕСТ_ПРЕДУПРЕЖДЕНИЕ_ВОЗДУХ_ПОДАН_НА_ШПИНДЕЛЬ_НА_КАРЕТКЕ_НЕТ,
	КОМ_ВЫХОДНАЯ_ТЕСТ_ПРЕДУПРЕЖДЕНИЕ_ВОЗДУХ_ПОДАН_НА_ШПИНДЕЛЬ_НА_ОПОРЕ_НЕТ,
	КОМ_ВЫХОДНАЯ_ТЕСТ_НА_ШПИНДЕЛЕ_АВАРИЙНОЕ_ДАВЛЕНИЕ,
	КОМ_ВЫХОДНАЯ_ТЕСТ_ВЫКЛЮЧЕНА_ПОДАЧА_ВОЗДУХА_НА_ШПИНДЕЛЬ,
	КОМ_ВЫХОДНАЯ_ТЕСТ_ПИТАНИЕ_ПОДАНО_НА_ЛАЗЕР,
	КОМ_ВЫХОДНАЯ_ТЕСТ_ПИТАНИЕ_НА_ЛАЗЕРЕ_НЕТ,
	КОМ_ВЫХОДНАЯ_ТЕСТ_СВЕТОДИОД_ВКЛЮЧЕН,
	КОМ_ВЫХОДНАЯ_ТЕСТ_СВЕТОДИОД_ВЫКЛЮЧЕН,
	КОМ_ВЫХОДНАЯ_ТЕСТ_СВЕТОДИОД_ОТКАЗ_ВКЛЮЧЕНИЯ,
	КОМ_ВЫХОДНАЯ_ТЕСТ_СВЕТОДИОД_ОТКАЗ_ВЫКЛЮЧЕНИЯ,
	КОМ_ВЫХОДНАЯ_ТЕСТ_НАСОС_ВЫКЛЮЧЕН,
	КОМ_ВЫХОДНАЯ_ТЕСТ_КЛАПАН_НАСОС_ВКЛЮЧЕН,
	КОМ_ВЫХОДНАЯ_ТЕСТ_КЛАПАН_АТМОСФЕРЫ_ОТКРЫТ,
	КОМ_ВЫХОДНАЯ_ТЕСТ_КЛАПАН_АТМОСФЕРЫ_ЗАКРЫТ,
	КОМ_ВЫХОДНАЯ_ТЕСТ_КЛАПАН_ВАКУУМА_ОТКРЫТ,
	КОМ_ВЫХОДНАЯ_ТЕСТ_КЛАПАН_ВАКУУМА_ЗАКРЫТ,
	КОМ_ВЫХОДНАЯ_ТЕСТ_ОБА_КЛАПАНА_ОТКРЫТЫ_ДИСК_РАЗАЖАТ,
	КОМ_ВЫХОДНАЯ_ТЕСТ_ВАКУУМ_ОТКРЫТ_АТМОСФЕРА_ЗАКРЫТ_ДИСК_ЗАЖАТ,
	КОМ_ВЫХОДНАЯ_ТЕСТ_ВАКУУМ_ОТКРЫТ_АТМОСФЕРА_ЗАКРЫТ_ДИСК_РАЗЖАТ,
	КОМ_ВЫХОДНАЯ_ТЕСТ_ВАКУУМ_ЗАКРЫТ_АТМОСФЕРА_ОТКРЫТ_ДИСК_РАЗАЖАТ,
	КОМ_ВЫХОДНАЯ_ТЕСТ_ВАКУУМ_ЗАКРЫТ_АТМОСФЕРА_ЗАКРЫТ_ДИСК_ЗАЖАТ,
	КОМ_ВЫХОДНАЯ_ТЕСТ_ВАКУУМ_ЗАКРЫТ_АТМОСФЕРА_ЗАКРЫТ_ДИСК_РАЗЖАТ,
	КОМ_ВЫХОДНАЯ_ТЕСТ_КОНТРОЛЬНОЕ_СООБЩЕНИЕ 
 };
 
 /*ФЛАГИ СОСТОЯНИЙ*/
 
 ПЕРЕЧИСЛЕНИЕ 
 {
	ДАВЛЕНИЕ_АВАРИЙНОЕ,
	ДАВЛЕНИЕ_НИЗКОЕ,
	ДАВЛЕНИЕ_В_НОРМЕ
 };
 
 ПЕРЕЧИСЛЕНИЕ 
{
	ИСПРАВНОСТЬ,
	ОТКАЗ_ВКЛЮЧЕНИЯ,
	ОТКАЗ_ВЫКЛЮЧЕНИЯ
};
/*=========================================================================*/
/*========================== ФУНКЦИИ ======================================*/
/*=========================================================================*/
/*:::::::::Функции для работы  с ГУИ :::::::::::::::::::::   */

/*чтение кода и параметра события от ПИВ из кольцевого буфера в память */
ФУНКЦИЯ ЦЕЛ GetNextMsgGUI(ПУСТО);
ФУНКЦИЯ ЦЕЛ  GetMsgGUICode        		(ПУСТО);  /*чтение из памяти кода события */
ФУНКЦИЯ ПЛАВ GetFloatParamGUI     		(ПУСТО);  /*чтение из памяти параметра события */
ФУНКЦИЯ ЦЕЛ  SendMsgLongParamGUI  		(ЦЕЛ, ДЦЕЛ);
ФУНКЦИЯ ДЦЕЛ GetLongParamGUI      		(ПУСТО);  /*чтение с экрана гуи - исползуется */
ФУНКЦИЯ ЦЕЛ  SendMsgGUICode       		(ЦЕЛ);
ФУНКЦИЯ ЦЕЛ  SendMsgFloatParamGUI       (ЦЕЛ, ПЛАВ);


ВХОД ВХОД_МОХА_1214_ДАВЛ_ВАКУУМ		  	0 0 8; 
ВХОД ВХОД_МОХА_1212_ЛАЗЕР		  			0 1 8; 

ВЫХОД  ВЫХОД_МОХА_1214_ДАВЛ_ВАКУУМ  			1 0 8; 
ВЫХОД  ВЫХОД_МОХА_1212_ЛАЗЕР		  			1 1 8; 

ПРОЦ Инициализация
{
/*===========================================================*/
/*=========== ОПИСАНИЕ ПЕРЕМЕННЫХ ===========================*/
/*===========================================================*/

/*=========== МОХА 1214 (ДАВЛЕНИЕ И ВАКУУМ) =======*/
/* ВЫХОДНЫЕ СИГНАЛЫ 1214 "ВОЗД_ПОД" -- воздушная подушка, "ЛУ" -- локальное управление */

ЛОГ У_ВОЗД_ПОД_КАРЕТКИ			= {ВХОД_МОХА_1214_ДАВЛ_ВАКУУМ[1]} ДЛЯ ВСЕХ; 	/* К1 */
ЛОГ У_ВОЗД_ПОД_ШПИНДЕЛЯ			= {ВХОД_МОХА_1214_ДАВЛ_ВАКУУМ[1]} ДЛЯ ВСЕХ;	/* К2 */
ЛОГ У_ВОЗД_ПОД_СТОЛА			= {ВХОД_МОХА_1214_ДАВЛ_ВАКУУМ[1]} ДЛЯ ВСЕХ;	/* К3 */  /* ноги*/
ЛОГ У_ЗАЖИМ_ДИСКА				= {ВХОД_МОХА_1214_ДАВЛ_ВАКУУМ[1]} ДЛЯ ВСЕХ;	/* К4 */
ЛОГ У_ВКЛ_НАСОС					= {ВХОД_МОХА_1214_ДАВЛ_ВАКУУМ[1]} ДЛЯ ВСЕХ;	/* питание насоса */ 
ЛОГ У_РАЗЖИМ_ДИСКА				= {ВХОД_МОХА_1214_ДАВЛ_ВАКУУМ[1]} ДЛЯ ВСЕХ;	/* К5 */ 

/* ВЫХОДНЫЕ ДАВЛЕНИЕ И ВАКУУМ */  
ЛОГ К_ВОЗД_ПОД_Р_НЕ_КРИТИЧЕСКОЕ		= {ВЫХОД_МОХА_1214_ДАВЛ_ВАКУУМ[1]} ДЛЯ ВСЕХ;	/*ВЫХОД МОХИ  	0*/  
ЛОГ К_ВОЗД_ПОД_Р_РАБОЧЕЕ			= {ВЫХОД_МОХА_1214_ДАВЛ_ВАКУУМ[1]} ДЛЯ ВСЕХ;	/*ВЫХОД МОХИ  	1*/ 
ЛОГ К_ДИСК_РАЗЖАТ					= {ВЫХОД_МОХА_1214_ДАВЛ_ВАКУУМ[1]} ДЛЯ ВСЕХ; /*ВЫХОД МОХИ  	2*/     /* тут если 0, то вакуум есть ТАК ?????*/
ЛОГ К_1214_ДИСТАНЦИОННОЕ			= {ВЫХОД_МОХА_1214_ДАВЛ_ВАКУУМ[1]} ДЛЯ ВСЕХ; /*ВЫХОД МОХИ  	3*/		/* true - от программы, означает, что все управление false на оборудовании*/


/*=========== МОХА 1212 =======*/
/* ВХОДНЫЕ СИГНАЛЫ 1212 "ВОЗД_ПОД" -- воздушная подушка, "ЛУ" -- локальное управление */
ЛОГ У_ПИТАНИЕ_ЛАЗЕРА				= {ВХОД_МОХА_1212_ЛАЗЕР[1]} ДЛЯ ВСЕХ; /*ВХОД МОХИ  0*/
ЛОГ У_ВКЛ_ЛАЗЕРА					= {ВХОД_МОХА_1212_ЛАЗЕР[1]} ДЛЯ ВСЕХ; /*ВХОД МОХИ  1*/
ЛОГ У_ИНТЕНСИВНОСТЬ_ЛАЗЕРА			= {ВХОД_МОХА_1212_ЛАЗЕР[1]} ДЛЯ ВСЕХ;  /*ВХОД МОХИ 2*/	/*МОДУЛЯТОР_ЛАЗЕРА*/
ЛОГ У_ВКЛ_СВЕТОДИОДА				= {ВХОД_МОХА_1212_ЛАЗЕР[1]} ДЛЯ ВСЕХ;  /*освещение диска*/
ЛОГ У_ВЫКЛ_СВЕТОДИОДА				= {ВХОД_МОХА_1212_ЛАЗЕР[1]} ДЛЯ ВСЕХ; /*освещение диска*/


/*-----от  ЖЕЛЕЗА --ЛАЗЕР---------*/	/* читаются входы с железа через МОХУ функцией inputf2  */
ЛОГ К_1212_ДИСТАНЦИОННОЕ				= {ВХОД_МОХА_1212_ЛАЗЕР[1]} ДЛЯ ВСЕХ;	/*вход МОХИ  	0*/
ЛОГ К_ИНТЕНСИВНОСТЬ_ЛАЗЕРА				= {ВХОД_МОХА_1212_ЛАЗЕР[1]} ДЛЯ ВСЕХ;	/*вход МОХИ  	1*/ /* Перебейнос думает о значении сигнала */

/*===========================================================*/
/*=========== ЯЧЕЙКИ ИНФОРМАЦИОННЫХ КАНАЛОВ =================*/
/*===========================================================*/
ЦЕЛ КодСобытия_от_ГУИ        ДЛЯ ВСЕХ;
ЦЕЛ КодСобытия_к_ГУИ         ДЛЯ ВСЕХ;

/*===========================================================*/
/*=========== ЯЧЕЙКИ ФАКТИЧЕСКИХ ЗНАЧЕНИЙ ===================*/
/*===========================================================*/
ПЛАВ   	ЗД_R_КАРЕТКИ			ДЛЯ ВСЕХ;
ПЛАВ 	ЗД_W_УгловойСкорости	ДЛЯ ВСЕХ;
ПЛАВ   	ФП_R_КАРЕТКИ			ДЛЯ ВСЕХ;
ПЛАВ 	ФП_W_ПРЕДМЕТНОГО_СТОЛИКА	ДЛЯ ВСЕХ;

/*===Флаги сценариев работы===*/
ЦЕЛ ФлагСостояниеДавления ДЛЯ ВСЕХ;
ЦЕЛ ФлагРежимРаботыОпоры ДЛЯ ВСЕХ;
ЦЕЛ ФлагРежимРаботыКаретки ДЛЯ ВСЕХ;
ЦЕЛ ФлагРежимРаботыШпинделя ДЛЯ ВСЕХ;
ЦЕЛ ФлагРаботыСветодиода ДЛЯ ВСЕХ;
ЦЕЛ ФлагРежимКлапанаАтмосферы ДЛЯ ВСЕХ;
ЦЕЛ ФлагРежимКлапанаВакуума ДЛЯ ВСЕХ;

	 СОСТ Начало
	 {	 

		ФлагРежимРаботыОпоры = ИСПРАВНОСТЬ;
		ФлагРежимРаботыКаретки = ИСПРАВНОСТЬ;
		ФлагРежимРаботыШпинделя = ИСПРАВНОСТЬ;		
		ФлагРаботыСветодиода = ИСПРАВНОСТЬ;
		ФлагРежимКлапанаАтмосферы = ИСПРАВНОСТЬ;
		ФлагРежимКлапанаВакуума = ИСПРАВНОСТЬ;
		
		ФлагСостояниеДавления = ДАВЛЕНИЕ_В_НОРМЕ;
		
		ЗД_R_КАРЕТКИ = R_ПО_УМОЛЧАНИЮ;
		ЗД_W_УгловойСкорости = W_ПО_УМОЛЧАНИЮ;		
		ФП_R_КАРЕТКИ	= R_ФП_ПО_УМОЛЧАНИЮ; 
		ФП_W_ПРЕДМЕТНОГО_СТОЛИКА = 	ФП_W_ПО_УМОЛЧАНИЮ;
		
		SendMsgFloatParamGUI(КОМ_ВЫХОДНАЯ_ТЕСТ_ИЗМН_R_КАРЕТКИ, ЗД_R_КАРЕТКИ);
		SendMsgFloatParamGUI(КОМ_ВЫХОДНАЯ_ТЕСТ_ИЗМН_W_КАРЕТКИ, ЗД_W_УгловойСкорости);
		SendMsgFloatParamGUI(КОМ_ВЫХОДНАЯ_ТЕСТ_ИЗМН_R_ФП_КАРЕТКИ, ФП_R_КАРЕТКИ);
		SendMsgFloatParamGUI(КОМ_ВЫХОДНАЯ_ТЕСТ_ИЗМН_W_ФП_КАРЕТКИ, ФП_W_ПРЕДМЕТНОГО_СТОЛИКА);
		СТАРТ ПРОЦ ЧтениеКольцевогоБуфера;
		СТАРТ ПРОЦ КОНТРОЛЬ_ДАВЛЕНИЯ_ВОЗД_ПОДУШКИ; 
		СТАРТ ПРОЦ КонтрольПодачиВоздухаНаОпору; 
		СТАРТ ПРОЦ КонтрольПодачиВоздухаНаКаретку; 
		СТАРТ ПРОЦ КонтрольПодачиВоздухаНаШпиндель; 
		СТАРТ ПРОЦ КонтрольКлапанаАтмосферы; 
		СТАРТ ПРОЦ КонтрольРазжатияЗажатияДиска; 
		СТАРТ ПРОЦ КонтрольКлапанаВакуума;
		
		
		СТОП;		
		
		
	 }
}

ПРОЦ ЧтениеКольцевогоБуфера
{
ИЗ ПРОЦ Инициализация КодСобытия_от_ГУИ,
		ЗД_R_КАРЕТКИ,
		ЗД_W_УгловойСкорости,
		ФлагСостояниеДавления, 
		ФлагРежимКлапанаАтмосферы , 
		ФлагРежимКлапанаВакуума,
		ФлагРежимРаботыКаретки,
		ФлагРежимРаботыШпинделя,
		ФлагРаботыСветодиода,
		ФлагРежимРаботыОпоры;       
ЦЕЛ КодСобытия_от_ГИO ЛОКАЛ;

	СОСТ Начало
	 {
		/*  читаем код и параметр события из кольцевого буфера в структуру */
		ЕСЛИ (GetNextMsgGUI()) 
		{
			КодСобытия_от_ГИO = GetMsgGUICode(); 
			РАЗБОР (КодСобытия_от_ГИO) {
				СЛУЧАЙ КОМ_ВХОДНАЯ_ТЕСТ_ДАВЛЕНИЕ_В_НОРМУ:
					ФлагСостояниеДавления = ДАВЛЕНИЕ_В_НОРМЕ;
					КОНЕЦ;
				СЛУЧАЙ КОМ_ВХОДНАЯ_ТЕСТ_ДАВЛЕНИЕ_В_НИЗКОЕ:
					ФлагСостояниеДавления = ДАВЛЕНИЕ_НИЗКОЕ;
					КОНЕЦ;
				СЛУЧАЙ КОМ_ВХОДНАЯ_ТЕСТ_ДАВЛЕНИЕ_В_АВАРИНОЕ:
					ФлагСостояниеДавления = ДАВЛЕНИЕ_АВАРИЙНОЕ;
					КОНЕЦ;
				
				СЛУЧАЙ КОМ_ВХОДНАЯ_ТЕСТ_ОПОРА_ИСПРАВНА:
					ФлагРежимРаботыОпоры  = ИСПРАВНОСТЬ;
					КОНЕЦ;
				СЛУЧАЙ КОМ_ВХОДНАЯ_ТЕСТ_ОПОРА_ОТКАЗ_ВКЛЮЧЕНИЯ:
					ФлагРежимРаботыОпоры  = ОТКАЗ_ВКЛЮЧЕНИЯ;
					КОНЕЦ;
				СЛУЧАЙ КОМ_ВХОДНАЯ_ТЕСТ_ОПОРА_ОТКАЗ_ВЫКЛЮЧЕНИЯ:
					ФлагРежимРаботыОпоры  = ОТКАЗ_ВЫКЛЮЧЕНИЯ;
					КОНЕЦ;
				
				СЛУЧАЙ КОМ_ВХОДНАЯ_ТЕСТ_КАРЕТКА_ИСПРАВНА:
					ФлагРежимРаботыКаретки  = ИСПРАВНОСТЬ;
					КОНЕЦ;
				СЛУЧАЙ КОМ_ВХОДНАЯ_ТЕСТ_КАРЕТКА_ОТКАЗ_ВКЛЮЧЕНИЯ:
					ФлагРежимРаботыКаретки  = ОТКАЗ_ВКЛЮЧЕНИЯ;
					КОНЕЦ;
				СЛУЧАЙ КОМ_ВХОДНАЯ_ТЕСТ_КАРЕТКА_ОТКАЗ_ВЫКЛЮЧЕНИЯ:
					ФлагРежимРаботыКаретки  = ОТКАЗ_ВЫКЛЮЧЕНИЯ;
					КОНЕЦ;
					
				СЛУЧАЙ КОМ_ВХОДНАЯ_ТЕСТ_ШПИНДЕЛЬ_ИСПРАВЕН:
					ФлагРежимРаботыШпинделя   = ИСПРАВНОСТЬ;
					КОНЕЦ;
				СЛУЧАЙ КОМ_ВХОДНАЯ_ТЕСТ_ШПИНДЕЛЬ_ОТКАЗ_ВКЛЮЧЕНИЯ:
					ФлагРежимРаботыШпинделя   = ОТКАЗ_ВКЛЮЧЕНИЯ;
					КОНЕЦ;
				СЛУЧАЙ КОМ_ВХОДНАЯ_ТЕСТ_ШПИНДЕЛЬ_ОТКАЗ_ВЫКЛЮЧЕНИЯ:
					ФлагРежимРаботыШпинделя   = ОТКАЗ_ВЫКЛЮЧЕНИЯ;
					КОНЕЦ;	
					
				СЛУЧАЙ КОМ_ВХОДНАЯ_ТЕСТ_СВЕТОДИОД_ИСПРАВЕН:
					ФлагРаботыСветодиода    = ИСПРАВНОСТЬ;
					КОНЕЦ;
				СЛУЧАЙ КОМ_ВХОДНАЯ_ТЕСТ_СВЕТОДИОД_ОТКАЗ_ВКЛЮЧЕНИЯ:
					ФлагРаботыСветодиода    = ОТКАЗ_ВКЛЮЧЕНИЯ;
					КОНЕЦ;
				СЛУЧАЙ КОМ_ВХОДНАЯ_ТЕСТ_СВЕТОДИОД_ОТКАЗ_ВЫКЛЮЧЕНИЯ:
					ФлагРаботыСветодиода    = ОТКАЗ_ВЫКЛЮЧЕНИЯ;
					КОНЕЦ;	
					
				СЛУЧАЙ КОМ_ВХОДНАЯ_ТЕСТ_КЛАПАН_АТМОСФЕРЫ_ИСПРАВЕН:
					ФлагРежимКлапанаАтмосферы     = ИСПРАВНОСТЬ;
					КОНЕЦ;
				СЛУЧАЙ КОМ_ВХОДНАЯ_ТЕСТ_КЛАПАН_АТМОСФЕРЫ_ОТКАЗ_ВКЛЮЧЕНИЯ:
					ФлагРежимКлапанаАтмосферы     = ОТКАЗ_ВКЛЮЧЕНИЯ;
					КОНЕЦ;
				СЛУЧАЙ КОМ_ВХОДНАЯ_ТЕСТ_КЛАПАН_АТМОСФЕРЫ_ОТКАЗ_ВЫКЛЮЧЕНИЯ:
					ФлагРежимКлапанаАтмосферы     = ОТКАЗ_ВЫКЛЮЧЕНИЯ;
					КОНЕЦ;	
					
				СЛУЧАЙ КОМ_ВХОДНАЯ_ТЕСТ_КЛАПАН_ВАКУУМА_ИСПРАВЕН:
					ФлагРежимКлапанаВакуума      = ИСПРАВНОСТЬ;
					КОНЕЦ;
				СЛУЧАЙ КОМ_ВХОДНАЯ_ТЕСТ_КЛАПАН_ВАКУУМА_ОТКАЗ_ВКЛЮЧЕНИЯ:
					ФлагРежимКлапанаВакуума      = ОТКАЗ_ВКЛЮЧЕНИЯ;
					КОНЕЦ;
				СЛУЧАЙ КОМ_ВХОДНАЯ_ТЕСТ_КЛАПАН_ВАКУУМА_ОТКАЗ_ВЫКЛЮЧЕНИЯ:
					ФлагРежимКлапанаВакуума      = ОТКАЗ_ВЫКЛЮЧЕНИЯ;
					КОНЕЦ;	
									
				УМОЛЧАНИЕ:
					В СОСТ НекорректноеСобытие;
				КОНЕЦ;
				}
		}
	 }
	 СОСТ НекорректноеСобытие
	 {
		SendMsgLongParamGUI(КОМ_ВЫХОДНАЯ_ОШИБКА_КОДА_КОМАНДЫ_ОТ_ГУИ, КодСобытия_от_ГУИ);
		В СОСТ Начало;
	 }
}

ПРОЦ КОНТРОЛЬ_ДАВЛЕНИЯ_ВОЗД_ПОДУШКИ
{
ИЗ ПРОЦ Инициализация ФлагСостояниеДавления,
					К_ВОЗД_ПОД_Р_РАБОЧЕЕ,
					К_ВОЗД_ПОД_Р_НЕ_КРИТИЧЕСКОЕ;
	СОСТ Начало
	{ 
		ЕСЛИ (ФлагСостояниеДавления == ДАВЛЕНИЕ_В_НОРМЕ)
		{
			SendMsgGUICode(КОМ_ВЫХОДНАЯ_ТЕСТ_ДАВЛЕНИЕ_В_НОРМЕ);
			В СОСТ ДавлениеВНорме;
		}
		ИНАЧЕ ЕСЛИ (ФлагСостояниеДавления == ДАВЛЕНИЕ_НИЗКОЕ)
		{
			В СОСТ ДавлениеНизкое;	
			SendMsgGUICode(КОМ_ВЫХОДНАЯ_ТЕСТ_ДАВЛЕНИЕ_НИЗКОЕ);
		}
		ИНАЧЕ ЕСЛИ (ФлагСостояниеДавления == ДАВЛЕНИЕ_АВАРИЙНОЕ)
		{
			В СОСТ ДавлениеАварийное;
			SendMsgGUICode(КОМ_ВЫХОДНАЯ_ТЕСТ_ДАВЕЛЕНИЕ_АВАРИЙНОЕ);
		}			
	}
	СОСТ ДавлениеВНорме
	{
		ЕСЛИ (ФлагСостояниеДавления == ДАВЛЕНИЕ_В_НОРМЕ) 
		{
			К_ВОЗД_ПОД_Р_РАБОЧЕЕ = ВКЛ;
			К_ВОЗД_ПОД_Р_НЕ_КРИТИЧЕСКОЕ = ВКЛ;			
		}
		ИНАЧЕ В СОСТ Начало;
	}
	СОСТ ДавлениеНизкое
	{
		ЕСЛИ (ФлагСостояниеДавления == ДАВЛЕНИЕ_НИЗКОЕ)
		{
			К_ВОЗД_ПОД_Р_РАБОЧЕЕ = ВЫКЛ;
			К_ВОЗД_ПОД_Р_НЕ_КРИТИЧЕСКОЕ = ВКЛ;
		}
		ИНАЧЕ В СОСТ Начало;
	}
	СОСТ ДавлениеАварийное
	{
		ЕСЛИ (ФлагСостояниеДавления == ДАВЛЕНИЕ_АВАРИЙНОЕ)
		{
			К_ВОЗД_ПОД_Р_РАБОЧЕЕ = ВЫКЛ;
			К_ВОЗД_ПОД_Р_НЕ_КРИТИЧЕСКОЕ = ВЫКЛ;			
		}
		ИНАЧЕ В СОСТ Начало;
	}

}

/*ОПОРА*/
ПРОЦ КонтрольПодачиВоздухаНаОпору
{
ИЗ ПРОЦ Инициализация ФлагРежимРаботыОпоры,
		У_ВОЗД_ПОД_СТОЛА; 		
	СОСТ Начало
	{
		СТОП ПРОЦ ВКЛЮЧЕНИЕ_ПОДАЧИ_ВОЗДУХА_НА_ОПОРУ;
		СТОП ПРОЦ ВЫКЛЮЧЕНИЕ_ПОДАЧИ_ВОЗДУХА_НА_ОПОРУ;
		ЕСЛИ (ФлагРежимРаботыОпоры == ИСПРАВНОСТЬ) В СОСТ ЗапускИсправногоОборудования;
		ИНАЧЕ В СОСТ ИмитацияНеисправностиОборудования;
	}
	СОСТ ЗапускИсправногоОборудования
	{
		ЕСЛИ (У_ВОЗД_ПОД_СТОЛА == ВКЛ) СТАРТ ПРОЦ ВКЛЮЧЕНИЕ_ПОДАЧИ_ВОЗДУХА_НА_ОПОРУ;
		ИНАЧЕ ЕСЛИ (У_ВОЗД_ПОД_СТОЛА == ВЫКЛ) СТАРТ ПРОЦ ВЫКЛЮЧЕНИЕ_ПОДАЧИ_ВОЗДУХА_НА_ОПОРУ;
		В СОСТ ИсправностьРаботы;
	}
	СОСТ ИсправностьРаботы
	{
		ЕСЛИ (ФлагРежимРаботыОпоры != ИСПРАВНОСТЬ) В СОСТ Начало;
	}
	СОСТ ИмитацияНеисправностиОборудования
	{
		ЕСЛИ (ФлагРежимРаботыОпоры == ОТКАЗ_ВКЛЮЧЕНИЯ)  СТАРТ ПРОЦ ВЫКЛЮЧЕНИЕ_ПОДАЧИ_ВОЗДУХА_НА_ОПОРУ;
		ИНАЧЕ ЕСЛИ (ФлагРежимРаботыОпоры == ОТКАЗ_ВЫКЛЮЧЕНИЯ) СТАРТ ПРОЦ ВКЛЮЧЕНИЕ_ПОДАЧИ_ВОЗДУХА_НА_ОПОРУ;
		В СОСТ Неисправность;
	}
	СОСТ Неисправность
	{
		ЕСЛИ (ФлагРежимРаботыОпоры != ОТКАЗ_ВКЛЮЧЕНИЯ
			&& ФлагРежимРаботыОпоры != ОТКАЗ_ВЫКЛЮЧЕНИЯ) В СОСТ Начало;
	}	
}


ПРОЦ ВКЛЮЧЕНИЕ_ПОДАЧИ_ВОЗДУХА_НА_ОПОРУ
{
	СОСТ Начало
	{
		ТАЙМАУТ ТАЙМАУТ_ВОЗД_ПОДУШКИ В СЛЕДУЮЩЕЕ;
	}
	СОСТ ОтправкаСообщения
	{
		SendMsgGUICode(КОМ_ВЫХОДНАЯ_ТЕСТ_ВОЗДУХ_ПОДАН_НА_ОПОРУ);
		В СЛЕДУЮЩЕЕ;
	}
	СОСТ РаботаВоздушнойПодушкиНаОпоре 
	{
		ЕСЛИ ( ПРОЦ КОНТРОЛЬ_ДАВЛЕНИЯ_ВОЗД_ПОДУШКИ В СОСТ ДавлениеАварийное)
		{
			SendMsgGUICode(КОМ_ВЫХОДНАЯ_ТЕСТ_НА_ОПОРЕ_СТОЛА_АВАРИЙНОЕ_ДАВЛЕНИЕ);
			В СОСТ ОжиданиеНормльногоДавления;
		}
		ИНАЧЕ ЗАЦИКЛИТЬ; 
	}
	СОСТ ОжиданиеНормльногоДавления
	{
	ЕСЛИ ( !(ПРОЦ КОНТРОЛЬ_ДАВЛЕНИЯ_ВОЗД_ПОДУШКИ В СОСТ ДавлениеАварийное))
		В СОСТ Начало;
	}
}

ПРОЦ ВЫКЛЮЧЕНИЕ_ПОДАЧИ_ВОЗДУХА_НА_ОПОРУ
{
	СОСТ Начало
	{
		ТАЙМАУТ ТАЙМАУТ_ВОЗД_ПОДУШКИ В СЛЕДУЮЩЕЕ;
	}
	СОСТ ОтправкаСообщения
	{
		SendMsgGUICode(КОМ_ВЫХОДНАЯ_ТЕСТ_ВЫКЛЮЧЕНА_ПОДАЧА_ВОЗДУХА_НА_ОПОРУ);
		В СЛЕДУЮЩЕЕ;
	}
	СОСТ ВоздушнойПодушкиНаОпореНет { ЗАЦИКЛИТЬ; }
}

/*КАРЕТКА*/
ПРОЦ КонтрольПодачиВоздухаНаКаретку
{
ИЗ ПРОЦ Инициализация  ФлагРежимРаботыКаретки, У_ВОЗД_ПОД_КАРЕТКИ;
	СОСТ Начало
	{
		СТОП ПРОЦ ВКЛЮЧЕНИЕ_ПОДАЧИ_ВОЗДУХА_НА_КАРЕТКУ;
		СТОП ПРОЦ ВЫКЛЮЧЕНИЕ_ПОДАЧИ_ВОЗДУХА_НА_КАРЕТКУ;
		ЕСЛИ (ФлагРежимРаботыКаретки== ИСПРАВНОСТЬ) В СОСТ ЗапускИсправногоОборудования;
		ИНАЧЕ В СОСТ ИмитацияНеисправностиОборудования;
	}
	СОСТ ЗапускИсправногоОборудования
	{
		ЕСЛИ (У_ВОЗД_ПОД_КАРЕТКИ == ВКЛ) СТАРТ ПРОЦ ВКЛЮЧЕНИЕ_ПОДАЧИ_ВОЗДУХА_НА_КАРЕТКУ;
		ИНАЧЕ ЕСЛИ (У_ВОЗД_ПОД_КАРЕТКИ == ВЫКЛ) СТАРТ ПРОЦ ВЫКЛЮЧЕНИЕ_ПОДАЧИ_ВОЗДУХА_НА_КАРЕТКУ;
		В СОСТ ИсправностьРаботы;
	}
	СОСТ ИсправностьРаботы
	{
		ЕСЛИ (ФлагРежимРаботыКаретки != ИСПРАВНОСТЬ) В СОСТ Начало;
	}
	СОСТ ИмитацияНеисправностиОборудования
	{
		ЕСЛИ (ФлагРежимРаботыКаретки == ОТКАЗ_ВКЛЮЧЕНИЯ)  СТАРТ ПРОЦ ВЫКЛЮЧЕНИЕ_ПОДАЧИ_ВОЗДУХА_НА_КАРЕТКУ;
		ИНАЧЕ ЕСЛИ (ФлагРежимРаботыКаретки == ОТКАЗ_ВЫКЛЮЧЕНИЯ) СТАРТ ПРОЦ ВКЛЮЧЕНИЕ_ПОДАЧИ_ВОЗДУХА_НА_КАРЕТКУ;
		В СОСТ Неисправность;
	}
	СОСТ Неисправность
	{
		ЕСЛИ (ФлагРежимРаботыКаретки != ОТКАЗ_ВКЛЮЧЕНИЯ 
			&& ФлагРежимРаботыКаретки != ОТКАЗ_ВЫКЛЮЧЕНИЯ) В СОСТ Начало;
	}	
}


ПРОЦ ВКЛЮЧЕНИЕ_ПОДАЧИ_ВОЗДУХА_НА_КАРЕТКУ
{
	СОСТ Начало
	{
		ТАЙМАУТ ТАЙМАУТ_ВОЗД_ПОДУШКИ В СЛЕДУЮЩЕЕ;
	}
	СОСТ ОтправкаСообщения
	{
		SendMsgGUICode(КОМ_ВЫХОДНАЯ_ТЕСТ_ВОЗДУХ_ПОДАН_НА_КАРЕТКУ);
		В СЛЕДУЮЩЕЕ;
	}
	СОСТ РаботаВоздушнойПодушкиНаКаретке 
	{
		ЕСЛИ ( ПРОЦ КОНТРОЛЬ_ДАВЛЕНИЯ_ВОЗД_ПОДУШКИ В СОСТ ДавлениеАварийное)
		{
			SendMsgGUICode(КОМ_ВЫХОДНАЯ_ТЕСТ_НА_КАРЕТКЕ_АВАРИЙНОЕ_ДАВЛЕНИЕ);
			В СОСТ ОжиданиеНормльногоДавления;
		}
		ИНАЧЕ ЗАЦИКЛИТЬ; 
	}
	СОСТ ОжиданиеНормльногоДавления
	{
	ЕСЛИ ( !(ПРОЦ КОНТРОЛЬ_ДАВЛЕНИЯ_ВОЗД_ПОДУШКИ В СОСТ ДавлениеАварийное))
		В СОСТ Начало;
	}
}

ПРОЦ ВЫКЛЮЧЕНИЕ_ПОДАЧИ_ВОЗДУХА_НА_КАРЕТКУ
{
	СОСТ Начало
	{
		ТАЙМАУТ ТАЙМАУТ_ВОЗД_ПОДУШКИ В СЛЕДУЮЩЕЕ;
	}
	СОСТ ОтправкаСообщения
	{
		SendMsgGUICode(КОМ_ВЫХОДНАЯ_ТЕСТ_ВЫКЛЮЧЕНА_ПОДАЧА_ВОЗДУХА_НА_КАРЕТКУ);
		В СЛЕДУЮЩЕЕ;
	}
	СОСТ ВоздушнойПодушкиНаКареткеНет { ЗАЦИКЛИТЬ; }
}

/*ШПИНДЕЛЬ*/
ПРОЦ КонтрольПодачиВоздухаНаШпиндель
{
ИЗ ПРОЦ Инициализация ФлагРежимРаботыШпинделя, У_ВОЗД_ПОД_КАРЕТКИ; 
	СОСТ Начало
	{
		СТОП ПРОЦ ВКЛЮЧЕНИЕ_ПОДАЧИ_ВОЗДУХА_НА_ШПИНДЕЛЬ;
		СТОП ПРОЦ ВЫКЛЮЧЕНИЕ_ПОДАЧИ_ВОЗДУХА_НА_ШПИНДЕЛЬ;
		ЕСЛИ (ФлагРежимРаботыШпинделя== ИСПРАВНОСТЬ) В СОСТ ЗапускИсправногоОборудования;
		ИНАЧЕ В СОСТ ИмитацияНеисправностиОборудования;
	}
	СОСТ ЗапускИсправногоОборудования
	{
		ЕСЛИ (У_ВОЗД_ПОД_КАРЕТКИ == ВКЛ) СТАРТ ПРОЦ ВКЛЮЧЕНИЕ_ПОДАЧИ_ВОЗДУХА_НА_ШПИНДЕЛЬ;
		ИНАЧЕ ЕСЛИ (У_ВОЗД_ПОД_КАРЕТКИ == ВЫКЛ) СТАРТ ПРОЦ ВЫКЛЮЧЕНИЕ_ПОДАЧИ_ВОЗДУХА_НА_ШПИНДЕЛЬ;
		В СОСТ ИсправностьРаботы;
	}
	СОСТ ИсправностьРаботы
	{
		ЕСЛИ (ФлагРежимРаботыШпинделя != ИСПРАВНОСТЬ) В СОСТ Начало;
	}
	СОСТ ИмитацияНеисправностиОборудования
	{
		ЕСЛИ (ФлагРежимРаботыШпинделя == ОТКАЗ_ВКЛЮЧЕНИЯ)  СТАРТ ПРОЦ ВЫКЛЮЧЕНИЕ_ПОДАЧИ_ВОЗДУХА_НА_ШПИНДЕЛЬ;
		ИНАЧЕ ЕСЛИ (ФлагРежимРаботыШпинделя == ОТКАЗ_ВЫКЛЮЧЕНИЯ) СТАРТ ПРОЦ ВКЛЮЧЕНИЕ_ПОДАЧИ_ВОЗДУХА_НА_ШПИНДЕЛЬ;
		В СОСТ Неисправность;
	}
	СОСТ Неисправность
	{
		ЕСЛИ (ФлагРежимРаботыШпинделя != ОТКАЗ_ВКЛЮЧЕНИЯ 
			&& ФлагРежимРаботыШпинделя != ОТКАЗ_ВЫКЛЮЧЕНИЯ) В СОСТ Начало;
	}	
}

ПРОЦ ВКЛЮЧЕНИЕ_ПОДАЧИ_ВОЗДУХА_НА_ШПИНДЕЛЬ
{
	СОСТ Начало
	{
		ТАЙМАУТ ТАЙМАУТ_ВОЗД_ПОДУШКИ В СЛЕДУЮЩЕЕ;
	}
	СОСТ ОтправкаСообщений
	{
		SendMsgGUICode(КОМ_ВЫХОДНАЯ_ТЕСТ_ВОЗДУХ_ПОДАН_НА_ШПИНДЕЛЬ);
		ЕСЛИ (ПРОЦ ВКЛЮЧЕНИЕ_ПОДАЧИ_ВОЗДУХА_НА_КАРЕТКУ В СОСТ РаботаВоздушнойПодушкиНаКаретке)
			SendMsgGUICode(КОМ_ВЫХОДНАЯ_ТЕСТ_ПРЕДУПРЕЖДЕНИЕ_ВОЗДУХ_ПОДАН_НА_ШПИНДЕЛЬ_НА_КАРЕТКЕ_НЕТ);
		ЕСЛИ (ПРОЦ ВКЛЮЧЕНИЕ_ПОДАЧИ_ВОЗДУХА_НА_ОПОРУ В СОСТ РаботаВоздушнойПодушкиНаОпоре)
			SendMsgGUICode(КОМ_ВЫХОДНАЯ_ТЕСТ_ПРЕДУПРЕЖДЕНИЕ_ВОЗДУХ_ПОДАН_НА_ШПИНДЕЛЬ_НА_ОПОРЕ_НЕТ);
		В СЛЕДУЮЩЕЕ;
	}
	СОСТ РаботаВоздушнойПодушкиНаШпнделе
	{
		ЕСЛИ ( ПРОЦ КОНТРОЛЬ_ДАВЛЕНИЯ_ВОЗД_ПОДУШКИ В СОСТ ДавлениеАварийное)
		{
			SendMsgGUICode(КОМ_ВЫХОДНАЯ_ТЕСТ_НА_ШПИНДЕЛЕ_АВАРИЙНОЕ_ДАВЛЕНИЕ);
			В СОСТ ОжиданиеНормльногоДавления;
		}
		ИНАЧЕ ЗАЦИКЛИТЬ; 
	}
	СОСТ ОжиданиеНормльногоДавления
	{
	ЕСЛИ ( !(ПРОЦ КОНТРОЛЬ_ДАВЛЕНИЯ_ВОЗД_ПОДУШКИ В СОСТ ДавлениеАварийное))
		В СОСТ Начало;
	}
}

ПРОЦ ВЫКЛЮЧЕНИЕ_ПОДАЧИ_ВОЗДУХА_НА_ШПИНДЕЛЬ
{
	СОСТ Начало
	{
		ТАЙМАУТ ТАЙМАУТ_ВОЗД_ПОДУШКИ В СЛЕДУЮЩЕЕ;
	}
	СОСТ ОтправкаСообщения
	{
		SendMsgGUICode(КОМ_ВЫХОДНАЯ_ТЕСТ_ВЫКЛЮЧЕНА_ПОДАЧА_ВОЗДУХА_НА_ШПИНДЕЛЬ);
		В СЛЕДУЮЩЕЕ;
	}
	СОСТ ВоздушнойПодушкиНаШпинделеНет { ЗАЦИКЛИТЬ; }
}

/*РАБОТА С ПИТАНИЕМ ЛАЗЕРА*/
ПРОЦ УправлениеПитаниемЛазера
{
ИЗ ПРОЦ Инициализация У_ПИТАНИЕ_ЛАЗЕРА; 
	СОСТ Начало
	{
		ЕСЛИ (У_ПИТАНИЕ_ЛАЗЕРА == ВКЛ)
			{ 
				SendMsgGUICode(КОМ_ВЫХОДНАЯ_ТЕСТ_ПИТАНИЕ_ПОДАНО_НА_ЛАЗЕР);
				В СОСТ ПитаниеПоданоНаЛазер;
			}
		ИНАЧЕ 
		{
			SendMsgGUICode(КОМ_ВЫХОДНАЯ_ТЕСТ_ПИТАНИЕ_НА_ЛАЗЕРЕ_НЕТ);
			В СОСТ ПитанияНаЛазереНет;
		}
		
	}
	СОСТ ПитаниеПоданоНаЛазер
	{
		ЕСЛИ (У_ПИТАНИЕ_ЛАЗЕРА != ВКЛ) В СОСТ Начало;
	}
	СОСТ ПитанияНаЛазереНет
	{
		ЕСЛИ (У_ПИТАНИЕ_ЛАЗЕРА != ВЫКЛ) В СОСТ Начало;
	}

}

ПРОЦ УПРАВЛЕНИЕ_СВЕТОДИОДОМ
{
ИЗ ПРОЦ Инициализация ФлагРаботыСветодиода,У_ВКЛ_СВЕТОДИОДА;
	СОСТ Начало
	{
		ЕСЛИ (ФлагРаботыСветодиода == ИСПРАВНОСТЬ )
			ЕСЛИ (У_ВКЛ_СВЕТОДИОДА == ВКЛ) 
			{
				SendMsgGUICode(КОМ_ВЫХОДНАЯ_ТЕСТ_СВЕТОДИОД_ВКЛЮЧЕН);
				В СОСТ СветодиодИсправенИВключен;
			}
			ИНАЧЕ  
			{	
				SendMsgGUICode(КОМ_ВЫХОДНАЯ_ТЕСТ_СВЕТОДИОД_ВЫКЛЮЧЕН);
				В СОСТ СветодиодИсправенИВыключен;
			}
		ИНАЧЕ ЕСЛИ (ФлагРаботыСветодиода == ОТКАЗ_ВКЛЮЧЕНИЯ ) 
			{
				SendMsgGUICode(КОМ_ВЫХОДНАЯ_ТЕСТ_СВЕТОДИОД_ОТКАЗ_ВКЛЮЧЕНИЯ);
				В СОСТ СветодиодНЕИсправенИВыключен;
			}
		ИНАЧЕ ЕСЛИ (ФлагРаботыСветодиода == ОТКАЗ_ВЫКЛЮЧЕНИЯ)
		{
			SendMsgGUICode(КОМ_ВЫХОДНАЯ_ТЕСТ_СВЕТОДИОД_ОТКАЗ_ВЫКЛЮЧЕНИЯ);
			В СОСТ СветодиодНЕИсправенИВключен;
		}
	}
	СОСТ СветодиодИсправенИВключен 
	{
		ЕСЛИ (ФлагРаботыСветодиода != ИСПРАВНОСТЬ ||  У_ВКЛ_СВЕТОДИОДА != ВКЛ) В СОСТ Начало;
	}
	СОСТ СветодиодИсправенИВыключен
	{
		ЕСЛИ (ФлагРаботыСветодиода != ИСПРАВНОСТЬ ||  У_ВКЛ_СВЕТОДИОДА != ВЫКЛ) В СОСТ Начало;
	}
	СОСТ СветодиодНЕИсправенИВыключен
	{
		ЕСЛИ (ФлагРаботыСветодиода != ОТКАЗ_ВКЛЮЧЕНИЯ ) В СОСТ Начало;
	}
	СОСТ СветодиодНЕИсправенИВключен
	{
		ЕСЛИ (ФлагРаботыСветодиода != ОТКАЗ_ВЫКЛЮЧЕНИЯ ) В СОСТ Начало;
	}
	
}


ПРОЦ УПРАВЛЕНИЕ_НАСОСОМ
{
ИЗ ПРОЦ Инициализация У_ВКЛ_НАСОС;
	СОСТ Начало
	{
		ЕСЛИ (У_ВКЛ_НАСОС == ВЫКЛ)
		{
			SendMsgGUICode(КОМ_ВЫХОДНАЯ_ТЕСТ_НАСОС_ВЫКЛЮЧЕН);
			В СОСТ НасосВыключен;
		}
		ИНАЧЕ ЕСЛИ (У_ВКЛ_НАСОС == ВКЛ)
		{
			SendMsgGUICode(КОМ_ВЫХОДНАЯ_ТЕСТ_КЛАПАН_НАСОС_ВКЛЮЧЕН);
			В СОСТ НасосВключен;
		}	
	}
	СОСТ НасосВыключен
	{
		ЕСЛИ (У_ВКЛ_НАСОС != ВЫКЛ) В СОСТ Начало;
	}
	СОСТ НасосВключен
	{
		ЕСЛИ (У_ВКЛ_НАСОС != ВКЛ) В СОСТ Начало;
	}
}

/*КЛАПАН АТМОСФЕРЫ*/
ПРОЦ КонтрольКлапанаАтмосферы
{
ИЗ ПРОЦ Инициализация ФлагРежимКлапанаАтмосферы, У_РАЗЖИМ_ДИСКА;
	СОСТ Начало
	{
		СТОП ПРОЦ ОТКРЫТИЕ_КЛАПАНА_АТМОСФЕРЫ;
		СТОП ПРОЦ ЗАКРЫТИЕ_КЛАПАНА_АТМОСФЕРЫ;
		ЕСЛИ (ФлагРежимКлапанаАтмосферы == ИСПРАВНОСТЬ) В СОСТ ЗапускИсправногоОборудования;
		ИНАЧЕ В СОСТ ИмитацияНеисправностиОборудования;
	}
	СОСТ ЗапускИсправногоОборудования
	{
		ЕСЛИ (У_РАЗЖИМ_ДИСКА == ВКЛ) СТАРТ ПРОЦ ОТКРЫТИЕ_КЛАПАНА_АТМОСФЕРЫ;
		ИНАЧЕ ЕСЛИ (У_РАЗЖИМ_ДИСКА == ВЫКЛ) СТАРТ ПРОЦ ЗАКРЫТИЕ_КЛАПАНА_АТМОСФЕРЫ;
		В СОСТ ИсправностьРаботы;
	}
	СОСТ ИсправностьРаботы
	{
		ЕСЛИ (ФлагРежимКлапанаАтмосферы != ИСПРАВНОСТЬ) В СОСТ Начало;
	}
	СОСТ ИмитацияНеисправностиОборудования
	{
		ЕСЛИ (ФлагРежимКлапанаАтмосферы == ОТКАЗ_ВКЛЮЧЕНИЯ)  СТАРТ ПРОЦ ЗАКРЫТИЕ_КЛАПАНА_АТМОСФЕРЫ;
		ИНАЧЕ ЕСЛИ (ФлагРежимКлапанаАтмосферы == ОТКАЗ_ВЫКЛЮЧЕНИЯ) СТАРТ ПРОЦ ОТКРЫТИЕ_КЛАПАНА_АТМОСФЕРЫ;
		В СОСТ Неисправность;
	}
	СОСТ Неисправность
	{
		ЕСЛИ (ФлагРежимКлапанаАтмосферы != ОТКАЗ_ВКЛЮЧЕНИЯ 
			&& ФлагРежимКлапанаАтмосферы != ОТКАЗ_ВЫКЛЮЧЕНИЯ) В СОСТ Начало;
	}	
}


ПРОЦ ОТКРЫТИЕ_КЛАПАНА_АТМОСФЕРЫ
{
	СОСТ Начало
	{
		ТАЙМАУТ ТАЙМАУТ_КЛАПАНОВ В СЛЕДУЮЩЕЕ;
	}
	СОСТ ОтправкаСообщения
	{
		SendMsgGUICode(КОМ_ВЫХОДНАЯ_ТЕСТ_КЛАПАН_АТМОСФЕРЫ_ОТКРЫТ);
		В СЛЕДУЮЩЕЕ;
	}
	СОСТ КлапанАтмосферыОткрыт 
	{
		ЗАЦИКЛИТЬ; 
	}
}

ПРОЦ ЗАКРЫТИЕ_КЛАПАНА_АТМОСФЕРЫ
{
	СОСТ Начало
	{
		ТАЙМАУТ ТАЙМАУТ_КЛАПАНОВ В СЛЕДУЮЩЕЕ;
	}
	СОСТ ОтправкаСообщения
	{
		SendMsgGUICode(КОМ_ВЫХОДНАЯ_ТЕСТ_КЛАПАН_АТМОСФЕРЫ_ЗАКРЫТ);
		В СЛЕДУЮЩЕЕ;
	}
	СОСТ КлапанЗакрыт 
	{ 
		ЗАЦИКЛИТЬ; 
	}
}

/*Контроль отжатияя-разжатия диска*/
ПРОЦ КонтрольРазжатияЗажатияДиска
{
ИЗ ПРОЦ Инициализация К_ДИСК_РАЗЖАТ;
	СОСТ Начало
	{
		ЕСЛИ (ПРОЦ ОТКРЫТИЕ_КЛАПАНА_ВАКУУМА В СОСТ КлапанВакуумаОткрыт
			&& ПРОЦ ОТКРЫТИЕ_КЛАПАНА_АТМОСФЕРЫ В СОСТ КлапанАтмосферыОткрыт)
		{
			К_ДИСК_РАЗЖАТ = ВКЛ;
			SendMsgGUICode(КОМ_ВЫХОДНАЯ_ТЕСТ_ОБА_КЛАПАНА_ОТКРЫТЫ_ДИСК_РАЗАЖАТ);
			В СОСТ ДискРазжатКлапанаОткрыты;
		}
		ИНАЧЕ ЕСЛИ (ПРОЦ ОТКРЫТИЕ_КЛАПАНА_ВАКУУМА В СОСТ КлапанВакуумаОткрыт 
					&& ПРОЦ ЗАКРЫТИЕ_КЛАПАНА_АТМОСФЕРЫ В СОСТ КлапанЗакрыт)
		{
		
		//СПРОСИТЬ
		// Насос должен быть включен!  Он должен все время работать или нет?
		// Влияет ли давление на шпинделе на разжатие-отжатие?
		// забыла написать АВАРИЯ при резком падении давления 
			ЕСЛИ (! (ПРОЦ КОНТРОЛЬ_ДАВЛЕНИЯ_ВОЗД_ПОДУШКИ В СОСТ ДавлениеАварийное)
					&& ПРОЦ ВКЛЮЧЕНИЕ_ПОДАЧИ_ВОЗДУХА_НА_ШПИНДЕЛЬ В СОСТ РаботаВоздушнойПодушкиНаШпнделе)
					{
						К_ДИСК_РАЗЖАТ = ВЫКЛ;
						SendMsgGUICode(КОМ_ВЫХОДНАЯ_ТЕСТ_ВАКУУМ_ОТКРЫТ_АТМОСФЕРА_ЗАКРЫТ_ДИСК_ЗАЖАТ);
						В СОСТ ДискЗажатВакуумОткрытАтмсфераЗакрыт;
					}
			ИНАЧЕ ЕСЛИ (ПРОЦ КОНТРОЛЬ_ДАВЛЕНИЯ_ВОЗД_ПОДУШКИ В СОСТ ДавлениеАварийное
						|| ПРОЦ ВЫКЛЮЧЕНИЕ_ПОДАЧИ_ВОЗДУХА_НА_ШПИНДЕЛЬ В СОСТ ВоздушнойПодушкиНаШпинделеНет)
			{
				К_ДИСК_РАЗЖАТ = ВКЛ;
				SendMsgGUICode(КОМ_ВЫХОДНАЯ_ТЕСТ_ВАКУУМ_ОТКРЫТ_АТМОСФЕРА_ЗАКРЫТ_ДИСК_РАЗЖАТ);
				В СОСТ ДискРазажатВакуумОткрытАтмсфераЗакрыт;
			}			
		}
		ИНАЧЕ ЕСЛИ (ПРОЦ ЗАКРЫТИЕ_КЛАПАНА_ВАКУУМА В СОСТ КлапанЗакрыт 
					&& ПРОЦ ОТКРЫТИЕ_КЛАПАНА_АТМОСФЕРЫ В СОСТ КлапанАтмосферыОткрыт)
		{
			К_ДИСК_РАЗЖАТ = ВКЛ;
			SendMsgGUICode(КОМ_ВЫХОДНАЯ_ТЕСТ_ВАКУУМ_ЗАКРЫТ_АТМОСФЕРА_ОТКРЫТ_ДИСК_РАЗАЖАТ);
			В СОСТ ДискРазжатАтмосфераОткрытВакуумЗакрыт;
		}
		ИНАЧЕ ЕСЛИ ( ПРОЦ ЗАКРЫТИЕ_КЛАПАНА_АТМОСФЕРЫ В СОСТ КлапанЗакрыт 
					&& ПРОЦ ЗАКРЫТИЕ_КЛАПАНА_ВАКУУМА В СОСТ КлапанЗакрыт)
		{
			ЕСЛИ (! (ПРОЦ КОНТРОЛЬ_ДАВЛЕНИЯ_ВОЗД_ПОДУШКИ В СОСТ ДавлениеАварийное)
					&& ПРОЦ ВКЛЮЧЕНИЕ_ПОДАЧИ_ВОЗДУХА_НА_ШПИНДЕЛЬ В СОСТ РаботаВоздушнойПодушкиНаШпнделе)
			{
						К_ДИСК_РАЗЖАТ = ВЫКЛ;
						SendMsgGUICode(КОМ_ВЫХОДНАЯ_ТЕСТ_ВАКУУМ_ЗАКРЫТ_АТМОСФЕРА_ЗАКРЫТ_ДИСК_ЗАЖАТ);
						В СОСТ ДискЗажатВакуумЗакрытАтмсфераЗакрыт;
			}
			ИНАЧЕ ЕСЛИ (ПРОЦ КОНТРОЛЬ_ДАВЛЕНИЯ_ВОЗД_ПОДУШКИ В СОСТ ДавлениеАварийное
						|| ПРОЦ ВЫКЛЮЧЕНИЕ_ПОДАЧИ_ВОЗДУХА_НА_ШПИНДЕЛЬ В СОСТ ВоздушнойПодушкиНаШпинделеНет)
			{
				К_ДИСК_РАЗЖАТ = ВКЛ;
				SendMsgGUICode(КОМ_ВЫХОДНАЯ_ТЕСТ_ВАКУУМ_ЗАКРЫТ_АТМОСФЕРА_ЗАКРЫТ_ДИСК_РАЗЖАТ);
				В СОСТ ДискРазажатВакуумЗакрытАтмсфераЗакрыт;
			}		
		}
		
	}
	СОСТ ДискРазжатКлапанаОткрыты
	{
		ЕСЛИ ( !( ПРОЦ ОТКРЫТИЕ_КЛАПАНА_ВАКУУМА В СОСТ КлапанВакуумаОткрыт)
		||	 !(ПРОЦ ОТКРЫТИЕ_КЛАПАНА_АТМОСФЕРЫ В СОСТ КлапанАтмосферыОткрыт))
		В СОСТ Начало;
	}
	СОСТ ДискРазжатАтмосфераОткрытВакуумЗакрыт
	{
		ЕСЛИ ( !(ПРОЦ ЗАКРЫТИЕ_КЛАПАНА_ВАКУУМА В СОСТ КлапанЗакрыт)
		||	 !(ПРОЦ ОТКРЫТИЕ_КЛАПАНА_АТМОСФЕРЫ В СОСТ КлапанАтмосферыОткрыт))
		В СОСТ Начало;
	}
	СОСТ ДискЗажатВакуумОткрытАтмсфераЗакрыт
	{
		ЕСЛИ ( !( ПРОЦ ОТКРЫТИЕ_КЛАПАНА_ВАКУУМА В СОСТ КлапанВакуумаОткрыт) 
				|| ! (ПРОЦ ЗАКРЫТИЕ_КЛАПАНА_АТМОСФЕРЫ В СОСТ КлапанЗакрыт)
				|| (ПРОЦ КОНТРОЛЬ_ДАВЛЕНИЯ_ВОЗД_ПОДУШКИ В СОСТ ДавлениеАварийное)
				|| ПРОЦ ВКЛЮЧЕНИЕ_ПОДАЧИ_ВОЗДУХА_НА_ШПИНДЕЛЬ В СОСТ РаботаВоздушнойПодушкиНаШпнделе)
				В СОСТ Начало;
	}
	СОСТ ДискРазажатВакуумОткрытАтмсфераЗакрыт
	{
		ЕСЛИ ( !( ПРОЦ ОТКРЫТИЕ_КЛАПАНА_ВАКУУМА В СОСТ КлапанВакуумаОткрыт) 
				|| ! (ПРОЦ ЗАКРЫТИЕ_КЛАПАНА_АТМОСФЕРЫ В СОСТ КлапанЗакрыт)
				|| !(ПРОЦ КОНТРОЛЬ_ДАВЛЕНИЯ_ВОЗД_ПОДУШКИ В СОСТ ДавлениеАварийное)
				|| !(ПРОЦ ВКЛЮЧЕНИЕ_ПОДАЧИ_ВОЗДУХА_НА_ШПИНДЕЛЬ В СОСТ РаботаВоздушнойПодушкиНаШпнделе))
				В СОСТ Начало;
	}
	СОСТ ДискЗажатВакуумЗакрытАтмсфераЗакрыт
	{
		ЕСЛИ (!(ПРОЦ ЗАКРЫТИЕ_КЛАПАНА_АТМОСФЕРЫ В СОСТ КлапанЗакрыт) 
				|| !( ПРОЦ ЗАКРЫТИЕ_КЛАПАНА_ВАКУУМА В СОСТ КлапанЗакрыт)
				|| !(ПРОЦ ВКЛЮЧЕНИЕ_ПОДАЧИ_ВОЗДУХА_НА_ШПИНДЕЛЬ В СОСТ РаботаВоздушнойПодушкиНаШпнделе))
				В СОСТ Начало;
	}
	СОСТ ДискРазажатВакуумЗакрытАтмсфераЗакрыт
	{
			ЕСЛИ (!(ПРОЦ ЗАКРЫТИЕ_КЛАПАНА_АТМОСФЕРЫ В СОСТ КлапанЗакрыт) 
				|| !( ПРОЦ ЗАКРЫТИЕ_КЛАПАНА_ВАКУУМА В СОСТ КлапанЗакрыт)
				|| (ПРОЦ ВКЛЮЧЕНИЕ_ПОДАЧИ_ВОЗДУХА_НА_ШПИНДЕЛЬ В СОСТ РаботаВоздушнойПодушкиНаШпнделе))
				В СОСТ Начало;
	}
}
/*КЛАПАН ВАКУУМА*/
ПРОЦ КонтрольКлапанаВакуума
{
ИЗ ПРОЦ Инициализация ФлагРежимКлапанаВакуума, У_РАЗЖИМ_ДИСКА;
	СОСТ Начало
	{
		СТОП ПРОЦ ОТКРЫТИЕ_КЛАПАНА_ВАКУУМА;
		СТОП ПРОЦ ЗАКРЫТИЕ_КЛАПАНА_ВАКУУМА;
		ЕСЛИ (ФлагРежимКлапанаВакуума == ИСПРАВНОСТЬ) В СОСТ ЗапускИсправногоОборудования;
		ИНАЧЕ В СОСТ ИмитацияНеисправностиОборудования;
	}
	СОСТ ЗапускИсправногоОборудования
	{
		ЕСЛИ (У_РАЗЖИМ_ДИСКА == ВКЛ) СТАРТ ПРОЦ ОТКРЫТИЕ_КЛАПАНА_ВАКУУМА;
		ИНАЧЕ ЕСЛИ (У_РАЗЖИМ_ДИСКА == ВЫКЛ) СТАРТ ПРОЦ ЗАКРЫТИЕ_КЛАПАНА_ВАКУУМА;
		В СОСТ ИсправностьРаботы;
	}
	СОСТ ИсправностьРаботы
	{
		ЕСЛИ (ФлагРежимКлапанаВакуума != ИСПРАВНОСТЬ) В СОСТ Начало;
	}
	СОСТ ИмитацияНеисправностиОборудования
	{
		ЕСЛИ (ФлагРежимКлапанаВакуума == ОТКАЗ_ВКЛЮЧЕНИЯ)  СТАРТ ПРОЦ ЗАКРЫТИЕ_КЛАПАНА_ВАКУУМА;
		ИНАЧЕ ЕСЛИ (ФлагРежимКлапанаВакуума == ОТКАЗ_ВЫКЛЮЧЕНИЯ) СТАРТ ПРОЦ ОТКРЫТИЕ_КЛАПАНА_ВАКУУМА;
		В СОСТ Неисправность;
	}
	СОСТ Неисправность
	{
		ЕСЛИ (ФлагРежимКлапанаВакуума != ОТКАЗ_ВКЛЮЧЕНИЯ 
			&& ФлагРежимКлапанаВакуума != ОТКАЗ_ВЫКЛЮЧЕНИЯ) В СОСТ Начало;
	}	
}


ПРОЦ ОТКРЫТИЕ_КЛАПАНА_ВАКУУМА
{
ИЗ ПРОЦ Инициализация К_ДИСК_РАЗЖАТ;
	СОСТ Начало
	{
		ТАЙМАУТ ТАЙМАУТ_КЛАПАНОВ В СЛЕДУЮЩЕЕ;
	}
	СОСТ ОтправкаСообщения
	{
		SendMsgGUICode(КОМ_ВЫХОДНАЯ_ТЕСТ_КЛАПАН_ВАКУУМА_ОТКРЫТ);
		В СЛЕДУЮЩЕЕ;
	}
	СОСТ КлапанВакуумаОткрыт 
	{
		К_ДИСК_РАЗЖАТ = ВКЛ;
		ЗАЦИКЛИТЬ; 
	}
}

ПРОЦ ЗАКРЫТИЕ_КЛАПАНА_ВАКУУМА
{
ИЗ ПРОЦ Инициализация К_ДИСК_РАЗЖАТ;
	СОСТ Начало
	{
		ТАЙМАУТ ТАЙМАУТ_КЛАПАНОВ В СЛЕДУЮЩЕЕ;
	}
	СОСТ ОтправкаСообщения
	{
		SendMsgGUICode(КОМ_ВЫХОДНАЯ_ТЕСТ_КЛАПАН_ВАКУУМА_ЗАКРЫТ);
		В СЛЕДУЮЩЕЕ;
	}
	СОСТ КлапанЗакрыт 
	{ 
		К_ДИСК_РАЗЖАТ = ВКЛ;
		ЗАЦИКЛИТЬ; 
	}
}

}
