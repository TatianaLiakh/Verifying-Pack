                        Прогр sapun { 


/*###########################################################*/
/*#########                     #############################*/
/*#########  ОПИСАНИЕ КОНСТАНТ  #############################*/
/*#########                     #############################*/
/*###########################################################*/
/* ?????????????? Таймауты взяты с потолка */
ТАКТ     50;    /* такт равен 500 миллисекундам */
КОНСТ Т0_1                       0;  /* Константа обеспечивающая паузу в 0.5 сек */
КОНСТ ОДНА_СЕКУНДА               2;  /* Константа обеспечивающая паузу в 1 сек */
КОНСТ ДВЕ_СЕКУНДЫ             2*ОДНА_СЕКУНДА;
КОНСТ ТРИ_СЕКУНДЫ             3*ОДНА_СЕКУНДА;
КОНСТ ЗАДЕРЖКА_НА_ПОЛУЧЕНИЕ_ДОСТ_ДАННЫХ_ОТ_ОБОРУДОВАНИЯ ТРИ_СЕКУНДЫ;
КОНСТ ПЯТЬ_СЕКУНД             5*ОДНА_СЕКУНДА;
КОНСТ ДЕСЯТЬ_СЕКУНД          10*ОДНА_СЕКУНДА;
КОНСТ ДВАДЦАТЬ_СЕКУНД        20*ОДНА_СЕКУНДА;
КОНСТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_ЗАТ_ВАК			10*ОДНА_СЕКУНДА;
КОНСТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_САПУНА           	ДЕСЯТЬ_СЕКУНД;
КОНСТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_УСТР_ПЛАВ_ПУСКА   3*ОДНА_СЕКУНДА;
КОНСТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_ВАК_НАСОСА 		3*ОДНА_СЕКУНДА;
КОНСТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_ВЕНТИЛЯТОРА		ПЯТЬ_СЕКУНД;
КОНСТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_РЕГ_Т				3*ОДНА_СЕКУНДА;
КОНСТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_РЕГ_УР			3*ОДНА_СЕКУНДА;
КОНСТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_НАСОС_ОХЛ			3*ОДНА_СЕКУНДА;
КОНСТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ  ДЕСЯТЬ_СЕКУНД;
КОНСТ ОДНА_МИНУТА          60*ОДНА_СЕКУНДА;
КОНСТ ДВЕ_МИНУТЫ          2*ОДНА_МИНУТА;
КОНСТ ПЯТЬ_МИНУТ          5*60*ОДНА_СЕКУНДА;
/* КОНСТ ТАЙМАУТ_ВАКУУМИРОВАНИЯ_ТРУБЫ  30*ОДНА_МИНУТА;*/
КОНСТ ТРИДЦАТЬ_МИНУТ          30*ОДНА_МИНУТА;
КОНСТ ТАЙМАУТ_ЗАДЕРЖКА_НА_ОТКАЧКУ ПЯТЬ_СЕКУНД;

КОНСТ ВКЛ                1;
КОНСТ ВЫК               0;
КОНСТ ОТКР              1;
КОНСТ ЗАКР          	0;
КОНСТ ЕСТЬ               1;
КОНСТ НЕТ                0;
КОНСТ НОРМА              1;
КОНСТ ВНЕ_НОРМЫ          0;
КОНСТ ДОСТОВЕРНО                1;
КОНСТ НЕДОСТОВЕРНО               0;


КОНСТ ТАЙМАУТ_НА_ДИАГНОСТИКУ_РАЗРЫВА_СВЯЗИ 5*ОДНА_СЕКУНДА;

КОНСТ Т_ПредельнаяВбакеОхлаждения          99.0; 

/*КОНСТ ЗД_P_ВбакеКомпрессора						10.0; */  /*?????  атмосфер */

КОНСТ Т_ГИСТЕРЕЗИС_ВОДЫ_В_БАКЕ  1.0;
КОНСТ Т_ГИСТЕРЕЗИС_ВОДЫ_В_РУБАХЕ  1.0;


/*===========================================================*/
/*=== КОДЫ СООБЩЕНИЙ ПРОЦЕССОВ КОНТРОЛЯ                     =*/
/*===========================================================*/
ПЕРЕЧИСЛЕНИЕ {
    ПК_ГУИ_УРОВЕНЬ_ВОДЫ_В_БАКЕ_ОХЛАЖДЕНИЯ_НИЗКИЙ,
    ПК_ГУИ_УРОВЕНЬ_ВОДЫ_В_БАКЕ_ОХЛАЖДЕНИЯ_ВЫСОКИЙ,
	ПК_ГУИ_УРОВЕНЬ_ВОДЫ_В_БАКЕ_ОХЛАЖДЕНИЯ_В_НОРМЕ,
	ПК_ГУИ_ТЕМПЕРАТУРА_ВОДЫ_В_БАКЕ_ОХЛАЖДЕНИЯ_НИЗКАЯ,
	ПК_ГУИ_ТЕМПЕРАТУРА_ВОДЫ_В_БАКЕ_ОХЛАЖДЕНИЯ_ВЫСОКАЯ,
	ПК_ГУИ_ТЕМПЕРАТУРА_ВОДЫ_В_БАКЕ_ОХЛАЖДЕНИЯ_В_НОРМЕ,

    ПК_ГУИ_ТЕМПЕРАТУРА_ВОДЫ_В_РУБАШКЕ_ОХЛАЖДЕНИЯ_НИЗКАЯ,
	ПК_ГУИ_ТЕМПЕРАТУРА_ВОДЫ_В_РУБАШКЕ_ОХЛАЖДЕНИЯ_ВЫСОКАЯ,
	ПК_ГУИ_ТЕМПЕРАТУРА_ВОДЫ_В_РУБАШКЕ_ОХЛАЖДЕНИЯ_В_НОРМЕ,

	ПК_ГУИ_ДАВЛЕНИЕ_В_ВАКУУМНОЙ_ТРУБЕ_В_НОРМЕ,
	ПК_ГУИ_ДАВЛЕНИЕ_В_ВАКУУМНОЙ_ТРУБЕ_ВНЕ_НОРМЫ,
	
	ПК_ГУИ_СКОРОСТЬ_ОТКАЧКИ_В_НОРМЕ,
	ПК_ГУИ_СКОРОСТЬ_ОТКАЧКИ_НИЖЕ_НОРМЫ,
	
	ПК_ГУИ_ДАВЛЕНИЕ_В_БАКЕ_КОМПРЕССОРА_В_НОРМЕ,
    ПК_ГУИ_ДАВЛЕНИЕ_В_БАКЕ_КОМПРЕССОРА_НИЖЕ_НОРМЫ
	
};
/*===========================================================*/
/*========= КОМАНДЫ ОПЕРАТОРА =============15================*/
/*===========================================================*/
ПЕРЕЧИСЛЕНИЕ {
     КОМ_ГУИ_ОТКР_ЗАТВОР_ВАК= ПК_ГУИ_ДАВЛЕНИЕ_В_БАКЕ_КОМПРЕССОРА_НИЖЕ_НОРМЫ + 1,
     КОМ_ГУИ_ЗАКР_ЗАТВОР_ВАК    	,
     КОМ_ГУИ_ОТКР_САПУН			    ,
     КОМ_ГУИ_ЗАКР_САПУН			    ,
     КОМ_ГУИ_ВКЛ_УСТР_ПЛАВ_ПУСКА    ,
     КОМ_ГУИ_ВЫК_УСТР_ПЛАВ_ПУСКА    ,
	 КОМ_ГУИ_ВКЛ_ПИТ_ВАК_НАСОСА     ,
	 КОМ_ГУИ_ВЫК_ПИТ_ВАК_НАСОСА     ,
	 КОМ_ГУИ_ВКЛ_ПИТ_ВЕНТИЛЯТОР    ,
	 КОМ_ГУИ_ВЫК_ПИТ_ВЕНТИЛЯТОР    ,
     КОМ_ГУИ_ВКЛ_ПИТ_РЕГ_Т    ,
	 КОМ_ГУИ_ВЫК_ПИТ_РЕГ_Т    ,
     КОМ_ГУИ_ВКЛ_ПИТ_РЕГ_УР     ,
	 КОМ_ГУИ_ВЫК_ПИТ_РЕГ_УР     , 
	 КОМ_ГУИ_ВКЛ_ПИТ_НАС_ОХЛ ,
	 КОМ_ГУИ_ВЫК_ПИТ_НАС_ОХЛ    , 
	 КОМ_ГУИ_ВКЛ_ВАКУУМИРОВАНИЕ_ТРУБЫ,
	 КОМ_ГУИ_ВЫК_ВАКУУМИРОВАНИЕ_ТРУБЫ,
	 КОМ_ГУИ_ВКЛ_ВАКУУМНУЮ_ПОДСИСТЕМУ ,
	 КОМ_ГУИ_ВЫК_ВАКУУМНУЮ_ПОДСИСТЕМУ,   /*  35 */
     КОМАНДЫ_ОТ_ГУИ = КОМ_ГУИ_ВЫК_ВАКУУМНУЮ_ПОДСИСТЕМУ 
	
};


/*===========================================================*/
/*========= КОМАНДЫ ИЗМЕНЕНИЯ ФАКТИЧЕСКИХ ПАРАМЕТРОВ ========*/
/*===========================================================*/
ПЕРЕЧИСЛЕНИЕ {
	ИЗМН_ФП_P_ВбакеКомпрессора = КОМАНДЫ_ОТ_ГУИ + 1,
	ИЗМН_ФП_P_Впатрубке,
	ИЗМН_ФП_T_Врубашке,	
	ИЗМН_ФП_T_Вбаке,	
    ИЗМН_ФП_P_ВтрубеСидер,
    КОМАНДЫ_ИЗМ_ФП = ИЗМН_ФП_P_ВтрубеСидер  /*  42 */
};

/*===========================================================*/
/*===========================================================*/
/*============= КОДЫ СООБЩЕНИЙ ВАКУУМНОЙ СИСТЕМЫ ============*/
/*===========================================================*/
/*===========================================================*/
ПЕРЕЧИСЛЕНИЕ {                    
      ПК_ГУИ_ОТКАЗ_ОТКРЫТИЯ_ЗАТВОРА_ВАКУУМНОГО = КОМАНДЫ_ИЗМ_ФП + 1,
      ПК_ГУИ_ОТКАЗ_ЗАКРЫТИЯ_ЗАТВОРА_ВАКУУМНОГО,
      ПК_ГУИ_ОТКАЗ_ОТКРЫТИЯ_САПУНА,
      ПК_ГУИ_ОТКАЗ_ЗАКРЫТИЯ_САПУНА,
	  ПК_ГУИ_ОТКАЗ_ВКЛЮЧЕНИЯ_ВЕНТИЛЯТОРА,
	  ПК_ГУИ_ОТКАЗ_ВЫКЛЮЧЕНИЯ_ВЕНТИЛЯТОРА,
	  ПК_ГУИ_ОТКАЗ_ВКЛЮЧЕНИЯ_УСТР_ПЛАВ_ПУСКА,
	  ПК_ГУИ_ОТКАЗ_ВЫКЛЮЧЕНИЯ_УСТР_ПЛАВ_ПУСКА,
	  ПК_ГУИ_ОТКАЗ_ВКЛЮЧЕНИЯ_РЕГУЛЯТОРА_ТЕМПЕРАТУРЫ,
	  ПК_ГУИ_ОТКАЗ_ВЫКЛЮЧЕНИЯ_РЕГУЛЯТОРА_ТЕМПЕРАТУРЫ,
	  ПК_ГУИ_ОТКАЗ_ВКЛЮЧЕНИЯ_РЕГУЛЯТОРА_УРОВНЯ,
	  ПК_ГУИ_ОТКАЗ_ВЫКЛЮЧЕНИЯ_РЕГУЛЯТОРА_УРОВНЯ,
	  ПК_ГУИ_ОТКАЗ_ВКЛЮЧЕНИЯ_НАСОСА_ОХЛАЖДЕНИЯ,
	  ПК_ГУИ_ОТКАЗ_ВЫКЛЮЧЕНИЯ_НАСОСА_ОХЛАЖДЕНИЯ,
	  ПК_ГУИ_ИДЕТ_ВАКУУМИРОВАНИЕ_ОТКАЗ_ВЫПОЛНЕНИЯ_КОМАНДЫ,
	  ПК_ГУИ_НЕТ_УСЛОВИЙ_ДЛЯ_ВЫПОЛНЕНИЯ_КОМАНДЫ,
	  ПК_ГУИ_УРОВЕНЬ_ВОДЫ_В_БАКЕ_ОХЛАЖДЕНИЯ_ВНЕ_НОРМЫ_ОТКАЗ_ВЫПОЛНЕНИЯ_КОМАНДЫ, 				
	  ПК_ГУИ_ОТРИЦАТЕЛЬНАЯ_ТЕМПЕРАТУРА_ВОДЫ_ОТКАЗ_ВЫПОЛНЕНИЯ_КОМАНДЫ,
	  ПК_ГУИ_ДАВЛЕНИЕ_В_ПАТРУБКЕ_НЕ_УМЕНЬШАЕТСЯ,
	  ПК_ГУИ_ДАВЛЕНИЕ_В_ВАК_ТРУБЕ_НЕ_УМЕНЬШАЕТСЯ,
	  ПК_ГУИ_ВАКУУМИРОВАНИЕ_УСПЕШНО_ЗАВЕРШЕНО,	  
	  ПК_ГУИ_ОТКАЗ_ВКЛЮЧЕНИЯ_ПИТАНИЯ_ВАКУУМНОГО_НАСОСА,   /*   */
	  ПК_ГУИ_ОТКАЗ_ВЫКЛЮЧЕНИЯ_ПИТАНИЯ_ВАКУУМНОГО_НАСОСА,
	  ПК_ГУИ_АВАРИЯ_НЕТ_ДОСТОВЕРНЫХ_ДАННЫХ_ОТ_ОБОРУДОВАНИЯ_ВАКУУМНОЙ_СИСТЕМЫ,
	  ПК_ГУИ_ВЕНТИЛЯТОР_ВКЛЮЧЕН,
	  ПК_ГУИ_ВЕНТИЛЯТОР_ВЫКЛЮЧЕН,
	  ПК_ГУИ_СПОНТАННОЕ_ВЫКЛЮЧЕНИЕ_ВЕНТИЛЯТОРА,
	  ПК_ГУИ_СПОНТАННОЕ_ВКЛЮЧЕНИЕ_ВЕНТИЛЯТОРА,
	  ПК_ГУИ_ЗАТВОР_ВАКУУМНЫЙ_ОТКРЫТ,
	  ПК_ГУИ_ЗАТВОР_ВАКУУМНЫЙ_ЗАКРЫТ,
	  ПК_ГУИ_СПОНТАННОЕ_ОТКРЫТИЕ_ЗАТ_ВАКУУМНОГО,
	  ПК_ГУИ_СПОНТАННОЕ_ЗАКРЫТИЕ_ЗАТ_ВАКУУМНОГО,
	  ПК_ГУИ_САПУН_ОТКРЫТ,
	  ПК_ГУИ_САПУН_ЗАКРЫТ,
	  ПК_ГУИ_СПОНТАННОЕ_ОТКРЫТИЕ_САПУНА,
	  ПК_ГУИ_СПОНТАННОЕ_ЗАКРЫТИЕ_САПУНА,
	  ПК_ГУИ_ПИТАНИЕ_ВАКУУМНОГО_НАСОСА_ВКЛЮЧЕНО,
	  ПК_ГУИ_ПИТАНИЕ_ВАКУУМНОГО_НАСОСА_ВЫКЛЮЧЕНО,
	  ПК_ГУИ_СПОНТАННОЕ_ВКЛЮЧЕНИЕ_ВАКУУМНОГО_НАСОСА,
	  ПК_ГУИ_СПОНТАННОЕ_ВЫКЛЮЧЕНИЕ_ВАКУУМНОГО_НАСОСА,
	  ПК_ГУИ_ПИТАНИЕ_РЕГУЛЯТОРА_ТЕМПЕРАТУРЫ_ВКЛЮЧЕНО,
	  ПК_ГУИ_ПИТАНИЕ_РЕГУЛЯТОРА_ТЕМПЕРАТУРЫ_ВЫКЛЮЧЕНО,
  	  ПК_ГУИ_СПОНТАННОЕ_ВКЛЮЧЕНИЕ_РЕГУЛЯТОРА_ТЕМПЕРАТУРЫ,
	  ПК_ГУИ_СПОНТАННОЕ_ВЫКЛЮЧЕНИЕ_РЕГУЛЯТОРА_ТЕМПЕРАТУРЫ,
	  ПК_ГУИ_ПИТАНИЕ_РЕГУЛЯТОРА_УРОВНЯ_ВКЛЮЧЕНО,
	  ПК_ГУИ_ПИТАНИЕ_РЕГУЛЯТОРА_УРОВНЯ_ВЫКЛЮЧЕНО,
	  ПК_ГУИ_СПОНТАННОЕ_ВКЛЮЧЕНИЕ_РЕГУЛЯТОРА_УРОВНЯ,
	  ПК_ГУИ_СПОНТАННОЕ_ВЫКЛЮЧЕНИЕ_РЕГУЛЯТОРА_УРОВНЯ,
	  ПК_ГУИ_ПИТАНИЕ_НАСОСА_ОХЛАЖДЕНИЯ_ВКЛЮЧЕНО,
	  ПК_ГУИ_ПИТАНИЕ_НАСОСА_ОХЛАЖДЕНИЯ_ВЫКЛЮЧЕНО,
	  ПК_ГУИ_СПОНТАННОЕ_ВКЛЮЧЕНИЕ_НАСОСА_ОХЛАЖДЕНИЯ,
	  ПК_ГУИ_СПОНТАННОЕ_ВЫКЛЮЧЕНИЕ_НАСОСА_ОХЛАЖДЕНИЯ,
	  ПК_ГУИ_УСТРОЙСТВО_ПЛАВНОГО_ПУСКА_ВКЛЮЧЕНО,
	  ПК_ГУИ_УСТРОЙСТВО_ПЛАВНОГО_ПУСКА_ВЫКЛЮЧЕНО,
	  ПК_ГУИ_СПОНТАННОЕ_ВКЛЮЧЕНИЕ_УСТРОЙСТВА_ПЛАВНОГО_ПУСКА,
	  ПК_ГУИ_СПОНТАННОЕ_ВЫКЛЮЧЕНИЕ_УСТРОЙСТВА_ПЛАВНОГО_ПУСКА,
	  ПК_ГУИ_ОТКАЗ_ВАКУУМИРОВАНИЯ,
	  ПК_ГУИ_ОТКАЗ_ВКЛЮЧЕНИЯ_ВАКУУМНОЙ_ПОДСИСТЕМЫ,
	  ПК_ГУИ_ОТКАЗ_ВЫКЛЮЧЕНИЯ_ВАКУУМНОЙ_ПОДСИСТЕМЫ,
	  ПК_ГУИ_ЕСТЬ_ДОСТОВЕРНОСТЬ_КОНТРОЛЯ_ОТКРЫТИЯ_ЗАТВОРА_ВАКУУМНОГО,
      ПК_ГУИ_НЕТ_ДОСТОВЕРНОСТИ_КОНТРОЛЯ_ОТКРЫТИЯ_ЗАТВОРА_ВАКУУМНОГО,
	  ПК_ГУИ_ЕСТЬ_ДОСТОВЕРНОСТЬ_КОНТРОЛЯ_ЗАКРЫТИЯ_ЗАТВОРА_ВАКУУМНОГО,
	  ПК_ГУИ_НЕТ_ДОСТОВЕРНОСТИ_КОНТРОЛЯ_ЗАКРЫТИЯ_ЗАТВОРА_ВАКУУМНОГО,
      ПК_ГУИ_ЕСТЬ_ДОСТОВЕРНОСТЬ_КОНТРОЛЯ_САПУНА,
	  ПК_ГУИ_НЕТ_ДОСТОВЕРНОСТИ_КОНТРОЛЯ_САПУНА,
	  ПК_ГУИ_ЕСТЬ_ДОСТОВЕРНОСТЬ_КОНТРОЛЯ_УСТР_ПЛАВ_ПУСКА,
	  ПК_ГУИ_НЕТ_ДОСТОВЕРНОСТИ_КОНТРОЛЯ_УСТР_ПЛАВ_ПУСКА,
	  ПК_ГУИ_ЕСТЬ_ДОСТОВЕРНОСТЬ_КОНТРОЛЯ_ПИТ_ВАК_НАСОСА,
	  ПК_ГУИ_НЕТ_ДОСТОВЕРНОСТИ_КОНТРОЛЯ_ПИТ_ВАК_НАСОСА,
	  ПК_ГУИ_ЕСТЬ_ДОСТОВЕРНОСТЬ_КОНТРОЛЯ_ПИТ_ВЕНТИЛЯТОРА,
	  ПК_ГУИ_НЕТ_ДОСТОВЕРНОСТИ_КОНТРОЛЯ_ПИТ_ВЕНТИЛЯТОРА
};

/*===========================================================*/
/*========= КОДЫ СООБЩЕНИЙ ИЗМЕНЕНИЙ ФП         =============*/
/*===========================================================*/
ПЕРЕЧИСЛЕНИЕ {                    
      ПК_ГУИ_ФП_P_ВбакеКомпрессора = ПК_ГУИ_НЕТ_ДОСТОВЕРНОСТИ_КОНТРОЛЯ_ПИТ_ВЕНТИЛЯТОРА + 1,
      ПК_ГУИ_ФП_P_Впатрубке,
      ПК_ГУИ_ФП_T_Врубашке,
      ПК_ГУИ_ФП_T_Вбаке,
      ПК_ГУИ_ФП_P_ВтрубеСидер
      
};
/*===========================================================*/
/*= КОДЫ КОМАНД ИЗМЕНЕНИЯ НАСТРОЕЧНЫХ ПАРАМЕТРОВ   ==========*/
/*===========================================================*/
ПЕРЕЧИСЛЕНИЕ {
ИЗМН_НП_1 = ПК_ГУИ_ФП_P_ВтрубеСидер + 1,
ИЗМН_НП_P_ВбакеКомпрессора = ИЗМН_НП_1,
ИЗМН_НП_Pатмосф,
ИЗМН_НП_ГлубокийВакуумТрубы,

ИЗМН_НП_N
};

/*===========================================================*/
/*========= КОДЫ СООБЩЕНИЙ  О НЕВЕРНОЙ КОМАНДЕ ==============*/
/*===========================================================*/
ПЕРЕЧИСЛЕНИЕ {
     ПК_ГУИ_ОШИБКА_КОДА_КОМАНДЫ_ОТ_ГУИ = ИЗМН_НП_N + 1  
}; 

/*###########################################################*/
/*###########################################################*/
/*########                                          #########*/
/*########  ОПИСАНИЕ ФУНКЦИЙ                        #########*/
/*########                                          #########*/
/*########                                          #########*/
/*###########################################################*/

/*:::::::::Функции для работы  с ГУИ :::::::::::::::::::::   */

/*чтение кода и параметра события от ПИВ из кольцевого буфера в память */
ФУНКЦИЯ ЦЕЛ GetNextMsgGUI(ПУСТО);

ФУНКЦИЯ ЦЕЛ  GetMsgGUICode        (ПУСТО);  /*чтение из памяти кода события */
ФУНКЦИЯ ПЛАВ GetFloatParamGUI     (ПУСТО);  /*чтение из памяти параметра события  используется */
/* ФУНКЦИЯ КЦЕЛ GetShortParamGUI     (ПУСТО); */
ФУНКЦИЯ ЦЕЛ  GetIntParamGUI       (ПУСТО);  /*чтение с экрана гуи - не исползуется */
ФУНКЦИЯ ДЦЕЛ GetLongParamGUI      (ПУСТО);  /*чтение с экрана гуи - исползуется */

ФУНКЦИЯ ЦЕЛ  SendMsgGUICode       (ЦЕЛ);
ФУНКЦИЯ ЦЕЛ  SendMsgFloatParamGUI (ЦЕЛ, ПЛАВ);
/* ФУНКЦИЯ ЦЕЛ  SendMsgShortParamGUI (ЦЕЛ, КЦЕЛ); */
/* ФУНКЦИЯ ЦЕЛ  SendMsgIntParamGUI   (ЦЕЛ, ЦЕЛ); */
ФУНКЦИЯ ЦЕЛ  SendMsgLongParamGUI  (ЦЕЛ, ДЦЕЛ);

ФУНКЦИЯ ЦЕЛ  InitChannelStatus_GUI (ПУСТО); /* инициализация статуса, обнуление числа ошибок */
ФУНКЦИЯ ЦЕЛ  GetChannelStatus_GUI  (ПУСТО);  /* чтение статуса канала 0/1 - не исползуется*/
ФУНКЦИЯ ЦЕЛ  GetErrorNumberCS_GUI  (ПУСТО);  /* чтение числа ошибок КС  - не исползуется*/
ФУНКЦИЯ ЦЕЛ  GetErrorNumberTO_GUI  (ПУСТО);  /* чтение числа ошибок по тайм ауту - не исползуется*/
ФУНКЦИЯ ЦЕЛ  GetInpMsgNumber_GUI (ПУСТО);  /* чтение числа сообщ. во входн. буфере - не исползуется*/
ФУНКЦИЯ ЦЕЛ  GetOutMsgNumber_GUI (ПУСТО); /* чтение числа сообщ. в выходн. буфере -  не исползуется*/

ФУНКЦИЯ ПЛАВ GetFP_P_BAK_COMPRESSOR		(ПУСТО);	/* чтение давления в баке компрессора */ 
ФУНКЦИЯ ПЛАВ GetFP_P_PATRUBKA			(ПУСТО);   /* чтение давления  в патрубке      */
ФУНКЦИЯ ПЛАВ GetFP_T_RUBACHKA			(ПУСТО);	/* чтение температуры воды в рубашке охлаждения */
ФУНКЦИЯ ПЛАВ GetFP_T_BAK				(ПУСТО);	/* чтение температуры воды в баке охлаждения */ 
ФУНКЦИЯ ПЛАВ GetFP_P_TRUBA				(ПУСТО);	/* чтение давления в трубе сидера */


/*###########################################################*/
/*########                                          #########*/
/*########  ОПИСАНИЕ РЕГИСТРОВ МОДУЛЕЙ УСО          #########*/
/*########  (устройство связи с объектом)           #########*/
/*###########################################################*/

ВХОД  К_ЛОГ_ВХОД_ОТКР_ЗАТВОР_ВАК  	0 0 8; /* имя, базовый адрес, доп. адр. порта, 8бит */
ВХОД  К_ЛОГ_ВХОД_ЗАКР_ЗАТВОР_ВАК  	0 1 8; /* имя, базовый адрес, доп. адр. порта, 8бит */
ВХОД  К_ЛОГ_ВХОД_САПУН  			0 2 8; /* имя, базовый адрес, доп. адр. порта, 8бит */
ВХОД  К_ЛОГ_ВХОД_УСТР_ПЛАВ_ПУСКА  	0 3 8; /* имя, базовый адрес, доп. адр. порта, 8бит */
ВХОД  К_ЛОГ_ВХОД_ПИТ_ВАК_НАСОСА  	0 4 8; /* имя, базовый адрес, доп. адр. порта, 8бит */
ВХОД  К_ЛОГ_ВХОД_ПИТ_ВЕНТИЛЯТОРА  	0 5 8; /* имя, базовый адрес, доп. адр. порта, 8бит */
ВХОД  К_ЛОГ_ВХОД_ПИТ_РЕГ_Т  		0 6 8; /* имя, базовый адрес, доп. адр. порта, 8бит */
ВХОД  К_ЛОГ_ВХОД_ПИТ_РЕГ_УР  		0 7 8; /* имя, базовый адрес, доп. адр. порта, 8бит */
ВХОД  К_ЛОГ_ВХОД_УР_ВОДЫ_MIN  		0 8 8; /* имя, базовый адрес, доп. адр. порта, 8бит */
ВХОД  К_ЛОГ_ВХОД_УР_ВОДЫ_MAX  		0 9 8; /* имя, базовый адрес, доп. адр. порта, 8бит */
ВХОД  К_ЛОГ_ВХОД_ПИТ_НАСОС_ОХЛ  	0 10 8; /* имя, базовый адрес, доп. адр. порта, 8бит */


/* ВХОД АЦП_T_Вбаке		 			0 66 16; */  /* регистр АЦП */
/*ВХОД АЦП_T_Врубашке	 			0 67 16; */ /* регистр АЦП */
/*ВХОД АЦП_P_ВтрубеСидер	 		0 68 16; */ /* регистр АЦП */
/*ВХОД АЦП_P_Впатрубке 				0 69 16; */ /*   регистр АЦП */
/*ВХОД АЦП_P_ВбакеКомпрессора 		0 70 16; */ /* регистр АЦП */

ВЫХОД  У_ЛОГ_ВЫХОД_ПИТ_ВЕНТИЛЯТОРА  1 0 8; /* имя, базовый адрес, доп. адр. порта, 8бит */
ВЫХОД  У_ЛОГ_ВЫХОД_ПИТ_РЕГ_Т		1 1 8;
ВЫХОД  У_ЛОГ_ВЫХОД_ПИТ_НАС_ОХЛ		1 2 8;
ВЫХОД  У_ЛОГ_ВЫХОД_ПИТ_ВАК_НАСОСА	1 3 8;
ВЫХОД  У_ЛОГ_ВЫХОД_ЗАТВОР_ВАК		1 4 8;
ВЫХОД  У_ЛОГ_ВЫХОД_САПУН			1 5 8;
ВЫХОД  У_ЛОГ_ВЫХОД_УСТР_ПЛАВ_ПУСКА	1 6 8;
ВЫХОД  У_ЛОГ_ВЫХОД_ПИТ_РЕГ_УР		1 7 8;


/*ВЫХОД  ЛОГ_ВЫХОД_GPIO21  1 1 8; */ /* имя, базовый адрес, доп. адр. порта, 8бит */


/*###########################################################*/
/*########                                          #########*/
/*########  ПРОЦЕССЫ                                #########*/
/*########                                          #########*/
/*###########################################################*/

/*
 * Процесс Инициализация. Служит для
 * развертывания программы. Этот процесс (описанный
 * первым) - единственно активный процесс по запуску.
 * Кроме этого, процесс Инициализация содержит описание 
 * переменных для ссылок из других
 * процессов. Это удобно: описания локализованы в одном
 * месте.
*/
ПРОЦ Инициализация{
/*===========================================================*/
/*=========== ОПИСАНИЕ ПЕРЕМЕННЫХ ===========================*/
/*===========================================================*/

/* ВХОДНЫЕ СИГНАЛЫ: */
/*-----------------*/


ЛОГ К_ОТКР_ЗАТВОР_ВАК             	= {К_ЛОГ_ВХОД_ОТКР_ЗАТВОР_ВАК[1]} ДЛЯ ВСЕХ; /*  концевик затвора вакуумного*/
ЛОГ Д_ОТКР_ЗАТВОР_ВАК             	= {К_ЛОГ_ВХОД_ОТКР_ЗАТВОР_ВАК[1]} ДЛЯ ВСЕХ; /*  достоверность*/

ЛОГ К_ЗАКР_ЗАТВОР_ВАК             	= {К_ЛОГ_ВХОД_ЗАКР_ЗАТВОР_ВАК[1]} ДЛЯ ВСЕХ; /*  концевик затвора вакуумного*/
ЛОГ Д_ЗАКР_ЗАТВОР_ВАК             	= {К_ЛОГ_ВХОД_ЗАКР_ЗАТВОР_ВАК[1]} ДЛЯ ВСЕХ;

ЛОГ К_САПУН                         = {К_ЛОГ_ВХОД_САПУН[1]} ДЛЯ ВСЕХ;
ЛОГ Д_САПУН                         = {К_ЛОГ_ВХОД_САПУН[1]} ДЛЯ ВСЕХ;

ЛОГ К_УСТР_ПЛАВ_ПУСКА				= {К_ЛОГ_ВХОД_УСТР_ПЛАВ_ПУСКА[1]} ДЛЯ ВСЕХ;
ЛОГ Д_УСТР_ПЛАВ_ПУСКА				= {К_ЛОГ_ВХОД_УСТР_ПЛАВ_ПУСКА[1]} ДЛЯ ВСЕХ;

ЛОГ К_ПИТ_ВАК_НАСОСА			 	= {К_ЛОГ_ВХОД_ПИТ_ВАК_НАСОСА[1]} ДЛЯ ВСЕХ; 
ЛОГ Д_ПИТ_ВАК_НАСОСА			 	= {К_ЛОГ_ВХОД_ПИТ_ВАК_НАСОСА[1]} ДЛЯ ВСЕХ; 

ЛОГ К_ПИТ_ВЕНТИЛЯТОРА               = {К_ЛОГ_ВХОД_ПИТ_ВЕНТИЛЯТОРА[1]} ДЛЯ ВСЕХ; 
ЛОГ Д_ПИТ_ВЕНТИЛЯТОРА               = {К_ЛОГ_ВХОД_ПИТ_ВЕНТИЛЯТОРА[1]} ДЛЯ ВСЕХ; 

ЛОГ К_ПИТ_РЕГ_Т 			        = {К_ЛОГ_ВХОД_ПИТ_РЕГ_Т[1]} ДЛЯ ВСЕХ;
ЛОГ Д_ПИТ_РЕГ_Т 			        = {К_ЛОГ_ВХОД_ПИТ_РЕГ_Т[1]} ДЛЯ ВСЕХ;

ЛОГ К_ПИТ_РЕГ_УР     				= {К_ЛОГ_ВХОД_ПИТ_РЕГ_УР[1]} ДЛЯ ВСЕХ;
ЛОГ Д_ПИТ_РЕГ_УР     				= {К_ЛОГ_ВХОД_ПИТ_РЕГ_УР[1]} ДЛЯ ВСЕХ;

ЛОГ К_УР_ВОДЫ_MIN  					= {К_ЛОГ_ВХОД_УР_ВОДЫ_MIN[1]} ДЛЯ ВСЕХ;
ЛОГ Д_УР_ВОДЫ_MIN  					= {К_ЛОГ_ВХОД_УР_ВОДЫ_MIN[1]} ДЛЯ ВСЕХ;

ЛОГ К_УР_ВОДЫ_MAX   				= {К_ЛОГ_ВХОД_УР_ВОДЫ_MAX[1]} ДЛЯ ВСЕХ;
ЛОГ Д_УР_ВОДЫ_MAX   				= {К_ЛОГ_ВХОД_УР_ВОДЫ_MAX[1]} ДЛЯ ВСЕХ;

ЛОГ К_ПИТ_НАСОС_ОХЛ 				= {К_ЛОГ_ВХОД_ПИТ_НАСОС_ОХЛ[1]} ДЛЯ ВСЕХ;
ЛОГ Д_ПИТ_НАСОС_ОХЛ 				= {К_ЛОГ_ВХОД_ПИТ_НАСОС_ОХЛ[1]} ДЛЯ ВСЕХ;


/* ВЫХОДНЫЕ СИГНАЛЫ (т.к. привязаны к модулю выходов): */ 

ЛОГ У_ПИТ_ВЕНТИЛЯТОРА   			= {У_ЛОГ_ВЫХОД_ПИТ_ВЕНТИЛЯТОРА[1]} ДЛЯ ВСЕХ; 
ЛОГ У_ПИТ_РЕГ_Т      				= {У_ЛОГ_ВЫХОД_ПИТ_РЕГ_Т[1]} ДЛЯ ВСЕХ;   
ЛОГ У_ПИТ_НАС_ОХЛ      				= {У_ЛОГ_ВЫХОД_ПИТ_НАС_ОХЛ[1]} ДЛЯ ВСЕХ;   
ЛОГ У_ПИТ_ВАК_НАСОСА				= {У_ЛОГ_ВЫХОД_ПИТ_ВАК_НАСОСА[1]} ДЛЯ ВСЕХ; 
ЛОГ У_ЗАТВОР_ВАК					= {У_ЛОГ_ВЫХОД_ЗАТВОР_ВАК[1]} ДЛЯ ВСЕХ; 
ЛОГ У_САПУН							= {У_ЛОГ_ВЫХОД_САПУН[1]} ДЛЯ ВСЕХ; 
ЛОГ У_УСТР_ПЛАВ_ПУСКА				= {У_ЛОГ_ВЫХОД_УСТР_ПЛАВ_ПУСКА[1]} ДЛЯ ВСЕХ; 
ЛОГ У_ПИТ_РЕГ_УР					= {У_ЛОГ_ВЫХОД_ПИТ_РЕГ_УР[1]} ДЛЯ ВСЕХ; 



	
/* ЛОГ ОШИБКА_ФУНКЦИОНИРОВАНИЯ  		= {ЛОГ_ВЫХОД_GPIO5[1]} ДЛЯ ВСЕХ; */ /*резерв */
/* предложение Саши Курочкина: ввести ошибку функционирования для контроля всех устройств, */
/* составляющих вакуумную подсистему. Если Саша не достучится до них, или две единички одновременно */
/* увидит у себя (ВКЛ/ВЫКЛ, например вентилятор). Правда, что в этом случае делать D2 - непонятно*/
/* Предлагается ошибку функционирования обнулять перед каждым запуском соответствующего процесса */ 
/* например: ВКЛ_ПИТ_ВЕНТИЛЯТОР, ВЫК_ПИТ_ВЕНТИЛЯТОР*/
/* И еще, для каждой ошибки функционирования будет int - код ошибки */
/* И еще, Саша говорит, что при обнаружении ош. функционирования надо прекращать вакуумирование ???*/

/*ЛОГ ОШИБКА_ФУНКЦ_ЗАТВОР_ВАК 			= {ЛОГ_ВХОД_GPIO6[1]} ДЛЯ ВСЕХ;*/
/*ЛОГ ОШИБКА_ФУНКЦ_САПУН 					= {ЛОГ_ВХОД_GPIO6[1]} ДЛЯ ВСЕХ;*/
/*ЛОГ ОШИБКА_ФУНКЦ_УСТР_ПЛАВ_ПУСКА 		= {ЛОГ_ВХОД_GPIO6[1]} ДЛЯ ВСЕХ;*/
/*ЛОГ ОШИБКА_ФУНКЦ_ПИТ_ВАК_НАСОСА 		= {ЛОГ_ВХОД_GPIO6[1]} ДЛЯ ВСЕХ;*/
/*ЛОГ ОШИБКА_ФУНКЦ_ВЕНТИЛЯТОР 			= {ЛОГ_ВХОД_GPIO6[1]} ДЛЯ ВСЕХ;*/
/*ЛОГ ОШИБКА_ФУНКЦ_ПИТ_РЕГ_Т 				= {ЛОГ_ВХОД_GPIO6[1]} ДЛЯ ВСЕХ;*/
/*ЛОГ ОШИБКА_ФУНКЦ_ПИТ_РЕГ_УР 			= {ЛОГ_ВХОД_GPIO6[1]} ДЛЯ ВСЕХ;*/
/*ЛОГ ОШИБКА_ФУНКЦ_НАСОС_ОХЛ 				= {ЛОГ_ВХОД_GPIO6[1]} ДЛЯ ВСЕХ;*/


/*===========================================================*/
/*=========== ЯЧЕЙКИ ИНФОРМАЦИОННЫХ КАНАЛОВ =================*/
/*===========================================================*/
ЦЕЛ КодСобытия_от_ГУИ        ДЛЯ ВСЕХ;
ЦЕЛ КодСобытия_к_ГУИ         ДЛЯ ВСЕХ;

  
  
/*===========================================================*/
/*=========== ЯЧЕЙКИ ОБЩЕГО УПРАВЛЕНИЯ ======================*/
/*===========================================================*/
ЛОГ ФлагГотовУрВодыБО ДЛЯ ВСЕХ;  /* Флаг установлен, если уровень воды в баке охлаждения в норме*/ 
ЛОГ ФлагГотовТемпВодыБО ДЛЯ ВСЕХ;  /* Флаг установлен, если  температура в баке охлаждения в норме*/ 
ЛОГ ФлагГотовTВодыРО ДЛЯ ВСЕХ;  /* Флаг установлен, если  температура в рубашке охлаждения в норме*/ 
ЛОГ ФлагГотовВакуумаВТ ДЛЯ ВСЕХ;
ЛОГ ФлагОткачкиТрубыСидеростата ДЛЯ ВСЕХ;
ЛОГ ФлагДавленияКомпрессора ДЛЯ ВСЕХ;

/* Состояние оборудования (пока не используется)*/

ЛОГ ФлагСостоянияВентилятора ДЛЯ ВСЕХ;
ЛОГ ФлагСостоянияЗатвораВакуумного ДЛЯ ВСЕХ;
ЛОГ ФлагСостоянияСапуна ДЛЯ ВСЕХ;
ЛОГ ФлагСостоянияУстройстваПлавногоПуска ДЛЯ ВСЕХ;
ЛОГ ФлагСостоянияВакуумногоНасоса ДЛЯ ВСЕХ;
ЛОГ ФлагСостоянияРегулятораТемпературы ДЛЯ ВСЕХ;			
ЛОГ ФлагСостоянияРегулятораУровня ДЛЯ ВСЕХ;
ЛОГ ФлагСостоянияНасосаОхлаждения ДЛЯ ВСЕХ;			


ДЦЕЛ ЗАДЕРЖКА_НА_РАЗГРУЗКУ_ТРАФИКА ДЛЯ ВСЕХ; /* ????????чтобы в отмену не уходить */


/*===========================================================*/
/*=========== ФАКТИЧЕСКИЕ ПАРАМЕТРЫ =========================*/
/*===========================================================*/
/*ПЛАВ ФП_Pтрубы ДЛЯ ВСЕХ;*/
/* ????????????????????????????????????????????????????????????????? */
/*  Саша Курочкин будет ацепировать */
/*ЦЕЛ	ФП_P_ВбакеКомпрессора 	= {P_ВбакеКомпрессора [16]} 	ДЛЯ ВСЕХ; */
/*ЦЕЛ	ФП_P_Впатрубке	 		= {P_Впатрубке [16]}			ДЛЯ ВСЕХ; 	*/
/*ЦЕЛ	ФП_T_Врубашке 			= {T_Врубашке [16]}				ДЛЯ ВСЕХ; 	*/
/*ЦЕЛ	ФП_T_Вбаке 				= {T_Вбаке [16]}				ДЛЯ ВСЕХ;	*/ 
/*ЦЕЛ	ФП_P_ВтрубеСидер 		= {P_ВтрубеСидер [16]} 			ДЛЯ ВСЕХ; 	*/


ПЛАВ ФП_P_ВбакеКомпрессора ДЛЯ ВСЕХ; 
ПЛАВ ФП_P_Впатрубке ДЛЯ ВСЕХ;
ПЛАВ ФП_T_Врубашке ДЛЯ ВСЕХ;	
ПЛАВ ФП_T_Вбаке ДЛЯ ВСЕХ;

/*ПЛАВ ЗД_P_ВтрубеСидер ДЛЯ ВСЕХ;	*/
ПЛАВ ФП_P_ВтрубеСидер ДЛЯ ВСЕХ;
/*===========================================================*/
/*============       НАСТРОЕЧНЫЕ ПАРАМЕТРЫ        ===========*/
/*===========================================================*/
ПЛАВ НП_P_ВбакеКомпрессора  ДЛЯ ВСЕХ;
ПЛАВ НП_Pатмосф             ДЛЯ ВСЕХ;
ПЛАВ НП_ГлубокийВакуумТрубы ДЛЯ ВСЕХ;


/*########  СОСТОЯНИЯ ПРОЦЕССА  #########*/
 СОСТ Начало{  
	ЕСЛИ ( 	(Д_ОТКР_ЗАТВОР_ВАК) &&
			(Д_ЗАКР_ЗАТВОР_ВАК) &&
			(Д_САПУН) &&
			(Д_УСТР_ПЛАВ_ПУСКА) &&
			(Д_ПИТ_ВАК_НАСОСА) &&
			(Д_ПИТ_ВЕНТИЛЯТОРА) &&
			(Д_ПИТ_РЕГ_Т) &&
			(Д_ПИТ_РЕГ_УР) &&
			(Д_УР_ВОДЫ_MIN) &&
			(Д_УР_ВОДЫ_MAX) &&
			(Д_ПИТ_НАСОС_ОХЛ))
	В СЛЕДУЮЩЕЕ;  /* */
 }

СОСТ НачалоРаботы{	
/*    ОШИБКА_ФУНКЦИОНИРОВАНИЯ = ВЫК;  */
    ЗАДЕРЖКА_НА_РАЗГРУЗКУ_ТРАФИКА = 1; /* параметр регулирует частоту подачи */
	                                   /* сообщений в зависимости от заполненности буфера сообщений... */
/*===========================================================*/
/*=================== ИНИЦИАЛИЗАЦИЯ ФЛАГОВ   ================*/
/*===========================================================*/	
/*	ФлагГотовУрВодыБО  = ВНЕ_НОРМЫ;  */
/*	ФлагГотовТемпВодыБО = ВНЕ_НОРМЫ; */
/*	ФлагГотовTВодыРО = ВНЕ_НОРМЫ;  */
/*	ФлагГотовВакуумаВТ = ВНЕ_НОРМЫ; */
/*	ФлагОткачкиТрубыСидеростата = ВНЕ_НОРМЫ; */
/*	ФлагДавленияКомпрессора = ВНЕ_НОРМЫ; */
/*===========================================================*/


/*	У_ПИТ_ВЕНТИЛЯТОРА   			= ВЫК; */
/*	У_ПИТ_РЕГ_Т      				= ВЫК;  */          
/*	У_ПИТ_НАС_ОХЛ					= ВЫК; */
/*	У_ПИТ_ВАК_НАСОСА       			= ВЫК;  */                  
/*	У_ЗАТВОР_ВАК					= ЗАКР; */
/*	У_САПУН 						= ЗАКР; */
/*	У_УСТР_ПЛАВ_ПУСКА        		= ВЫК;  */                 
/*	У_ПИТ_РЕГ_УР      				= ВЫК; */

/*===========================================================*/
/*========= ИНИЦИАЛИЗАЦИЯ НАСТРОЕЧНЫХ ПАРАМЕТРОВ   ==========*/
/*===========================================================*/

	НП_P_ВбакеКомпрессора = 8; /*атмосфер (бар) */
	НП_Pатмосф              = 760.00;
	НП_ГлубокийВакуумТрубы =  0.02; /*мм. рт. ст*/



/* СТАРТ ПРОЦ ЧтениеАЦП; */
    СТАРТ ПРОЦ РЕГУЛЯТОР_ТРАФИКА;
	СТАРТ ПРОЦ ЧтениеФактПараметров_с_Датчиков;
	СТАРТ ПРОЦ ЧтениеКольцевогоБуфера;
	СТАРТ ПРОЦ КонтрольДостоверности;
	СТОП;

 }
}

/*========================================================*/
/*                                                        */
/*==            Процесс контроля достоверности          ==*/
/*                                                        */
/*========================================================*/
ПРОЦ ОстановВсехПроцессовИстартПроцИнициализация{
	СОСТ Начало{
/*		СТОП ПРОЦ ЧтениеФактПараметров_с_Датчиков;*/
		СТОП ПРОЦ РЕГУЛЯТОР_ТРАФИКА; 
		СТОП ПРОЦ ЧтениеКольцевогоБуфера;
		СТОП ПРОЦ ОбработкаКомандИзмененияНП_от_ГУИ;
		СТОП ПРОЦ ОбработкаКомандУправления_от_ГУИ;		
	/*	СТОП ПРОЦ КонтрольИзменения_ФП_P_ВбакеКомпрессора;            */
	/*	СТОП ПРОЦ КонтрольИзменения_ФП_P_Впатрубке;                   */ 
	/*	СТОП ПРОЦ КонтрольИзменения_ФП_T_Врубашке;                    */ 
	/*	СТОП ПРОЦ КонтрольИзменения_ФП_T_Вбаке;                       */
	/*	СТОП ПРОЦ КонтрольИзменения_ФП_P_ВтрубеСидер;                 */ 
	/*	СТОП ПРОЦ КонтрольВеличиныОткачкиВауумнойТрубыСидеростата;    */ 
	/*	СТОП ПРОЦ КонтрольГотовностиВакуума_в_Вакуумной_Трубе;        */ 
	/*	СТОП ПРОЦ КонтрольУровняВодыБакаОхлаждения;                   */
	/*	СТОП ПРОЦ КонтрольТемпературыВодыБакаОхлаждения;              */ 
	/*	СТОП ПРОЦ КонтрольТемпературыРубашкиОхлаждения;				  */
	/*	СТОП ПРОЦ КонтрольДавленияВбакеКомпрессора;                   */
		СТОП ПРОЦ ВКЛ_ПИТ_ВЕНТИЛЯТОР;
		СТОП ПРОЦ ВЫК_ПИТ_ВЕНТИЛЯТОР;
		СТОП ПРОЦ КонтрольСпонтанногоВыключенияВентилятора;
		СТОП ПРОЦ КонтрольСпонтанногоВключенияВентилятора;
	 /*	СТОП ПРОЦ ОТКР_ЗАТВОР_ВАК; */
	/*	СТОП ПРОЦ ЗАКР_ЗАТВОР_ВАК; */
	/*	СТОП ПРОЦ КонтрольСпонтанногоЗакрытияЗатвораВакуумного; */
	/*	СТОП ПРОЦ КонтрольСпонтанногоОткрытияЗатвораВакуумного; */
	/*	СТОП ПРОЦ ОТКР_САПУН; */
	/*	СТОП ПРОЦ ЗАКР_САПУН; */
	/*	СТОП ПРОЦ КонтрольСпонтанногоЗакрытияСапуна; */
	/*	СТОП ПРОЦ КонтрольСпонтанногоОткрытияСапуна; */
	/*	СТОП ПРОЦ ВКЛ_ПИТ_ВАК_НАСОСА; */
	/*	СТОП ПРОЦ ВЫК_ПИТ_ВАК_НАСОСА; */
	/*	СТОП ПРОЦ КонтрольСпонтанногоВыключенияВакНасоса; */
	/*	СТОП ПРОЦ КонтрольСпонтанногоВключенияВакНасоса; */

		
	/*	СТОП ПРОЦ КонтрольСпонтанногоВыключенияРЕГ_Т; */
	/*	СТОП ПРОЦ КонтрольСпонтанногоВключенияРЕГ_Т;  */

		
	/*	СТОП ПРОЦ КонтрольСпонтанногоВыключенияРЕГ_УР; */
	/*	СТОП ПРОЦ КонтрольСпонтанногоВключенияРЕГ_УР; */
	/*	СТОП ПРОЦ ВКЛ_ПИТ_НАС_ОХЛ; */
	/*	СТОП ПРОЦ ВЫК_ПИТ_НАС_ОХЛ; */
	/*	СТОП ПРОЦ КонтрольСпонтанногоВыключенияНасосаОхлаждения; */
	/*	СТОП ПРОЦ КонтрольСпонтанногоВключенияНасосаОхлаждения; */
	/*	СТОП ПРОЦ ВКЛ_УСТР_ПЛАВ_ПУСКА;*/
	/*	СТОП ПРОЦ ВЫК_УСТР_ПЛАВ_ПУСКА;*/
	/*	СТОП ПРОЦ КонтрольСпонтанногоВыключенияПлавнПуска; */
	/*	СТОП ПРОЦ КонтрольСпонтанногоВключенияПлавнПуска; */
	/*	СТОП ПРОЦ ВКЛ_ВАКУУМНУЮ_ПОДСИСТЕМУ;*/
	/*	СТОП ПРОЦ ВЫК_ВАКУУМНУЮ_ПОДСИСТЕМУ; */
	/*	СТОП ПРОЦ ВКЛ_ВАКУУМИРОВАНИЕ_ТРУБЫ; */
	/*	СТОП ПРОЦ ВЫК_ВАКУУМИРОВАНИЕ_ТРУБЫ; */

		СТАРТ ПРОЦ Инициализация;
		СТОП;
	}
}

/*========================================================*/
/*                                                        */
/*==            Процесс контроля достоверности          ==*/
/*                                                        */
/*========================================================*/

ПРОЦ КонтрольДостоверности{
 ИЗ ПРОЦ Инициализация  Д_ОТКР_ЗАТВОР_ВАК, Д_ЗАКР_ЗАТВОР_ВАК,
						Д_САПУН,
						Д_УСТР_ПЛАВ_ПУСКА,
						Д_ПИТ_ВАК_НАСОСА,
						Д_ПИТ_ВЕНТИЛЯТОРА,
						Д_ПИТ_РЕГ_Т,
						Д_ПИТ_РЕГ_УР,
						Д_УР_ВОДЫ_MIN,
						Д_УР_ВОДЫ_MAX,
						Д_ПИТ_НАСОС_ОХЛ;
 
 СОСТ Начало{
	ЕСЛИ ( 	(Д_ОТКР_ЗАТВОР_ВАК) &&
			(Д_ЗАКР_ЗАТВОР_ВАК) &&
			(Д_САПУН) &&
			(Д_УСТР_ПЛАВ_ПУСКА) &&
			(Д_ПИТ_ВАК_НАСОСА) &&
			(Д_ПИТ_ВЕНТИЛЯТОРА) &&
			(Д_ПИТ_РЕГ_Т) &&
			(Д_ПИТ_РЕГ_УР) &&
			(Д_УР_ВОДЫ_MIN) &&
			(Д_УР_ВОДЫ_MAX) &&
			(Д_ПИТ_НАСОС_ОХЛ))	В СЛЕДУЮЩЕЕ;
	
	ТАЙМАУТ ЗАДЕРЖКА_НА_ПОЛУЧЕНИЕ_ДОСТ_ДАННЫХ_ОТ_ОБОРУДОВАНИЯ В СОСТ ОстановПроцессов;
   }
   
   СОСТ ПаузаНаОчереднуюПроверку{
   ТАЙМАУТ ОДНА_МИНУТА В СОСТ Начало; 
   }
   СОСТ ОстановПроцессов{
		СТАРТ ПРОЦ ОстановВсехПроцессовИстартПроцИнициализация;
		В СЛЕДУЮЩЕЕ;
   } 
    СОСТ СообК_ГУИ_НЕТ_ДОСТОВЕРНОСТИ {
     ЕСЛИ (SendMsgGUICode(ПК_ГУИ_АВАРИЯ_НЕТ_ДОСТОВЕРНЫХ_ДАННЫХ_ОТ_ОБОРУДОВАНИЯ_ВАКУУМНОЙ_СИСТЕМЫ)) СТОП;
     ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
    /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/	
	СОСТ НарушениеСвязи {
		ОШИБКА;
	} 
 }


/*========================================================*/
/*                                                        */
/*==            ЧТЕНИЕ ФАКТИЧЕСКИХ ПАРАМЕТРОВ           ==*/
/*                                                        */
/*========================================================*/
ПРОЦ ЧтениеФактПараметров_с_Датчиков{
ИЗ ПРОЦ Инициализация ФП_P_ВбакеКомпрессора,
                      ФП_P_Впатрубке,
                      ФП_T_Врубашке,
                      ФП_T_Вбаке,
					  ФП_P_ВтрубеСидер;

 СОСТ Начало {
	ФП_P_ВбакеКомпрессора 	= GetFP_P_BAK_COMPRESSOR();     /* чтение давления в баке компрессора */
	ФП_P_Впатрубке 		= GetFP_P_PATRUBKA();     			/* чтение давления  в патрубке      */
	ФП_T_Врубашке 		= GetFP_T_RUBACHKA();     			/* чтение температуры воды в рубашке охлаждения */   
	ФП_T_Вбаке 			= GetFP_T_BAK();     				/* чтение температуры воды в баке охлаждения */ 
	ФП_P_ВтрубеСидер 	= GetFP_P_TRUBA();     				/* чтение давления в трубе сидера */ 
   В СОСТ Начало;
 }
}

/*=============================================================*/
/*=         РЕГУЛЯТОР ТРАФИКА                    ==============*/
/*=         ЕСЛИ КАНАЛЫ ЗАБИТЫ, ТО                           ==*/
/*=         УВЕЛИЧИВАЕТСЯ ВРЕМЯ МЕЖДУ                        ==*/
/*=         СЕРВЕРОМ И ВЫДАЧЕЙ В GUI                         ==*/
/*=         ФАКТИЧЕСКИХ ПАРАМЕТРОВ                           ==*/
/*= GetOutMsgNumber_GUI читает число сообщ. в выходн. буфере ==*/
/*=============================================================*/
ПРОЦ РЕГУЛЯТОР_ТРАФИКА{
 ИЗ ПРОЦ Инициализация ЗАДЕРЖКА_НА_РАЗГРУЗКУ_ТРАФИКА;
 СОСТ Начало{
    ЗАДЕРЖКА_НА_РАЗГРУЗКУ_ТРАФИКА = GetOutMsgNumber_GUI()/5 + 1;  /*динамический таймаут*/
    ЗАЦИКЛИТЬ;
 }
}

/*==============================================*/
/*==============================================*/
/*=                                            =*/
/*=      ПРОЦЕСС ЧтениеКольцевогоБуфера        =*/
/*=                                            =*/
/*==============================================*/
/*==============================================*/
ПРОЦ ЧтениеКольцевогоБуфера{
ИЗ ПРОЦ Инициализация
                        КодСобытия_от_ГУИ;       /* */

 СОСТ Начало{
    /*  читаем код и параметр события из кольцевого буфера в структуру */
    ЕСЛИ (GetNextMsgGUI()) {
      КодСобытия_от_ГУИ = GetMsgGUICode();
            ЕСЛИ ((КодСобытия_от_ГУИ >= КОМ_ГУИ_ОТКР_ЗАТВОР_ВАК) &&
                  (КодСобытия_от_ГУИ <= КОМАНДЫ_ОТ_ГУИ)) {
				СТАРТ ПРОЦ ОбработкаКомандУправления_от_ГУИ;
				В СОСТ КонтрольВыполненияК;
            } 
			ИНАЧЕ ЕСЛИ ((КодСобытия_от_ГУИ >= ИЗМН_НП_1) &&
                         (КодСобытия_от_ГУИ <= ИЗМН_НП_N)) {
                     СТАРТ ПРОЦ ОбработкаКомандИзмененияНП_от_ГУИ;
                     В СОСТ КонтрольИзмененияНП;
			}
			ИНАЧЕ В СОСТ НекорректноеСобытие;
    }
 } 
 
 СОСТ КонтрольВыполненияК{
    ЕСЛИ(ПРОЦ ОбработкаКомандУправления_от_ГУИ В СОСТ ПАССИВНОЕ)
    В СОСТ Начало;
 }
  СОСТ КонтрольИзмененияНП{
    ЕСЛИ(ПРОЦ ОбработкаКомандИзмененияНП_от_ГУИ В СОСТ ПАССИВНОЕ)
    В СОСТ Начало;
 }
  /*===== НЕНОРМАЛЬНЫЕ СИТУАЦИИ === */
  СОСТ НекорректноеСобытие{
     ЕСЛИ (SendMsgLongParamGUI(ПК_ГУИ_ОШИБКА_КОДА_КОМАНДЫ_ОТ_ГУИ, КодСобытия_от_ГУИ))
       В СОСТ Начало;
     ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }

 СОСТ НарушениеСвязи {
	В СОСТ Начало;
     СТОП;   /* м.б. в начало?!!!!!!!!!!!!!! что я и сделал*/
 }
}
/*===================================================*/
/*==                                               ==*/
/*==  ПРОЦЕСС ИЗМЕНЕНИЯ НАСТРОЕЧНЫХ ПАРАМЕТРОВ     ==*/
/*==                                               ==*/
/*===================================================*/
ПРОЦ ОбработкаКомандИзмененияНП_от_ГУИ{
ИЗ ПРОЦ Инициализация 	КодСобытия_от_ГУИ,
						КодСобытия_к_ГУИ,
						НП_Pатмосф,
						НП_ГлубокийВакуумТрубы,
						НП_P_ВбакеКомпрессора;
ПЛАВ ЛП_Параметр_к_ПИВ ЛОКАЛ;
 СОСТ Анализ{
    РАЗБОР (КодСобытия_от_ГУИ) {
	    СЛУЧАЙ ИЗМН_НП_Pатмосф:
          НП_Pатмосф  = GetFloatParamGUI();
          КодСобытия_к_ГУИ = ИЗМН_НП_Pатмосф;
          В СОСТ ПодтверждениеИзмененияНП;
			КОНЕЦ;
	    СЛУЧАЙ ИЗМН_НП_P_ВбакеКомпрессора:
		   НП_P_ВбакеКомпрессора  = GetFloatParamGUI();
          КодСобытия_к_ГУИ = НП_P_ВбакеКомпрессора;
          В СОСТ ПодтверждениеИзмененияНП;		   
			КОНЕЦ;
	    СЛУЧАЙ ИЗМН_НП_ГлубокийВакуумТрубы:
		   НП_ГлубокийВакуумТрубы  = GetFloatParamGUI();
          КодСобытия_к_ГУИ = НП_ГлубокийВакуумТрубы;
          В СОСТ ПодтверждениеИзмененияНП;		   
			КОНЕЦ;
		УМОЛЧАНИЕ:
          В СОСТ НекорректноеСобытие;
          КОНЕЦ;
    } /* конец разбора */
	СТОП;	

 }
 СОСТ ПодтверждениеИзмененияНП{
    ЕСЛИ (SendMsgLongParamGUI(КодСобытия_к_ГУИ, GetLongParamGUI())) СТОП;
    ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
 /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ НекорректноеСобытие {
    ЕСЛИ (SendMsgLongParamGUI(ПК_ГУИ_ОШИБКА_КОДА_КОМАНДЫ_ОТ_ГУИ, КодСобытия_от_ГУИ))
         СТОП;
    ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
  СОСТ НарушениеСвязи {
    ОШИБКА;
 }
}
/*=========================================*/
/*=                                       =*/
/*=     ОБРАБОТКА КОМАНД ОТ ГУИ           =*/
/*=                                       =*/
/*=========================================*/
ПРОЦ ОбработкаКомандУправления_от_ГУИ{
ИЗ ПРОЦ Инициализация
						/*	ТекущийРежим,*/
                    КодСобытия_от_ГУИ,
					К_УСТР_ПЛАВ_ПУСКА,
					ФлагГотовУрВодыБО,
					ФП_T_Вбаке,
					К_ПИТ_ВЕНТИЛЯТОРА,
					К_ПИТ_НАСОС_ОХЛ,
					К_ОТКР_ЗАТВОР_ВАК,
					К_ЗАКР_ЗАТВОР_ВАК,
					К_САПУН,
					ФлагГотовTВодыРО,
					ФП_P_ВтрубеСидер,
					ФП_P_Впатрубке,
					НП_Pатмосф;
ЦЕЛ ЛП_КодСообщенияДляGUI ЛОКАЛ;

 СОСТ Анализ{
    РАЗБОР (КодСобытия_от_ГУИ) {
	    			  
	    СЛУЧАЙ КОМ_ГУИ_ВКЛ_ПИТ_ВЕНТИЛЯТОР:
			СТАРТ ПРОЦ ВКЛ_ПИТ_ВЕНТИЛЯТОР;
			КОНЕЦ;
	    СЛУЧАЙ КОМ_ГУИ_ВЫК_ПИТ_ВЕНТИЛЯТОР:
			СТАРТ ПРОЦ ВЫК_ПИТ_ВЕНТИЛЯТОР;	
			КОНЕЦ;	
		  
		СЛУЧАЙ КОМ_ГУИ_ВКЛ_ПИТ_РЕГ_УР:
			СТАРТ ПРОЦ ВКЛ_ПИТ_РЕГ_УР;	
			КОНЕЦ;
		СЛУЧАЙ КОМ_ГУИ_ВЫК_ПИТ_РЕГ_УР:
			СТАРТ ПРОЦ ВЫК_ПИТ_РЕГ_УР;	
			КОНЕЦ;
		  
		УМОЛЧАНИЕ:
          В СОСТ НекорректноеСобытие;
          КОНЕЦ;
    } /* конец разбора */
	СТОП;
 }


 /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ НекорректноеСобытие {
    ЕСЛИ (SendMsgLongParamGUI(ПК_ГУИ_ОШИБКА_КОДА_КОМАНДЫ_ОТ_ГУИ, КодСобытия_от_ГУИ))
         СТОП;   
    ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
 СОСТ ПередачаСообщенияСПараметромГУИ{
    ЕСЛИ (SendMsgLongParamGUI(ЛП_КодСообщенияДляGUI, КодСобытия_от_ГУИ))
        СТОП;
    ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }

 СОСТ НарушениеСвязи {
    ОШИБКА;
 }
} /* конец ПРОЦ ОбработкаКомандУправленияРегуляторами_от_ПИВ   */

/*=========================================*/
/*=                                       =*/
/*=     ПРОЦЕССЫ  УПРАВЛЕНИЯ ВАКУУМНЫМ    =*/
/*=          ОБОРУДОВАНИЕМ                =*/
/*=========================================*/

/*-------------------------------------------------------------------------------------------------------------------------------------------------------------------------------*/
/*----------------------------------------------------------------------------ВЕНТИЛЯТОР-----------------------------------------------------------------------------------------*/
/*-------------------------------------------------------------------------------------------------------------------------------------------------------------------------------*/

/*=======================================================*/
/*=====   Процесс подачи питания на вентилятор		=====*/
/*=======================================================*/
ПРОЦ ВКЛ_ПИТ_ВЕНТИЛЯТОР{
 ИЗ ПРОЦ Инициализация 	К_ПИТ_ВЕНТИЛЯТОРА,
						У_ПИТ_ВЕНТИЛЯТОРА,
						Д_ПИТ_ВЕНТИЛЯТОРА
					/*	ОШИБКА_ФУНКЦ_ВЕНТИЛЯТОР */
						;
 					  
 СОСТ Начало{
   ЕСЛИ (ПРОЦ ВЫК_ПИТ_ВЕНТИЛЯТОР В СОСТ АКТИВНОЕ) ОШИБКА ПРОЦ ВЫК_ПИТ_ВЕНТИЛЯТОР;
   СТОП ПРОЦ КонтрольСпонтанногоВключенияВентилятора;
	У_ПИТ_ВЕНТИЛЯТОРА = ВКЛ;
	В СЛЕДУЮЩЕЕ;      
 }
 СОСТ КонтрольВключения{
	ЕСЛИ (К_ПИТ_ВЕНТИЛЯТОРА == ВКЛ)  {
		СТАРТ ПРОЦ КонтрольСпонтанногоВыключенияВентилятора;
		В СОСТ ПередачаСообщения;
	}
	ТАЙМАУТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_ВЕНТИЛЯТОРА В СОСТ Ошибка;
  }
 СОСТ ПередачаСообщения {
    ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ВЕНТИЛЯТОР_ВКЛЮЧЕН)) СТОП;
    ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
  
/*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ Ошибка {
       ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ОТКАЗ_ВКЛЮЧЕНИЯ_ВЕНТИЛЯТОРА))	ОШИБКА;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
  СОСТ НарушениеСвязи {
	   ОШИБКА;
 }   
} /*конец процесса ВКЛ_ПИТ_ВЕНТИЛЯТОР  */

/*=======================================================*/
/*=====   Процесс отключения питания вентилятора    =====*/
/*=======================================================*/
ПРОЦ ВЫК_ПИТ_ВЕНТИЛЯТОР{
 ИЗ ПРОЦ Инициализация 	К_ПИТ_ВЕНТИЛЯТОРА,
						У_ПИТ_ВЕНТИЛЯТОРА,
						Д_ПИТ_ВЕНТИЛЯТОРА
					/*	ОШИБКА_ФУНКЦ_ВЕНТИЛЯТОР  */
						;
 СОСТ Начало{
	ЕСЛИ (ПРОЦ ВКЛ_ПИТ_ВЕНТИЛЯТОР В СОСТ АКТИВНОЕ) ОШИБКА ПРОЦ ВКЛ_ПИТ_ВЕНТИЛЯТОР;
	СТОП ПРОЦ КонтрольСпонтанногоВыключенияВентилятора;
/*	ОШИБКА_ФУНКЦ_ВЕНТИЛЯТОР = НЕТ; */
	У_ПИТ_ВЕНТИЛЯТОРА = ВЫК;
	В СЛЕДУЮЩЕЕ;      
 }
 
 СОСТ КонтрольВыключения{
/* 	ЕСЛИ ((К_ПИТ_ВЕНТИЛЯТОРА == ВЫК) && (Д_ПИТ_ВЕНТИЛЯТОРА)) {*/
	ЕСЛИ (К_ПИТ_ВЕНТИЛЯТОРА == ВЫК) {
		СТАРТ ПРОЦ КонтрольСпонтанногоВключенияВентилятора;
		В СОСТ ПередачаСообщения;
	} 
	ТАЙМАУТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_ВЕНТИЛЯТОРА В СОСТ Ошибка;
 }
   
 СОСТ ПередачаСообщения {
    ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ВЕНТИЛЯТОР_ВЫКЛЮЧЕН)) СТОП;
    ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
  
/*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
 СОСТ Ошибка {
       ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ОТКАЗ_ВЫКЛЮЧЕНИЯ_ВЕНТИЛЯТОРА))	ОШИБКА;
      ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
 }
  СОСТ НарушениеСвязи {
	   ОШИБКА;
 }   
} /*конец процесса ВЫК_ПИТ_ВЕНТИЛЯТОРА  */					  
					  

/*========================================================*/
/*=====   Процесс контроля включенности вентилятора  =====*/
/*========================================================*/
ПРОЦ КонтрольСпонтанногоВыключенияВентилятора{
	ИЗ ПРОЦ Инициализация 	К_ПИТ_ВЕНТИЛЯТОРА,
							Д_ПИТ_ВЕНТИЛЯТОРА;

	СОСТ Контроль {
/*		ЕСЛИ ((К_ПИТ_ВЕНТИЛЯТОРА == ВЫК) && (Д_ПИТ_ВЕНТИЛЯТОРА)) В СОСТ Ошибка; */
		ЕСЛИ (К_ПИТ_ВЕНТИЛЯТОРА == ВЫК)  В СОСТ Ошибка;
	}

	СОСТ Ошибка {
		ЕСЛИ (SendMsgGUICode(ПК_ГУИ_СПОНТАННОЕ_ВЫКЛЮЧЕНИЕ_ВЕНТИЛЯТОРА)) {
/*			СТАРТ ПРОЦ ВЫК_ВАКУУМИРОВАНИЕ_ТРУБЫ;  */		/* прекращаем вакуумирование ??????*/
			ОШИБКА;}
		ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
	}
	
    /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/	
	СОСТ НарушениеСвязи {
		ОШИБКА;
	}
}


/*=========================================================*/
/*=====   Процесс контроля выключенности вентилятора  =====*/
/*=========================================================*/
ПРОЦ КонтрольСпонтанногоВключенияВентилятора{
	ИЗ ПРОЦ Инициализация 	К_ПИТ_ВЕНТИЛЯТОРА,
							Д_ПИТ_ВЕНТИЛЯТОРА;
	
	СОСТ Контроль {
/*		ЕСЛИ ((К_ПИТ_ВЕНТИЛЯТОРА == ВКЛ) && (Д_ПИТ_ВЕНТИЛЯТОРА))В СОСТ Ошибка; */
		ЕСЛИ (К_ПИТ_ВЕНТИЛЯТОРА == ВКЛ) В СОСТ Ошибка; 
	}
	
	СОСТ Ошибка {
		ЕСЛИ (SendMsgGUICode(ПК_ГУИ_СПОНТАННОЕ_ВКЛЮЧЕНИЕ_ВЕНТИЛЯТОРА)) ОШИБКА;
				/*  если включился, то и ладно, вакуумирования-то нет !!!!!!!!!!!!!!!!!*/
		ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
	}

	
    /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/		
	СОСТ НарушениеСвязи {
		ОШИБКА;
	}
}				  

/*-------------------------------------------------------------------------------------------------------------------------------------------------------------------------------*/
/*----------------------------------------------------------------------------РЕГУЛЯТОР УРОВНЯ ВОДЫ------------------------------------------------------------------------------*/
/*-------------------------------------------------------------------------------------------------------------------------------------------------------------------------------*/

/*================================================================*/
/*        Процесс Подать питание на регулятор уровня воды         */
/*================================================================*/
ПРОЦ ВКЛ_ПИТ_РЕГ_УР{
	ИЗ ПРОЦ Инициализация 	У_ПИТ_РЕГ_УР,
							К_ПИТ_РЕГ_УР,
							Д_ПИТ_РЕГ_УР
						/*	ОШИБКА_ФУНКЦ_ПИТ_РЕГ_УР */
							;

	СОСТ Начало{
		ЕСЛИ (ПРОЦ ВЫК_ПИТ_РЕГ_УР В СОСТ АКТИВНОЕ) ОШИБКА ПРОЦ ВЫК_ПИТ_РЕГ_УР;
		СТОП ПРОЦ КонтрольСпонтанногоВключенияРЕГ_УР;
		/*	ОШИБКА_ФУНКЦ_ПИТ_РЕГ_УР = НЕТ; */
		У_ПИТ_РЕГ_УР = ВКЛ;
		В СЛЕДУЮЩЕЕ;      
	}

	СОСТ Контроль{  
		ЕСЛИ (К_ПИТ_РЕГ_УР == ВКЛ){
			СТАРТ ПРОЦ КонтрольСпонтанногоВыключенияРЕГ_УР;
			В СОСТ ПередачаСообщения;
		} 
		ТАЙМАУТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_РЕГ_УР В СОСТ Ошибка; 
	}
	СОСТ ПередачаСообщения {
		ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ПИТАНИЕ_РЕГУЛЯТОРА_УРОВНЯ_ВКЛЮЧЕНО)) СТОП;
		ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
	}  

		 /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
	СОСТ Ошибка {
		ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ОТКАЗ_ВКЛЮЧЕНИЯ_РЕГУЛЯТОРА_УРОВНЯ))	ОШИБКА;
		ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
	}
	СОСТ НарушениеСвязи {
		ОШИБКА;
	} 
}  /*конец процесса ВКЛ_ПИТ_РЕГ_УР */

/*================================================================*/
/*        Процесс Снять питание с регулятора уровня воды          */
/*================================================================*/
ПРОЦ ВЫК_ПИТ_РЕГ_УР{
	ИЗ ПРОЦ Инициализация 	У_ПИТ_РЕГ_УР,
						К_ПИТ_РЕГ_УР,
						Д_ПИТ_РЕГ_УР
					/*	ОШИБКА_ФУНКЦ_ПИТ_РЕГ_УР */
						;

	СОСТ Начало{
		ЕСЛИ (ПРОЦ ВКЛ_ПИТ_РЕГ_УР В СОСТ АКТИВНОЕ) ОШИБКА ПРОЦ ВКЛ_ПИТ_РЕГ_УР;
		СТОП ПРОЦ КонтрольСпонтанногоВыключенияРЕГ_УР;
		/*	ОШИБКА_ФУНКЦ_ПИТ_РЕГ_УР = НЕТ; */
		У_ПИТ_РЕГ_УР = ВЫК;
		В СЛЕДУЮЩЕЕ;      
	}

	СОСТ Контроль{  
		ЕСЛИ (К_ПИТ_РЕГ_УР == ВЫК) {
			СТАРТ ПРОЦ КонтрольСпонтанногоВключенияРЕГ_УР;  
			В СОСТ ПередачаСообщения;
		} 
		ТАЙМАУТ ТАЙМАУТ_НА_СРАБАТЫВАНИЕ_РЕГ_УР 	В СОСТ Ошибка; 
	}
	СОСТ ПередачаСообщения {
		ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ПИТАНИЕ_РЕГУЛЯТОРА_УРОВНЯ_ВЫКЛЮЧЕНО)) СТОП;
		ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
	}  

		  /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/
	СОСТ Ошибка {
		ЕСЛИ (SendMsgGUICode(ПК_ГУИ_ОТКАЗ_ВЫКЛЮЧЕНИЯ_РЕГУЛЯТОРА_УРОВНЯ))	ОШИБКА;
		ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
	}
	СОСТ НарушениеСвязи {
	   ОШИБКА;
	} 
}  /*конец процесса ВЫК_ПИТ_РЕГ_УР */

/*========================================================*/
/*=====   Процесс контроля спонтанного выключения  =====*/
/*========================================================*/
ПРОЦ КонтрольСпонтанногоВыключенияРЕГ_УР{
	ИЗ ПРОЦ Инициализация 	К_ПИТ_РЕГ_УР,
							Д_ПИТ_РЕГ_УР;

	СОСТ Контроль {
		ЕСЛИ (К_ПИТ_РЕГ_УР == ВЫК) В СОСТ Ошибка;
	}
	СОСТ Ошибка {
		ЕСЛИ (SendMsgGUICode(ПК_ГУИ_СПОНТАННОЕ_ВЫКЛЮЧЕНИЕ_РЕГУЛЯТОРА_УРОВНЯ)) ОШИБКА;
		/*  здесь надо выключать вакуумирование !!!!!!!!!!!!!!!!!*/
		ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
	}
	
    /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/	
	СОСТ НарушениеСвязи {
		ОШИБКА;
	}
}	
/*=========================================================*/
/*=====   Процесс контроля спонтанного включения  =====*/
/*=========================================================*/
ПРОЦ КонтрольСпонтанногоВключенияРЕГ_УР{
	ИЗ ПРОЦ Инициализация 	К_ПИТ_РЕГ_УР,
							Д_ПИТ_РЕГ_УР;
	
	СОСТ Контроль {
		ЕСЛИ (К_ПИТ_РЕГ_УР == ВКЛ) В СОСТ Ошибка;
	}
	СОСТ Ошибка {
		ЕСЛИ (SendMsgGUICode(ПК_ГУИ_СПОНТАННОЕ_ВКЛЮЧЕНИЕ_РЕГУЛЯТОРА_УРОВНЯ)) ОШИБКА;
				/*  если включился, то и ладно, вакуумирования-то нет !!!!!!!!!!!!!!!!!*/
		ТАЙМАУТ ТАЙМАУТ_НА_СВЯЗЬ_С_ГУИ В СОСТ НарушениеСвязи;
	}
    /*= НЕНОРМАЛЬНЫЕ СИТУАЦИИ: ================*/		
	СОСТ НарушениеСвязи {
		ОШИБКА;
	}
}	


/*-------------------------------------------------------------------------------------------------------------------------------------------------------------------------------*/
/*----------------------------------------------------------------------------РЕГУЛЯТОР ТЕМПЕРАТУРЫ ВОДЫ-------------------------------------------------------------------------*/
/*-------------------------------------------------------------------------------------------------------------------------------------------------------------------------------*/


}  /* \Прогр */

